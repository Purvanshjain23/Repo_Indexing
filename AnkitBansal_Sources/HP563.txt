      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:   HOG PRODUCTION
      * PROGRAM:  HP563
      * TITLE:    SELECT SEQUENCE FOR FARM COST LISTING
      * AUTHOR:   LEANNE FEDOR
      * CREATED:  06/29/94
      *
      * FUNCTION: THIS IS A SELECT FUNCTION WINDOW DISPLAY OF
      *           THE VALID SORT SEQUENCES FOR THE FARM COST
      *           LISTING.
      *
      *         IT DOES NOT HAVE SELECTORS OR POSITIONERS.
      *         IT LOADS THE DATA FROM COMPILE-TIME ARRAYS.
      *         IT LOADS THE WINDOW SUBFILE A PAGE AT A TIME.
      *         IT DOES NOT USE THE STANDARD MESSAGE HANDLING PROGRAM.
      *         A RECORD IS SELECTED BY POSITIONING THE CURSOR
      *         ON THE LINE AND PRESSING ENTER.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99
      *
      ****************************************************************
      * VALID FUNCTION KEYS
      ****************************************************************
      * 12 F12-PREVIOUS
      * 25 ROLLUP
      *
      ****************************************************************
      * SUBFILE SELECTORS
      ****************************************************************
      *  *NONE
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD563    CF   E             WORKSTN
     F                                     SFILE(HP563S1:HDRRN1)
      *
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      * DEFINE STATUS CODES AND DESCRIPTIONS IN COMPILE-TIME ARRAYS
      *
     D CDE             S              1    DIM(2) CTDATA PERRCD(2)
     D DSC             S             30    DIM(2) CTDATA PERRCD(1)
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      *
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOO
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOO
      *
      /EJECT
      ****************************************************************
      * SUBROUTINES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *  CLEAR/FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
     C                   WRITE     HP563K1
     C                   EXFMT     HP563C1
      *
      * RESET ERROR FLAG
     C                   Z-ADD     0             ERROR
      *
      * GET USER'S ENTRY AND RESET STANDARD RESPONSE
      *
     C                   SELECT
      *
     C     *IN12         WHENEQ    *ON                                          F12=PREVIOUS
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C                   EXSR      $LOAD1
     C                   MOVEL     SCRN1         ROUTNE
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      *
     C     NORECS        IFEQ      YES                                          IF EMPTY SFL
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDIF                                                  IF EMPTY SFL
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * IF THERE ARE NO ERRORS, READ AND PROCESS ANY RECORD SELECTED
      * BY THE USER. IF THE USER SELECTED A RECORD, THEN EXIT THE
      * PROGRAM.
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
     C                   EXSR      $PROC1
      *
     C     XXSQ          IFNE      *BLANKS
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
      * ELSE THERE WERE ERRORS, SO SET TO REDISPLAY SCREEN
      *
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      *
      /EJECT
      *---------------------------------------------------------------
      * $CLR1 - SUBROUTINE TO CLEAR THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
     C                   CLEAR                   HP563S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP563C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * CLEAR THE SUBFILE
      * RELOAD THE SUBFILE FROM STATUS ARRAYS "CDE" AND "DSC"
      *
     C                   EXSR      $CLR1
     C                   EXSR      $LOAD1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1 - SUBROUTINE TO LOAD THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
      * FILL THE SUBFILE WITH THE TWO COMPILE TIME ARRAY ENTRIES
      *
     C                   Z-ADD     1             X                 1 0
      *
     C                   DO        2                                            FILL LOOP
     C                   MOVEL     CDE(X)        DFSQ
     C                   MOVEL     DSC(X)        DFSQDS
     C                   ADD       1             HDRRN1
     C                   WRITE     HP563S1
     C                   SETON                                        81
     C                   ADD       1             X
      *
      * FOR POSITIONING, PLACE THE SUBFILE RELATIVE RECORD NUMBER INTO
      * THE SFLRCDNBR FIELD (HDPAG1)
      *
     C                   Z-ADD     HDRRN1        HDPAG1
      *
      * *IN82 IS ASSOCIATED WITH THE SFLEND KEYWORD. SET IT ON TO
      * PREVENT THE '+' SIGN FROM APPEARING ON THE LAST SUBFILE RECORD.
      *
     C                   SETON                                        82
      *
     C                   ENDDO                                                  DO FILL LOOP
      *
      * IF NO RECORDS WERE WRITTEN TO THE SUBFILE, THEN ISSUE
      * THE STANDARD MESSAGE 'NO DATA TO DISPLAY'.
      *
     C     HDRRN1        IFEQ      0
     C                   Z-ADD     1             ERROR
     C                   MOVE      YES           NORECS
     C                   ELSE
     C                   MOVE      NO            NORECS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO READ SUBFILE RECORDS AND PROCESS
      *          USER SELECTIONS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
     C                   Z-ADD     0             ERROR
      *
      * IF CURSOR LOCATION RETURNED IS THE RRN OF A RCD IN THE SUBFILE,
      * CHAIN TO THE SUBFILE FOR THAT RECORD AND LOAD THE DATA INTO THE
      * PARMS TO RETURN TO THE CALLING PROGRAM.
      *
     C                   SETOFF                                       82
     C     HDRRN         IFNE      0
     C                   Z-ADD     HDRRN         HDRRN1
     C     HDRRN1        CHAIN     HP563S1                            82
     C                   MOVEL     DFSQ          XXSQ
     C                   MOVEL     DFSQDS        XXSQDS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      *
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *     NONE
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    XXSQ
     C                   PARM                    XXSQDS
      *
     C     *LIKE         DEFINE    DFSQ          XXSQ
     C     *LIKE         DEFINE    DFSQDS        XXSQDS
      *
      * GLOBAL DEFINES
      *
     C                   MOVEL     *BLANK        ROUTNE            6            ROUTINE
     C                   MOVEL     NO            NORECS            1            NO RECORDS
     C                   Z-ADD     0             ERROR             3 0          ERRORS
      *
      *  DO ONE TIME ONLY:  MAKE SURE CALLING PROGRAM SCREEN IS NOT
      *  CLEARED AND WRITE THE WINDOW DEFINITION RCDFMT
      *
     C                   WRITE     HP563R2                                      DUMMY FMT
     C                   WRITE     HP563R1                                      REF WINDOW
     C                   ENDSR
      /EJECT
     ******************************************************************
      * ARRAYS FOR LOADING OF DATA TO SUBFILE
     ******************************************************************
**  VALID SORT SEQUENCE CODES
FE
**  DESCRIPTIONS
SEQUENCE BY FARM SITE
SEQUENCE BY EFFECTIVE DATE
