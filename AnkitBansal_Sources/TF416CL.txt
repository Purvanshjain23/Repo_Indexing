/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF416CL                                       */
/*  PROGRAM NAME:    MARGIN ADJUSTMENT REPORTS                     */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     09/27/06                                      */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 11/21/08  LeAnne Ramsey                                         */
/*           As part of synchronizing the LDAs between the         */
/*           TFS Margin Adjustment Close and the Meat Costing,     */
/*           the LDA positions were changed.                       */
/*                                                                     */
/* 05/07/09  LEANNE RAMSEY                                             */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.                */
/* 05/07/24  SANTOSH PATIL - P310149 - FIELD LENGTH IS INCREASED       */
/*           IN TFP014, TFP015, TF315. FUNCTION IS RECOMPILED.         */
/*‚05/14/24  Jagdish Mistry (JM-P310149),Service request number-3009756*/
/*‚          Requirement 1:                                            */
/*‚          Damon called to say that we need to take items which has  */
/*‚          Volume flag = No & Co-owned = T.                          */
/*‚          Same will be excluded in Volume Adjustment Report.        */
/*‚          Requirement 2:                                            */
/*‚          Include WP items to be printed in Volume adjustment report*/
/*‚          MIXREPORT-Copied from TF415CL as TF617 is called here     */
/*‚          ADDWIPITEM-Copied from TF415CL as TF615,TF616 is called here*/
/*‚          Requirement 3 & 4:                                        */
/*‚          This will match weights between volume adjustment         */
/*‚11/25/24  Jagdish Mistry (JM-P310149),Service request number-3014062*/
/*‚          Remove duplicate pages using option 2                     */
/*‚          Margin Adjustment Reports.The new option 2 calculates     */
/*‚          amounts similar to Margin Adjustment Close.               */
/***********************************************************************/

/* JM-P310149 START */
/*           PGM    */
             PGM        PARM(&WK_WIP)
/* JM-P310149 END   */

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(117)
/*‚JM-P310149 START                                                */
/*           DCL        VAR(&QDATA2) TYPE(*CHAR) LEN(90)           */
             DCL        VAR(&QDATA2) TYPE(*CHAR) LEN(130)
/*‚JM-P310149-11/25/24                                             */
             DCL        VAR(&LDWESYN) TYPE(*DEC)  LEN(7)
/*‚JM-P310149 END                                                  */
             DCL        VAR(&QDATA3) TYPE(*CHAR) LEN(130)
/* JM-P310149 START */
             DCL        VAR(&WK_WIP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDWK)    TYPE(*DEC) LEN(2)
             DCL        VAR(&QDATA4) TYPE(*CHAR) LEN(130) VALUE(' ')
             DCL        VAR(&QDATA5) TYPE(*CHAR) LEN(130) VALUE(' ')
/* JM-P310149 END   */

             DCL        VAR(&ERR)    TYPE(*CHAR) LEN(1) VALUE('N')

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDRPT)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDWPFL)   TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDYR)     TYPE(*DEC) LEN(4)
             DCL        VAR(&LDWEDT)   TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPEDT)   TYPE(*DEC) LEN(8)

             DCL        VAR(&LDISTYCD) TYPE(*DEC) LEN(3)
             DCL        VAR(&LDTFCLCD) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LDTFCGCD) TYPE(*CHAR) LEN(2)

             CHGVAR     VAR(&LDYR)   VALUE(%SST(*LDA  2 4))
             CHGVAR     VAR(&LDWEDT) VALUE(%SST(*LDA 29 8))
/*‚JM-P310149 START-11/25/24                                       */
             CHGVAR     VAR(&LDWESYN)  VALUE(%SST(*LDA 37 7))
/*‚JM-P310149 END-11/25/24                                         */
             CHGVAR     VAR(&LDPEDT) VALUE(%SST(*LDA 67 8))

             CHGVAR     VAR(&LDRPT)    VALUE(%SST(*LDA 113 1))
             CHGVAR     VAR(&LDWPFL)   VALUE(%SST(*LDA 134 1))

             CHGVAR     VAR(&LDTFCGCD) VALUE(%SST(*LDA 139 2))
             CHGVAR     VAR(&LDTFCLCD) VALUE(%SST(*LDA 156 3))
             CHGVAR     VAR(&LDISTYCD) VALUE(%SST(*LDA 189 3))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

/* JM-P310149 START */
             IF         COND(&WK_WIP *EQ 'Y') THEN(DO)
/*‚JM-P310149 START-11/25/2024 */
/*‚ALWAYS DELETE ANY/ALL EXISTING RECORDS FOR THE YEAR/WEEK            */
             CALL       PGM(TF217)
/*‚UPDATE STANDARD COSTS/YIELDS IN THE REVENUE DETAIL RECORDS         */
             CALL       PGM(TF243) PARM(&LDWEDT &LDWESYN)
/*‚EXTRACT DATA FOR THE MARGIN ADJUSTMENT DETAIL FILE                  */
             CALL       PGM(TF216)
/*‚RETRIEVE "PRODUCT SPLITS" AND UPDATE MARGIN ADJUSTMENT DETAIL FILE  */
             CALL       PGM(TF245)
/*‚JM-P310149 END-11/25/24 */
             CALLSUBR   SUBR(ADDWIPITEM)
             CALLSUBR   SUBR(MIXREPORT)
             ENDDO
/* JM-P310149 END   */
/*---------------------------------------------------------------------*/
/* MIX SUMMARY                                                         */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRPT *EQ 'M' *OR &LDRPT *EQ 'B') THEN(DO)

             CRTDUPOBJ  OBJ(TFP315) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(TFL315A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* WEEKLY MIX                                                          */
             IF         COND(&LDWPFL *EQ 'W') THEN(DO)

             CPYF       FROMFILE(TFP015) TOFILE(QTEMP/TFP315) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)

             CHGVAR     VAR(%SST(&QDATA 1 9)) VALUE('MSEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 11 8)) VALUE(&LDWEDT)
             ENDDO

/* PERIOD MIX                                                          */
             IF         COND(&LDWPFL *EQ 'P') THEN(DO)
             CPYF       FROMFILE(TFP115) TOFILE(QTEMP/TFP315) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
             CHGVAR     VAR(%SST(&QDATA 1 9)) VALUE('MSEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDPEDT)
             ENDDO

/* THIS WILL BE OUR "DRIVER" FILE; WE ONLY WANT THE "TOTAL" SUMMARY    */
/* RECORDS HERE---NOT THE WP OR FG SUMMARY RECORDS.                    */

             CHGVAR     VAR(%SST(&QDATA 21 20)) VALUE('*AND MSITYCD *EQ " "')

/* IF TF CLASSIFICATION CODE                                        */

             IF         COND(&LDTFCLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 42 19)) VALUE('*AND MSTFCLCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 61 3)) VALUE(&LDTFCLCD)
             CHGVAR     VAR(%SST(&QDATA 64 1)) VALUE('"')
             ENDDO

/* IF TF CLASS GROUP CODE                                              */

             IF         COND(&LDTFCGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 66 19)) VALUE('*AND MSTFCGCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 85 2)) VALUE(&LDTFCGCD)
             CHGVAR     VAR(%SST(&QDATA 87 1)) VALUE('"')
             ENDDO

/* IF ITEM STRUCTURE TYPE CODE                                      */

             IF         COND(&LDISTYCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 89 17)) VALUE('*AND MSISTYCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 107 3)) VALUE(&LDISTYCD)
             ENDDO

/* OVERRIDE MIX SUMMARY FILES                                 */

             OVRDBF     FILE(TFP315)  SHARE(*YES)
             OPNQRYF    FILE((TFP315)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/* SET UP QUERY AND OVERRIDE MARGIN ADJUSTMENT FILE           */

             CHGVAR     VAR(&QDATA2) VALUE(' ')

             IF         COND(&LDWPFL *EQ 'W') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA2 1 10)) VALUE('ADWEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA2 12 8)) VALUE(&LDWEDT)
             ENDDO

             IF         COND(&LDWPFL *EQ 'P') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA2 1 10)) VALUE('ADPEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA2 12 8)) VALUE(&LDPEDT)
             ENDDO

             CHGVAR     VAR(%SST(&QDATA2 21 45)) VALUE('*AND ADTFCGCD +
                          *NE "OT" *AND ADTFCGCD *NE "BP"')

/*‚JM-P310149 START                                                */
/*           CHGVAR     VAR(%SST(&QDATA2 67 20)) VALUE('*AND ADMIXFL + */
/*                        *EQ "Y"')                                    */
             CHGVAR     VAR(%SST(&QDATA2 78 22)) VALUE('*AND (ADITYCD +
                          *EQ "FG"')
             CHGVAR     VAR(%SST(&QDATA2 101 24)) VALUE(' *OR ADITYCD +
                          *EQ "WP")')
/*‚JM-P310149 END                                                  */


             OVRDBF     FILE(TFL014D) SHARE(*YES)
             OPNQRYF    FILE((TFL014D)) OPTION(*INP) QRYSLT(&QDATA2) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* OVERRIDE PRINT FILE                                                  */

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF617)
             DLTOVR     FILE(PRINT198)

             CLOF       OPNID(TFP315)
             DLTOVR     FILE(TFP315)

             DLTF       FILE(QTEMP/TFL315A)
             DLTF       FILE(QTEMP/TFP315)

             CLOF       OPNID(TFL014D)
             DLTOVR     FILE(TFL014D)
             DLTOVR     FILE(*ALL)

             ENDDO


/*---------------------------------------------------------------------*/
/* VOLUME SUMMARY                                                      */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRPT *EQ 'V' *OR &LDRPT *EQ 'B') THEN(DO)

             CRTDUPOBJ  OBJ(TFP318) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Volume Summary */

             CRTDUPOBJ  OBJ(TFP331) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Volume Summary Extension */

             CHGVAR     VAR(&QDATA)  VALUE(' ')
             CHGVAR     VAR(&QDATA3) VALUE(' ')

/* WEEKLY VOLUME                                                       */

             IF         COND(&LDWPFL *EQ 'W') THEN(DO)

             CHGVAR     VAR(%SST(&QDATA3 1 10)) VALUE('ADWEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA3 12 8)) VALUE(&LDWEDT)
/* JM-P310149 START */
             CHGVAR     VAR(%SST(&QDATA4 1 10)) VALUE('ADWEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA4 12 8)) VALUE(&LDWEDT)
/* JM-P310149 END   */

             CPYF       FROMFILE(TFP018) TOFILE(QTEMP/TFP318) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
             CHGVAR     VAR(%SST(&QDATA 1 9)) VALUE('VSEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDWEDT)

             CPYF       FROMFILE(TFP031) TOFILE(QTEMP/TFP331) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
/*‚JM-P310149 START                                                */
             DLTF       FILE(QTEMP/TFL331A)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(TFL331A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
/*‚JM-P310149 END                                                  */
             ENDDO

/* PERIOD VOLUME                                                       */

             IF         COND(&LDWPFL *EQ 'P') THEN(DO)

             CHGVAR     VAR(%SST(&QDATA3 1 10)) VALUE('ADPEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA3 12 8)) VALUE(&LDPEDT)

             CPYF       FROMFILE(TFP118) TOFILE(QTEMP/TFP318) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('VSEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDPEDT)

             CPYF       FROMFILE(TFP131) TOFILE(QTEMP/TFP331) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
/*‚JM-P310149 START                                                */
             DLTF       FILE(QTEMP/TFL331A)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(TFL331A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
/*‚JM-P310149 END                                                  */
             ENDDO

/*-----------------------------------------------------------------------*/
/* QUERY/OVERRIDE MARGIN DETAIL FILE                                     */
/*-----------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA3 21 45)) VALUE('*AND ADTFCGCD +
                          *NE "OT" *AND ADTFCGCD *NE "BP"')
             CHGVAR     VAR(%SST(&QDATA3 67 22)) VALUE('*AND ADITYCD +
                          *EQ "FG "')
             CHGVAR     VAR(%SST(&QDATA3 90 38)) VALUE('*AND (ADSPULB +
                          *NE 0 *OR ADTPULB *NE 0)')
/* JM-P310149 START */
             CHGVAR     VAR(%SST(&QDATA4 21 45)) VALUE('*AND ADTFCGCD +
                          *NE "OT" *AND ADTFCGCD *NE "BP"')
             CHGVAR     VAR(%SST(&QDATA4 67 22)) VALUE('*AND ADITYCD +
                          *EQ "WP "')
             CHGVAR     VAR(%SST(&QDATA4 90 38)) VALUE('*AND (ADSPULB +
                          *NE 0 *OR ADTPULB *NE 0)')
/* JM-P310149 END   */

             OVRDBF     FILE(TFL014C) SHARE(*YES)
             OPNQRYF    FILE((TFL014C)) OPTION(*INP) QRYSLT(&QDATA3) +
                          KEYFLD(*FILE) SEQONLY(*YES)
/* JM-P310149 START */
             OVRDBF     FILE(TFL014D) SHARE(*YES)
             OPNQRYF    FILE((TFL014D)) OPTION(*INP) QRYSLT(&QDATA4) +
                          KEYFLD((ADITYCD) (ADTFCLCD) (ADISTYCD)) +
                          SEQONLY(*YES)
/* JM-P310149 END   */

/*-----------------------------------------------------------------------*/
/* QUERY/OVERRIDE VOLUME SUMMARY FILE                                    */
/*-----------------------------------------------------------------------*/

/* IF TF CLASSIFICATION CODE                                        */

             IF         COND(&LDTFCLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 21 19)) VALUE('*AND VSTFCLCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 40 3)) VALUE(&LDTFCLCD)
             CHGVAR     VAR(%SST(&QDATA 43 1)) VALUE('"')
             ENDDO

/* IF TF CLASS GROUP CODE                                              */

             IF         COND(&LDTFCGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 19)) VALUE('*AND VSTFCGCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 64 2)) VALUE(&LDTFCGCD)
             CHGVAR     VAR(%SST(&QDATA 66 1)) VALUE('"')
             ENDDO

/* IF ITEM STRUCTURE TYPE CODE                                      */

             IF         COND(&LDISTYCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 68 17)) VALUE('*AND VSISTYCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 86 3)) VALUE(&LDISTYCD)
             ENDDO

             OVRDBF     FILE(TFP318) SHARE(*YES)
             OPNQRYF    FILE((TFP318)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)


 /* VOLUME ADJUSTMENT REPORT                                              */

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF616)

 /* VOLUME ADJUSTMENT BACKUP REPORT                                       */

             CALL       PGM(TF615)
             DLTOVR     FILE(PRINT198)


             CLOF       OPNID(TFP318)
             DLTOVR     FILE(TFP318)
             DLTF       FILE(QTEMP/TFP318)
/* JM-P310149 START */
             DLTF       FILE(QTEMP/TFL331A)
             MONMSG     MSGID(CPF0000)
/* JM-P310149 END   */
             DLTF       FILE(QTEMP/TFP331)

             CLOF       OPNID(TFL014C)
             DLTOVR     FILE(TFL014C)
/* JM-P310149 START */
             CLOF       OPNID(TFL014D)
             DLTOVR     FILE(TFL014D)
/* JM-P310149 END   */
             DLTOVR     FILE(*ALL)
             ENDDO

/*‚JM-P310149 START                                                */
/*‚--------------------------------------------------------------- */
/*‚Add work in process items in work file                          */
/*‚--------------------------------------------------------------- */
             SUBR       SUBR(ADDWIPITEM)
             CHGVAR     VAR(&QDATA5) VALUE(' ')

             CHGVAR     VAR(&LDWK) VALUE(%SST(*LDA 6 2))
             CHGVAR     VAR(%SST(&QDATA5 1 8)) VALUE('ADYR *EQ')
             CHGVAR     VAR(%SST(&QDATA5 10 4)) VALUE(&LDYR)
             CHGVAR     VAR(%SST(&QDATA5 15 13)) VALUE('*AND ADWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA5 29 2)) VALUE(&LDWK)

             CHGVAR     VAR(%SST(&QDATA5 32 45)) VALUE('*AND ADTFCGCD +
                          *NE "OT" *AND ADTFCGCD *NE "BP"')

             CHGVAR     VAR(%SST(&QDATA5 78 22)) VALUE('*AND (ADITYCD +
                          *EQ "FG"')
             CHGVAR     VAR(%SST(&QDATA5 101 24)) VALUE(' *OR ADITYCD +
                          *EQ "WP")')

             OVRDBF     FILE(TFL014C) SHARE(*YES)
             OPNQRYF    FILE((TFL014C)) OPTION(*INP) QRYSLT(&QDATA5) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* WEEKLY VOLUME SUMMARY AND WEEKLY VOLUME SUMMARY EXTENSION           */

             DLTF       FILE(QTEMP/TFL318A)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFP318)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFP331)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFL331A)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(TFP318) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Summary */
             CRTDUPOBJ  OBJ(TFL318A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Summary */

             CRTDUPOBJ  OBJ(TFP331) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Summary Extension */
             CRTDUPOBJ  OBJ(TFL331A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Summary Extension */

             CALL       PGM(TF218) PARM('W')
             CALL       PGM(TF231) PARM('W')

             CPYF       FROMFILE(QTEMP/TFP318) TOFILE(TFP018) +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF2817)

             CPYF       FROMFILE(QTEMP/TFP331) TOFILE(TFP031) +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF2817)

             CLOF       OPNID(TFL014C)
             DLTOVR     FILE(TFL014C)

             DLTF       FILE(QTEMP/TFL318A)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFP318)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFL331A)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFP331)
             MONMSG     MSGID(CPF0000)
             ENDSUBR
/*‚JM-P310149 END                                                  */
/*‚--------------------------------------------------------------- */
/*‚Match summary report with mix report                            */
/*‚--------------------------------------------------------------- */
             SUBR       SUBR(MIXREPORT)

             CHGVAR     VAR(&QDATA5) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA5 1 8)) VALUE('ADYR *EQ')
             CHGVAR     VAR(%SST(&QDATA5 10 4)) VALUE(&LDYR)
             CHGVAR     VAR(%SST(&QDATA5 15 13)) VALUE('*AND ADWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA5 29 2)) VALUE(&LDWK)

             CHGVAR     VAR(%SST(&QDATA5 32 45)) VALUE('*AND ADTFCGCD +
                          *NE "OT" *AND ADTFCGCD *NE "BP"')
             CHGVAR     VAR(%SST(&QDATA5 78 22)) VALUE('*AND (ADITYCD +
                          *EQ "FG"')
             CHGVAR     VAR(%SST(&QDATA5 101 24)) VALUE(' *OR ADITYCD +
                          *EQ "WP")')

             OVRDBF     FILE(TFL014B) SHARE(*YES)
             OPNQRYF    FILE((TFL014B)) OPTION(*INP) QRYSLT(&QDATA5) +
                          KEYFLD(*FILE) SEQONLY(*YES)

             CRTDUPOBJ  OBJ(TFP315) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(TFL315A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(TF215) PARM('W')

             CPYF       FROMFILE(QTEMP/TFP315) TOFILE(TFP015) +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF2817)

             CLOF       OPNID(TFL014B)
             DLTOVR     FILE(TFL014B)
             DLTF       FILE(QTEMP/TFL315A)
             DLTF       FILE(QTEMP/TFP315)
             ENDSUBR
/*‚JM-P310149 END                                                  */
             ENDPGM

