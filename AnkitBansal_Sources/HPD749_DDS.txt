     A*%%TS  SD  19950622  085549  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD749
     A*  TITLE:         VIEW REJECTED HEAD REASONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       05/30/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP749S1                   SFL
     A*%%TS  SD  19950530  162112  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFRJHD    R        O 11 14REFFLD(RJRJHD)
     A                                      EDTCDE(K)
     A            DFDRCD    R        O 11 24REFFLD(DRDRCD)
     A            DFDRDS    R        O 11 32REFFLD(DRDRDS)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP749C1                   SFLCTL(HP749S1)
     A*%%TS  SD  19950622  085549  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 25'VIEW REJECTED HEAD REASONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ......:'
     A            C1FSCD    R        O  4 21REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 31REFFLD(FSFSNM)
     A                                  5  2'Hog group ......:'
     A            C1HGCD    R        O  5 21REFFLD(HGHGCD)
     A                                  6  2'Movement .......:'
     A            C1MVSN    R        O  6 21REFFLD(RJMVSN)
     A                                      EDTCDE(M)
     A                                  7  2'Rejected head ..:'
     A            C1RJHD    R     Y  O  7 21REFFLD(RJRJHD)
     A                                      EDTCDE(K)
     A                                  9 16'Head'
     A                                      DSPATR(HI)
     A                                  9 24'Reason'
     A                                      DSPATR(HI)
     A                                  9 32'Description'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP749K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*----------------------------------------------------------------
     A          R HP749ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A          R HP749EC                   SFLCTL(HP749ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
