/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              AND PRINT LISTING (PASSED AS PARAMETER - &PGM) WITH */
/*              OVERRIDES. PROMPTS FOR COMPANY, FROM AND TO DATE    */
/*                                                                  */
/*  COMPANY   - SEABOARD CORPORATION                                */
/*  SYSTEM    - SOMC OPERATING MARGIN REPORTS                       */
/*  PROGRAM   - OPERATING MARGIN REPORTS                            */
/*  USER NAME - MARK ARENSBERG                                      */
/*  DATE      - 02/17/94                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &PGM        - PROGRAM NAME                              */
/*          &DESC       - PROGRAM DESCRIPTION                       */
/*                                                                  */
/*      PARAMETERS USED:                                            */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPIES     - NUMBER OF COPIES (ALPHA)                  */
/*          &FORMS      - FORMS TYPE                                */
/*          &SUBMIT     - SUBMIT JOB (Y OR N)                       */
/*          &COMP       - COMPANY NUMBER                            */
/*          &FDATE      - FROM DATE                                 */
/*          &TDATE      - THROUGH DATE                              */
/*                                                                  */
/********************************************************************/

 OMOMLCLP:   PGM        PARM(&PGM &DESC)

/* PARAMETERS RECEIVED                                              */
        DCL    &PGM       *CHAR   10             /*  PGM NAME       */
        DCL    &DESC      *CHAR   30             /*  PGM DESC.      */

/* PARAMETERS USED                                                  */

        DCL    &COMP      *DEC    (3 0)          /*  COMPANY #      */
        DCL    &COMPL     *DEC                   /*  FOR PASSING    */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &COPIES    *DEC    (3 0)          /*  # OF COPIES    */
        DCL    &COPIESL   *DEC                   /*  FOR PASSING    */
        DCL    &DESCC     *CHAR   30             /*  CENTERED DESC  */
        DCL    &OUTQ      *CHAR   10             /*  OUTPUT QUEUE   */
        DCL    &FORMS     *CHAR   10             /*  FORMS NAME     */
        DCL    &SUBMIT    *CHAR   1              /*  SUBMIT JOB     */
        DCL    &JOBQ      *CHAR  10              /*  JOB QUEUE      */
        DCL    &X         *DEC    (2 0) VALUE(30) /* OFFSET CNTR    */
        DCL    &L         *DEC    (2 0)          /*  STRING LENGTH  */
        DCL    &RETURN    *CHAR   7              /*  RETURN CODE    */
        DCL    &MSG       *CHAR   512
        DCL    &MSGID     *CHAR   7
        DCL    &MSGL      *DEC    (5 0)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   RETRIEVE USER PROFILE TO DETERMINE OUTPUT QUEUE FOR DEFAULT.   */
/********************************************************************/

             RTVUSRPRF  OUTQ(&OUTQ)
             IF         COND(&OUTQ *EQ '*DEV') THEN(DO)
             RTVUSRPRF  PRTDEV(&OUTQ)
             ENDDO
             IF         COND(&OUTQ *EQ '*SYSVAL') THEN(DO)
             RTVSYSVAL  SYSVAL(QPRTDEV) RTNVAR(&OUTQ)
             ENDDO

/********************************************************************/
/*   CENTER THE PROGRAM DESCRIPTION STRING                          */
/********************************************************************/

             IF         COND(&DESC *EQ ' ') THEN(GOTO +
                          CMDLBL(JOBCONTROL))

 SCAN:       IF         COND(%SUBSTRING(&DESC &X 1) *NE ' ') THEN(DO)
             CHGVAR     VAR(&X) VALUE(30-&X)
             CHGVAR     VAR(&L) VALUE(30-&X)
             CHGVAR     VAR(&X) VALUE(&X /2)
             IF         COND(&X *GT 0) THEN(DO)
             CHGVAR     VAR(&DESCC) VALUE(%SST(&DESCC 1 &X) *CAT +
                          %SST(&DESC 1 &L))
                          ENDDO
                          ELSE DO
             CHGVAR     VAR(&DESCC) VALUE(&DESC)
                          ENDDO
             GOTO       CMDLBL(JOBCONTROL)
             ENDDO
             IF         COND(&X *EQ 1) THEN(GOTO CMDLBL(JOBCONTROL))
             CHGVAR     VAR(&X) VALUE(&X-1)
             GOTO       CMDLBL(SCAN) /* read more */

/********************************************************************/
/*   CALL JOB CONTROL PROGRAM TO PROMPT FOR PRINT INFORMATION.      */
/********************************************************************/

 JOBCONTROL: CALL       PGM(OMNGPVR) PARM(&RETURN &DESCC &COMP +
                          &FORMS &COPIES &OUTQ &SUBMIT +
                          &FDATE &TDATE)

/********************************************************************/
/*   CHECK FOR END OF JOB REQUEST.                                  */
/********************************************************************/

 ENDJOB:     IF         COND(&RETURN *EQ Y2U9999) THEN(DO)
             RCVMSG     MSGTYPE(*LAST) MSG(&MSG) MSGID(&MSGID)
             IF         (&MSGID = 'USR0810') THEN(DO)
             SNDPGMMSG  MSGID(CPF9898) +
                        MSGF(QCPFMSG) +
                        MSGDTA(&MSG) +
                        TOPGMQ(*PRV *) +
                        MSGTYPE(*COMP)
             ENDDO
             GOTO CMDLBL(END)
             ENDDO

/********************************************************************/
/*   DETERMINE IF JOB IS SUBMITTED TO BATCH OR INTERACTIVE.         */
/********************************************************************/

             CHGVAR     VAR(&COMPL) VALUE(&COMP) /* Change for +
                          passing */
             CHGVAR     VAR(&FDATEL) VALUE(&FDATE) /* Change for +
                          passing */
             CHGVAR     VAR(&TDATEL) VALUE(&TDATE) /* Change for +
                          passing */
             CHGVAR     VAR(&COPIESL) VALUE(&COPIES) /* change for +
                          passing */


 SUBMIT:     IF         COND(&SUBMIT *EQ Y) THEN(DO) /* submit report +
                          to batch */

             SBMJOB     CMD(CALL PGM(OMOMPCLP) PARM(&OUTQ &COPIESL +
                          &FORMS &PGM &COMPL &FDATEL &TDATEL)) +
                          JOB(&PGM)
             ENDDO

             ELSE       CMD(DO)

 INTERACT:   CALL       PGM(OMOMPCLP) PARM(&OUTQ &COPIESL &FORMS +
                          &PGM &COMPL &FDATEL &TDATEL)
             ENDDO

             GOTO       CMDLBL(END)

/********************************************************************/
/*    RECEIVE MESSAGES                                              */
/********************************************************************/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

