     A*%%TS  SD  20030812  170607  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD496
     A*  TITLE:         SPECIFY OPTIONS FOR PERIOD CHANGE-IN-INVENTORY REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       07/07/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP496R1
     A*%%TS  SD  20030812  170607  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 12'SPECIFY OPTIONS FOR PERIOD CHANGE--
     A                                      IN-INVENTORY REPORT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Accounting year ....'
     A N99        D1ACYR    R        B  4 25REFFLD(@@CDYR)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                  5  2'Accounting period ..'
     A N99        D1ACPE    R        B  5 25REFFLD(@@CDWK)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                  6  2'Groups to include ..'
     A                                  6 23'?'
     A            D1INCD         1A  B  6 25
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A            D1INDS        50A  O  6 30
     A                                  8  2'Business office .......'
     A            D1FSBO    R        B  8 28REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1BODS    R        O  8 38REFFLD(HSRREF/BOBODS *LIBL/HSPREF)
     A                                  8 69'(Blank=all)'
     A                                  9  2'Production type .......'
     A                                  9 26'?'
     A            D1PTCD    R        B  9 28REFFLD(PTPTCD)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1PTDS    R        O  9 38REFFLD(PTPTDS)
     A                                  9 69'(Blank=all)'
     A                                 10  2'Production phase ......'
     A                                 10 26'?'
     A            D1PPCD    R        B 10 28REFFLD(PPPPCD)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34
     AO 54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1PPDS    R        O 10 38REFFLD(PPPPDS)
     A                                 10 69'(Blank=all)'
     A                                 11  2'Cell ..................'
     A            D1CELL    R        B 11 28REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 11 69'(Blank=all)'
     A                                 12  2'Company relationship ..'
     A                                 12 26'?'
     A            D1CRCD    R        B 12 28REFFLD(HSRREF/@@CRCD *LIBL/HSPREF)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1CRDS    R        O 12 38REFFLD(CRREC/CRCRDS *LIBL/HSP012)
     A                                 12 69'(Blank=all)'
     A                                 13  2'Farm site .............'
     A                                 13 26'?'
     A            D1FSCD    R        B 13 28REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1FSNM    R        O 13 38REFFLD(FSFSNM)
     A                                 13 69'(Blank=all)'
     A                                 14  2'Group type ............'
     A                                 14 26'?'
     A            D1GTCD    R        B 14 28REFFLD(@@GTCD)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38
     AO 58                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1GTDS    R        O 14 38REFFLD(GTGTDS)
     A                                 14 69'(Blank=all)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP496K1
     A*%%TS  SD  20030626  073932  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP496ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP496EC                   SFLCTL(HP496ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
