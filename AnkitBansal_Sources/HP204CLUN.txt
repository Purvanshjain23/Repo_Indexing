/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP204CLUN                                     */
/*  PROGRAM NAME:    BUILD JDE UPLOAD FILE: HPS UNITS              */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     10/12/01                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE DRIVER CL       */
/*                   HP404CL.                                      */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 02/28/02  LEANNE FEDOR                                          */
/*           REMOVED PIG DAY LOGIC. PIG DAYS HAS BEEN MOVED TO A   */
/*           SEPARATE FUNCTION.                                    */
/*                                                                 */
/* 07/25/02  LEANNE FEDOR                                          */
/*           ADDED SEMEN DOSES LOGIC.                              */
/*                                                                 */
/* 07/31/02  LEANNE FEDOR                                          */
/*           REMOVED SEMEN DOSES LOGIC--IT IS NOT A SEPARATE       */
/*           FUNCTION.                                             */
/*                                                                 */
/* 01/06/10  LEANNE RAMSEY                                         */
/*           TWO INSTALLS:                                         */
/*           PUT IN TEMP CHANGES TO HSP092 LOGIC TRYING TO GET     */
/*           USERS THRU A WEEK 53 PROBLEM.                         */
/*                                                                 */
/*           TOOK OUT THE TEMP CHANGES; THEY DIDN'T WORK.          */
/*           I WILL ANALYZE/FIX THIS LATER. FOR NOW, THE USERS     */
/*           WILL ENTER THEIR GILT/SOW ENDING INVENTORY MANUALLY   */
/*           INTO THE JDE BATCH.                                   */
/*                                                                 */
/* 01/08/10  LEANNE RAMSEY                                         */
/*           REMOVED UNUSED VARIABLES THAT HAD BEEN ADDED ON       */
/*           01/06/10.                                             */
/*                                                                 */
/* 10/11/18  Danny Nguyen (S13682)                                 */
/*           Retrieve Company Code from LDA. Change OPNQRYF logic  */
/*           for HSL034K & HSL018I to select by Company. Increase  */
/*           &QDATA from 140 to 163.                               */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(163)

             DCL        VAR(&LDACYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDACPE) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)

/* TWO FOLLOWING 2 FIELDS (CELL AND REPORT FLAG) ARE ONLY REQUIRED   */
/* FOR PARMS ON A CALL TO A WORKFILE BUILD PROGRAM. WE WILL HARDCODE */
/* SINCE THEY ARE NOT LDA FIELDS FOR OUR UPLOAD FUNCTION.            */

             DCL        VAR(&XXRPFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&XXCELL) TYPE(*DEC) LEN(2)
             CHGVAR     VAR(&XXRPFL) VALUE(' ')
             CHGVAR     VAR(&XXCELL) VALUE(00)

             CHGVAR     VAR(&LDACYR) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDACPE) VALUE(%SST(*LDA 5 2))
             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 25 3))


/*********************************************************************/
/* SOME OF THE UNITS VALUES WE WILL ULTIMATELY NEED MUST MATCH       */
/* THE VALUES ON EXISTING REPORTS THAT RUN OVER WORKFILES.  SO, WE   */
/* USE THE EXISTING PROGRAMS TO BUILD WORKFILES FOR OUR UPLOAD LOGIC.*/
/*********************************************************************/

/*-------------------------------------------------------------------*/
/* BUILD AN INVENTORY BALANCE WORKFILE                               */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP369) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL369A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL369B) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL369C) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*  ALWAYS OMIT "BGF" PRODUCTION PHASE GROUPS                    */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('HGPPCD +
                          *NE "BGF  "')

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED               */

             CHGVAR     VAR(%SST(&QDATA 20 20)) VALUE('*AND HGGSCD +
                          *NE "VD"')

/*  SELECT GROUPS WHERE OPENED DATE IS NOT ZERO AND LESS THAN PERIOD END*/

             CHGVAR     VAR(%SST(&QDATA 41 15)) VALUE('*AND HGOPDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 57 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 66 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 82 8)) VALUE(&LDEPDT)

/*  SELECT GROUPS WHERE DISPOSED DATE IS ZERO OR GREATER THAN PERIOD */
/*  END DATE                                                         */

             CHGVAR     VAR(%SST(&QDATA 91 16)) VALUE('*AND (HGDSDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 108 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 117 14)) VALUE('*OR HGDSDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 132 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 140 1)) VALUE(')')

/*  S13682-ALWAYS SELECT BY COMPANY CODE.                        */

             CHGVAR     VAR(%SST(&QDATA 142 17)) VALUE('*AND HGCOCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 159 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA 162 1)) VALUE('"')

             OVRDBF     FILE(HSL034K) SHARE(*YES)
             OPNQRYF    FILE((HSL034K)) QRYSLT(&QDATA)

             CALL       PGM(HP369) PARM(&XXRPFL &XXCELL &LDEPDT)

             CLOF       OPNID(HSL034K)
             DLTOVR     FILE(*ALL)


/*********************************************************************/
/*  SETUP/RUN OPEN QUERIES FOR FILES OTHER THAN THE WORKFILES ABOVE  */
/*********************************************************************/

/* ALWAYS SELECT ONLY FARMS THAT HAVE A COST CENTER VALUE         */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 13)) VALUE('FSCJD *NE " "')

/*  S13682-ALWAYS SELECT BY COMPANY CODE.                        */

             CHGVAR     VAR(%SST(&QDATA 15 17)) VALUE('*AND FSCOCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 32 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA 35 1)) VALUE('"')

             OVRDBF     FILE(HSL018I) SHARE(*YES)
             OPNQRYF    FILE((HSL018I)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/* ALWAYS SELECT ONLY BGF WEEKLY PRODUCTION RECORDS FOR THE      */
/* ACCOUNTING YEAR/PERIOD                                         */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BPACYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDACYR)
             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND BPACPE +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 33 2)) VALUE(&LDACPE)

             OVRDBF     FILE(HSL092B) SHARE(*YES)
             OPNQRYF    FILE((HSL092B)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/* SELECT ALL TRANSFER RECORDS WITH A RECEIVED DATE IN RANGE */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

             OVRDBF     FILE(HSL074H) SHARE(*YES)
             OPNQRYF    FILE((HSL074H)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/* SELECT ALL SALES RECORDS WITH A RECEIVED DATE IN RANGE */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

             OVRDBF     FILE(HSL084C) SHARE(*YES)
             OPNQRYF    FILE((HSL084C)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/* SELECT ALL KILLED/DEAD RECORDS WITH A DATE IN RANGE */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('KDKDDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND KDKDDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

             OVRDBF     FILE(HSL068C) SHARE(*YES)
             OPNQRYF    FILE((HSL068C)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*  SELECT ALL INVENTORY ADJUSTMENT RECORDS WITHIN THE DATE RANGE        */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('IAIADT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND IAIADT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

             OVRDBF     FILE(HSL069D) SHARE(*YES)
             OPNQRYF    FILE((HSL069D)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*********************************************************************/
/*  CALL PROGRAM TO BUILD INTERFACE FILE OF HPS UNITS                */
/*********************************************************************/

             CALL       PGM(HP204UN)

/*******************************************************************/
/*  DELETE FILE OVERRIDES                                          */
/*******************************************************************/

             CLOF       OPNID(HSL018I)
             DLTOVR     FILE(HSL018I)

             CLOF       OPNID(HSL068C)
             DLTOVR     FILE(HSL068C)

             CLOF       OPNID(HSL069D)
             DLTOVR     FILE(HSL069D)

             CLOF       OPNID(HSL074H)
             DLTOVR     FILE(HSL074H)

             CLOF       OPNID(HSL084C)
             DLTOVR     FILE(HSL084C)

             CLOF       OPNID(HSL092B)
             DLTOVR     FILE(HSL092B)

             ENDPGM

