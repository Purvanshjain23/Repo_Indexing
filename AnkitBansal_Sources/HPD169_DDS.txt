     A*%%TS  SD  20030107  094402  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD169
     A*  TITLE:         CREATE SCHEDULED TRANSFERS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/11/99
     A*
     A*****************************************************************
     A* INDICATORS
     A* 90  PROTECT ALL FIELDS. USER NEEDS TO SEE THE SYSTEM ASSIGNED
     A*     MOVEMENT NUMBER FOR EACH DETAIL LINE. SO, AFTER THEY ACCEPT
     A*     WE MUST REDISPLAY THE SCREEN WITH ALL FIELDS PROTECTED.
     A* 98  MAKE FIELDS NON-DISPLAY
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A                                      CA12(12 'PREVIOUS')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP169S1                   SFL
     A*%%TS  SD  20021210  163642  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDORHGSN  R        H      REFFLD(HGHGSN)
     A            HDORFSBO  R        H      REFFLD(HGFSBO)
     A            HDORPPCD  R        H      REFFLD(HGPPCD)
     A            HDORPTCD  R        H      REFFLD(HGPTCD)
     A            HDDNHGSN  R        H      REFFLD(HGHGSN)
     A            HDDNFSBO  R        H      REFFLD(HGFSBO)
     A            HDDNPPCD  R        H      REFFLD(HGPPCD)
     A            HDDNPTCD  R        H      REFFLD(HGPTCD)
     A            DFORFSCD  R        B 10  2REFFLD(@@FSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N31N90                               DSPATR(HI)
     A N31N90                               DSPATR(UL)
     A            DFORBLCD  R        B 10  9REFFLD(@@BLCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N35N90                               DSPATR(HI)
     A N35N90                               DSPATR(UL)
     A            DFORRMCD  R        B 10 15REFFLD(@@RMCD)
     A  40
     AO 50                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N40N90                               DSPATR(HI)
     A N40N90                               DSPATR(UL)
     A            DFORHGCD  R        B 10 21REFFLD(HGHGCD)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N34N90                               DSPATR(HI)
     A N34N90                               DSPATR(UL)
     A            DFSCHD    R        B 10 29REFFLD(MDSCHD)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N38N90                               DSPATR(HI)
     A N38N90                               DSPATR(UL)
     A            DFORGSCD  R        O 10 37REFFLD(HGGSCD)
     A            DFDNFSCD  R        B 10 41REFFLD(@@FSCD)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N32N90                               DSPATR(HI)
     A N32N90                               DSPATR(UL)
     A            DFDNBLCD  R        B 10 48REFFLD(@@BLCD)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N37N90                               DSPATR(HI)
     A N37N90                               DSPATR(UL)
     A            DFDNRMCD  R        B 10 54REFFLD(@@RMCD)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N48N90                               DSPATR(HI)
     A N48N90                               DSPATR(UL)
     A            DFDNHGCD  R     A  B 10 60REFFLD(HGHGCD)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N36N90                               DSPATR(HI)
     A N36N90                               DSPATR(UL)
     A            DFDNGSCD  R        O 10 68REFFLD(HGGSCD)
     A            DFMVSN    R        O 10 71REFFLD(HSRREF/@@MVSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            DFORFSNM  R        O 11  2REFFLD(FSFSNM)
     A                                 11 28'Grp typ:'
     A            DFORGTCD  R        O 11 37REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFDNFSNM  R        O 11 41REFFLD(FSFSNM)
     A                                 11 68'Grp typ:'
     A            DFDNGTCD  R        O 11 77REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP169C1                   SFLCTL(HP169S1)
     A*%%TS  SD  20030107  094402  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(48)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 24'CREATE SCHEDULED TRANSFERS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Scheduled ship date ...'
     A            C1MDY     R     S  B  4 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N39                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A                                  5  2'Trucker ...............'
     A N90                              5 26'?'
     A            C1TICD    R        B  5 28REFFLD(MHTICD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A            C1TINM    R        O  5 38REFFLD(TITINM)
     A                                  7  2'-------------- ORIGIN -------------
     A                                      ---'
     A                                      DSPATR(HI)
     A                                  7 41'--------------- DESTINATION -------
     A                                      -----'
     A                                      DSPATR(HI)
     A N90                              8  4'?'
     A N90                              8 11'?'
     A N90                              8 16'?'
     A N90                              8 23'?'
     A                                  8 30'Sched'
     A N90                              8 43'?'
     A N90                              8 49'?'
     A N90                              8 55'?'
     A N90                              8 62'?'
     A                                  8 71'Movement'
     A                                  9  2'Farm'
     A                                  9  9'Bldg'
     A                                  9 15'Room'
     A                                  9 21'Group'
     A                                  9 31'Head'
     A                                  9 37'St'
     A                                  9 42'Farm'
     A                                  9 48'Bldg'
     A                                  9 54'Room'
     A                                  9 60'Group'
     A                                  9 68'St'
     A                                  9 72'Number'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP169K1
     A*%%TS  SD  19991111  095829  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A N90                             23 34'F9=Accept'
     A                                 23 45'F12=Previous'
     A                                 23 59'F13=Fold/Unfold Name'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP169ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP169EC                   SFLCTL(HP169ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
