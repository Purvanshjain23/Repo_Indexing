/***************************************************************** */
/*                                                                 */
/*  ENVIRONMENT -----> PORK                                        */
/*  SYSTEM ----------> ACCOUNTS RECEIVABLE SYSTEM                  */
/*  PROGRAM NUMBER --> AR362CL                                     */
/*  PROGRAM NAME ----> FOREIGN CORPORATE TAX SAVINGS               */
/*                     YEARLY TAX ANAYLSIS FOR FOREIGN SALES       */
/*                     PC FILES                                    */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 10/23/97                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> GENERATE ONCE-A-YEAR FILES TO DOWNLOAD TO   */
/*                     A PC FOR TAX.                               */
/*                                                                 */
/***************************************************************** */
/* MODIFICATIONS                                                   */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(60)
             DCL        VAR(&LDCCYY) TYPE(*DEC) LEN(4)
             DCL        VAR(&LD0YMD) TYPE(*DEC) LEN(7)


             CHGVAR     VAR(&LDCCYY) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LD0YMD) VALUE(%SST(*LDA 5 7))


/*-----------------------------------------------------------------*/
/* CLEAR PHYSICAL FILES USED FOR PC DOWNLOAD                       */
/*-----------------------------------------------------------------*/

             CLRPFM     FILE(OEIP362E)
             CLRPFM     FILE(OEIP362A)

/*-----------------------------------------------------------------*/
/*  SET UP QUERY USED IN CREATING THE 'EXPORT' FILE ONLY           */
/*-----------------------------------------------------------------*/

/* ONLY SELECT RECORDS WITH AN ACCOUNTING YEAR EQUAL TO SELECTED YEAR */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('F4ESNB *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDCCYY)


/* ONLY SELECT RECORDS WITH AN EXPORT STATUS OF YES                   */

             CHGVAR     VAR(%SST(&QDATA 17 19)) VALUE('*AND F4U2ST +
                          *EQ "Y"')

/* ONLY SELECT RECORDS WITH AN INVOICE SUFFIX OF 'IN'            */

             CHGVAR     VAR(%SST(&QDATA 37 20)) VALUE('*AND F4CEST +
                          *EQ "IN"')


/*------------------------------------------------------------------*/
/*  RUN QUERY OVER OMS SALES HISTORY FILE                           */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(OMHSTPLA) SHARE(*YES)
             OPNQRYF    FILE((OMHSTPLA)) QRYSLT(&QDATA) +
                          KEYFLD((F4S5NB) (F4CONB)) SEQONLY(*YES)



/*------------------------------------------------------------------*/
/* CALL PROGRAM TO CREATE EXPORT ONLY FILE                          */
/*------------------------------------------------------------------*/

             CALL AR362E


             CLOF       OPNID(OMHSTPLA)
             DLTOVR     FILE(*ALL)


/*-----------------------------------------------------------------*/
/*  SET UP QUERY USED IN CREATING THE FILE OF ALL SALES            */
/*-----------------------------------------------------------------*/

/* ONLY SELECT RECORDS WITH AN ACCOUNTING YEAR EQUAL TO SELECTED YEAR */

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('F4ESNB *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDCCYY)


/* ONLY SELECT RECORDS WITH AN INVOICE SUFFIX OF 'IN'            */

             CHGVAR     VAR(%SST(&QDATA 17 20)) VALUE('*AND F4CEST +
                          *EQ "IN"')


/*------------------------------------------------------------------*/
/*  RUN QUERY OVER OMS SALES HISTORY FILE                           */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(OMHSTPLA) SHARE(*YES)
             OPNQRYF    FILE((OMHSTPLA)) QRYSLT(&QDATA) +
                          KEYFLD((F4S5NB)) SEQONLY(*YES)



/*------------------------------------------------------------------*/
/*  CALL PROGRAM TO CREATE FILE OF EXPORT AND DOMESTIC SALES        */
/*------------------------------------------------------------------*/

             CALL       PGM(AR362A)

             CLOF       OPNID(OMHSTPLA)
             DLTOVR     FILE(*ALL)

             ENDPGM

