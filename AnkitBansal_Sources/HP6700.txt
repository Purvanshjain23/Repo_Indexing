      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6700
      * TITLE:       Budget Template Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/09/04
      *
      *            This listing is submitted from HP4700-Work With Budget Templates.
      *
      *            There are 3 sections:
      *                  1) Budget Template Header section
      *                  2) Budgeted Items section
      *                  3) Linked Farms Section
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      * 11/17/04  LeAnne Fedor
      *           Changed layout of 'Budgeted Items Section', adding:
      *                   1) budget unit
      *                   2) cost/unit amount (2 decimal positions only)
      *                   3) cost amount      (whole dollars, no decimals)
      *                   4) a Total line with Total Cost Amount and Cost per Head
      *           We also truncated Budget Item to 10 characters, and Quantity to 1 decimal
      *           position.
      *
      *           Changed layout of 'Linked Farms Section', adding:
      *                   1) production phase
      *                   2) cell
      *                   3) type of farm
      *
      * 11/22/04  LeAnne Fedor
      *           Added 'Hog Capacity' to Section 3 for each Farm Detail Line.
      *
      * 02/07/05  LeAnne Fedor
      *           Moved everything to the right 10 characters---so, the users can
      *           punch holes and put this in a binder.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp101    if   e           k disk
      *    Type of farm
      *
      *
     Fhsl181d   if   e           k disk
      *    Items
      *
      *
     Fhsp183    if   e           k disk
      *    Creation schedules
      *
      *
     Fhsp184    if   e           k disk
      *    Budget availability
      *
      *
     Fhsp185    if   e           k disk
      *    Budget template header
      *
      *
     Fhsp186    if   e           k disk
      *    Budget template detail
      *
      *
     Fhsp187    if   e           k disk
      *    Budget template/farm links
      *
      *
     Fhsl188b   if   e           k disk
      *    Farm budget header
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     d rtime           s              6  0
      *
      *
      * Workfields
      *
     d count           s              3  0
     d wksize          s              6  0
     d wkalphsize      s              6
     D nbrchar         s              1  0
     D start           s              1  0
     D wkvol           s             15  4
     D wkcpuam         s             15  4
      *
      *
      * Parms
      *
     D xxfscd          s                   like(fsfscd)
     D xxfssqft        s              7p 0
      *
      *
      * Print fields
      *
     D l1ppds          s                   like(ppppds)
     D l1tfds          s                   like(tftfds)
     D l1bads          s                   like(babads)
     D l1csds          s                   like(cscsds)
      *
     D l2bgit          s             10
     D l2desc          s                   like(imitds)
     D l2btqt          s              8  1
     D l2unit          s                   like(imuom)
     D l2cpuam         s              7  2
     D l2costam        s             12  0
      *
     D t2costam        s             15  0
     D t2costhd        s              7  2
      *
     D l3fsnm          s                   like(fsfsnm)
     D l3ppcd          s                   like(fsppcd)
     D l3tfcd          s                   like(fstfcd)
     D l3cell          s                   like(fscell)
     D l3cphd          s              5  0
     D l3inafl         s              3
     D l3actfl         s              3
     D l3clofl         s              3
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldbtcd                 2      7
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Retrieve/print the Budget Template Header info.
      *
     C     ldbtcd        chain     hsp185                             92
     C                   if        *in92 = *off                                 If header hit
     C                   exsr      $desc
     C                   except    h1hdr
     C                   except    l1dtl
      *
      * Retrieve/print the Budget Template Detail records.
      *
     C                   exsr      $items
      *
      * Retrieve/print the Linked Farms
      *
     C                   exsr      $farms
      *
      * No template header record was found
      *
     C                   else
     C                   except    h1hdr
     C                   except    nodata
     C                   endif                                                  If header hit
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Retrieve descriptions for Budget Template Header values
      *---------------------------------------------------------------
      *
     C     $desc         begsr
      *
      * Budget Availability
      *
     C     thbacd        chain     hsp184                             92
     C                   if        *in92 = *off
     C                   movel     babads        l1bads
     C                   else
     C                   eval      l1bads = 'Unknown'
     C                   endif
      *
      * Creation Schedule
      *
     C     thcscd        chain     hsp183                             92
     C                   if        *in92 = *off
     C                   movel     cscsds        l1csds
     C                   else
     C                   eval      l1csds = 'Unknown'
     C                   endif
      *
      * Production Phase
      *
     C                   if        thppcd <> *blank
     C     thppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   movel     ppppds        l1ppds
     C                   else
     C                   eval      l1ppds = 'Unknown'
     C                   endif
     C                   endif
      *
      * Type of Farm
      *
     C                   if        thtfcd <> *blank
     C     thtfcd        chain     hsp101                             92
     C                   if        *in92 = *off
     C                   movel     tftfds        l1tfds
     C                   else
     C                   eval      l1tfds = 'Unknown'
     C                   endif
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve/print Budget Template Detail (budgeted items)
      *---------------------------------------------------------------
      *
     C     $items        begsr
      *
      * Print Budgeted Items Heading
      *
     C                   except    h2hdr
      *
      * Prepare to read/print Budget Template detail records
      *
     C                   move      no            prtfl
     C     ldbtcd        setll     hsp186
      *
     C                   dou       *in91 = *on                                  Do detail
     C     ldbtcd        reade     hsp186                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Set up print fields.
      *
     C                   movel     tdbgit        l2bgit
     C                   z-add     tdbtqt        l2btqt
      *
      * Retrieve the first record in the Item Master for this Budget Item.
      *
     C     tdbgit        chain     hsl181d                            92
     C                   if        *in92 = *on                                  If no hit
     C                   eval      l2desc = 'Unknown'
     C                   else
      *
      * Set the 'budget units'---the units that this Budget Item is budgeted in
      * based on Unit of Weight vs Unit of Measure.
      *
     C                   if        imuow <> *blank
     C                   movel     imuow         l2unit
     C                   else
     C                   movel     imuom         l2unit
     C                   endif
      *
      *
      * Set up a 'description'
      *
     C                   if        imitcd = imbgit
     C                   eval      l2desc = imitds
     C                   else
     C                   eval      l2desc = imbgit
     C                   endif
      *
      * Determine a Cost Per Unit
      *
     C                   exsr      $cpuam
     C                   endif                                                  If no hit
      *
      * Calculate Cost Amount and accumulate for total line.
      *
     C     l2btqt        mult(h)   l2cpuam       l2costam
     C                   add       l2costam      t2costam
      *
      * Print
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   except    h2hdr
     C                   endif
      *
     C                   except    l2dtl
     C                   move      yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
      * Print either the 'no items' message or the 'total' line.
      *
     C                   if        prtfl = no
     C                   except    noitems
     C                   else
     C                   exsr      $t2tot
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Calculate Cost Per Unit Amount
      *---------------------------------------------------------------
      *
     C     $cpuam        begsr
      *
     C                   z-add     0             wkcpuam
     C                   z-add     0             wkvol
     C                   z-add     0             count
      *
      * Process all active 'Item' records for this 'Budget Item' from the Item Master
      * accumulating Volume and Cost per Unit.
      *
     C     tdbgit        setll     hsl181d
      *
     C                   dou       *in90 = *on                                  Do loop
     C     tdbgit        reade     hsl181d                                90
     C                   if        *in90 = *off and imaist = 'A'                If not EOF
     C                   add       1             count
     C                   add       imvol         wkvol
     C                   add       imcpuam       wkcpuam
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
      * Calculate Cost per Budget Unit.
      *
     C                   select
     C                   when      wkvol <> 0
     C     wkcpuam       div(h)    wkvol         l2cpuam
      *
     C                   when      count <> 0
     C     wkcpuam       div(h)    count         l2cpuam
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Total line for Budget Item Detail section
      *---------------------------------------------------------------
      *
     C     $t2tot        begsr
      *
      * Before printing the Total line, you have to calculate a Cost Per Head value.
      *
      * We think the users are keying the 'number of head' into the 6A field 'Size'.
      * So, convert the value in the alphanumeric 'Size' field into a numeric value.
      *
     C                   z-add     0             wksize
     C                   move      '000000'      wkalphsize
      *
     C                   if        thsize <> *blank                             If not blank
      *
     C                   eval      thsize = %triml(thsize)
     C     ' '           checkr    thsize        nbrchar
      *
     C                   eval      start = 6 - nbrchar + 1
      *
     C                   eval      %subst(wkalphsize:start:nbrchar) =
     C                             %subst(thsize:1:nbrchar)
      *
      * If the 'size' value is numeric, then move it to a numeric workfield.
      *
     C                   testn                   wkalphsize           92
     C                   if        *in92 = *on
     C                   move      wkalphsize    wksize
     C                   endif
     C                   endif                                                  If not blank
      *
      * If 'size' is not zero, calculate a 'cost per head' value.
      *
     C                   if        wksize <> 0
     C     t2costam      div(h)    wksize        t2costhd
     C                   endif
      *
      * Print total line
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    t2tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print farms currently linked to Budget Template.
      *---------------------------------------------------------------
      *
     C     $farms        begsr
      *
      * Print Linked Farms Heading
      *
     C                   except    h3hdr
      *
      * Prepare to read/print the Linked Farms file.
      *
     C                   move      no            prtfl
     C     ldbtcd        setll     hsp187
      *
     C                   dou       *in91 = *on                                  Do farms
     C     ldbtcd        reade     hsp187                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Retrieve Farm Info
      *
     C     tlfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
     C                   movel     fsfsnm        l3fsnm
     C                   move      fsppcd        l3ppcd
     C                   move      fstfcd        l3tfcd
     C                   z-add     fscell        l3cell
     C                   exsr      $cphd
     C                   else
     C                   eval      l3fsnm = 'Unknown'
     C                   endif                                                  If farm hit
      *
      * Determine if the Farm has any Inactive/active/closed budgets.
      *
     C     key01         setll     hsl188b
      *
     C                   dou       *in93 = *on or                               Do budgets
     C                             (l3actfl = 'Yes' and l3inafl = 'Yes'
     C                              and l3clofl = 'Yes')
     C     key01         reade     hsl188b                                93
     C                   if        *in93 = *off                                 If budgets
      *
     C                   select
     C                   when      jhfbscd = 'A'
     C                   eval      l3actfl = 'Yes'
      *
     C                   when      jhfbscd = 'I'
     C                   eval      l3inafl = 'Yes'
      *
     C                   when      jhfbscd = 'C'
     C                   eval      l3clofl = 'Yes'
     C                   endsl
      *
     C                   endif                                                  If budgets
     C                   enddo                                                  Do budgets
      *
      * Print
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   except    h3hdr
     C                   endif
      *
     C                   except    l3dtl
     C                   move      yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do farms
      *
     C                   if        prtfl = no
     C                   except    nofarms
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Calculate 'Capacity Head' for a Farm Site
      *---------------------------------------------------------------
      *
     C     $cphd         begsr
      *
      * If the farm as a value for Target Square Feet per Pig
      *
     C                   if        fshdsqft <> 0                                If target
      *
      * Call the generic program to return the 'total square feet' for the farm.
      *
     C                   call      'HP8003'
     C                   parm      fsfscd        xxfscd
     C                   parm      0             xxfssqft
      *
      * Calculate Capacity Head as:
      *            Total Square Feet / Target Square Feet per Pig
      *
     C     xxfssqft      div(h)    fshdsqft      l3cphd
     C                   endif                                                  If target
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldbtcd
     C                   kfld                    tlfscd
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               20
     O                                           70 'HOG PRODUCTION SYSTEM'
     O                                          121 'DATE'
     O                       udate         y    131
      *
     O          e            h1hdr          1
     O                       sdusr               20
     O                                           71 'BUDGET TEMPLATE LISTING'
     O                                          121 'TIME'
     O                       rtime              131 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          121 'PAGE'
     O                       page          z    131
      *
      *
     O          e            h1hdr          2
     O                                           26 'Budget template:'
     O                       ldbtcd              34
     O                       thbtds              61
      *
      *
      *-------------------------------------------------------------
      * Headings for the Budget Template Detail info
      *-------------------------------------------------------------
      *
     O          e            h2hdr       2  1
     O                                           33 'Budgeted Items Section:'
      *
     O          e            h2hdr          1
     O                                           46 'Budget'
     O                                           61 'Cost/Unit'
     O                                           78 'Cost'
      *
     O          e            h2hdr          1
     O                                           21 'Budget Item'
     O                                           34 'Quantity'
     O                                           44 'Unit'
     O                                           61 'Amount'
     O                                           78 'Amount'
      *
     O          e            h2hdr          1
     O                                           21 '-----------'
     O                                           34 '-----------'
     O                                           48 '--------'
     O                                           61 '---------'
     O                                           78 '---------------'
      *
      *
      *
      *-------------------------------------------------------------
      * Headings for Linked Farms
      *-------------------------------------------------------------
      *
     O          e            h3hdr       2  2
     O                                           31 'Linked Farms Section:'
      *
     O          e            h3hdr          1
     O                                           25 'Farm'
     O                                           64 'Production'
     O                                           76 'Farm'
     O                                           87 'Hog'
     O                                           97 'Inactive'
     O                                          107 'Active'
     O                                          117 'Closed'
      *
     O          e            h3hdr          1
     O                                           25 'Site'
     O                                           31 'Name'
     O                                           61 'Phase'
     O                                           70 'Cell'
     O                                           76 'Type'
     O                                           87 'Capacity'
     O                                           97 'Budgets'
     O                                          107 'Budgets'
     O                                          117 'Budgets'
      *
     O          e            h3hdr          1
     O                                           25 '-----'
     O                                           52 '-------------------------'
     O                                           64 '----------'
     O                                           70 '----'
     O                                           77 '-----'
     O                                           87 '-------'
     O                                           97 '--------'
     O                                          107 '-------'
     O                                          117 '-------'
      *
      *-------------------------------------------------------------
      * Detail lines for Budget Template Header info
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  1
     O                                           46 'Budget template weeks ....'
     O                       thbtwk        k     54
      *
      *
     O          e            l1dtl       0  1
     O                                           46 'Budget availability code .'
     O                       thbacd              48
     O                       l1bads              75
      *
      *
     O          e            l1dtl       0  1
     O                                           46 'Creation schedule code ...'
     O                       thcscd              48
     O                       l1csds              75
      *
      *
     O          e            l1dtl       0  1
     O                                           46 'Production phase code ....'
     O                       thppcd              52
     O                       l1ppds              85
      *
      *
     O          e            l1dtl       0  1
     O                                           46 'Type of farm code ........'
     O                       thtfcd              52
     O                       l1tfds              80
      *
      *
     O          e            l1dtl       0  1
     O                                           46 'Cell .....................'
     O                       thcell        k     54
      *
      *
     O          e            l1dtl       0  1
     O                                           46 'Size .....................'
     O                       thsize              53
      *
      *
     O          e            l1dtl       0  1
     O                                           46 'Active/Inactive status ...'
     O                       thaist              48
      *
      *-------------------------------------------------------------
      * Detail line of template detail records
      *-------------------------------------------------------------
      *
     O          e            l2dtl          1
     O                       l2bgit         b    20
     O                       l2btqt        kb    35
     O                       l2unit         b    48
     O                       l2cpuam       kb    62
     O                       l2costam      kb    79
     O                       l2desc         b   131
      *
      *-------------------------------------------------------------
      * Total line for template detail records
      *-------------------------------------------------------------
      *
     O          e            t2tot       1  1
     O                                           55 'Total:'
     O                       t2costam      kb    79
     O                                           95 'Cost per head:'
     O                       t2costhd      kb   106
      *
      *-------------------------------------------------------------
      * Detail line of linked farms
      *-------------------------------------------------------------
      *
     O          e            l3dtl          1
     O                       tlfscd        z     25
     O                       l3fsnm         b    52
     O                       l3ppcd         b    61
     O                       l3cell        zb    69
     O                       l3tfcd         b    77
     O                       l3cphd        kb    88
     O                       l3inafl        b    95
     O                       l3actfl        b   105
     O                       l3clofl        b   115
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           29 'NO DATA MEETS YOUR'
     O                                           48 'SELECTION CRITERIA'
      *-------------------------------------------------------------
      * No items message line
      *-------------------------------------------------------------
      *
     O          e            noitems     1  1
     O                                           38 'No items have been'
     O                                           48 ' budgeted.'
      *-------------------------------------------------------------
      * No farms message line
      *-------------------------------------------------------------
      *
     O          e            nofarms     1  1
     O                                           38 'No farms have been'
     O                                           46 ' linked.'
