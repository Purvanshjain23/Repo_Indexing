     A*%%TS  SD  20040902  130746  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1016
     A*  TITLE:         REVISE GROUP OVERRIDES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       08/30/04
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A N62                                  CF09(09 'ACCEPT')
     A  60N62                               CF11(11 'DELETE')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1016R1
     A*%%TS  SD  20040902  130746  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 26'REVISE GROUP OVERRIDES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Hog group code ..'
     A  61                              4 20'?'
     A            D1HGCD    R        B  4 22REFFLD(@@HGCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N62N60                               DSPATR(UL)
     A  61N35                               DSPATR(HI)
     A                                  4 35'Status code:'
     A            D1GSCD    R        O  4 48REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A                                  5 30'Production phase:'
     A            D1PPCD    R        O  5 48REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                  6 37'Farm site:'
     A            D1FSCD    R        O  6 48REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1FSNM    R        O  6 55REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  7 33'Building/room:'
     A            D1BLCD    R        O  7 48REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            D1RMCD    R        O  7 55REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A                                  9 32'OVERRIDE VALUES'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A                                  9 55'CURRENT DATAMART VALUES'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A                                 11  2'Sire line code ............'
     A N62                             11 30'?'
     A            D1SICD    R        B 11 32REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N36                               DSPATR(HI)
     A            D1DMSICD  R        O 11 56REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A                                 12  2'Origin BGF farm site ......'
     A N62                             12 30'?'
     A            D1OBFS    R        B 12 32REFFLD(HSRREF/@@ONFS *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N37                               DSPATR(HI)
     A            D1DMOBFS  R        O 12 56REFFLD(HSRREF/@@ONFS *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                 13  2'Origin nursery farm site ..'
     A N62                             13 30'?'
     A            D1ONFS    R        B 13 32REFFLD(HSRREF/@@ONFS *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  38                                  DSPATR(RI)
     A  38
     AO 58                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N38                               DSPATR(HI)
     A            D1DMONFS  R        O 13 56REFFLD(HSRREF/@@ONFS *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                 16  2'Origin BGF pod ...........:'
     A            D1OBPOD   R        O 16 34REFFLD(HSRREF/@@POD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1DMOBPOD R        O 16 58REFFLD(HSRREF/@@POD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                 17  2'Origin BGF cell ..........:'
     A            D1OBCELL  R        O 17 35REFFLD(HSRREF/@@OBCELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1DMOBCELLR        O 17 59REFFLD(HSRREF/@@OBCELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                 18  2'Origin nursery cell ......:'
     A            D1ONCELL  R        O 18 35REFFLD(HSRREF/@@ONCELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1DMONCELLR        O 18 59REFFLD(HSRREF/@@ONCELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1016K1
     A*%%TS  SD  20040831  150553  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 23'F5=Refresh'
     A N62                             23 35'F9=Accept'
     A N62N61                          23 46'F11=Delete'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1016ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1016EC                  SFLCTL(HP1016ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
