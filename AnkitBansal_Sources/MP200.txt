      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      MP2 Data Mart
      * PROGRAM:     HP200 - Build DataMart Milk Consumption File
      * PROGRAMMER:  Barb Gutierrez
      * CREATED:     12/20/01
      *
      * FUNCTION:    This file in the Data Mart will be re-created with
      *              each load of the Data Mart.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fmpp100    uf   e           k disk
      *  Raw data from MP2
      *
     Fmpp200    o    e           k disk
      *  Formated data from MP2
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      * Workfields
      *
     D begin           s              2  0
     D wkfrm#          s              3
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
     Dwkismdy          ds
     D  wkismm                 1      2
     D  wkisdd                 4      5
     D  wkisyy                 7     10
      *
      * Workfields for date manipulation
      *
     D wkisodat        s               D   datfmt(*iso)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read Raw Data and format into datamart file.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      mpp100                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Populate fields
      *
     C                   z-add     mpitm#        mcitm#
     C                   move      mpdesc        mcdesc
      *
     C     *usa          move      mpisdt        wkisodat
     C                   move      wkisodat      mcisdt
      *
      * If this is an IR (Issue Return), write out as a negative number.
      * MP2 stores it as a positive number.
      *
     C                   move      '000'         wkfrm#
      *
     C                   if        mptrtyp = 'IR'                               If not EOF
     C     ' '           checkr    mpfmcu        begin
     C                   sub       2             begin
     C                   if        begin > 0
     C                   eval      wkfrm# = %subst(mpfmcu:begin:3)
     C                   endif
     C     mpqty         mult      -1            mcqty
     C                   else                                                   If not EOF
      *
     C     ' '           checkr    mpmcu         begin
     C                   sub       2             begin
     C                   if        begin > 0
     C                   eval      wkfrm# = %subst(mpmcu:begin:3)
     C                   endif
     C                   z-add     mpqty         mcqty
     C                   endif                                                  If not EOF
      *
     C                   move(p)   wkfrm#        mcfrm#
      *
     C                   move      *date         mcupdt
      * Write record
      *
     C                   write     mcrec
     C                   delete    mprec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
