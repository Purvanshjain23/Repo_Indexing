      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP2099 - Update HPS Sale LBS When the Scale Was Broken
      * PROGRAMMER:  Alice Brownfield
      * CREATED:     01/20/03
      *
      * FUNCTION:  This function is intended for I.S. Personnel only.
      *
      *            Alice originally wrote this fix program when the scale was broken at
      *            the plant. With the scale broken, the pounds in HPE were incorrect.
      *            Thus, the pounds on the HPS records that were interfaced from HPE
      *            were also incorrect.
      *
      *            So, once the scale was repaired, Purva/Rose wrote a program to fix
      *            the data in HPE. Alice then wrote/ran a program to fix the pounds
      *            in HPS.
      *
      *            This program is simply the cleaned-up version of Alice's fix program.
      *
      ****************************************************************
      * MODIFICATIONS
      ****************************************************************
      *
      * 02/25/03  LeAnne Fedor
      *           Alice had me clean-up her original fix program giving it standard
      *           names, subroutines, etc.  She also had me add a 'specify options'
      *           screen on the front end to allow entry of from/to dates...instead
      *           of hardcoding them in the CL.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      * 11/02/20  ISE  (H16853)
      *           Recompile only. Buy Order Number field length is increased from 5 to 7.
      *
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034d   uf   e           k disk
      *    Hog group
      *
      *
     Fhsl064b   uf   e           k disk
      *    Check detail
      *
      *
     Fhsl084b   if   e           k disk
      *   Sales movement header
      *
      *
     Fhsp085    uf   e           k disk
      *    Sales movement detail
      *
      *
     Fpka1cpla  if   e           k disk
      *    SYNON: Tattoo header
      *
      /EJECT
      ****************************************************************
      * Definition specs
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Workfields
      *
     D wkcono          s                   like(a1hmnb) inz(360)
     D wkavglb         s                   like(a1f7nb)
      *
      * Parms
      *
     D xxccyymmdd      s              8  0
     D xxsynondt       s              7  0
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read each movement in the Sales Movement Header file (records selected
      * by open query.) For each movement, retrieve/process each Check Detail
      * record.
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl084b                                90
     C                   if        *in90 = *off                                 If not EOF
     C                   exsr      $checkdtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Process each check detail record for a Sales Movement
      *---------------------------------------------------------------
      *
     C     $checkdtl     begsr
      *
     C     shmvsn        setll     hsl064b
      *
     C                   dou       *in91 = *on                                  Do check detail
     C     shmvsn        reade     hsl064b                                91
     C                   if        *in91 = *off                                 If check detail
      *
      * Determine if the group is closed. If it is, you cannot change this
      * record...just go on to the next check detail record.
      *
     C     cdhgsn        chain     hsl034d                            92
     C                   if        *in92 = *off and hggscd <> 'CL'              If not closed
      *
      * Retrieve the SYNON average weight for this tattoo/kill date.
      *
     C                   exsr      $avglb
      *
      * If the SYNON average weight is greater than zero, recalc 4 weights
      * for HPS check detail record and update.
      *   1) live pounds
      *   2) DOA pounds
      *   3) condemned pounds
      *   4) yard dead pounds
      *
     C                   if        wkavglb > 0                                  If Synon lbs
     C     wkavglb       mult(h)   cdlvhd        cdlvlb
     C     wkavglb       mult(h)   cddohd        cddolb
     C     wkavglb       mult(h)   cdcdhd        cdcdlb
     C     wkavglb       mult(h)   cdydhd        cdydlb
     C                   update    cdrec
      *
      * Now, retrieve/update all Sales Movement Detail records for this
      * movement/hog group.
      *
     C                   exsr      $salesdtl
      *
     C                   endif                                                  If SYNON lbs
     C                   endif                                                  If not closed
      *
     C                   endif                                                  If check detail
     C                   enddo                                                  Do check detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Update each Sales Movement Detail record for movement/group
      *---------------------------------------------------------------
      *
      * Process all Sales Movement Detail records for this movement/group
      * recalcing the pounds.
      *
     C     $salesdtl     begsr
      *
     C     key02         setll     hsp085
      *
     C                   dou       *in93 = *on                                  Do sales detail
     C     key02         reade     hsp085                                 93
     C                   if        *in93 = *off                                 If not EOF
      *
     C     wkavglb       mult(h)   sglvhd        sglvlb
     C     wkavglb       mult(h)   sgdohd        sgdolb
     C     wkavglb       mult(h)   sgcdhd        sgcdlb
     C     wkavglb       mult(h)   sgydhd        sgydlb
     C                   update    sgrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do sales detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve SYNON Average Live Pounds for a Tattoo/Date
      *---------------------------------------------------------------
      *
     C     $avglb        begsr
      *
      * Convert the HPS Kill date to the Synon format
      *
     C                   call      'HP8008'
     C                   parm      cdkldt        xxccyymmdd
     C                   parm      0             xxsynondt
      *
      * Retrieve the 'average live weight' from the SYNON Tattoo Header
      * for this company/date/tattoo
      *
     C     key01         chain     pka1cpla                           92
     C                   if        *in92 = *off and a1f7nb > 0
     C                   z-add     a1f7nb        wkavglb
     C                   else
     C                   z-add     0             wkavglb
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key list
      *
     C     key01         klist
     C                   kfld                    wkcono
     C                   kfld                    xxsynondt
     C                   kfld                    cdtano
      *
     C     key02         klist
     C                   kfld                    cdmvsn
     C                   kfld                    cdhgsn
      *
     c                   endsr
      *
