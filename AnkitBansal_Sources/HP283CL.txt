/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          HOG PRODUCTION SYSTEM                         */
/*  PROGRAM NUMBER:  HP283CL                                       */
/*  PROGRAM NAME:    SAFEDATA: UPLOAD BGF WEEKLY WEAN PROJECTIONS  */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     07/12/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED FROM AN FTP CL WHICH IS     */
/*                   SUBMITTED FROM THE JOB SCHEDULER.             */
/*                                                                 */
/*  THE FUNCTION:                                                  */
/*  1) WRITES THE RECEIVING FILE TO AN EDITING FILE                */
/*  2) PRINTS AN EDIT LISTING OVER THE EDITING FILE                */
/*     AND WRITES RECORDS WITHOUT ERRORS TO HPS DATABASE           */
/*  3) PRINTS THE WEAN PIG PROJECTION REPORT                       */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/* 07/11/07  LEANNE RAMSEY                                         */
/*           TO SAVE TIME FOR USER STEVE BROWN (WHO COMES IN VERY, */
/*           VERY EARLY ON TUESDAY MORNINGS TO RUN HIS WEAN PIG    */
/*           PROJECTION REPORT), WE WILL SUBMIT HIS REPORT AFTER   */
/*           THE UPLOAD COMPLETES. THE UPLOADED DATA IS WHAT HIS   */
/*           REPORT USES.                                          */
/*  02/28/23  ERIC LOUCHEZ    TICKET 120610                        */
/*            TRYING TO GET THE REPORT ATTACHED TO THE EMAILS      */
/*            MOVE ESNDMAIL TO THIS CL FROM FTPSDIRTVP             */
/*            THIS CL IS BEING SUBMITTED BY FTPSDIRTVP             */
/*            CPYF AND CLRPFM MOVED HERE FROM FTPSDIRTVP           */
/*******************************************************************/

/*           PGM                                       120610 */
             PGM        PARM(&TSTPRDFL)             /* 120610 */

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)  /* 120610 */
             DCL        VAR(&EMAIL  )  TYPE(*CHAR) LEN(38) /* 120610 */

/*-------------------------------------------------------------------*/
/*  WRITE THE BGF WEEKLY WEAN RECEIVING TO THE EDITING FILE          */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP283)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES AND OVERRIDE FILE                    */
/*------------------------------------------------------------------*/

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(*NO)
 COPYS:      CHGVAR     VAR(&COPYS) VALUE(2)

 /*  OVRPRTF:    OVRPRTF    FILE(QPRINT) OUTQ(GUYPRT06) COPIES(&COPYS) +
                            HOLD(&HOLD) SHARE(*YES)                   120610 */
 OVRPRTF:    OVRPRTF    FILE(QPRINT) OUTQ(GUYPRT06) COPIES(&COPYS) +
                          HOLD(&HOLD) SHARE(*YES) SAVE(*YES)       /* 120610 */

/*-------------------------------------------------------------------*/
/*  CALL PROGRAM TO EDIT/WRITE TO DATABASE                           */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP284)
             /* FORCE QPRINT CLOSED BEFORE EMAILING.          120610     */
                          RCLRSC                                   /* 120610 */
/*--------------------------------------------------------------*/ /* 120610 */
/*     SENDING EMAILS:                                          */ /* 120610 */
/*--------------------------------------------------------------*/ /* 120610 */

  IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)                      /* 120610 */
   CHGVAR VAR(&EMAIL) VALUE('hps weekly wean projections')         /* 120610 */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Wkly Wean +
                          Projection Error List') MSG('Distribution +
                          List: HPS Safedata Weekly Wean Projection +
                          Error List') ATTLIST((QPRINT *PDF *N *ALL +
                          * *LAST *ONLY))
    MONMSG     MSGID(CPF0000)                                      /* 120610 */
    ENDDO                                                          /* 120610 */

  IF         COND(&TSTPRDFL *EQ 'T') THEN(DO)                      /* 120610 */
   CHGVAR     VAR(&EMAIL) VALUE('hps safedata test')               /* 120610 */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('TEST: Wkly Wean +
                          Projections Error List') +
                          MSG('Distribution List: TEST: HPS +
                          Safedata Weekly Wean +
                          Projection Error List') ATTLIST((QPRINT +
                          *PDF *N *ALL * *LAST *ONLY))
   MONMSG     MSGID(CPF0000)                                       /* 120610 */
   ENDDO                                                           /* 120610 */

             DLTOVR     FILE(*ALL)

/*-------------------------------------------------------------------*/
/*  REORG THE EDITING FILE                                           */
/*-------------------------------------------------------------------*/
             RGZPFM     *LIBL/HSP284
             MONMSG     MSGID(CPF0000)


/*-------------------------------------------------------------------*/
/*  GENERATE THE "LISTING OF BGF WEEKLY WEAN PROJECTIONS"            */
/*-------------------------------------------------------------------*/
/* This report is also generated On-Demand. So, set the LDA:         */
/* 1) set for "multiple" listing                                     */
/* 2) set for "upload"                                               */
/* 3) set "copies" to 1--so HP4021CL doesn't blow up                 */

             CHGDTAARA  DTAARA(*LDA (1 1))   VALUE('M') /* Multiple */
             CHGDTAARA  DTAARA(*LDA (70 1))  VALUE('U') /* Upload */
             CHGDTAARA  DTAARA(*LDA (412 1)) VALUE('1') /* Copies */
             CALL       PGM(HP4021CL) PARM(&TSTPRDFL)

         CPYF FROMFILE(HSP283) TOFILE(HSP283SV) MBROPT(*REPLACE)   /* 120610 */
         CLRPFM     FILE(HSP283)                                   /* 120610 */

             ENDPGM

