     A*%%TS  SD  20081231  082103  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD102
     A*  TITLE:         MAINTAIN ITEM CONTROL DATA
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/08/06
     A*
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  62  IS ON WHEN THE RECORD HAS BEEN DELETED OUT FROM UNDER THE USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF09(09 'ACCEPT')
     A                                      CF13(13 'FOLD/DROP')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R RO102S1                   SFL
     A*%%TS  SD  20070205  094836  ISLRAMS     REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            HDITCD    R        H      REFFLD(@@ITCD)
     A*
     A            D1ITCD    R        B 10  2REFFLD(@@ITCD)
     A                                      EDTCDE(M)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N33N62                               DSPATR(HI)
     A N33N62                               DSPATR(UL)
     A            D1SRITCD  R        B 10 11REFFLD(@@SRITCD)
     A                                      EDTCDE(M)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N34N62                               DSPATR(HI)
     A N34N62                               DSPATR(UL)
     A            D1DFPR    R        B 10 21REFFLD(ICREC/ICDFPR *LIBL/ROP012)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N32N62                               DSPATR(HI)
     A N32N62                               DSPATR(UL)
     A            D1DOCD    R        B 10 37REFFLD(ICREC/ICDOCD *LIBL/ROP012)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N35N62                               DSPATR(HI)
     A N35N62                               DSPATR(UL)
     A            D1DOBSPR  R        B 10 41REFFLD(ICREC/ICDOBSPR *LIBL/ROP012)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N36N62                               DSPATR(HI)
     A N36N62                               DSPATR(UL)
     A            D1MUPC    R        B 10 54REFFLD(ICREC/ICMUPC *LIBL/ROP012)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N37N62                               DSPATR(HI)
     A N37N62                               DSPATR(UL)
     A            D1FRXFL   R        B 10 71REFFLD(@@FRXFL)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N30N62                               DSPATR(HI)
     A N30N62                               DSPATR(UL)
     A                                 11  2'Item:'
     A                                      COLOR(PNK)
     A            D1ITDS    R        O 11  8REFFLD(@@ITDS)
     A                                      COLOR(BLU)
     A            D1SRTEXT       7A  O 11 41COLOR(PNK)
     A            D1SRITDS  R        O 11 49REFFLD(@@ITDS)
     A                                      COLOR(BLU)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R RO102C1                   SFLCTL(RO102S1)
     A*%%TS  SD  20070205  094836  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(30)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(50)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1     R        H      REFFLD(@@RRN)
     A            FOLD           1A  H
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 25'MAINTAIN ITEM CONTROL DATA'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Company:'
     A            C1CONO    R        O  4 11REFFLD(@@CONO)
     A                                      DSPATR(HI)
     A                                  4 17'Week-ending date:'
     A            C1WEMDY   R        O  4 35REFFLD(ROREF/@@DT06 *LIBL/ROPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A                                  6  6'?'
     A                                  6 15'?'
     A                                  6 71'?'
     A                                  7 12'Source'
     A                                  8  5'Item'
     A                                  8 14'Item'
     A                                  8 21'Difference'
     A                                  8 36'Default/Override'
     A                                  8 54'Multiplier'
     A                                  8 66'Fresh Product'
     A                                  9  5'Code'
     A                                  9 14'Code'
     A                                  9 23'Price/LB'
     A                                  9 36'Code'
     A                                  9 41'Base Price'
     A                                  9 55'Percent'
     A                                  9 66'Exception Flag'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO102K1
     A*%%TS  SD  20081231  082103  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F9=Accept'
     A                                 23 45'F13=Fold/Unfold'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R RO102ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R RO102EC                   SFLCTL(RO102ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
