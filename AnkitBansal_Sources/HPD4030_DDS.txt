     A*%%TS  SD  20090514  154556  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4030
     A*  TITLE:         WORK WITH REPORTED DAILY ACTIVITY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/21/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF19(19 'REPORT')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4030S1                  SFL
     A*%%TS  SD  20090514  154556  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDDYDT    R        H      REFFLD(@@DT08)
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            D1KDMDY   R        O 11  7REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CELL    R        O 11 18REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A            D1FSCD    R        O 11 23REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1WEMDY   R        O 11 32REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1DDHD    R        O 11 42REFFLD(HSRREF/@@HD05 *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1DSHD    R        O 11 51REFFLD(HSRREF/@@HD05 *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1MANHR   R        O 11 61REFFLD(HSRREF/@@MANHR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4030C1                  SFLCTL(HP4030S1)
     A*%%TS  SD  20090514  154556  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'WORK WITH REPORTED DAILY ACTIVITY'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ..'
     A N99                              4 21'?'
     A            C1FSBO    R        B  4 23REFFLD(FSFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N99                               DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A N99                              6  2'2=Revise'
     A N99                              6 12'4=Delete'
     A N99                              7 26'?'
     A                                      DSPATR(HI)
     A                                  7 34'Week'
     A                                      DSPATR(HI)
     A                                  8  8'Daily'
     A                                      DSPATR(HI)
     A                                  8 33'Ending'
     A                                      DSPATR(HI)
     A                                  8 44'Dead'
     A                                      DSPATR(HI)
     A                                  8 50'Destroyed'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  9'Date'
     A                                      DSPATR(HI)
     A                                  9 17'Cell'
     A                                      DSPATR(HI)
     A                                  9 24'Farm'
     A                                      DSPATR(HI)
     A                                  9 34'Date'
     A                                      DSPATR(HI)
     A                                  9 44'Head'
     A                                      DSPATR(HI)
     A                                  9 53'Head'
     A                                      DSPATR(HI)
     A                                  9 62'Manhours'
     A                                      DSPATR(HI)
     A N99        C1KDMDY   R        B 10  7REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A N31N51                               DSPATR(PC)
     A N99        C1CELL    R        B 10 18REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A N99        C1FSCD    R        B 10 23REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A N99        C1WEMDY   R        B 10 32REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A N31N51                               DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4030K1
     A*%%TS  SD  20021106  132249  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A N99                             22 22'F5=Refresh'
     A N99                             22 34'F6=Create'
     A N99                             22 45'F7=Listing'
     A N99                             22 57'F17=Print Options'
     A N99                             23 34'F19=No Data Report'
     A N99                             23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4030ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4030EC                  SFLCTL(HP4030ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
