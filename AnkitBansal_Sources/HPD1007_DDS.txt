     A*%%TS  SD  20010904  105922  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1007
     A*  TITLE:         ASSIGN MULTIPLE BGF TABLES/FARMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/18/00
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP1007S1                  SFL
     A*%%TS  SD  20000925  103232  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            HDEFDT    R        H      REFFLD(@@EFDT)
     A            D1FSCD    R        O  8  2REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            D1OLDTTBCDR        O  8  9REFFLD(@@TTBCD)
     A            D1OLDEFDT R        O  8 17REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  -')
     A            D1NEWTTBCDR        B  8 28REFFLD(@@TTBCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1NEWEFDT R        B  8 36REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  -')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A            D1FSBO    R        O  8 50REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A            D1PTCD    R        O  8 61REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP1007C1                  SFLCTL(HP1007S1)
     A*%%TS  SD  20010904  105922  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0014)
     A                                      SFLCSRRRN(&HDRR1)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRR1     R        H      REFFLD(@@RRN)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 21'ASSIGN MULTIPLE BGF TABLES/FARMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 30'?'
     A                                      DSPATR(HI)
     A                                  5  9'---- Current ----'
     A                                      DSPATR(HI)
     A                                  5 28'-- New Current --'
     A                                      DSPATR(HI)
     A                                  6  2'Farm'
     A                                      DSPATR(HI)
     A                                  6  9'Target'
     A                                      DSPATR(HI)
     A                                  6 17'Effective'
     A                                      DSPATR(HI)
     A                                  6 28'Target'
     A                                      DSPATR(HI)
     A                                  6 36'Effective'
     A                                      DSPATR(HI)
     A                                  6 48'Business'
     A                                      DSPATR(HI)
     A                                  6 58'Production'
     A                                      DSPATR(HI)
     A                                  7  2'Site'
     A                                      DSPATR(HI)
     A                                  7  9'Table'
     A                                      DSPATR(HI)
     A                                  7 19'Date'
     A                                      DSPATR(HI)
     A                                  7 28'Table'
     A                                      DSPATR(HI)
     A                                  7 38'Date'
     A                                      DSPATR(HI)
     A                                  7 49'Office'
     A                                      DSPATR(HI)
     A                                  7 61'Type'
     A                                      DSPATR(HI)
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP1007K1
     A*%%TS  SD  20000918  124740  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90                             23 35'F9=Accept'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP1007ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP1007EC                  SFLCTL(HP1007ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
