/*****************************************************************  */
/*                                                                  */
/*  PROGRAM NUMBER --> HP484CL                                      */
/*  PROGRAM NAME ----> SALES MOVEMENT/CHECK REPORT                  */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE       ------> 09/07/94                                    */
/*                                                                  */
/*  PROGRAM PURPOSE -> THIS CL USES OPNQRY TO BUILD A WORKFILE      */
/*                     THAT IS USED TO PRINT THE REPORT             */
/*                                                                  */
/***************************************************************** */
/*  MODIFICATIONS:                                                 */
/***************************************************************** */
/*                                                                 */
/*  12/05/94  LEANNE FEDOR                                         */
/*            CHANGED WORKFILE LOGIC. WE WILL NOW CREATE WORKFILES */
/*            IN QTEMP INSTEAD OF CLEARING THEM IN PRODUCTION.     */
/*                                                                 */
/*  06/30/95  LEANNE FEDOR                                         */
/*            CHANGED THE OPEN QUERY FILES FOR THE MOVEMENT        */
/*            REWRITE.                                             */
/*                                                                 */
/*  08/31/95  LEANNE FEDOR                                         */
/*            MADE BUSINESS OFFICE AN OPTIONAL SELECTION.          */
/*                                                                 */
/*  02/06/96  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED  */
/*            TO SALES MOVEMENT HEADER FILE.                       */
/*                                                                 */
/*  04/25/96  LEANNE FEDOR                                         */
/*            THE CHANGE VARIABLE COMMAND FOR PRODUCTION TYPE WAS  */
/*            REFERENCING THE WRONG POSITION IN THE LDA.           */
/*                                                                 */
/* 04/25/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*                                                                 */
/* 07/22/98  LEANNE FEDOR                                          */
/*           ADDED QUERY STATEMENT FOR 'SALES TYPE'. THIS WAS      */
/*           INADVERTANTLY OMITTED.                                */
/*                                                                 */
/* 12/18/02 LEANNE FEDOR                                           */
/*          'PRODUCTION TYPE' AND 'PRODUCTION PHASE' WERE REMOVED  */
/*          FROM THE SALES MOVEMENT HEADER FILE. SO, OPEN QUERY    */
/*          LOGIC HERE WAS REPLACED WITH BUILD LOGIC IN THE        */
/*          WORKFILE PROGRAM.                                      */
/*                                                                 */
/*  7/11/19 BRAD BADEN  E15217                                     */
/*           Added new fields &LDEMAIL and &FLAG.  If the user did */
/*           not enter an email address on screen HP484, the       */
/*           report will print as in the past.  Otherwise, file    */
/*           HSP4841 is duplicated in QTEMP.  New program HP4841   */
/*           is called to copy the records from file HSP484 to     */
/*           file HSP4841, and then Sequel Viewpoint HSP4841 is    */
/*           executed to produce and email the spreadsheet to the  */
/*           user.                                                 */
/*                                                                 */
/*  8/13/19 Brad Baden  E15323                                     */
/*           Change the execute state ment to call the new Sequel  */
/*           ViewPoint table SLSMVMCK_T. Also change the size of   */
/*           field LDSEL from 150 to 154 to accomodate the Hog     */
/*           Group Code in the body of the email. The positions of */
/*           LDSEL is now 451 to 604.                              */
/*                                                                 */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA1)  TYPE(*CHAR) LEN(250)
             DCL        VAR(&QDATA2)  TYPE(*CHAR) LEN(50)

             DCL        VAR(&LDFYMD)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCVNO)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFSBO)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDSTCD)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD)  TYPE(*DEC) LEN(5)
             DCL        VAR(&LDHGSN)  TYPE(*DEC) LEN(7)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&LDSEL)   TYPE(*CHAR) LEN(154)

             DCL        VAR(&HOLD)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS)   TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&FLAG)    TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDFSBO)  VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD)  VALUE(%SST(*LDA 36 8))
             CHGVAR     VAR(&LDTYMD)  VALUE(%SST(*LDA 50 8))
             CHGVAR     VAR(&LDPTCD)  VALUE(%SST(*LDA 64 5))
             CHGVAR     VAR(&LDPPCD)  VALUE(%SST(*LDA 79 5))
             CHGVAR     VAR(&LDSTCD)  VALUE(%SST(*LDA 114 5))
             CHGVAR     VAR(&LDFSCD)  VALUE(%SST(*LDA 149 5))
             CHGVAR     VAR(&LDCVNO)  VALUE(%SST(*LDA 179 8))
             CHGVAR     VAR(&LDHGSN)  VALUE(%SST(*LDA 217 7))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 256 50))


/*******************************************************************/
/*  CREATE WORKFILE                                                */
/*******************************************************************/

             CRTDUPOBJL OBJ(HSP384) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*******************************************************************/
/*  CREATE SELECT STATEMENT FOR SALES MOVEMENT HEADER               */
/*******************************************************************/

             CHGVAR     VAR(&QDATA1) VALUE(' ')

/*  ALWAYS SELECT ONLY SALES MOVEMENTS THAT HAVE SOME PAYMENT    */

             CHGVAR     VAR(%SST(&QDATA1 1 38)) VALUE('(SHMSCD +
                          *NE "SC" *AND SHMSCD *NE "SH")')

/*  ALWAYS SELECT ONLY SALES MOVEMENTS WITH A RECEIVE DATE IN THE */
/*  USERS REQUESTED DATE RANGE                                    */

             CHGVAR     VAR(%SST(&QDATA1 40 15)) VALUE('*AND SHRCDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA1 56 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA1 65 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA1 81 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE                                           */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA1 90 17)) VALUE('*AND SHFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA1 107 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA1 112 1)) VALUE('"')
             ENDDO


/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA1 162 15)) VALUE('*AND SHFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA1 178 5)) VALUE(&LDFSCD)
             ENDDO


/* IF CUSTOMER                                                   */

             IF         COND(&LDCVNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA1 184 15)) VALUE('*AND SHCVNO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA1 200 8)) VALUE(&LDCVNO)
             ENDDO

/* IF SALES TYPE                                                 */

             IF         COND(&LDSTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA1 209 17)) VALUE('*AND SHSTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA1 226 5)) VALUE(&LDSTCD)
             CHGVAR     VAR(%SST(&QDATA1 231 1)) VALUE('"')
             ENDDO


/*********************************************************************/
/* PERFORM THE OPEN QUERY OVER THE SALES HEADER                      */
/*********************************************************************/

             OVRDBF     FILE(HSP084) SHARE(*YES)
             OPNQRYF    FILE((HSP084)) QRYSLT(&QDATA1) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*********************************************************************/
/* SET UP AND PERFORM AN OPEN QUERY ON THE CHECK DETAIL IF NECESSARY */
/*********************************************************************/

/*  IF HOG GROUP IS NOT BLANK, QUERY ON CHECK DETAIL FOR GROUP       */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA2 1 10)) VALUE('CDHGSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA2 12 7)) VALUE(&LDHGSN)

             OVRDBF     FILE(HSL064C) SHARE(*YES)
             OPNQRYF    FILE((HSL064C)) QRYSLT(&QDATA2) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             ENDDO

/*********************************************************************/
/*  CALL THE PROGRAM TO BUILD THE WORKFILE                           */
/*********************************************************************/

             CALL       PGM(HP384)

/*********************************************************************/
/* SET OUTPUT PARMS FROM LDA                                         */
/*********************************************************************/

             SELECT
             WHEN       COND(&LDEMAIL *EQ ' ') THEN(DO)

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT OF 15CPI ON 198 COLUMNS OF PRINT  */
/* FORCE IT TO PRINT OUT OF DRAWER 2 ON LANDSCAPE                   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/*  CALL THE PROGRAM TO PRINT THE REPORT                             */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP684)

             ENDDO      /* SELECT WHEN COND(&LDEMAIL *EQ ' ') */

/*-------------------------------------------------------------------*/
/*  CALL THE PROGRAM TO COPY RECORDS TO FILE HSP4841 FOR SPREADSHEET */
/*-------------------------------------------------------------------*/

             WHEN       COND(&LDEMAIL *NE ' ') THEN(DO)

 /* Create workfile for spreadsheet                                  */
             CRTDUPOBJL OBJ(HSP4841) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

 /* Create program to copy records from HSP484 to HSP4841            */
             CALL       PGM(HP4841) PARM(&FLAG)

             IF         COND(&FLAG = 'Z')     THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('HPS: Sales +
                          Movement/Check Email') MSG('Your +
                          selections on the Specify Sales +
                          Movement/Check Report Options screen did +
                          not extract any data for the spreadsheet. +
                          Please change your selections and submit +
                          again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&FLAG = 'T')     THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('HPS: Sales +
                          Movement/Check Email') MSG('Your +
                          selections on the Specify Sales +
                          Movement/Check Report Options screen +
                          extracted over 66,000 records--which is +
                          too many records for the spreadsheet to +
                          handle. Please change your selections to +
                          extract fewer records and submit again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&FLAG = ' ')     THEN(DO)
             CHGVAR     VAR(&LDSEL)   VALUE(%SST(*LDA 451 154))

             EXECUTE    VIEW(*LIBL/SLSMVMCK_T) PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&LDEMAIL) +
                          EMLMSG(&LDSEL) TEXT('Sales Movement/Check +
                          Report Spreadsheet')
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDDO      /* SELECT WHEN COND(&LDEMAIL *NE ' ') */
             ENDSELECT

/* Close files and delete overrides                                 */
             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CLOF       OPNID(HSL064C)
             ENDDO      /* IF COND(&LDHGSN *NE 0) */

             CLOF       OPNID(HSP084)

             DLTOVR     FILE(*ALL)

 ENDIT:      ENDPGM
