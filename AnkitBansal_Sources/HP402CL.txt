/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:    HP402CL                                     */
/*  PROGRAM NAME:      WORK WITH FARM MANAGERS                     */
/*                       PRINT LISTINGS                            */
/*  PROGRAMMER:        LEANNER FEDOR                               */
/*  DATE:              06/16/97                                    */
/*                                                                 */
/*  PROGRAM PURPOSE:   PRINT LISTINGS FROM W/W FARM MANAGERS.      */
/*                                                                 */
/*                     TWO LISTINGS CAN BE PRINTED:                */
/*                       L=LISTING OF ALL MANAGERS                 */
/*                       P=PRINT FARMS FOR A SINGLE MANAGER        */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/* 06/26/01  LEANNE FEDOR                                          */
/*           ADDED AN OPEN QUERY FOR THE 'P' VERSION OF THE REPORT.*/
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(67)

             DCL        VAR(&MGCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&RPFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)


             CHGVAR     VAR(&RPFL) VALUE(%SST(*LDA  1 1))
             CHGVAR     VAR(&MGCD) VALUE(%SST(*LDA  2 5))

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* BOTH LISTINGS PRINT AT 10 CPI ON STANDARD PAPER.                 */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*YES) COPIES(&COPYS) +
                          HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/*  LISTING OF ALL FARM MANAGERS                                    */
/*------------------------------------------------------------------*/

             IF         COND(&RPFL *EQ 'L') THEN(DO)
             CALL       PGM(HP602)
             ENDDO

/*------------------------------------------------------------------*/
/*  PRINT ALL FARMS FOR A SINGLE MANAGER                            */
/*------------------------------------------------------------------*/

             IF         COND(&RPFL *EQ 'P') THEN(DO)

             CHGVAR     VAR(%SST(&QDATA 1 13)) VALUE('FSPMGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 14 5)) VALUE(&MGCD)
             CHGVAR     VAR(%SST(&QDATA 19 1)) VALUE('"')
             CHGVAR     VAR(%SST(&QDATA 21 17)) VALUE('*OR FSCMGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 38 5)) VALUE(&MGCD)
             CHGVAR     VAR(%SST(&QDATA 43 1)) VALUE('"')
             CHGVAR     VAR(%SST(&QDATA 45 17)) VALUE('*OR FSMMGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 62 5)) VALUE(&MGCD)
             CHGVAR     VAR(%SST(&QDATA 67 1)) VALUE('"')

             OVRDBF     FILE(HSP018) SHARE(*YES)
             OPNQRYF    FILE((HSP018)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP612)

             CLOF       OPNID(HSP018)
             ENDDO


             DLTOVR     FILE(*ALL)

             ENDPGM

