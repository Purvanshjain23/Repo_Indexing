      *
      * SYSTEM:      ORDER MANAGEMENT
      * PROGRAM:     PDX0UPR
      * TITLE:       Extract Order Number and Ship to Cust # from
      *              order header trigger. Update order header that
      *              order has been emailed.
      *
      * PROGRAMMER:  Mark Arensberg
      * CREATED:     04/08/99
      *
      * FUNCTION:
      *           The maintenance function is kicked off by placing a
      *           trigger on the physical file. The trigger has two
      *           2 parms--a before image and an after image of the
      *           file being changed. The file that will have the
      *           trigger is the Order Header (OPBFCPP).
      *
      *           We were unable to extract the hex into numeric fields
      *           in a CL program so this program will be used to
      *           extract the order number and ship to customer.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 03/17/00 RMC -- SEND OUT EMAIL ADDRESS XXUGTX
      * 06/09/00 RMC -- SEND OUT CUSTOMER PO NUMBER
      * 03/20/02 RMC -- ADD 7 TO BUFFER POSITIONS STARTING WITH FIELD
      *                 COMP#
      * 06/19/07 RMC -- RECOMP FOR PDNEREP DB CHANGE - BROKER REMOVED
      * 12/07/09 SAZ -- ADD ASN FLAG
      ****************************************************************
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
     FPDNEREL1IF  E           K        DISK
      * RTV : Customer Electronic Commerce File
      *
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *-------------------------------------------------------------------------
      * Data structures
      *-------------------------------------------------------------------------
      *
      *-----------------------------------------------------------------------
      * Declare the fields/structures that are to be passed into this program
      *-----------------------------------------------------------------------
      *
     IXXPRM1      DS
     I                                        1  96 FILL1
     I                                       97 352 FILL2
     I                                      353 608 FILL3
     I                                      609 864 FILL4
     I                                      8651008 FILL5
     I                                    P100910100COMP#
     I                                    P101110140ORDER#
     I                                     10151016 ORDTYP
     I                                    P101710200ARCUST
     I                                    P102110240SHIPTO
     I                                     10251280 FILL6
     I                                     12811332 FILL7
     I                                     13351349 CUSTPO
     I                                     13501536 FILL8
     I                                     15371792 FILL9
     I                                     17931906 FILL0
     I* BEFORE 3/20/02
     I*                                     8651001 FILL5
     I*                                   P100210030COMP#
     I*                                   P100410070ORDER#
     I*                                    10081009 ORDTYP
     I*                                   P101010130ARCUST
     I*                                   P101410170SHIPTO
     I*                                    10181273 FILL6
     I*                                    12741325 FILL7
     I*                                    13281342 CUSTPO
     I*                                    13431529 FILL8
     I*                                    15301785 FILL9
     I*                                    17861906 FILL0
      *
      *
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C           SHIPTO    CHAIN@NEREAT              90
     C           *IN90     IFEQ '0'                        90 EQ 0
      *
      * IF USER ID IS NOT BLANK THEN EMAIL CUSTOMER
     C           NEPFNA    IFNE *BLANK                     IF USER NOT BLANK
     C                     MOVELCOMP#     XXCMP#           COMP# ALPHA
     C                     MOVELORDER#    XXORD#           ORDER# ALPHA
     C                     MOVE NEPFNA    XXUSID           CUST USER ID
     C                     MOVE NEUGTX    XXUGTX           EMAIL ADDR
     C                     MOVEL'Y'       XXEMYN           Send Email (Y/N)
     C                     MOVELCUSTPO    XXCSPO           CUST PO NBR
     C                     ENDIF                           IF USER NOT BLANK
      *
     C                     ENDIF                           90 = 0
      *
     C                     EXSR $ASNFL
      *
     C                     SETON                     LR
      /EJECT
      *---------------------------------------------------------------
      * $ASNFL - RETRIEVE THE ASN FLAG BY AR CUSTOMER
      *---------------------------------------------------------------
      *SZ - ADD EDI ASN FLAG
      *
     C           $ASNFL    BEGSR
     C           ARCUST    CHAIN@NEREAT              91
     C           *IN91     IFEQ '0'                        91 EQ 0
     C                     MOVELNEHVSX    XXHVSX           ASN FLAG  (Y/N))
     C                     MOVELNEJ5NX    XXTRDP           TRADE PARTNER
      *
     C                     ENDIF                           91 = 1
     C                     ENDSR
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
      * Parm list
      *
     C           *ENTRY    PLIST
     C                     PARM           XXPRM1
     C                     PARM           XXUSID  8
     C                     PARM           XXCMP#  3
     C                     PARM           XXORD#  7
     C                     PARM           XXEMYN  1
     C                     PARM           XXUGTX 50
     C                     PARM           XXCSPO 15
      *
      *SZ - ADD EDI ASN FLAG & TRADE PARTNER
     C                     PARM           XXHVSX  1
     C                     PARM           XXTRDP  6
      *
     C                     MOVE 'N'       XXEMYN
      *
     C                     ENDSR
