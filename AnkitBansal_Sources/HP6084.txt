      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6084
      * TITLE:       Low Inventory Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/19/04
      *
      * FUNCTION:  Batch program to highlight groups that have a low inventory.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE       PROGRAMMER
      *
      * 06/07//04  LeAnne Fedor
      *            We replaced the page break when Production Phase changes with just
      *            just printing a few blank lines to give some space.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp011    if   e           k disk
      *  Production phase
      *
      *
     Fhsp3084   if   e           k disk
      *  Workfile: Low inventory report
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D phasebreak      s              1    inz('N')
     D prtfl           s              1    inz('N')
     D svppcd          s                   like(w1ppcd)
     D svagewk         s                   like(w1agewk)
     D svageday        s                   like(w1ageday)
      *
      *
      * Print fields
      *
     d h1ef            s              9
      *
     D h2ppds          s                   like(ppppds)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfcymd                1     08  0
     D  ldtcymd                9     16  0
     D  ldper                 17     18  0
     D  ldeffl                19     19
     D  ldccyy                20     23  0
     D  ldfmdy                60     66  0
     D  ldtmdy                70     76  0
      *
     D  ldhd                 297    299  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print each group in the workfile.
      *
     C     *loval        setll     hsp3084
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3084                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      *
      * If nothing was printed, print the standard 'no data...' message
      *
     C                   if        prtfl = no
     C                   except    h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print detail
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      *
      * If Production Phase has changed, retrieve the Production Phase Description
      * and print some blank lines.
      *
     C                   if        svppcd <> w1ppcd
     C                   exsr      $ppds
     C                   endif
      *
      * Print headings
     C                   select
     C                   when      first = yes or *inof = *on
     C                   exsr      $h1hdr
      *
     C                   when      phasebreak = yes
     C                   except    h2hdr
     C                   move      no            phasebreak
      *
     C                   when      svagewk <> w1agewk or
     C                             svageday <> w1ageday
     C                   z-add     w1agewk       svagewk
     C                   z-add     w1ageday      svageday
     C                   except    blank
     C                   endsl
      *
      * Print detail
     C                   except    l1dtl
      *
      * Set flags to control processing
      *
     C                   move      yes           prtfl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print header
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
     C                   except    h2hdr
      *
     C                   move      no            first
     C                   move      no            phasebreak
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Production Phase description
      *---------------------------------------------------------------
      *
     C     $ppds         begsr
      *
     C                   move      w1ppcd        svppcd
      *
     C     svppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   move      ppppds        h2ppds
     C                   else
     C                   eval      h2ppds = 'Unknown'
     C                   endif
      *
      *
      * Print some blank lines.
      *
     C                   if        first = no                                   If not first
      *
     C                   do        4
     C                   if        *inof = *off
     C                   except    blank
     C                   move      yes           phasebreak
     C                   endif
     C                   enddo
      *
     C                   endif                                                  If not first
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Edit/Final/On-demand
      *
     C                   select
     C                   when      ldeffl = 'D'
     C                   movel(p)  'ON-DEMAND'   h1ef
      *
     C                   when      ldeffl = 'E'
     C                   movel(p)  '  EDIT   '   h1ef
      *
     C                   when      ldeffl = 'F'
     C                   movel(p)  '  FINAL  '   h1ef
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           82 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           81 'LOW INVENTORY REPORT'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          2
     O                       h1ef                76
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          1
     O                                           15 'From date .....'
     O                       ldfmdy        y     25
      *
      *
     O          e            h1hdr          1
     O                                           15 'To date .......'
     O                       ldtmdy        y     25
      *
      *
     O          e            h1hdr          1
     O                                           15 'Period/year ...'
     O                       ldper         z     19
     O                       ldccyy        z     25
      *
      *
     O          e            h1hdr          1
     O                                           15 'Cut-off head ..'
     O                       ldhd          j     26
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                           10 '-- AGE ---'
     O                                           21 'INVENTORY'
     O                                           27 'HOG'
     O                                           44 'OPENED'
     O                                           55 'DISPOSED'
     O                                           64 'FARM'
     O                                          115 'PRODUCTION'
      *
     O          e            h1hdr          1
     O                                           10 'WEEKS/DAYS'
     O                                           18 'HEAD'
     O                                           29 'GROUP'
     O                                           43 'DATE'
     O                                           53 'DATE'
     O                                           64 'SITE'
     O                                           73 'BLDG'
     O                                           80 'ROOM'
     O                                           99 'GROUP TYPE DESC'
     O                                          109 'TYPE'

     O          e            h1hdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Production phase heading
      *-------------------------------------------------------------
      *
     O          e            h2hdr       1  2
     O                                           17 'Production Phase:'
     O                       h2ppds              50
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       w1agewk       k      4
     O                       w1ageday      k      8
     O                       w1inhd        j     19
     O                       w1hgcd              31
      *
     O                       w1opdtmdy           45 '  /  /  '
     O                       w1dsdtmdy           55 '  /  /  '
      *
     O                       w1fscd        z     64
     O                       w1blcd              74
     O                       w1rmcd              81
      *
     O                       w1gtds              99
     O                       w1ptcd             110
      *
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
