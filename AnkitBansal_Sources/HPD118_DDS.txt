     A*%%TS  SD  20010718  112835  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD118
     A*  TITLE:         REVISE EXPENSE TICKET
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/05/94
     A*
     A*  INDICATORS:
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  63  DELETE
     A*  64  RESTRICTED USER
     A*  90  THE TICKET THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90N63                               CF04(04 'PROMPT')
     A N90N63                               CF05(05 'REFRESH')
     A N90N63                               CF09(09 'MAINTAIN')
     A N90 63                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP118R1
     A*%%TS  SD  20010718  112835  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDXBDT    R        H      REFFLD(XTXBDT)
     A            HDXEDT    R        H      REFFLD(XTXEDT)
     A            HDECCD    R        H      REFFLD(XTECCD)
     A            HDESCD    R        H      REFFLD(XTESCD)
     A            HDRPCD    R        H      REFFLD(XTRPCD)
     A            HDGRFL    R        H      REFFLD(AMGRFL)
     A            HDHGSN    R        H      REFFLD(XTHGSN)
     A            HDRGCD    R        H      REFFLD(XTRGCD)
     A            HDDVCD    R        H      REFFLD(XTDVCD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 27'REVISE EXPENSE TICKET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A N61                              4  2'Ticket number .....:'
     A N61        DFXTSN    R        O  4 25REFFLD(XTXTSN)
     A                                      EDTCDE(M)
     A                                  5  2'Begin date .........'
     A            DFBMDY    R        B  5 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  63
     AO 90                                  DSPATR(PR)
     A  26                                  DSPATR(PC)
     A  26                                  DSPATR(RI)
     A N26N63N90
     A  42                                  DSPATR(HI)
     A N26N63N90
     A  42                                  DSPATR(UL)
     A                                  6  2'End date ...........'
     A            DFEMDY    R        B  6 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  63
     AO 90                                  DSPATR(PR)
     A  27                                  DSPATR(PC)
     A  27                                  DSPATR(RI)
     A N27N63N90
     A  42                                  DSPATR(HI)
     A N27N63N90
     A  42                                  DSPATR(UL)
     A                                  7  2'Expense code .......'
     A N63                              7 23'?'
     A            DFEXCD    R        B  7 25REFFLD(XTEXCD)
     A  63
     AO 90                                  DSPATR(PR)
     A  29
     AO 59                                  DSPATR(PC)
     A  29                                  DSPATR(RI)
     A N29N63N90
     A  42                                  DSPATR(HI)
     A N29N63N90
     A  42                                  DSPATR(UL)
     A                                  8  2'Amount .............'
     A            DFXTAM    R        B  8 25REFFLD(XTREC/XTXTAM *LIBL/HSP048)
     A                                      EDTCDE(K)
     A  63
     AO 90                                  DSPATR(PR)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39N63N90
     A  42                                  DSPATR(HI)
     A N39N63N90
     A  42                                  DSPATR(UL)
     A                                  9  2'Quantity ...........'
     A            DFXTQT    R        B  9 28REFFLD(XTREC/XTXTQT *LIBL/HSP048)
     A                                      EDTCDE(K)
     A  63
     AO 90                                  DSPATR(PR)
     A  42N63N90                            DSPATR(HI)
     A  42N63N90                            DSPATR(UL)
     A                                 10  2'Pounds .............'
     A            DFXTLB    R        B 10 25REFFLD(XTREC/XTXTLB *LIBL/HSP048)
     A                                      EDTCDE(K)
     A  63
     AO 90                                  DSPATR(PR)
     A  42N63N90                            DSPATR(HI)
     A  42N63N90                            DSPATR(UL)
     A                                 11  2'Head ...............'
     A            DFXTHD    R        B 11 28REFFLD(XTREC/XTXTHD *LIBL/HSP048)
     A                                      EDTCDE(K)
     A  63
     AO 90                                  DSPATR(PR)
     A  42N63N90                            DSPATR(HI)
     A  42N63N90                            DSPATR(UL)
     A                                 12  2'Allocation method ..'
     A N63                             12 23'?'
     A            DFAMCD    R        B 12 25REFFLD(XTAMCD)
     A  63
     AO 90                                  DSPATR(PR)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N63N90
     A  42                                  DSPATR(HI)
     A N30N63N90
     A  42                                  DSPATR(UL)
     A                                 13  4'Hog group .........'
     A N63                             13 24'?'
     A            DFHGCD    R        B 13 26REFFLD(HGHGCD HSPREF)
     A  63
     AO 90                                  DSPATR(PR)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N63N90
     A  42                                  DSPATR(HI)
     A N31N63N90
     A  42                                  DSPATR(UL)
     A                                 13 36' -- OR --'
     A                                      DSPATR(HI)
     A                                 14  8'Farm site .........'
     A N63                             14 28'?'
     A            DFFSCD    R        B 14 30REFFLD(XTREC/XTFSCD *LIBL/HSP048)
     A                                      EDTCDE(M)
     A  63
     AO 90                                  DSPATR(PR)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N63N90
     A  42                                  DSPATR(HI)
     A N32N63N90
     A  42                                  DSPATR(UL)
     A                                 15  8'Building ..........'
     A N63                             15 28'?'
     A            DFBLCD    R        B 15 30REFFLD(XTREC/XTBLCD *LIBL/HSP048)
     A  63
     AO 90                                  DSPATR(PR)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N63N90
     A  42                                  DSPATR(HI)
     A N33N63N90
     A  42                                  DSPATR(UL)
     A                                 15 43'-- OR --'
     A                                      DSPATR(HI)
     A                                 16 12'Cost center .......'
     A                                 16 32'?'
     A            DFCJD     R        B 16 34REFFLD(@@CJD)
     A                                      CHECK(RB)
     A  63
     AO 90                                  DSPATR(PR)
     A  40
     AO 41                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N40N63N90
     A  42                                  DSPATR(HI)
     A N40N63N90
     A  42                                  DSPATR(UL)
     A                                 17 12'Production type ...'
     A N63                             17 32'?'
     A            DFPTCD    R        B 17 34REFFLD(XTREC/XTPTCD *LIBL/HSP048)
     A  63
     AO 90                                  DSPATR(PR)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N63N90
     A  42                                  DSPATR(HI)
     A N34N63N90
     A  42                                  DSPATR(UL)
     A                                 18 12'Production phase ..'
     A N63                             18 32'?'
     A            DFPPCD    R        B 18 34REFFLD(XTREC/XTPPCD *LIBL/HSP048)
     A  63
     AO 90                                  DSPATR(PR)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35N63N90
     A  42                                  DSPATR(HI)
     A N35N63N90
     A  42                                  DSPATR(UL)
     A                                 19 12'Business office ...'
     A  64                                  DSPATR(ND)
     A N63                             19 32'?'
     A  64                                  DSPATR(ND)
     A            DFALBO    R        B 19 34REFFLD(XTREC/XTALBO *LIBL/HSP048)
     A  64                                  DSPATR(ND)
     A  63
     AO 64
     AO 90                                  DSPATR(PR)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N63N90
     A N64 42                               DSPATR(HI)
     A N38N63N90
     A N64 42                               DSPATR(UL)
     A                                 20 12'Region ............'
     A  64                                  DSPATR(ND)
     A N63                             20 32'?'
     A  64                                  DSPATR(ND)
     A            DFRGCD    R        B 20 34REFFLD(XTREC/XTRGCD *LIBL/HSP048)
     A  64                                  DSPATR(ND)
     A  63
     AO 64
     AO 90                                  DSPATR(PR)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N63N90
     A N64 42                               DSPATR(HI)
     A N37N63N90
     A N64 42                               DSPATR(UL)
     A                                 21 12'Division ..........'
     A  64                                  DSPATR(ND)
     A N63                             21 32'?'
     A  64                                  DSPATR(ND)
     A            DFDVCD    R        B 21 34REFFLD(XTDVCD)
     A  64                                  DSPATR(ND)
     A  63
     AO 64
     AO 90                                  DSPATR(PR)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N63N90
     A N64 42                               DSPATR(HI)
     A N36N63N90
     A N64 42                               DSPATR(UL)
      *
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP118K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90N63                          23 11'F4=Prompt'
     A N90N63                          23 22'F5=Refresh'
     A N90    61                       23 34'F9=Create'
     A N90    60                       23 34'F9=Revise'
     A N90    63                       23 45'F11=Delete'
     A                                 23 57'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP118ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP118EC                   SFLCTL(HP118ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
