      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP140
      * TITLE:       REVISE HOG GROUP
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     04/06/94
      *
      * FUNCTION: USES AN EDIT RECORD FORMAT TO ADD/CHANGE/DELETE
      *           HOG GROUPS
      *
      *           THIS PROGRAM IS CALLED FROM HP440 - WORK WITH
      *           HOG GROUPS.
      *
      *           THE FIELD 'MODE' CONTAINS THE VALUE PASSED INTO
      *           THIS PROGRAM FROM THE CALLING PROGRAM.  THE FIELD
      *           'DFMODE' IS THE SCREEN FIELD THAT DISPLAYS THE
      *           VALUE OF 'MODE'.  MODE WILL BE CREATE, REVISE,
      *           RE-OPEN OR DISPOSE.  ONCE IN THE PROGRAM,
      *           MODE AND DFMODE WILL NEVER CHANGE.  HOWEVER, THE
      *           ACTION BEING PERFORMED CAN CHANGE DEPENDING ON THE
      *           FUNCTION KEYS THE USER TAKES.
      *
      *           IF THE USER IS REOPENING A HOG GROUP, THEN
      *           NO PANEL WILL BE DISPLAYED FROM THIS PROGRAM
      *           UNLESS THERE IS AN error.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/21/99  LeAnne Fedor
      *           We will now have a function to 'create' multiple groups on
      *           a farm.  Thus, in CREATE mode, this screen will be displayed
      *           for entry of default values.  When the user takes ENTER a
      *           subsequent program will be called for group entry.  To
      *           facilitate this, the fields that were not required until a
      *           group was disposed will now be required at the time of creating
      *           a group.
      *
      * 08/01/00  LeAnne Fedor
      *           Made 'breed stock' input capable and required. We have severed
      *           the relationship between genetic line and breed stock.
      *
      *           Made 'origin BGF farm', 'origin Nursery farm' and 'genetic line'
      *           output only. These fields will be non-display in CREATE mode.
      *           The system will now populate these fields during a nightly batch
      *           routine.
      *
      * 08/09/00  LeAnne Fedor
      *           I notice that the edit that checked for a match between the
      *           'business office' the user entered on the Work With and the
      *           'business office' for the farm the user entered in this function
      *           was missing.  This negates the security checks performed in the
      *           Work With.  I added the edit back in.
      *
      * 02/21/01  LeAnne Fedor
      *           Production type is now available as an input field on the screen.
      *
      * 07/11/01  LeAnne Fedor
      *           We are breaking the farm set up functions apart. The various set up
      *           pieces are being split among accounting/production/feed (Rindy/Anne Marie,
      *           Konner). So, we had to add a check that no group can go into a room that
      *           Konner has not set assigned a bin set to.
      *
      * 12/31/01  LeAnne Fedor
      *           Made 'average days old in' a required field for Nursery and Grow Finish groups.
      *
      * 07/24/02  LeAnne Fedor
      *           Added edits for various fields to enforce our new Production Phase concept.
      *
      * 11/26/02  LeAnne Fedor
      *           Added 'stocking density' and 'stayed/moved' codes and descriptions as output
      *           only fields.
      *
      * 12/19/02  LeAnne Fedor
      *           Since we have removed production phase/production type from the Expense Ticket
      *           and Feed Ticket files, we were able to change our logic here to allow the
      *           user to change the production type/phase for a group....even when it has
      *           feed and/or expenses....up until the time the group is opened.
      *
      * 01/09/02  LeAnne Fedor
      *           We have added active/inactive status to the Farm Master. So, when 'creating'
      *           a group, we will now edit that the farm is 'active'.
      *
      * 10/27/03  LeAnne Fedor
      *           Added logic to pass back out the original incoming Hog Group Code when the
      *           user attempted to change it to another number that already existed. Prior to
      *           this the program prevented the user from changing it (thus preventing dupli-
      *           cation of group codes) but passed back out the 'bad' value making it appear
      *           on the calling Work With that the value had been changed.)
      *
      * 04/05/04  LeAnne Fedor
      *           Found bug. Original deletion logic apparently checked for existing
      *           movements--but then did not issue a message or stop the user!!  This
      *           bug would only have occurred when the user attempted to delete an OPEN
      *           group with NO feed and NO expenses.
      *
      * 04/07/04  LeAnne Fedor
      *           Changed the deletion/void edits for FEED and EXPENSES. Originally,
      *           the user could VOID a group if the values netted to zero. Now, the
      *           values PER PERIOD must net to zero.
      *
      * 08/16/04  LeAnne Fedor
      *           There was a last-record bug in our 4/07/04 change. The bug bit if all
      *           the feed tickets were in the SAME period. It allowed a group to be VOIDED
      *           when it had feed tickets that did not net to zero in a period.
      *
      * 09/21/04  LeAnne Fedor
      *           Average Days Old In is no longer an entry field. It is output only and
      *           we derive a value when the group is created.
      *
      * 01/16/06  LeAnne Fedor
      *           To allow the users to assign multiple Bin Sets to a Room, we created
      *           a new file (HSP113-Rooms/Bin Sets) and logic to write records to the
      *           new file instead of plopping the "bin set" into the Room record when
      *           assigning Bin Sets to a Room in the Assign Bin Sets function.
      *           So, the edit here that a Room has been assigned a Bin Set has been
      *           changed to go against the new HSP113 file instead of HSP020-Building/Rooms.
      *
      * 02/21/08  LeAnne Ramsey
      *           We have changed our Purge function (which we have not ever run).
      *           We will now write the group record to a permanent purge file before
      *           deleting it from the Hog Group file. Why? Because Cognos/Datamart has
      *           a file at the Group level. We don't want the users to create new groups
      *           that have the same Hog Group Code as old/existing groups. So, we will
      *           now edit for a "hog group" in both HSP034-Hog Groups and our new
      *           permanent purge file HSP934-Purged Hog Groups.
      *
      * 05/13/09  LeAnne Ramsey
      *           Added logic to retrieve the overrides that a user may have entered
      *           for a group for: Origin BGF and NUR Farm Sites. These are output only
      *           fields; we only display them; we do not update HSP034 with them.
      *
      * 07/02/09  LeAnne Ramsey
      *           Added new field 'Continuous Flow' to the Hog Group file. So, added logic to
      *           edit; we will always default N=No.
      *
      * 08/17/09  LeAnne Ramsey
      *           Removed some out-dated documentation; no code change.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 03/25/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      * 12/13/23  Jagdish Mistry-Service Request-3000266,Project-S000266
      *           Do not allow the business to make the date created after any expenses
      *           incurred.
      *           This will assist in discrepancy reporting in cognos
      *           where some data is reported in a different period than what
      *           the business thinks it should be in.
      *           Also, do not allow the group to be deleted or voided if expenses
      *           have already been incurred.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD140    CF   E             WORKSTN INFDS(IOFEED)
      *
      *
     FHSP010    IF   E           K DISK
      *    Production type
      *
      *
     FHSP011    IF   E           K DISK
      *    Production phase
      *
      *
     FHSP017    IF   E           K DISK
      *    Feed group
      *
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP019    IF   E           K DISK
      *    FARM BUILDINGS
      *
     FHSP020    IF   E           K DISK
      *    BUILDING ROOMS
      *
      *
     FHSP025    IF   E           K DISK
      *    GROUP TYPE
      *
      *
     FHSP031    IF   E           K DISK
      *    BREED STOCK
      *
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSL034D   UF A E           K DISK    rename(hgrec:hgrecd)
      *    HOG GROUP
      *
      *
     FHSL038E   IF   E           K DISK
      *    FEED DETAIL
      *
      *
     FHSL048L   IF   E           K DISK
      *    EXPENSE TICKET
      *
      *
     FHSP056    IF   E           K DISK
      *  SEX
      *
      *
     FHSL058D   IF   E           K DISK
      *    MOVEMENT EVENT
      *
      *
     Fhsp105    if   e           k disk
      *    Stocking density
      *
      *
     Fhsp106    if   e           k disk
      *    Stayed/moved codes
      *
      *
     Fhsp113    if   e           k disk
      *    Rooms/Bin Sets
      *
      *
     Fhsp130    if   e           k disk
      *    Group overrides
      *
      *
     Fhsp934    if   e           k disk    rename(hgrec:hgrec934)
      *    Purged hog groups
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR error MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING error COUNT
      * FIELD CALLED 'error'. ALSO, DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D E                              3P 0
     D  error                         3P 0 overlay(E)
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D MSGFIL                        10    INZ('HSMSGF    ')
     ‚*
     ‚* Project-S000266-START
       dcl-s wkXTXTAM                Zoned(15:2) inz(*Zeros);
       dcl-s wkFDAM                  Zoned(15:2) inz(*Zeros);
     ‚* Project-S000266-END
     ‚*
     ‚* Project-S000266-START
     ‚* Added SQL process to program, this is standard for data read using SQL.
     ‚* Not all of below keywords are needed, but they are included due to standard.
        Exec sql Set option commit = *None,Naming = *Sys,CLOSQLCSR=*Endmod,
                   DATFMT=*USA,TIMFMT=*USA;
     ‚* Project-S000266-END
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO clear DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAHGSN'
      *---------------------------------------------------------------
     D                 DS
     D DAHGSN                         7  0
     D  HGSN                          7  0 overlay(DAHGSN)
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAAPER'
      *---------------------------------------------------------------
     D                 DS
     D DAAPER                        24
     D  DACCYY                        4    overlay(DAAPER)
     D  DAPER                         2    overlay(DAAPER:5)
     D  DABPDT                        8  0 overlay(DAAPER:7)
     D  DAEPDT                        8  0 overlay(DAAPER:15)
     D  DAPGFL                        1    overlay(DAAPER:23)
     D  DAPPFL                        1    overlay(DAAPER:24)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D DISPOS          C                   CONST('DISPOS')
     D REOPEN          C                   CONST('REOPEN')
     D VIEW            C                   CONST('VIEW  ')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
     D ADJUST          C                   CONST('A')
     D TRNSFR          C                   CONST('T')
     D SCHED           C                   CONST('SC')
     D VOID            C                   CONST('VD')
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D WKMDY                          6  0
     D  WKMM                          2  0 overlay(WKMDY)
     D  WKDD                          2  0 overlay(WKMDY:3)
     D  WKYY                          2  0 overlay(WKMDY:5)
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D WKCYMD                         8  0
     D  WKCEN                         2  0 overlay(WKCYMD)
     D  WKYER                         2  0 overlay(WKCYMD:3)
     D  WKMON                         2  0 overlay(WKCYMD:5)
     D  WKDAY                         2  0 overlay(WKCYMD:7)
      *
      *---------------------------------------------------------------------
      * Stand Alone Fields
      *---------------------------------------------------------------------
      *
     D @@NU50          s              5  0
     D @@NU92          s              9  2
     D ACTION          s                   like(DFMODE)
     D DLTFL           s              1
     D FEEDFL          s              1
     D OPENEDFL        s              1
     D GPIHD           s                   like(@@NU50)
     D INAHD           s                   like(@@NU50)
     D maxmsg          s              2  0
     D MODE            s                   like(DFMODE)
     D MORHD           s                   like(@@NU50)
     D MORLB           s                   like(@@NU92)
     D MSGFL           s             10
     D PCODE           s              1
     D PDAYS           s              4  0
     D PFRFMT          s              4
     D PFRM8           s              8  0
     D PGM             s             10
     D PINHD           s                   like(@@NU50)
     D PINLB           s                   like(@@NU92)
     D POUHD           s                   like(@@NU50)
     D POULB           s                   like(@@NU92)
     D PRTRN           s              1
     D PTOFMT          s              4
     D PTO8            s              8  0
     D QINHD           s                   like(@@NU50)
     D QINLB           s                   like(@@NU92)
     D QOUHD           s                   like(@@NU50)
     D QOULB           s                   like(@@NU92)
     D RINHD           s                   like(@@NU50)
     D RINLB           s                   like(@@NU92)
     D ROUHD           s                   like(@@NU50)
     D ROULB           s                   like(@@NU92)
     D routne          s              6
     D RTNCD           s              2
     D TIDPHD          s                   like(@@NU50)
     D TIDPLB          s                   like(@@NU92)
     D TINHD           s                   like(@@NU50)
     D TINLB           s                   like(@@NU92)
     D TISPHD          s                   like(@@NU50)
     D TISPLB          s                   like(@@NU92)
     D TODPHD          s                   like(@@NU50)
     D TODPLB          s                   like(@@NU92)
     D TOSPHD          s                   like(@@NU50)
     D TOSPLB          s                   like(@@NU92)
     D TOUHD           s                   like(@@NU50)
     D TOULB           s                   like(@@NU92)
     D WKADOI          s                   like(HGADOI)
     D WKADOO          s                   like(HGADOO)
     D WKCRDT          s                   like(HGCRDT)
     D WKDSDT          s                   like(HGDSDT)
     D WKFLAG          s              1
     D WKFSCD          s                   like(DFFSCD)
     D WKHD            s                   like(@@NU50)
     D WKHGCD          s                   like(HGHGCD)
     D WKHGSN          s                   like(HGHGSN)
     D WKPPCD          s                   like(HGPPCD)
     D WKPTCD          s                   like(HGPTCD)
     D WKUDAT          s                   like(HGDSDT)
     D WKZERO          s             15  2
      *
     D first           s              1
     D havebal         s              1
     D svacyr          s              4  0
     D svacpe          s              2  0
     D svadoi          s                   like(dfadoi)
     D chgadoifl       s              1
      *
      * Parms
      *
     D xxacyr          s              4  0
     D xxacpe          s              2  0
      *
     D XXAIST          s              1
     D XXHGCD          s                   like(DFHGCD)
     D XXALPHADOI      s              3
     D XXADOI          s                   like(DFADOI)
     D XXCRDTMDY       s                   like(DFCRDT)
     D XXCRDTCYMD      s                   like(WKCRDT)
     D XXBLCD          s                   like(DFBLCD)
     D XXBLDS          s                   like(DFBLDS)
     D XXBSCD          s                   like(DFBSCD)
     D XXBSDS          s                   like(DFBSDS)
     D XXFSBO          s                   like(DFFSBO)
     D XXALPHFSCD      s              5
     D XXFSCD          s                   like(DFFSCD)
     D XXFSNM          s                   like(DFFSNM)
     D XXGTCD          s                   like(DFGTCD)
     D XXGTDS          s                   like(DFGTDS)
     D XXMDY           s              6
     D XXMVSN          s              7
     D XXPPCD          s                   like(PPPPCD)
     D XXPPDS          s                   like(PPPPDS)
     D XXPTCD          s                   like(PTPTCD)
     D XXPTDS          s                   like(PTPTDS)
     D XXRMCD          s                   like(DFRMCD)
     D XXSXCD          s                   like(DFSXCD)
     D XXSXDS          s                   like(DFSXDS)
     D xxfgcd          s                   like(dffgcd)
     D xxfgds          s                   like(dffgds)
     ‚*
     D xxsdcd          s                   like(sdsdcd)
     D xxsdds          s                   like(sdsdds)
     ‚*
     D xxhgsn          s                   like(hghgsn)
     D xxdsdt          s                   like(hgdsdt)
     D xxadoo          s                   like(hgadoo)
     D xxcffl          s                   like(hgcffl)
     ‚*
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   movel     set1          routne
      *
     C                   dow       *inlr = *off                                 MAIN DO LOOP
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
      *
     C     routne        caseq     update        $upd1
      *
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear fields in the display file record
      *
     C                   eval      DFFSNM = *blank
     C                   eval      DFBLCD = *blank
     C                   eval      DFBLDS = *blank
     C                   eval      DFRMCD = *blank
     C                   eval      DFPTCD = *blank
     C                   eval      DFPTDS = *blank
     C                   eval      DFPPCD = *blank
     C                   eval      DFPPDS = *blank
     C                   eval      DFGTCD = *blank
     C                   eval      DFGTDS = *blank
     C                   eval      DFBSCD = *blank
     C                   eval      DFBSDS = *blank
     C                   eval      DFGNCD = *blank
     C                   eval      DFSXCD = *blank
     C                   eval      DFSXDS = *blank
     C                   eval      DFFGCD = *blank
     C                   eval      DFFGDS = *blank
     C                   eval      DFSDCD = *blank
     C                   eval      DFSDDS = *blank
     C                   eval      DFSMCD = *blank
     C                   eval      DFSMDS = *blank
     C                   z-add     0             DFFSCD
     C                   z-add     0             DFOBFS
     C                   z-add     0             DFONFS
     C                   z-add     0             DFADOI
     C                   z-add     0             DFADOO
     C                   z-add     0             DFCRDT
     C                   z-add     0             DFOPDT
     C                   z-add     0             DFDSDT
      *
      * IF INCOMING MODE IS 'CREATE',
      *   1) DEFAULT SYSTEM DATE INTO 'DATE CREATED' FIELD
      *   2) Default a N=No for Continuous Flow field
      *
     C                   if        MODE = CREATE                                IF CREATE
     C                   z-add     UDATE         DFCRDT
     C                   move      no            dfcffl
     C                   else
      *
      * IF INCOMING MODE IS NOT CREATE, THEN RETRIEVE DATABASE
      * RECORD USING THE HOG GROUP CODE THAT WAS PASSED IN.
      * DO NOT LOCK THE RECORD.
      *
     C     WKHGSN        chain(N)  HSL034D                            62
     C                   if        *IN62 = *off                                 IF FOUND
     C                   movel     HGHGCD        DFHGCD
     C                   z-add     HGHGSN        DFHGSN
     C                   z-add     HGFSCD        DFFSCD
     C                   movel     HGBLCD        DFBLCD
     C                   movel     HGRMCD        DFRMCD
     C                   movel     HGPTCD        DFPTCD
     C                   movel     HGPPCD        DFPPCD
     C                   movel     HGGTCD        DFGTCD
     C                   movel     HGSDCD        DFSDCD
     C                   movel     HGSMCD        DFSMCD
     C                   movel     HGBSCD        DFBSCD
     C                   movel     HGGNCD        DFGNCD
     C                   movel     HGSXCD        DFSXCD
     C                   movel     HGFGCD        DFFGCD
     C                   movel     hgcffl        dfcffl
     C                   z-add     HGOBFS        DFOBFS
     C                   z-add     HGONFS        DFONFS
     C                   z-add     HGADOI        DFADOI
     C                   z-add     HGADOO        DFADOO
     C                   z-add     HGCRDT        WKCRDT
      *
      * Retrieve any "origin overrides" that the user may have entered.
      *
     C     hghgsn        chain     hsp130                             92
     C                   if        *in92 = *off
     C                   z-add     goobfs        dfobfs
     C                   z-add     goonfs        dfonfs
     C                   endif
      *
      * If this is a Wean-to-finish Nursery group or any group that has a status
      * of Created, set the indicator that allows user-entry on Stocking Density.
      *
     C                   if        hggscd = 'CR' or
     C                             (hgppcd = 'NUR  ' and hggtcd = 'W')
     C                   seton                                        65
     C                   endif
      *
      * Set an indicator that allows user-entry on Average Days Old In.
      *
     C                   select
     C                   when      hggscd = 'DS' or hggscd = 'CR'
     C                   seton                                        66
      *
     C                   when      hggscd = 'OP' and hgppcd <> 'BGF  '
     C                             and (hggtcd = 'B' or hggtcd = 'G')
     C                   seton                                        66
     C                   endsl
      *
      * EXECUTE THE SUBROUTINE TO RETRIEVE VARIOUS 'DESCRIPTIONS' TO
      * DISPLAY ON THE SCREEN.
      *
     C                   exsr      $DESC1
      *
      * FLIP THE DATES FROM CCYYMMDD TO MMDDYY FOR DISPLAY PURPOSES
      *
     C                   exsr      $FLIP1
     C                   else
      *
      * HOG GROUP NOT FOUND
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00262'     MGID(E)
     C                   clear                   MGW
     C                   movea     DFHGCD        MGW(1)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   endif                                                  IF FOUND
     C                   endif                                                  IF CREATE
      *
      * IF YOU FOUND THE RECORD AND THE USER IS REOPENING,
      * GO TO THE UPDATE LOGIC. FOR ALL OTHERS CASES, DISPLAY THE
      * SCREEN FOR THE USER.
      *
     C                   if        error = 0 and action = reopen
     C                   movel     update        routne
     C                   else
     C                   movel     scrn1         routne
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      *  WRITE MESSAGES FROM ARRAYS TO MESSAGE SUBFILE
      *
     C                   exsr      $WRMSG
      *
      *  SET DSPF INDICATORS TO CONTROL INPUT/OUT FIELDS AND AVAILABLE
      *  FUNCTION KEYS.
      *
     C                   SELECT
     C                   when      MODE = REVISE
     C                   eval      *in60 = *on
      *
     C                   when      MODE = CREATE
     C                   eval      *in61 = *on
      *
     C                   when      MODE = DISPOS
     C                   eval      *in63 = *on
      *
     C                   when      MODE = REOPEN
     C                   eval      *in62 = *on
     C                   ENDSL
      *
      * IF THE GROUP IS DISPOSED, THEN F11=DELETE IS NOT
      * AN AVAILABLE FUNCTION.
      *
     C                   if        dfgscd = 'DS'
     C                   eval      *in64 = *on
     C                   endif
      *
     C                   if        feedfl = yes
     C                   seton                                        42
     C                   endif
      *
     C                   if        openedfl = yes
     C                   seton                                        45
     C                   endif
      *
     C                   eval      *in25 = *on
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   write     HP140K1
     C                   write     HP140EC
     C                   exfmt     HP140R1
      *
      * Clear messages
      *
     C                   exsr      $CLMSG
      *
      * RESET ACTION TO BE EQUAL TO THE INCOMING MODE.
      * THIS IS REQUIRED IN CASE THE USER TOOK F11=DELETE,
      * GOT AN error ON THE DELETION, AND NOW NEEDS TO BE ABLE TO
      * CHANGE THE RECORD AND TAKE AN ACTION OTHER THAN DELETE
      *
     C                   movel     MODE          ACTION
      *
      * GET USER'S INPUT AND SET ROUTINE
      *
     C                   SELECT
      *
     C                   when      *IN03 = *ON                                  F3=EXIT
     C                   movel     '03'          RTNCD
     C                   movel     EXIT          routne
      *
     C                   when      *IN04 = *ON                                  F4=PROMPT
     C                   exsr      $F4S1
      *
     C                   when      *IN12 = *ON                                  F12-PREVIOUS
     C                   movel     '12'          RTNCD
     C                   movel     EXIT          routne
      *
     C                   when      *IN05 = *ON                                  F5-REFRESH
     C                   movel     set1          routne
      *
     C                   when      *IN11 = *ON                                  F11-DELETE
     C                   if        DFGSCD = 'DS'                                IF DISPOSED
     C                   movel     scrn1         routne
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00237'     MGID(E)
     C                   endif
     C                   else
     C                   movel     DELETE        ACTION
     C                   movel     UPDATE        routne
     C                   endif                                                  IF DISPOSED
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      * *IN62 WILL BE ON IF THE RECORD THE USER IS TRYING TO ACCESS
      * HAS BEEN DELETED BY ANOTHER USER.  SO, ONLY PERFORM THE
      * EDITS FOR THE FIELDS ON THE SCREEN IF *IN62 IS OFF
      *
     C                   if        *IN62 = *off                                 IF HERE
     C                   movel     EDIT1         routne
     C                   endif                                                  IF HERE
     C                   ENDSL
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * IF YOU ARE REVISING A RECORD, VALIDATE THAT:
      *     HOG GROUP CODE WAS ENTERED
      *     HOG GROUP DOES NOT EXIST IN THE HOG GROUP MASTER IF CHANGED
      *
     C                   if        ACTION = REVISE and dfhgcd <> wkhgcd         If revise
     C                   if        DFHGCD = *blank                              IF NO GROUP
     C                   eval      *in31 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     MGID(E)
     C                   endif
     C                   else
      *
     C     dfhgcd        chain     hsp034                             92
     C                   if        *in92 = *off                                 IF FOUND
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00260'     MGID(E)
     C                   clear                   MGW
     C                   movea     DFHGCD        MGW(1)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   endif                                                  IF FOUND
      *
     C     dfhgcd        chain     hsp934                             92
     C                   if        *in92 = *off                                 If purged
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01233'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If purged
      *
     C                   endif                                                  IF NO GROUP
     C                   endif                                                  If revise
      *
      * IF FARM/BLDG/ROOM ARE NOT PROTECTED, VALIDATE:
      *
     C                   if        feedfl = no and
     C                             openedfl = no
     C                   exsr      $fscd
     C                   endif
      *
      *
      * Conditional validations when a group is still in CREATED status
      *
     C                   if        openedfl = no
     C                   exsr      $ptcd
     C                   exsr      $ppcd
     C                   exsr      $gtcd
     C                   endif
      *
      * Validate:
      *   1) stocking density (conditional)
      *   2) breed stock code
      *   3) sex code
      *   4) feed group code
      *   5) average days old in (conditional)
      *   6) continuous flow
      *   7) create date
      *
     C                   if        *in65 = *on
     C                   exsr      $sdcd
     C                   endif
      *
     C                   exsr      $BSCD
     C                   exsr      $SXCD
     C                   exsr      $fgcd
      *
     C                   if        *in66 = *on
     C                   exsr      $adoi
     C                   endif
      *
     C                   exsr      $cffl
     C                   exsr      $crdt
      *
      * DISPOSED DATE IS REQUIRED IN DISPOSE MODE; SO,
      *   EDIT DISPOSED DATE
      *   EDIT ALL MOVEMENTS FOR DISPOSED DATE > MOVEMENT DATE
      *
     C                   if        MODE = DISPOS                                IF DISPOSE
     C                   exsr      $DSDT
     C                   exsr      $DSMV
     C                   endif                                                  IF DISPOSE
      *
      * IF THERE ARE NO ERRORS, ATTEMPT THE WRITE/UPDATE; OTHERWISE,
      * REDISPLAY THE SCREEN.  IF A RECORD HAS BEEN DELETED OUT FROM
      * UNDER THE USER (*IN62 IS ON), THEN THE error MESSAGE WILL BE
      * ISSUED IN THE UPDATE SUBROUTINE.  SO, ALWAYS PERFORM THIS CODE.
      *
     C                   if        error = 0
     C                   exsr      $upd1
     C                   else
     C                   movel     scrn1         routne
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * EDIT FARM/BUILDING/ROOM
      *----------------------------------------------------------------
      *
     C     $fscd         begsr
     ‚*
     C                   eval      DFFSNM = *blank
     C                   eval      WKPTCD = *blank
     C                   eval      WKPPCD = *blank
      *
      *  FARM SITE VALIDATIONS
      *
     C                   if        DFFSCD = 0                                   IF NO FARM
     C                   eval      *in32 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     MGID(E)
     C                   endif
     C                   else
     ‚*
     C     DFFSCD        chain     HSP018                             92
     C                   if        *IN92 = *off                                 IF GOOD FARM
     C                   movel(P)  FSFSNM        DFFSNM
     C                   movel     FSPPCD        WKPPCD
     C                   movel     FSPTCD        WKPTCD
     ‚*
      *
      * If you are in 'create' mode, check that Farm is Active.
      *
     C                   if        mode = create and fsaist <> 'A'              If creating
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01206'     mgid(e)
     C                   endif
     C                   endif                                                  If creating
      *
      * Check for match on business office
      *
     C                   if        fsfsbo <> dffsbo                             If mismatch
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00158'     mgid(e)
     C                   clear                   mgw
     C                   move      dffscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     fsfsbo        mgw(6)
     C                   movea     dffsbo        mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If mismatch
     ‚*
     ‚*
     ‚* Invalid farm
     C                   else
     C                   eval      *in32 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00131'     MGID(E)
     C                   clear                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   movea     XXALPHFSCD    MGW(1)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   endif                                                  IF GOOD FARM
     C                   endif                                                  IF NO FARM
      *
      *  BUILDING VALIDATIONS
      *
     C                   if        DFBLCD = *blank                              IF NO BLDG
     C                   eval      *in33 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     MGID(E)
     C                   endif
     C                   else
      *
     C     K2P019        chain     HSP019                             92
     C                   if        *IN92 = *off                                 IF GOOD BLDG
     C                   movel(P)  FBBLDS        DFBLDS
     C                   else
     C                   eval      *in33 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00116'     MGID(E)
     C                   clear                   MGW
     C                   movea     DFBLCD        MGW(1)
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   movea     XXALPHFSCD    MGW(6)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   endif                                                  IF GOOD BLDG
     C                   endif                                                  IF NO BLDG
      *
      * VALIDATE ROOM
     C                   exsr      $rmcd
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate room
      *----------------------------------------------------------------
      *
     C     $rmcd         begsr
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   eval      *in34 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     MGID(E)
     C                   endif
     C                   other
     ‚*
     C     key03         chain     HSP020                             92
     C                   if        *in92 = *ON                                  IF BAD ROOM
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00117'     MGID(E)
     C                   clear                   MGW
     C                   movea     DFRMCD        MGW(1)
     C                   movea     DFBLCD        MGW(6)
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   movea     XXALPHFSCD    MGW(11)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   else
      *
      * ROOM MUST HAVE A STATUS OF 'ACTIVE'
      *
     C                   if        BRRMST <> 'A'                                IF NOT ACT
     C                   eval      *in34 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00726'     MGID(E)
     C                   clear                   MGW
     C                   movea     DFRMCD        MGW(1)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   endif                                                  IF NOT ACT
      *
      * Room must have a bin set assigned to it.
      *
     C     key03         chain     hsp113                             92
     C                   if        *in92 = *on                                  If no bin set
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00162'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfrmcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no bin set
      *
     C                   endif                                                  IF BAD ROOM
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate production type
      *----------------------------------------------------------------
      *
     C     $ptcd         begsr
      *
     C                   eval      dfptds = *blank
      *
     C                   select
     C                   when      mode = create
     C                   move      fsptcd        dfptcd
      *
     C                   when      dfptcd = *blank
     C                   if        mode <> create                               If not creating
     C                   seton                                        41
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   endif                                                  If not  creating
     C                   other
     ‚*
     C     dfptcd        chain     hsp010                             92
     C                   if        *in92 = *off                                 IF GOOD TYPE
     C                   movel(p)  ptptds        dfptds
     C                   else
     C                   seton                                        41
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00180'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfptcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  IF GOOD TYPE
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate production phase
      *----------------------------------------------------------------
      *
     C     $ppcd         begsr
      *
     C                   eval      dfppds = *blank
      *
     C                   select
     C                   when      mode = create
     C                   move      fsppcd        dfppcd
      *
     C                   when      dfppcd = *blank
     C                   if        mode <> create                               If not creating
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   endif                                                  If not creating
     C                   other
     ‚*
     C     dfppcd        chain     hsp011                             92
     C                   if        *in92 = *on                                  If bad
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00170'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfppcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     ‚*
     C                   movel(p)  ppppds        dfppds
     ‚*
     ‚* If the Production Phase of the farm is BGF, then the phase of this group
     ‚* has to be BGF.
      *
     C                   select
     C                   when      wkppcd = *blank
      *
     C                   when      (wkppcd = 'NUR  ' or wkppcd = 'GF   ') and
     C                             dfppcd = 'BGF  '
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01170'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfppcd        mgw(1)
     C                   movea     wkppcd        mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
      *
     C                   when      wkppcd = 'BGF  ' and dfppcd <> 'BGF  '
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01170'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfppcd        mgw(1)
     C                   movea     wkppcd        mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If bad
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate group type code
      *----------------------------------------------------------------
      *
      * In CREATE mode, 'group type' is nondisplay and we default in the value
      * associated with the Farm Site.
      *
     C     $gtcd         begsr
      *
     C                   eval      dfgtds = *blank
      *
     C                   select
     C                   when      dfmode = create
     C                   move      fsgtcd        dfgtcd
      *
     C                   when      dfgtcd = *blank
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     MGID(E)
     C                   endif
     C                   other
      *
     C     dfgtcd        chain     hsp025                             92
     C                   if        *in92 = *off                                 If valid type
     C                   movel(p)  gtgtds        dfgtds
      *
      * You can't have wean-to-finish groups when the production phase is BGF.
      *
     C                   if        dfppcd = 'BGF  ' and dfgtcd = 'W'
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01174'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfgtcd        mgw(1)
     C                   movea     dfppcd        mgw(2)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   else
     C                   eval      *in30 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00137'     MGID(E)
     C                   clear                   MGW
     C                   movea     DFGTCD        MGW(1)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   endif                                                  If valid type
     C                   endsl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Validate Stocking Density
      *--------------------------------------------------------------------------------------
      *
      * We have very conditional processing on Stocking Density. The first transfer into
      * a group finishes the Stocking Density and Stayed/Moved logic.
      *
     C     $sdcd         begsr
      *
     C                   move      *blank        dfsdds
      *
     C                   select
     C                   when      dfppcd = 'NUR  ' and dfgtcd = 'W'
      *
     C                   if        dfsdcd <> 'S' and dfsdcd <> 'D'
     C                   seton                                        40
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00841'     mgid(e)
     C                   endif
     C                   else
     C     dfsdcd        chain     hsp105                             92
     C                   if        *in92 = *off
     C                   move      sdsdds        dfsdds
     C                   endif
     C                   endif
     C                   other
      *
     C                   if        dfsdcd <> *blank
     C                   seton                                        40
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00843'     mgid(e)
     C                   endif
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE BREED STOCK CODE
      *----------------------------------------------------------------
      *
     C     $BSCD         begsr
      *
     C                   eval      DFBSDS = *blank
      *
     C                   select
     C                   when      DFBSCD = *blank
     C                   eval      *in36 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     MGID(E)
     C                   endif
     C                   other
      *
     C     dfbscd        chain     HSP031                             92
     C                   if        *in92 = *off                                 IF GOOD LINE
     C                   movel(P)  BSBSDS        DFBSDS
     C                   else
      *
      * INVALID BREED STOCK
      *
     C                   eval      *in36 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00251'     MGID(E)
     C                   clear                   MGW
     C                   movea     DFBSCD        MGW(1)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   endif                                                  IF GOOD LINE
     C                   endsl                                                  IF NO LINE
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE SEX
      *----------------------------------------------------------------
      *
     C     $SXCD         begsr
      *
     C                   eval      DFSXDS = *blank
      *
     C                   select
     C                   when      DFSXCD = *blank                              IF NO SEX
     C                   eval      *in39 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     MGID(E)
     C                   endif
     C                   other
      *
     C     DFSXCD        chain     HSP056                             92
     C                   if        *in92 = *off                                 IF GOOD SEX
     C                   movel(P)  SXSXDS        DFSXDS
     C                   else
      *
      * INVALID SEX
     C                   eval      *in39 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00209'     MGID(E)
     C                   clear                   MGW
     C                   movea     DFSXCD        MGW(1)
     C                   movea     MGW           MGDT(E)
     C                   endif
     C                   endif                                                  IF GOOD SEX
     C                   endsl                                                  IF NO SEX
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate Feed Group
      *----------------------------------------------------------------
      * Validations:
      *     1) Must be blank when Phase is Grow Finish or Nursery
      *     2) Required when Phase is BGF
      *
     C     $fgcd         begsr
      *
     C                   move      *blank        dffgds
      *
     C                   select
     C                   when      wkppcd = *blank
      *
     C                   when      (wkppcd = 'NUR  ' or wkppcd = 'GF   ')
     C                   if        dffgcd = *blank
     C                   else
     C                   seton                                        47
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00212'     mgid(e)
     C                   clear                   mgw
     C                   movea     wkppcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   when      wkppcd = 'BGF  '
     C                   if        dffgcd = *blank                              If BGF & blank
     C                   seton                                        47
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00213'     mgid(e)
     C                   clear                   mgw
     C                   movea     wkppcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
      *
     C     dffgcd        chain     hsp017                             92
     C                   if        *in92 = *off                                 If no hit
     C                   move      fgfgds        dffgds
     C                   else
     C                   seton                                        47
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00214'     mgid(e)
     C                   clear                   mgw
     C                   movea     dffgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not hit
     C                   endif                                                  If BGF & blank
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * RETRIEVE BREED STOCK DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $BSDS         begsr
      *
     C     DFBSCD        chain     HSP031                             92
     C                   if        *in92 = *off                                 IF GOOD BRED
     C                   movel(P)  BSBSDS        DFBSDS
     C                   else
     C                   movel(P)  'UNKNOWN'     DFBSDS
     C                   endif                                                  IF GOOD BRED
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Average Days Old In
      *----------------------------------------------------------------
      *
      * Users are required to enter the ADOI for non-BGF Boar or GDU groups;
      * and, we let them change the ADOI for any Disposed group.
      * Otherwise, we want to hold them to the defaults.
      *
      *
     C     $adoi         begsr
      *
     C                   select                                                 Select 1
     C                   when      *in48 = *on or *in30 = *on
      * BGF
     C                   when      dfppcd = 'BGF  '
     C                   if        dfadoi <> 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00842'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   when      dfgscd = 'DS'
      *
     C                   select                                                 Select 2
     C                   when      dfadoi = 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
      *
     C                   when      dfadoi < 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   endsl                                                  Select 2
      *
     C                   when      dfgtcd = 'B' or dfgtcd = 'G'
      *
     C                   select                                                 Select 2
     C                   when      dfadoi = 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00844'     mgid(e)
     C                   endif
      *
     C                   when      dfadoi < 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   endsl                                                  Select 2
     C                   other
      *
      * We are setting the default values in the remaining cases. So, we will
      * override any user entry.
      *
     C                   z-add     dfadoi        svadoi
     C                   exsr      $dftadoi
     C                   if        dfadoi <> svadoi
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00845'     mgid(e)
     C                   endif
     C                   endif
     C                   endsl                                                  Select 1
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Derive a default Average Days Old In
      *----------------------------------------------------------------
      *
     C     $dftadoi      begsr
      *
     C                   select
     C                   when      dfppcd <> 'BGF  ' and
     C                             (dfgtcd = 'B' or dfgtcd = 'G')
      *
     C                   when      dfppcd = 'BGF  '
     C                   z-add     0             dfadoi
      *
     C                   when      dfppcd = 'NUR  ' or
     C                             (dfppcd = 'GF   ' and dfgtcd = 'W')
     C                   z-add     18            dfadoi
     C                   other
      *
     C                   z-add     67            dfadoi
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate Continuous Flow flag
      *----------------------------------------------------------------
      *
     C     $cffl         begsr
      *
     C                   if        dfcffl <> yes and dfcffl <> no
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09106'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate create date
      *----------------------------------------------------------------
      *
     C     $crdt         begsr
      *
      * VALIDATE THAT THE CREATE DATE IS VALID AND ADD CENTURY VALUES
      * TO THE WORK FIELD FOR LATER USE IN THE UPDATING ROUTINE.
      *
     C                   z-add     DFCRDT        PFRM8
     C                   exsr      $DATE
     C                   if        PRTRN <> *blank                              IF BAD CRDT
     C                   eval      *in37 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   MOVE      'HS09004'     MGID(E)
     C                   endif
     C                   else
     C                   z-add     PTO8          WKCRDT
      *
      * SINCE CREATE DATE IS VALID, MAKE SURE IT IS NOT
      * LATER THAN OPEN OR DISPOSE DATE.
      *
     C                   if        HGOPDT <> 0
     C                             and WKCRDT > HGOPDT                          IF GT OPEN
     C                   eval      *in37 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   MOVE      'HS00500'     MGID(E)
     C                   endif
     C                   endif                                                  IF GT OPEN
     ‚*
     C                   if        HGDSDT <> 0
     C                             and WKCRDT > HGDSDT                          IF GT DISP
     C                   eval      *in37 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   MOVE      'HS00501'     MGID(E)
     C                   endif
     C                   endif                                                  IF GT DISP
     C                   endif                                                  IF BAD CRDT
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE DISPOSED DATE
      *----------------------------------------------------------------
      *
     C     $DSDT         begsr
      *
     C                   z-add     DFDSDT        PFRM8
     C                   exsr      $DATE
     C                   if        PRTRN <> *blank                              IF BAD DATE
     C                   eval      *in38 = *on
     C                   add       1             error
     C                   MOVE      'HS09004'     MGID(E)
     C                   else
     C                   z-add     PTO8          WKDSDT
      *
      * IF THE DATE IS VALID,
      *     CHECK THAT IT IS NOT IN A CLOSED PERIOD
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *
     C                   SELECT
     C                   when      WKDSDT < DABPDT
     C                   eval      *in38 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09007'     MGID(E)
     C                   endif
      *
      *         CHECK THAT DISPOSED DATE IS NOT GREATER THAN TODAY'S
      *
     C                   when      WKDSDT > WKUDAT
     C                   eval      *in38 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00426'     MGID(E)
     C                   endif
      *
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *
     C                   when      WKDSDT >= DABPDT
     C                             and WKDSDT <= DAEPDT
     C                             and DAPPFL <> *blank
     C                   eval      *in38 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09016'     MGID(E)
     C                   endif
     C                   ENDSL
      *
     C                   endif                                                  IF BAD DATE
      *
      * IF THE DISPOSED DATE IS STILL OK, CHECK TO SEE IF THE HOG
      * INVENTORY HAS COME TO ZERO AS OF THIS DATE--IF NOT, IT'S AN
      * error
      *
     C                   if        *IN38 = *off                                 IF STILL OK
     C                   MOVE      NO            WKFLAG                         REJ/QUA FLAG
      *
     C                   CALL      'HPMOVE'
     C                   PARM                    WKHGSN                         GROUP #
     C                   PARM                    WKDSDT                         CUT-OFF DATE
      * HEAD
     C                   PARM                    GPIHD                          GROUP INV
     C                   PARM                    PINHD                          PIG IN
     C                   PARM                    TINHD                          TRSF IN
     C                   PARM                    TISPHD                         SAME PHASE
     C                   PARM                    TIDPHD                         DIFF PHASE
     C                   PARM                    POUHD                          PIGS OUT
     C                   PARM                    TOUHD                          TRSF OUT
     C                   PARM                    TOSPHD                         SAME PHASE
     C                   PARM                    TODPHD                         DIFF PHASE
     C                   PARM                    RINHD                          REJECT IN
     C                   PARM                    ROUHD                          REJECT OUT
     C                   PARM                    QINHD                          QUALITY IN
     C                   PARM                    QOUHD                          QUALITY OUT
     C                   PARM                    MORHD                          MORTALITY
     C                   PARM                    INAHD                          INV ADJ
      * POUNDS
     C                   PARM                    PINLB                          PIG IN
     C                   PARM                    TINLB                          TRSF IN
     C                   PARM                    TISPLB                         SAME PHASE
     C                   PARM                    TIDPLB                         DIFF PHASE
     C                   PARM                    POULB                          PIG OUT
     C                   PARM                    TOULB                          TRSF OUT
     C                   PARM                    TOSPLB                         SAME PHASE
     C                   PARM                    TODPLB                         DIFF PHASE
     C                   PARM                    RINLB                          REJECT IN
     C                   PARM                    ROULB                          REJECT OUT
     C                   PARM                    QINLB                          QUALITY IN
     C                   PARM                    QOULB                          QUALITY OUT
     C                   PARM                    MORLB                          MORTALITY
      *
      * WHEN THE PROGRAM RETURNS WE WILL HAVE THE PIGS INVENTORY COUNT
      *
     C                   z-add     GPIHD         WKHD
     C                   if        WKHD <> 0                                    IF NOT GONE
     C                   eval      *in38 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     DFDSDT        XXMDY
     C                   clear                   MGW
     C                   movea     DFHGCD        MGW(1)
     C                   movea     XXMDY         MGW(8)
     C                   movea     MGW           MGDT(E)
     C                   movel     'HS00258'     MGID(E)
     C                   endif
     C                   endif                                                  IF NOT GONE
     C                   endif                                                  IF STILL OK
     C                   endsr
     -/EJECT
      *----------------------------------------------------------------
      * EDIT DISPOSED DATE > MOVEMENT DATE
      *----------------------------------------------------------------
      *
     C     $DSMV         begsr
      *
      * READ ALL MOVEMENT EVENT RECORDS FOR THE GROUP THAT HAVE AN
      * EVENT DATE THAT IS AFTER THE 'DISPOSE' DATE THAT THE USER HAS
      * ENTERED FOR THIS GROUP.
      *
     C                   eval      *in90 = *off
     C     K1L058        SETGT     HSL058D
      *
     C                   dou       *IN90 = *ON                                  DO EVENTS
     C     DFHGSN        READE     HSL058D                                90
     C                   if        *IN90 = *off                                 IF NOT EOF
      *
     C                   eval      *in38 = *on
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   MGW
     C                   movel     MEMVSN        XXMVSN
     C                   movea     XXMVSN        MGW(1)
     C                   movea     MGW           MGDT(E)
     C                   movel     'HS00297'     MGID(E)
     C                   endif
     C                   endif                                                  IF NOT EOF
     C                   enddo                                                  DO EVENTS
      *
     C                   endsr
     -/EJECT
      *----------------------------------------------------------------
      * WRITE/UPDATE/DELETE DATABASE FILE
      *----------------------------------------------------------------
      *
     C     $upd1         begsr
      *
      * IF USER ACTION IS REVISING/DELETING/DISPOSING/REOPENING, CHECK
      * THAT RECORD HE IS ACCESSING STILL EXISTS PRIOR TO ATTEMPTING TO
      * UPDATE.  LOCK THE RECORD FOR UPDATE.
      *
     C                   if        error = 0 and                                IF GOOD
     C                             (action = delete or
     C                              action = revise or
     C                              action = dispos or
     C                              action = reopen)
     C     WKHGSN        chain     HSL034D                            62
     C                   if        *in62 = *on and error < maxmsg               If not found
     C                   add       1             error
     C                   movel     'HS09008'     MGID(E)
     C                   endif                                                  If not found
     C                   endif                                                  IF GOOD
      *
      *  IF THE HOG GROUP RECORD STILL EXISTS, THEN
      *    PERFORM DELETION INTEGRITY CHECKS
      *
     C                   if        error = 0 and action = delete                IF DELETING
     C                   exsr      $dltck
     C                   endif                                                  IF DELETING
      *
      *--------------------------------------------------------------
      *  IF THERE ARE NO ERRORS, DELETE/DELETE/CREATE GROUP RECORD
      *--------------------------------------------------------------
      *
     C                   if        error = 0                                    If no error
      *
      * If the user has changed the Hog Group Code, put the new value into
      * the parm to pass back to the calling Work With. If the user did not
      * change the code, the parm value will already hold the correct value.
      *
     C                   if        wkhgcd <> dfhgcd
     C                   move      dfhgcd        xxhgcd
     C                   endif
      *
      * Delete
     C                   if        action = delete and dltfl = yes              If delete
     C                   delete    hgrecd
     C                   else
      *
      * Void: Set status to voided
      *
     C                   select
     C                   when      action = delete
     C                   movel     'VD'          HGGSCD
     C                   z-add     WKUDAT        HGVDDT
      *
      * Dispose: Set status to disposed and calculate the average days old 'out'
      *
     C                   when      action = dispos
     C                   movel     'DS'          HGGSCD
     C                   exsr      $ADOO
     C                   z-add     WKADOO        HGADOO
     C                   z-add     WKDSDT        HGDSDT
      *
      * Reopen: Set status to open; zero out disposed date; zero out average
      *         days old out.
      *
     C                   when      action = reopen
     C                   movel     'OP'          HGGSCD
     C                   z-add     0             HGDSDT
     C                   z-add     0             HGADOO
     C                   endsl
      *
      * Retrieve farm site defaults for the hog group fields
      *
     C     DFFSCD        chain     HSP018                             92
     C                   if        *in92 = *off
     C                   movel     FSCOCD        HGCOCD
     C                   movel     FSDVCD        HGDVCD
     C                   movel     FSRGCD        HGRGCD
     C                   movel     FSCRCD        HGCRCD
     C                   movel     FSFSBO        HGFSBO
     C                   movel     FSPPCD        HGPPCD
     C                   else
     C                   eval      HGCOCD = *blank
     C                   eval      HGDVCD = *blank
     C                   eval      HGRGCD = *blank
     C                   eval      HGCRCD = *blank
     C                   eval      HGFSBO = *blank
     C                   eval      HGPPCD = *blank
     C                   endif
      *
      * Check if the user changed the Average Days Old In.
      *
     C                   if        dfadoi <> hgadoi
     C                   move      yes           chgadoifl
     C                   else
     C                   move      no            chgadoifl
     C                   endif
      *
      *  MOVE DSPF FIELDS TO DATABASE FILE FIELDS
      *
     C                   movel     DFHGCD        HGHGCD
     C                   z-add     DFFSCD        HGFSCD
     C                   movel     DFBLCD        HGBLCD
     C                   movel     DFRMCD        HGRMCD
     C                   movel     DFPTCD        HGPTCD
     C                   movel     DFPPCD        HGPPCD
     C                   movel     DFGTCD        HGGTCD
     C                   movel     dfsdcd        hgsdcd
     C                   movel     DFBSCD        HGBSCD
     C                   movel     DFGNCD        HGGNCD
     C                   movel     DFSXCD        HGSXCD
     C                   movel     dffgcd        hgfgcd
     C                   movel     dfcffl        hgcffl
     C                   z-add     DFADOI        HGADOI
      *
      *
      * MOVE CREATE DATE WORK FIELD TO DATABASE FILE
      *
     ‚* Project-S000266-START
     ‚* Added sub routine getCosts to display error when date created is changed
     ‚* with non-zero costs.
     C                   If        HGCRDT <> WKCRDT And
     C                             action = revise  And
     C                             HGGSCD = 'CR'
     C                   ExSr      getCosts
     ‚* Display error message,Date created can not be changed.
     C                   If        wkXTXTAM <> *Zeros Or wkFDAM <> *Zeros
     C                   LeaveSr
     C                   EndIf
     C                   EndIf
     ‚* Project-S000266-END
     C                   z-add     WKCRDT        HGCRDT
      *
      * If creating, call the program that allows creation of multiple
      * hog groups for a farm site.
      *
     C                   if        action = create                              IF CREATE
     C                   exsr      $create
     C                   else
     C                   update    hgrecd
      *
      * If the group is Disposed, recalc the Average Days Old Out
      *
     C                   if        chgadoifl = yes                              If get adoo
     C     wkhgsn        chain     hsl034d                            92
     C                   if        *in92 = *off                                 If hit
      *
     C                   call      'HPPAGE'
     C                   parm      hghgsn        xxhgsn
     C                   parm      hgdsdt        xxdsdt
     C     hgadoo        parm      0             xxadoo
     C                   parm      0             xxadoi
     C                   update    hgrecd
     C                   endif                                                  If hit
     C                   endif                                                  If get adoo
      *
     C                   endif                                                  IF CREATE
      *
     C                   endif                                                  If delete
     C                   endif                                                  If no error
      *
      *  Redisplay the screen if:
      *   1) there are no errors, or
      *   2) the user has returned from the called create program with an
      *      F12=previous
      *  Otherwise, automatically exit the program.
      *
     C                   select
     C                   when      action = create and rtncd = '12'
     C                   movel     scrn1         routne
      *
     C                   when      error > 0
     C                   movel     scrn1         routne
     C                   other
      *
     C                   movel     exit          routne
     C                   endsl
      *
     C                   endsr
     -/EJECT
      *---------------------------------------------------------------
      * Create Hog Groups
      *---------------------------------------------------------------
      *
     C     $create       begsr
      *
     C                   select
     C                   when      dfptcd = *blank
     C                   move      wkptcd        xxptcd
     C                   other
     C                   move      dfptcd        xxptcd
     C                   endsl
      *
     C                   select
     C                   when      dfppcd = *blank
     C                   move      wkppcd        xxppcd
     C                   other
     C                   move      dfppcd        xxppcd
     C                   endsl
      *
      * Find the default Average Days Old In
      *
     C                   exsr      $dftadoi
      *
     C                   call      'HP142'
     C                   parm      dffsbo        xxfsbo
     C                   parm      dffscd        xxfscd
     C                   parm      dfblcd        xxblcd
     C                   parm      dfrmcd        xxrmcd
     C                   parm                    xxptcd
     C                   parm                    xxppcd
     C                   parm      dfgtcd        xxgtcd
     C                   parm      dfsxcd        xxsxcd
     C                   parm      dffgcd        xxfgcd
     C                   parm      dfcffl        xxcffl
     C                   parm      dfbscd        xxbscd
     C                   parm      dfadoi        xxadoi
     C                   parm      dfcrdt        xxcrdtmdy
     C                   parm      wkcrdt        xxcrdtcymd
     C                   parm      *blank        rtncd
      *
     C                   endsr
     -/EJECT
      *---------------------------------------------------------------
      * DELETION INTEGRITY CHECKS
      *---------------------------------------------------------------
      *
     C     $dltck        begsr
      *
      * RESET THE 'OK TO DELETE FLAG' TO YES
      *
     C                   move      yes           dltfl
      *
      *
      * A GROUP CANNOT BE DELETED OR VOIDED IF IT HAS ANY MOVEMENTS
      *
     C     hghgsn        chain     hsl058d                            92
     C                   if        *in92 = *off
     C                   move      no            dltfl
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00238'     mgid(e)
     C                   clear                   mgw
     C                   movea     hghgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
      * A GROUP CANNOT BE DELETED IF IT HAS FEED TICKETS;
      * IT MAY BE VOIDED IF THE VALUES FROM ALL ITS FEED
      * TICKETS NET TO ZERO FOR EACH PERIOD.
      *
     C                   exsr      $DLTFD
      *
      * A GROUP CANNOT BE DELETED IF IT HAS EXPENSE TICKETS;
      * IT MAY BE VOIDED IF THE VALUES FROM ALL ITS EXPENSE
      * TICKETS NET TO ZERO FOR EACH PERIOD.
      *
     C                   exsr      $DLTEX
      *
     C                   endsr
     -/EJECT
      *---------------------------------------------------------------
      * DELETION INTEGRITY CHECKS FOR FEED TICKETS
      *---------------------------------------------------------------
      *
     C     $dltfd        begsr
      *
     C                   z-add     0             svacyr
     C                   z-add     0             svacpe
     C                   z-add     0             wkzero
     C                   move      no            havebal
     C                   move      yes           first
      *
      * Read all the Feed Tickets for this group. If there are no feed tickets,
      * the group is eligible for deleting. If there are feed tickets, the group
      * can still be VOIDED if all the tickets NET TO ZERO for each accounting
      * period.
      *
     C     hghgsn        setll     hsl038e
      *
     C                   dou       *in91 = *on or havebal = yes                 Do feed
     C     hghgsn        reade     hsl038e                                91
     C                   if        *in91 = *off
      *
      * Get Accounting year/period associated with Ticket Date.
      *
     C                   call      'HP8015'
     C                   parm                    fdtkdt
     C                   parm      0             xxacyr
     C                   parm      0             xxacpe
      *
      * Control break
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     xxacyr        svacyr
     C                   z-add     xxacpe        svacpe
      *
     C                   when      xxacyr <> svacyr or
     C                             xxacpe <> svacpe
     C                   if        wkzero <> 0
     C                   move      yes           havebal
     C                   endif
     C                   z-add     0             wkzero
     C                   endsl
      *
     C                   add       fdfdlb        wkzero
     C                   add       fdinam        wkzero
     C                   add       fdmfam        wkzero
     C                   add       fddlam        wkzero
     C                   add       fdmdam        wkzero
     C                   endif
     C                   enddo                                                  Do feed
      *
      *
      * If there were ANY feed tickets for the group, the group cannot be
      * deleted. Also, do last-record checking for feed netting to zero.
      *
     C                   if        first = no                                   If had tickets
      *
     C                   move      no            dltfl
      *
     C                   if        wkzero <> 0
     C                   move      yes           havebal
     C                   endif
      *
     C                   endif                                                  If had tickets
      *
      *
      * If there is any Accounting Period where the Feed Tickets don't
      * net to zero, issue the 'Can't Void' message.
      *
     C                   if        havebal = yes
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00523'     mgid(e)
     C                   clear                   mgw
     C                   movea     hghgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * DELETION INTEGRITY CHECKS FOR EXPENSE TICKETS
      *---------------------------------------------------------------
      *
     C     $dltex        begsr
      *
     C                   z-add     0             svacyr
     C                   z-add     0             svacpe
     C                   z-add     0             wkzero
     C                   move      no            havebal
     C                   move      yes           first
      *
      * Read all the Expense Tickets for this group. If there are no tickets,
      * the group is eligible for deleting. If there are tickets, the group
      * can still be VOIDED if all the tickets NET TO ZERO for each accounting
      * period.
      *
     C     hghgsn        setll     hsl048l
      *
     C                   dou       *in91 = *on or havebal = yes                 Do expenses
     C     hghgsn        reade     hsl048l                                91
     C                   if        *in91 = *off
      *
      * Get Accounting year/period associated with Expense Ticket End Date.
      *
     C                   call      'HP8015'
     C                   parm                    xtxedt
     C                   parm      0             xxacyr
     C                   parm      0             xxacpe
      *
      * Control break
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     xxacyr        svacyr
     C                   z-add     xxacpe        svacpe
      *
     C                   when      xxacyr <> svacyr or
     C                             xxacpe <> svacpe
     C                   if        wkzero <> 0
     C                   move      yes           havebal
     C                   endif
     C                   z-add     0             wkzero
     C                   endsl
      *
     C                   add       xtxtam        wkzero
     C                   add       xtxthd        wkzero
     C                   add       xtxtlb        wkzero
     C                   add       xtxtqt        wkzero
     C                   endif
     C                   enddo                                                  Do expenses
      *
      *
      * If there were ANY expense tickets for the group, the group cannot
      * be deleted.
     C                   if        first = no
     C                   move      no            dltfl
     ‚* Project-S000266-START
     ‚* The below condition checks for expense cost for current period.
     C                   if        wkzero <> 0
     C                   move      yes           havebal
     C                   endif
     ‚* Project-S000266-END
     C                   endif
      *
      * If there is any Accounting Period where the Expenses don't
      * net to zero, issue the 'Can't Void' message.
      *
     C                   if        havebal = yes
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00524'     mgid(e)
     C                   clear                   mgw
     C                   movea     hghgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $ADOO -CALL THE PROGRAM TO CALCULATE THE AVERAGE DAYS OLD "OUT
      *---------------------------------------------------------------
      *
     C     $ADOO         begsr
      *
     C                   CALL      'HPPAGE'
     C                   PARM                    WKHGSN                         GROUP #
     C                   PARM                    WKDSDT                         DISPOSED DT
     C                   PARM                    WKADOO                         AGE OUT
     C                   PARM                    WKADOI                         AGE IN
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 PROMPT ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $F4S1         begsr
      *
      *
     C                   SELECT
      * Farm site
     C                   when      FLD = 'DFFSCD'
     C                             and *IN42 = *off
     C                   CALL      'HP5040'
     C                   PARM      'A'           XXAIST
     C                   PARM      0             WKFSCD
     C                   PARM      *blank        XXFSNM
     C                   eval      *in52 = *on
     C                   if        WKFSCD <> 0                                  IF SELECTED
     C                   z-add     WKFSCD        DFFSCD
     C                   movel     XXFSNM        DFFSNM
     C                   endif                                                  IF SELECTED
      *
      * Building
     C                   when      FLD = 'DFBLCD'
     C                             and *IN42 = *off
     C                   CALL      'HP523'
     C                   PARM      dffscd        WKFSCD
     C                   PARM      *blank        XXBLCD
     C                   PARM      *blank        XXBLDS
     C                   eval      *in53 = *on
     C                   if        XXBLCD <> *blank                             IF SELECTED
     C                   movel     XXBLCD        DFBLCD
     C                   movel     XXBLDS        DFBLDS
     C                   endif                                                  IF SELECTED
      *
      * Room
     C                   when      FLD = 'DFRMCD'
     C                             and *IN42 = *off
     C                   CALL      'HP524'
     C                   PARM      dffscd        WKFSCD
     C                   PARM      dfblcd        XXBLCD
     C                   PARM      *blank        XXRMCD
     C                   eval      *in54 = *on
     C                   if        XXRMCD <> *blank                             IF SELECTED
     C                   movel     XXRMCD        DFRMCD
     C                   endif                                                  IF SELECTED
      *
      * Production type
     C                   when      FLD = 'DFPTCD'
     C                             and *IN45 = *off
     C                   CALL      'HP504'
     C                   PARM      *BLANK        XXPTCD
     C                   PARM      *BLANK        XXPTDS
     C                   seton                                        51
     C                   if        XXPTCD <> *blank                             IF SELECTED
     C                   movel     XXPTCD        DFPTCD
     C                   movel     XXPTDS        DFPTDS
     C                   endif                                                  IF SELECTED
      *
      * Production phase
     C                   when      FLD = 'DFPPCD'
     C                             and *IN45 = *off
     C                   CALL      'HP507'
     C                   PARM      *BLANK        XXPPCD
     C                   PARM      *BLANK        XXPPDS
     C                   seton                                        58
     C                   if        XXPPCD <> *blank                             IF SELECTED
     C                   movel     XXPPCD        DFPPCD
     C                   movel     XXPPDS        DFPPDS
     C                   endif                                                  IF SELECTED
      *
      * Group type
     C                   when      FLD = 'DFGTCD'
     C                             and *IN45 = *off
     C                   CALL      'HP531'
     C                   PARM      *blank        XXGTCD
     C                   PARM      *blank        XXGTDS
     C                   eval      *in50 = *on
     C                   if        XXGTCD <> *blank
     C                   movel     XXGTCD        DFGTCD
     C                   movel     XXGTDS        DFGTDS
     C                   endif
      *
      * Stocking density
     C                   when      fld = 'DFSDCD' and *in65 = *on
     C                   call      'HP5024'
     C                   parm                    xxsdcd
     C                   parm                    xxsdds
     C                   seton                                        49
     C                   if        xxsdcd <> *blank
     C                   move      xxsdcd        dfsdcd
     C                   move      xxsdds        dfsdds
     C                   endif
      *
      * Breed stock
     C                   when      FLD = 'DFBSCD'
     C                   CALL      'HP516'
     C                   PARM      *blank        XXBSCD
     C                   PARM      *blank        XXBSDS
     C                   eval      *in56 = *on
     C                   IF        XXBSCD <> *blank                             IF SELECTED
     C                   MOVEL     XXBSCD        DFBSCD
     C                   MOVEL     XXBSDS        DFBSDS
     C                   ENDIF                                                  IF SELECTED
      *
      * Sex
     C                   when      FLD = 'DFSXCD'
     C                   CALL      'HP584'
     C                   PARM      *blank        XXSXCD
     C                   PARM      *blank        XXSXDS
     C                   eval      *in59 = *on
     C                   if        XXSXCD <> *blank                             IF SELECTED
     C                   movel     XXSXCD        DFSXCD
     C                   movel     XXSXDS        DFSXDS
     C                   endif                                                  IF SELECTED
      *
      * Feed group
     C                   when      FLD = 'DFFGCD'
     C                   call      'HP5013'
     C                   parm      *blank        xxfgcd
     C                   parm      *blank        xxfgds
     C                   seton                                        57
     C                   if        xxfgcd <> *blank                             IF SELECTED
     C                   movel     xxfgcd        dffgcd
     C                   movel     xxfgds        dffgds
     C                   endif                                                  IF SELECTED
      *
      * Continuous Flow
     C                   when      FLD = 'DFCFFL'
     C                   call      'HP522'
     C                   parm      *blank        xxcffl
     C                   seton                                        46
     C                   if        xxcffl <> *blank                             IF SELECTED
     C                   move      xxcffl        dfcffl
     C                   endif                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09011'     MGID(E)
     C                   endif
      *
     C                   ENDSL
      *
     C                   endsr
     -/EJECT
      *---------------------------------------------------------------
      * RETRIEVE VARIOUS DESCRIPTIONS FOR SCREEN1
      *---------------------------------------------------------------
      *
     C     $DESC1        begsr
      *
      * RETRIEVE FARM SITE DESCRIPTION
      *
     C     DFFSCD        chain     HSP018                             92
     C                   if        *IN92 = *off                                 IF FOUND
     C                   movel     FSFSNM        DFFSNM
     C                   movel     FSPPCD        WKPPCD
     C                   else
     C                   movel(P)  'UNKNOWN'     DFFSNM
     C                   endif                                                  IF FOUND
      *
      * RETRIEVE BUILDING DESCRIPTION
      *
     C     K2P019        chain     HSP019                             92
     C                   if        *IN92 = *off                                 IF FOUND
     C                   movel     FBBLDS        DFBLDS
     C                   else
     C                   movel(P)  'UNKNOWN'     DFBLDS
     C                   endif                                                  IF FOUND
      *
      * RETRIEVE PRODUCTION TYPE DESCRIPTION
      *
     C     DFPTCD        chain     HSP010                             92
     C                   if        *IN92 = *off                                 IF HIT
     C                   movel     PTPTDS        DFPTDS
     C                   else
     C                   movel(P)  'UNKNOWN'     DFPTDS
     C                   endif                                                  IF HIT
      *
      * RETRIEVE PRODUCTION PHASE DESCRIPTION
      *
     C     DFPPCD        chain     HSP011                             92
     C                   if        *IN92 = *off                                 IF HIT
     C                   movel     PPPPDS        DFPPDS
     C                   else
     C                   movel(P)  'UNKNOWN'     DFPPDS
     C                   endif                                                  IF HIT
      *
      *
      * RETRIEVE GROUP TYPE DESCRIPTION
      *
     C                   if        DFGTCD <> *blank
     C     DFGTCD        chain     HSP025                             92
     C                   if        *IN92 = *off                                 IF FOUND
     C                   movel     GTGTDS        DFGTDS
     C                   else
     C                   movel(P)  'UNKNOWN'     DFGTDS
     C                   endif                                                  IF FOUND
     C                   endif
      *
      *
      * RETRIEVE STOCKING DENSITY DESCRIPTION
      *
     C                   if        DFSDCD <> *blank
     C     DFSDCD        chain     HSP105                             92
     C                   if        *IN92 = *off                                 IF FOUND
     C                   movel     SDSDDS        DFSDDS
     C                   else
     C                   movel(P)  'UNKNOWN'     DFSDDS
     C                   endif                                                  IF FOUND
     C                   endif
      *
      *
      * RETRIEVE STAYED/MOVED CODE DESCRIPTION
      *
     C                   if        DFSMCD <> *blank
     C     DFSMCD        chain     HSP106                             92
     C                   if        *IN92 = *off                                 IF FOUND
     C                   movel     SMSMDS        DFSMDS
     C                   else
     C                   movel(P)  'UNKNOWN'     DFSMDS
     C                   endif                                                  IF FOUND
     C                   endif
      *
      * RETRIEVE BREED STOCK DESCRIPTION
      *
     C                   if        DFBSCD <> *blank
     C                   exsr      $BSDS
     C                   endif
      *
      *
      * RETRIEVE SEX DESCRIPTION
      *
     C                   if        DFSXCD <> *blank
     C     DFSXCD        chain     HSP056                             92
     C                   if        *IN92 = *off                                 IF FOUND
     C                   movel     SXSXDS        DFSXDS
     C                   else
     C                   movel(P)  'UNKNOWN'     DFSXDS
     C                   endif                                                  IF FOUND
     C                   endif
      *
      *
      * RETRIEVE FEED GROUP DESCRIPTION
      *
     C                   if        dffgcd <> *blank
     C     dffgcd        chain     hsp017                             92
     C                   if        *in92 = *off
     C                   move      fgfgds        dffgds
     C                   else
     C                   movel(p)  'Unknown'     dffgds
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *****************************************************************
      * FLIP DATES FROM CCYYMMDD TO MMDDYY
      *****************************************************************
      *
     C     $FLIP1        begsr
      *
      * FLIP CREATE DATE
      *
     C                   z-add     HGCRDT        WKCYMD
     C                   exsr      $TOMDY
     C                   z-add     WKMDY         DFCRDT
      *
      * FLIP DATE OPENED
      *
     C                   z-add     HGOPDT        WKCYMD
     C                   exsr      $TOMDY
     C                   z-add     WKMDY         DFOPDT
      *
      * FLIP DATE DISPOSED
      *
     C                   z-add     HGDSDT        WKCYMD
     C                   exsr      $TOMDY
     C                   z-add     WKMDY         DFDSDT
      *
     C                   endsr
      /EJECT
      ****************************************************************
      * MANIPULATE DATES WITH DATE UTILITY
      ****************************************************************
      *
     C     $DATE         begsr
      *
     C                   z-add     0             PTO8
     C                   movel     'MDY '        PFRFMT
     C                   movel     'CYMD'        PTOFMT
     C                   z-add     0             PDAYS
     C                   eval      PCODE = *blank
     C                   eval      PRTRN = *blank
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8
     C                   PARM                    PTO8
     C                   PARM                    PFRFMT
     C                   PARM                    PTOFMT
     C                   PARM                    PDAYS
     C                   PARM                    PCODE
     C                   PARM                    PRTRN
      *
     C                   endsr
      /EJECT
      *****************************************************************
      * FLIP DATE FROM YYMMDD TO MMDDYY
      *****************************************************************
      *
     C     $TOMDY        begsr
      *
     C                   z-add     WKMON         WKMM
     C                   z-add     WKDAY         WKDD
     C                   z-add     WKYER         WKYY
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $WRMSG        begsr
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    error
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C                   if        *IN43 = *ON
     C                   movel     set1          routne
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $CLMSG        begsr
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *blank        MGI
     C                   PARM      *blank        MGD
     C                   PARM      *ZEROS        error
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C                   if        *IN43 = *ON
     C                   movel     set1          routne
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * SET ON LR AND END JOB
      *----------------------------------------------------------------
      *
      * CALL UTILITY PROGRAM TO RECLAIM RESOURCES--CLOSE FILES
      * AND CLEANUP AFTER THE GENERIC PROGRAMS THAT DO NOT SETON
      * THE LAST RECORD INDICATOR.
      *
     C     $EXIT         begsr
     C                   CALL      'UTRCRSC'
     C                   eval      *inLR = *on
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
     ‚* Project-S000266-START
     ‚* Get active costs for Hog group code in current period
      *----------------------------------------------------------------
       BegSr getCosts;
         wkXTXTAM = *Zeros;
         wkFDAM   = *Zeros;
         //get sum of expense ticket amount
         Exec sql
           SELECT
                    SUM(C.XTXTAM) Into :wkXTXTAM
           FROM
                    HSP034 B,
                    HSP048 C
           WHERE
                    B.HGCOCD = C.XTCOCD
           AND      B.HGHGSN = C.XTHGSN
           AND      B.HGHGCD = :DFHGCD ;

         //get sum of feed ticket amount
         If wkXTXTAM = *Zeros;
         Exec sql
           SELECT
                  (SUM(FDINAM) + SUM(FDMDAM) + SUM(FDMFAM) + SUM(FDDLAM))
                    Into :wkFDAM
           FROM
                  HSP0071 A,
                  HSP034 B,
                  HSP038 C
           WHERE
                      A.CCCOCD = B.HGCOCD
                  AND B.HGHGSN = C.FDHGSN
                  AND B.HGHGCD = :DFHGCD;
         EndIf;

         //display error message on UI when Created Date can not be changed.
         If wkXTXTAM <> *Zeros Or wkFDAM <> *Zeros;
           *In37 = *On;
           error += 1;
           MGID(E) = 'HS09278';
           MGDT(E) = %Char(%date(WKCRDT:*iso):*MDY);
         EndIf;
       EndSr;
      *----------------------------------------------------------------
     ‚* Project-S000266-END
      *----------------------------------------------------------------
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        begsr
      *
      * KEY FOR MOVEMENT EVENT
      *
     C     K1L058        KLIST
     C                   KFLD                    DFHGSN
     C                   KFLD                    WKDSDT
      *
      *
     C     K2P019        KLIST
     C                   KFLD                    DFFSCD
     C                   KFLD                    DFBLCD
      *
     C     key03         KLIST
     C                   KFLD                    DFFSCD
     C                   KFLD                    DFBLCD
     C                   KFLD                    DFRMCD
      *
      * PARM LISTS
      *     MODE IS THE MODE COMING IN FROM THE WORK WITH.
      *     THE USER CAN COME IN IN REVISE MODE AND TAKE F11=DELETE
      *     WHICH WILL CHANGE THE PROCESSING TO THE 'DELETE' ACTION.
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE
     C                   PARM                    DFFSBO
     C                   PARM                    DFBODS
     C     DFHGCD        PARM                    XXHGCD
     C                   PARM                    WKHGSN
     C                   PARM                    DFGSCD
     C                   PARM                    RTNCD
      *
      * GLOBAL DEFINES
      *
     C                   eval      PGM = *blank                                 USED IN MSG
     C                   eval      MSGFL = *blank                               USED IN MSG
     C                   eval      routne = *blank
     C                   movel     MODE          ACTION
     C                   movel     MODE          DFMODE
     C                   movel     '00'          RTNCD                          RETURN CODE
      *
      * PARMS FOR F4=PROMPT CALLS AND MESSAGES
      *
     C                   eval      XXALPHFSCD = *blank
     C                   eval      XXALPHADOI = *blank
      *
      * DEFINE ALPHA FIELDS FOR PARMS
      *
     C                   eval      XXMDY = *blank
     C                   eval      XXMVSN = *blank
      *
      * DEFINE ACCUMULATOR AND FLAGS FOR DELETION INTEGRITY CHECKS
      *
     C                   movel     YES           DLTFL
     C                   z-add     0             WKZERO
      *
      * DEFINE PARAMETERS FOR THE MOVEMENT PROGRAM (HPMOVE)
      *
     C                   eval      WKFLAG = *blank
      * HEAD
     C                   z-add     0             @@NU50
      * POUNDS
     C                   z-add     0             @@NU92
      *
      *
      *
      * SAVE HOG GROUP CODE
      *
     C                   movel     DFHGCD        WKHGCD
      *
      * WHEN YOU ARE IN ANY MODE OTHER THAN CREATE:
      *      PROTECT FARM/BLDG/ROOM ONCE:
      *         a) THE HOG GROUP IS OPENED, OR
      *         b) ANY FEED DETAIL RECORDS EXIST FOR A 'CREATED' GROUP
      *      PROTECT PRODUCTION PHASE, PRODUCTION TYPE, GROUP TYPE ONCE:
      *         a) THE GROUP IS OPENED
      *
     C                   MOVEL     NO            FEEDFL
     C                   MOVEL     NO            OPENEDFL
      *
     C                   select
     C                   when      MODE = CREATE
      *
     C                   when      DFGSCD <> 'CR'
     C                   movel     YES           OPENEDFL
      *
     C                   when      DFGSCD = 'CR'
     C     WKHGSN        chain     HSL038E                            92
     C                   if        *IN92 = *off
     C                   movel     YES           FEEDFL
     C                   endif
     C                   endsl
      *
      * FORCE CURSOR TO GENETIC LINE FIELD ON FIRST DISPLAYING IN
      * DISPOSE MODE.
      * IF REVISE OR CREATE MOVE, POSITION CURSOR TO HOG GROUP
      *
     C                   if        MODE = DISPOS                                IF DISPOSE
     C                   eval      *in56 = *on
     C                   else
     C                   eval      *in55 = *on
     C                   endif                                                  IF DISPOSE
      *
      * THE SYSTEM DATE IS REQUIRED IN CCYYMMDD FORMAT FOR
      * EDITING AND UPDATING; SO, FLIP THE DATE.
      *
     C                   z-add     UDATE         PFRM8
     C                   z-add     0             PTO8
     C                   movel     'MDY '        PFRFMT
     C                   movel     'CYMD'        PTOFMT
     C                   z-add     0             PDAYS
     C                   eval      PCODE = *blank
     C                   eval      PRTRN = *blank
      *
     C                   exsr      $DATE
      *
     C                   if        PRTRN = *blank                               IF VALID
     C                   z-add     PTO8          WKUDAT
     C                   else
     C                   z-add     0             WKUDAT
     C                   endif                                                  IF VALID
      *
      * BRING IN THE DATA AREA THAT CONTAINS THE CURRENT ACCOUNTING
      * PERIOD
      *
     C     *DTAARA       DEFINE                  DAAPER
     C                   IN        *DTAARA
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD error MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND clearS THE PROGRAM MESSAGE QUEUE.
      *
     C                   z-add     20            maxmsg
     C                   movel     '*'           MSGPGM                         SET QUEUE
     C                   exsr      $CLMSG                                       clear MSG
      *
     C                   endsr
      /EJECT
