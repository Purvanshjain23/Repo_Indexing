     A*%%TS  SD  20090317  141233  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4012
     A*  TITLE:         WORK WITH DRIVERS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          12/23/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4012S1                  SFL
     A*%%TS  SD  20090212  133140  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1DICD    R        O  9  6REFFLD(HSRREF/@@DICD *LIBL/HSPREF)
     A            D1DINM    R        O  9 13REFFLD(HSRREF/@@DINM *LIBL/HSPREF)
     A            D1TICD    R        O  9 41REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A            D1TOCD    R        O  9 51REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A            D1SHIFT   R        O  9 65REFFLD(HSRREF/@@SHIFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1AIST    R        O  9 76REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4012C1                  SFLCTL(HP4012S1)
     A*%%TS  SD  20090317  141233  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 30'WORK WITH DRIVERS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  4 43'?'
     A                                      DSPATR(HI)
     A                                  4 53'?'
     A                                      DSPATR(HI)
     A                                  4 76'?'
     A                                      DSPATR(HI)
     A                                  5 51'Default'
     A                                      DSPATR(HI)
     A                                  5 73'Active/'
     A                                      DSPATR(HI)
     A                                  6  6'Driver'
     A                                      DSPATR(HI)
     A                                  6 40'Trucker'
     A                                      DSPATR(HI)
     A                                  6 51'Trans'
     A                                      DSPATR(HI)
     A                                  6 62'Default'
     A                                      DSPATR(HI)
     A                                  6 72'Inactive'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  6'Code'
     A                                      DSPATR(HI)
     A                                  7 13'Name'
     A                                      DSPATR(HI)
     A                                  7 41'Code'
     A                                      DSPATR(HI)
     A                                  7 51'Office'
     A                                      DSPATR(HI)
     A                                  7 63'Shift'
     A                                      DSPATR(HI)
     A                                  7 74'Status'
     A                                      DSPATR(HI)
     A            C1DICD    R        B  8  6REFFLD(HSRREF/@@DICD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1DINM    R        B  8 13REFFLD(HSRREF/@@DINM *LIBL/HSPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A            C1TICD    R        B  8 41REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1TOCD    R        B  8 51REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1SHIFT   R        B  8 65REFFLD(HSRREF/@@SHIFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A            C1AIST    R        B  8 76REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4012K1
     A*%%TS  SD  20011024  165925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F17=Print Options'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4012ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4012EC                  SFLCTL(HP4012ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
