      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF619
      * TITLE:       Product Exception Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     09/12/06
      *
      * FUNCTION:  Batch program to print a record(s) in the Product Exception file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/16/07  LeAnne Ramsey
      *           We switched the following flag fields:
      *             1) Exclude from Mix Flag    is now Mix Flag
      *             2) Exclude from Volume Flag is now Volume Flag
      *           Before this change Y=Yes meant EXCLUDE the product from processing
      *           After this change  Y=Yes means INCLUDE the product in processing
      *
      * 06/12/08  LeAnne Ramsey
      *           We have added fields to TFP019; the unique key to the file has changed.
      *           So, changed this program to read a logical that is keyed appropriately.
      *           Added more selectors for the report.
      *           Added more fields to the report.
      *
      * 07/14/08  LeAnne Ramsey
      *           We are now going to automatically generate this listing in
      *           the Margin Adjustment Close function. We had to change the
      *           LDA positions so that they were not in conflict with the
      *           LDA positions required for the Close. And, we added a report heading
      *           line indicating Preliminary/Final/On-Demand.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 02/02/09  LeAnne Ramsey
      *           Added "program" as a selection on the report.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl019c   if   e           k disk
      *    Product exceptions  (records selected by open query)
      *
      *
     Fombyrel1  if   e           k disk
      *    Item default
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     D dash30          s             30    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svprcd          s                   like(peprcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     d h1demand        s             35
     d h1prds          s             30
      *
     d l1wedtmdy       s              6  0
     d l1pgm           s              7
     d l1prds          s             30
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  lddemand               1      1
      *
     D  ldfwemdy              93     98  0
      *
     D  ldtwemdy             107    112  0
      *
     D  ldmixfl              136    136
     D  ldvolfl              137    137
     D  ldsysfl              138    138
      *
     D  ldtfcgcd             139    140
     D  ldtfclcd             156    158
     D  ldprcd               222    228  0
     D  ldcgcd               295    298
      *
     D  ldpgm                386    395
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read/print each record selected by the open query.
      *
     C     *loval        setll     tfl019c
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfl019c                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     peprcd        svprcd
      *
     C                   when      peprcd <> svprcd
     C                   z-add     peprcd        svprcd
     C                   except    blank
     C                   endsl
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------
      * EOF processing
      *---------------
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   eval      l1pgm = pepgm
      *
      * Flip Week-ending date to MMDDYY format
      *
     C     *iso          test(d)                 pewedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      pewedt        wkisodate
     C     *mdy          move      wkisodate     l1wedtmdy
     C                   endif                                                  If OK date
      *
      * Retrieve Product description
      *
     C     peprcd        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   move      byavna        l1prds
     C                   endif
      * Print
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Set up heading selections:
      *
      * Preliminary/Final/On-Demand
      *
     C                   select
     C                   when      lddemand = 'D'
     C                   eval      %subst(h1demand:14:9) = 'On-Demand'
      *
     C                   when      lddemand = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      lddemand = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      * Product
     C                   if        ldprcd = 0                                   If product
     C                   seton                                        99
     C                   else
      *
     C     ldprcd        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   movel(p)  byavna        h1prds
     C                   else
     C                   eval      h1prds = 'Unknown'
     C                   endif
     C                   endif                                                  If product
      *
     C                   if        ldmixfl = *blank
     C                   seton                                        98
     C                   endif
      *
     C                   if        ldvolfl = *blank
     C                   seton                                        97
     C                   endif
      *
     C                   if        ldsysfl = *blank
     C                   seton                                        96
     C                   endif
      *
     C                   if        ldcgcd = *blank
     C                   seton                                        95
     C                   endif
      *
     C                   if        ldtfcgcd = *blank
     C                   seton                                        94
     C                   endif
      *
     C                   if        ldtfclcd = *blank
     C                   seton                                        93
     C                   endif
      *
     C                   if        ldpgm = *blank
     C                   seton                                        87
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           95 'LISTING OF PRODUCT '
     O                                          105 'EXCEPTIONS'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           108
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           22 'From week-ending date:'
     O                       ldfwemdy            31 '  /  /  '
      *
     O                                           90 'Mix flag:'
     O               98                          95 'All'
     O              n98      ldmixfl             93
      *
     O                                          134 'TF Meat cost group code:'
     O               95                         139 'All'
     O              n95      ldcgcd             140
      *
     O                                          155 'Program:'
     O               87                         160 'All'
     O              n87      ldpgm              167
      *
     O          e            h1hdr          1
     O                                           22 'To week-ending date:'
     O                       ldtwemdy            31 '  /  /  '
      *
     O                                           90 'Volume flag:'
     O               97                          95 'All'
     O              n97      ldvolfl             93
      *
     O                                          134 'TF Class group code:'
     O               94                         139 'All'
     O              n94      ldtfcgcd           138
      *
      *
     O          e            h1hdr          2
     O                                           22 'Product code:'
     O               99                          27 'All'
     O              n99      ldprcd        z     31
     O              n99      h1prds              63
      *
     O                                           90 'System-generated flag:'
     O               96                          95 'All'
     O              n96      ldsysfl             93
      *
     O                                          134 'TF Classification code:'
     O               93                         139 'All'
     O              n93      ldtfclcd           139
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           16 'Week'
     O                                           32 '- Meat Cost -'
     O                                           50 'TF'
      *
     O                       dash30             147
     O                                          167 ' Adjustment Values '
     O                       dash30             198
      *
     O          e            h1hdr       0  1
     O                                            7 'Product'
     O                                           17 'Ending'
     O                                           29 'Sub'
     O                                           36 'Mix'
     O                                           52 'Class'
     O                                           57 'TF'
     O                                           64 'Item'
     O                                           82 '-Item Structure-'
     O                                           90 'PrdExc'
     O                                          103 '-- Flags --'
     O                                          136 'Produced'
     O                                          156 'Produced'
     O                                          177 'Sold'
     O                                          197 'Net Product'
      *
     O          e            h1hdr       0  1
     O                                            6 'Code'
     O                                           16 'Date'
     O                                           24 'Group'
     O                                           31 'Group'
     O                                           36 'Grp'
     O                                           45 'Program'
     O                                           52 'Group'
     O                                           59 'Class'
     O                                           64 'Type'
     O                                           70 'Type'
     O                                           76 'Group'
     O                                           82 'Class'
     O                                           90 'Number'
     O                                           95 'Mix'
     O                                           99 'Vol'
     O                                          103 'Sys'
     O                                          136 'Pounds'
     O                                          156 'Revenue Amount'
     O                                          177 'Pounds'
     O                                          197 'Amount'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       peprcd        z      7
     O                       l1wedtmdy      b    17 '  /  /  '
     O                       pecgcd              23
     O                       pesgcd              30
     O                       pemixgrp            35
     O                       l1pgm               45
     O                       petfcgcd       b    50
     O                       petfclcd            57
     O                       peitycd             64
      *
     O                       peistycd      k     70
     O                       peisgrcd      k     76
     O                       peisclcd      k     82
      *
     O                       pepesn        m     91
      *
     O                       pemixfl             94
     O                       pevolfl             98
     O                       pesysfl            102
      *
     O                                          114 'Seaboard:'
     O                       pesexpulb     k    137
     O                       pesexpuam     k    157
     O                       pesexsllb     k    178
     O                       pesexnpram    k    198
      *
     O          e            l1dtl          2
     O                       l1prds         b    50
     O                                           77 'Comment:'
     O                       peexcom            103
     O                                          114 'Triumph:'
     O                       petexpulb     k    137
     O                       petexpuam     k    157
     O                       petexsllb     k    178
     O                       petexnpram    k    198
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
