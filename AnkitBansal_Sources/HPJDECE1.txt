     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPJDECE1
      * TITLE:       JDE Cost Center validation for HPS from JDE Enterprise 1
      * PROGRAMMER:  Brad Baden   SDN465   TFS# 16169
      * CREATED:      3/13/2020
      *
      * FUNCTION:  THIS PROGRAM IS CALLED TO VALIDATE JDE COST CENTERS
      *            USED IN THE HOG PRODUCTION SYSTEM AGAINST THE JDE
      *            COST CENTER MASTER FILE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 09/21/94  ROSE MARTIN
      *           JDE RELEASE A6.1 COMPANY-CHANGED TO 5 POSITIONS.
      *  Modified by ----> BARB GUTIERREZ       Date--> 06/29/96
      *                    RECOMPILED FOR JDE RELEASE A7.1
      *
      *  3/12/2020 JBB E16169 - JDE World to JDE Enterprise 1
      *        Project SDN465   CVTRPGSRC for JFOODCUST/QRPGSRC for
      *                         member HPJDEC to E1IDEVGEN/QRPGLESEC
      *                         member HPJDECW.  Replace file F0006LB
      *                         with file E10006LB.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     F*F0006LB   IF   E           K DISK
     fE10006lb  if   e           k disk
      *    JDE COST CENTER MASTER (KEY: MCCO MCMCU)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
     D*
     D*  SETUP COMPANY NUMBER TO BE RIGHT JUSTIFIED ZERO FILLED.
     D*  IE. 100 = 00100
     D*
     D                 DS
     D  WKCO                   1      5
     D  WKCO1                  1      2    INZ('00')
     D  WKCO2                  3      5
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
     C     INPUT         TAG
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * CHAIN THE JDE COST CENTER MASTER TO VALIDATE THE NO. PASSED
      * THROUGH THE PARM. IF IT DOES NOT EXIST, MOVE THE ERROR MESSAGE
      * TO THE ARRAY.
     C*    KL0006        CHAIN     I0006                              90
     c     kl0006        chain     e10006lb
     C*    *IN90         IFEQ      *ON                                            NOT FOUND
     c                   if        not %found(e10006lb)                         if not%found(e10006l
     C                   MOVEL     YES           JDEFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS00210'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     INCJD         MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
      * IF COST CENTER RECORD FOUND,
      *        MOVE THE COST CENTER DESCRIPTION TO THE OUT PARM
      *
     C                   MOVEL     MCDL01        OUCJDS
     C                   ENDIF                                                  fi not%found(e10006l
      *
     C                   SETON                                        LR
      /EJECT
      ****************************************************************
      * SUBROUTINES
      ****************************************************************
      *
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    E                 3 0          # ERR MSGS
      *
      **** IN = SENT FROM CALLING PROGRAM
      *
     C                   PARM                    INCJD                          COST CENTER
     C                   PARM                    INCCO             3            COMPANY
      *
      **** OUT = RETURN TO CALLING PROGRAM
      *
     C                   PARM                    OUCJDS                         DESCRIPTION .
     C                   PARM                    JDEFL                          SECURITY ERROR FLG
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     NO            JDEFL             1
      *
      * SET UP WORK FIELDS FOR VALIDATIONS
      *
     C     *LIKE         DEFINE    MCMCU         INCJD
     C     *LIKE         DEFINE    MCDL01        OUCJDS
      *
      * KEY LIST
      *
      * F0006LB
     C     KL0006        KLIST
     C                   KFLD                    WKCO                           COMPANY
     C                   KFLD                    INCJD                          COST CENTER
      *
     C                   MOVE      INCCO         WKCO2
     C                   ENDSR
      /EJECT
