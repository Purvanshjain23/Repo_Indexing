     A*%%TS  SD  20080602  135344  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD401
     A*  TITLE:         WORK WITH CASH DISTRIBUTION DETAIL--USER GENERATED
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       04/24/08
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF401S1                   SFL
     A*%%TS  SD  20080602  132138  ISLEANNER   REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDDDT     R        H      REFFLD(@@DDT)
     A            HDCWEDT   R        H      REFFLD(@@CWEDT)
     A            HDRWEDT   R        H      REFFLD(@@RWEDT)
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1DMDY    R        O 11  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1RMDY    R        O 11 18REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1CMDY    R        O 11 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1SYSFL   R        O 11 42REFFLD(TFREF/@@SYSFL *LIBL/TFPREF)
     A            D1RCEXFL  R        O 11 53REFFLD(TFREF/@@RCEXFL *LIBL/TFPREF)
     A            D1BTSN    R        O 11 60REFFLD(TFREF/@@BTSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1MANUAL      10   O 11 69
     A            D1TEXT         7   O 12 21COLOR(PNK)
     A            D1AJRSN   R        O 12 29REFFLD(@@AJRSN)
     A                                      COLOR(PNK)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF401C1                   SFLCTL(TF401S1)
     A*%%TS  SD  20080602  135344  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 20'WORK WITH CASH DISTRIBUTION DETAIL'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 27'(I.S. Only Function)'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Options are only for Adjustment re-
     A                                      cords created by I.S. (System Gen F-
     A                                      lag = No)'
     A                                  5  3'2=Revise Manual Adjustment'
     A                                  5 31'6=Create Manual Adjustment'
     A                                  6 42'?'
     A                                      DSPATR(HI)
     A                                  6 53'?'
     A                                      DSPATR(HI)
     A                                  7 19'Revenue'
     A                                      DSPATR(HI)
     A                                  7 30'Cash'
     A                                      DSPATR(HI)
     A                                  7 40'System'
     A                                      DSPATR(HI)
     A                                  7 50'Record'
     A                                      DSPATR(HI)
     A                                  8  9'Daily'
     A                                      DSPATR(HI)
     A                                  8 18'Week End'
     A                                      DSPATR(HI)
     A                                  8 28'Week End'
     A                                      DSPATR(HI)
     A                                  8 38'Generated'
     A                                      DSPATR(HI)
     A                                  8 50'Exempt'
     A                                      DSPATR(HI)
     A                                  8 62'Batch'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  9'Date'
     A                                      DSPATR(HI)
     A                                  9 20'Date'
     A                                      DSPATR(HI)
     A                                  9 30'Date'
     A                                      DSPATR(HI)
     A                                  9 41'Flag'
     A                                      DSPATR(HI)
     A                                  9 51'Flag'
     A                                      DSPATR(HI)
     A                                  9 61'Number'
     A                                      DSPATR(HI)
     A            C1DMDY    R        B 10  7REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1RMDY    R        B 10 18REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1SYSFL   R        B 10 42REFFLD(TFREF/@@SYSFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1RCEXFL  R        B 10 53REFFLD(TFREF/@@RCEXFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1BTSN    R        B 10 60REFFLD(TFREF/@@BTSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF401K1
     A*%%TS  SD  20080602  112448  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F13=Fold/Unfold'
     A                                 23 64'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF401ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF401EC                   SFLCTL(TF401ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
