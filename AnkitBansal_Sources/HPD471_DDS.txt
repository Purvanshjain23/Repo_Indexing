     A*%%TS  SD  20060201  094718  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD471
     A*  TITLE:         SPECIFY PRELIMINARY DOP EDITS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/13/04
     A*  MODIFIED BY:   BARB GUTIERREZ   03/21/2016
     A*                 ADDED COMPANY AS SELECTION TO ACCOMODATE NEW
     A*                 IOWA FARMS (340) T005449.
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP471R1
     A*%%TS  SD  20060201  094718  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 23'SPECIFY PRELIMINARY EOP EDITS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Period:'
     A            D1ACPE    R        O  4 10REFFLD(@@ACPE)
     A            D1BMDY    R     Y  O  4 14REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  4 24'to'
     A            D1EMDY    R     Y  O  4 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  6  2'Company .......'
     A            D1COCD    R        B  6 18REFFLD(@@COCD)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  7  2'Cut-off date ..'
     A            D1CMDY    R     Y  B  7 18REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                  9  2'Please X each of the following lis-
     A                                      tings/reports that you wish to gene-
     A                                      rate:'
     A                                      DSPATR(HI)
     A            D1CLS          1A  B 11  5
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 11  8'Preliminary Period Close Error Lis-
     A                                      ting'
     A            D1GRP          1A  B 12  5
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 12  8'Preliminary Disposed Group Error L-
     A                                      isting'
     A            D1NEG          1A  B 13  5
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 13  8'Negative Inventory Exception Repor-
     A                                      t'
     A            D1LOW          1A  B 14  5
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 14  8'Low Inventory Report for groups wi-
     A                                      th a cut-off head value of:'
     A            D1HD           3Y 0B 14 70EDTCDE(J)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1EXP          1A  B 15  5
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 15  8'G/L Account/HPS Expense Code Excep-
     A                                      tion Report'
     A            D1FED          1A  B 16  5
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 16  8'Feed Report for Created Groups'
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP471K1
     A*%%TS  SD  20040513  143431  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F8=Submit'
     A N99                             23 44'F17=Print Options'
     A N99                             23 63'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP471ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP471EC                   SFLCTL(HP471ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
