/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES COMPANY, DATE, LOAD, AND       */
/*              CARRIER.                                            */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 12/18/96                                            */
/*                                                                  */
/*  PARAMETERS:                                                     */
/*  &RETURN     - RETURN CODE                                       */
/*  &APPLIC     - APPLICATION CODE                                  */
/*  &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT SCREEN)     */
/*  &OUTQ       - OUTPUT QUEUE                                      */
/*  &HOLD       - HOLD STATUS                                       */
/*  &SAVE       - SAVE STATUS                                       */
/*  &NQSTAT     - INDICATOR WHETHER MUST RUN IN NIGHT Q             */
/*  &NIGHTQ     - NIGHT JOB QUEUE                                   */
/*  &COMPNYN    - LOAD COMPANY NUMBER (PASSED)                      */
/*  &FRMDATN    - FROM DATE                                         */
/*  &THRDATN    - THRU DATE                                         */
/*  &LOADN      - LOAD NUMBER                                       */
/*  &CARRIER    - CARRIER CODE                                      */
/*  &COPYN      - NO. OF COPIES                                     */
/*  &CUST       - SHIP TO CUST                                      */
/*  &CNTRY1-5   - 5 COUNTRY CODES                                   */
/*  &WHS        - WAREHOUSE CODE                                    */
/********************************************************************/
/*  RMC 1/02/2001 ADD SALESPERSON AS INPUT PARM AND PASS TO CALLED  */
/*                PROGRAMS                                          */
/*  RMC 6/27/2003 ADDED REASON PARM                                 */
/*  SLM 1/06/2005 ADDED ORDER COMPANY, AND CHG DOCO ON COMPANY      */
/*                WILL NOW STATE LOAD COMPANY                       */
/*  LJB 12/04/2005 REMOVED SALESPERSON, REASON, LOADT, RPTSEL AND   */
/*                 UOM - THESE WERE REMOVED FROM THE CALLED PGM     */
/*  LJB 02/10/2006 CHANGED THE ORDER OF THE PARMS ON THE SBMJOB     */
/*  LJB 05/01/2007 THIS PROGRAM NOW RECEIVES PMGDUPC FROM THE       */
/*                 PROMPT PROGRAM                                   */
/* 01/30/2008 PKD E00025 ADDED WAREHOUSE & COUNTRY, PASS TO REPORT  */
/* 08/22/2008 LJB E00210 ADD COLLECT Y/N TO SCREEN AND PASS         */
/* 11/14/2008 LJB E00269 ADD EMAIL ADDRESS                          */
/* 11/17/2008 LJB E00281 ADD MULTIPLE COUNTRY CODES                 */
/********************************************************************/
 PPICUPC:    PGM        PARM(&RETURN &APPLIC &DESC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &COMPNYN &FRMDATN +
                          &THRDATN &LOADN &COPYN &CUST &COLLECT +
                          &CARRIER &CNTRY1 &CNTRY2 &CNTRY3 &CNTRY4 +
                          &CNTRY5 &WHS &EMAILYN &EMAIL)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Load Company   (Passed)   */
        DCL    &FRMDATN    *DEC                  /* FROM DATE      (Passed)   */
        DCL    &THRDATN    *DEC                  /* THRU DATE      (Passed)   */
        DCL    &LOADN      *DEC                  /* LOAD NUMBER    (Passed)   */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &CARRIER    *CHAR   (3)           /* CARRIER CODE              */
        DCL    &CNTRY1     *CHAR   (6)           /* COUNTRY                   */
        DCL    &CNTRY2     *CHAR   (6)           /* COUNTRY                   */
        DCL    &CNTRY3     *CHAR   (6)           /* COUNTRY                   */
        DCL    &CNTRY4     *CHAR   (6)           /* COUNTRY                   */
        DCL    &CNTRY5     *CHAR   (6)           /* COUNTRY                   */
        DCL    &WHS        *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &EMAIL      *CHAR   (50)
        DCL    &EMAILYN    *CHAR   (1)
        DCL    &CUST       *DEC                  /* Ship to Customer          */
        DCL    &COLLECT    *CHAR   (1)           /* COLLECT Y/N               */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   CALL PROMPT PROGRAM-HOLD IF REPORT IS TO BE EMAILED            */
/********************************************************************/
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&EMAIL *NE '                      ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*YES))
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM - SUBMITS PMGDUPC OVER THE             */
/*                             ORDER HEADER EXTENSION FILE          */
/********************************************************************/
             SBMJOB     CMD(CALL PGM(PDUJPCLP) PARM(&OUTQ &HOLD +
                          &SAVE &NIGHTQ &COMPNYN &FRMDATN &THRDATN +
                          &LOADN &COPYN &CUST &COLLECT &CARRIER +
                          &CNTRY1 &CNTRY2 &CNTRY3 &CNTRY4 &CNTRY5 +
                          &WHS &EMAILYN &EMAIL)) JOB(PDUJPCLP)

/*** TEST    CALL       PGM(PDUJPCLP) PARM(&OUTQ &HOLD &SAVE &NIGHTQ +
                          &COMPNYN &FRMDATN &THRDATN &LOADN &COPYN +
                          &CUST &COLLECT &CARRIER &CNTRY1 &CNTRY2 +
                         &CNTRY3 &CNTRY4 &CNTRY5 &WHS &EMAILYN &EMAIL)+
                                                               *****/
              GOTO       CMDLBL(END)
/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
