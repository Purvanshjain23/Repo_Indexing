      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3711-All Budgets: Close/Activate
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/25/04
      *
      * Function:    This function will be run on Saturday night prior to midnight, it:
      *                1) Reads all Active Farm Budgets, if the TO date is on/before
      *                   the system date, it sets the status to C=closed
      *                2) Reads all Inactive Farm Budgets, if the FROM date is on/before
      *                   the system date, it sets the status to A=Active and calculates
      *                   the Budget Amount for each Budget Item for each activated budget.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Cost Per Unit Amount was changed from 9,2 to 9,4.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp183    if   e           k disk
      *    Creation schedule
      *
      *
     Fhsp185    if   e           k disk
      *    Budget template header
      *
      *
     Fhsl188g   uf   e           k disk
      *    Farm budget header
      *
      *
     Fhsl188i   uf   e           k disk    rename(jhrec:jhreci) prefix(p1)
      *    Farm budget header
      *
      *
     Fhsp189    uf   e           k disk
      *    Farm budget detail
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
     D closed          c                   const('The following Active -
     D                                     budgets were Closed:')
      *
     D activated       c                   const('The following Inactive -
     D                                     budgets were Activated:')
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s             80    inz(*all'-')
     d rtime           s              6  0
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields
      *
     D wkisodate       s               D   datfmt(*iso)
     D wksysdt         s              8  0
      *
      *
      * Print fields
      *
     D h1desc          s             53
      *
     D l1csds          s                   like(cscsds)
     D l1fscd          s                   like(jhfscd)
     D l1fbsn          s                   like(jhfbsn)
     D l1btcd          s                   like(jhbtcd)
      *
      *
      * Parm fields
      *
     d xxprtfl         s              1
      *
      *
     D xxbgit          s                   like(jdbgit)
     D xxbgqt          s                   like(jdbgqt)
     D xxfbam          s                   like(jdfbam)
     D xxcpuam         s                   like(jdcpuam)
     D xxvol           s                   like(jdvol)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Look for Active Farm Budgets that must be Closed.
      *
     C                   exsr      $close
      *
      * Look for Inactive Farm Budgets that must be Activated.
      *
     C                   exsr      $activate
      *
      * EOF processing
      *
     C                   if        xxprtfl = yes
     C                   else
     C                   except    h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Close Farm Budgets
      *---------------------------------------------------------------
      *
     C     $close        begsr
      *
     C                   eval      h1desc = closed
     C                   seton                                        of
      *
      *
      * Process each Active Farm Budget Header record. If the TO date is
      * on/before to the System Date (and not zero), close the Farm Budget.
      *
     C     *loval        setll     hsl188i
      *
     C                   dou       *in91 = *on                                  Do close
     C                   read      hsl188i                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             p1jhbtdt <> 0 and
     C                             p1jhbtdt <= wksysdt
     C                   move      'C'           p1jhfbscd
     C                   update    jhreci
      *
      * Print
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   except    h2hdr
     C                   endif
      *
      * Populate print fields
      *
     C                   z-add     p1jhfscd      l1fscd
     C                   z-add     p1jhfbsn      l1fbsn
     C                   move      p1jhbtcd      l1btcd
      *
      * Retrieve the Creation Schedule description for printing.
      *
     C                   exsr      $csds
      *
     C                   except    l1dtl
     C                   move      yes           xxprtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do close
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Activate Farm Budgets
      *---------------------------------------------------------------
      *
     C     $activate     begsr
      *
     C                   eval      h1desc = activated
      *
      * Process each Inactive Farm Budget Header record. If the FROM
      * date is on/before the System Date, continue.
      *
     C     *loval        setll     hsl188g
      *
     C                   dou       *in91 = *on                                  Do inactive
     C                   read      hsl188g                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             jhbfdt <= wksysdt
      *
      *
      * Check to see if you already have an Active budget for this farm/
      * template. If you don't, then continue.
      *
     C     key01         chain     hsl188i                            92
     C                   if        *in92 = *on                                  If no active
      *
      * Make sure there is at least 1 Farm Budget Detail record. If so,
      * activate the budget.
      *
     C     jhfbsn        chain(n)  hsp189                             92
     C                   if        *in92 = *off                                 If some dtl
      *
     C                   move      'A'           jhfbscd
     C                   update    jhrec
      *
      * Go calculate the Budget Amount for each Farm Budget Detail Record
      *
     C                   exsr      $upd189
      *
      * Print
     C                   select
     C                   when      *inof = *on
     C                   except    h1hdr
     C                   except    h2hdr
      *
     C                   when      first = yes
     C                   move      no            first
     C                   except    h2hdr
     C                   endsl
      *
      * Populate print fields
      *
     C                   z-add     jhfscd        l1fscd
     C                   z-add     jhfbsn        l1fbsn
     C                   move      jhbtcd        l1btcd
      *
      * Retrieve the Creation Schedule description.
      *
     C                   exsr      $csds
      *
     C                   except    l1dtl
     C                   move      yes           xxprtfl
     C                   endif                                                  If some dtl
     C                   endif                                                  If no active
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do inactive
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------
      * Retrieve each Farm Budget Detail record and update with the Budget Amount
      *--------------------------------------------------------------------------
      *
     C     $upd189       begsr
      *
     C     jhfbsn        setll     hsp189
      *
     C                   dou       *in93 = *on                                  Do detail
     C     jhfbsn        reade     hsp189                                 93
     C                   if        *in93 = *off                                 If not eof
      *
     C                   call      'HP8701'
     C                   parm      jdbgit        xxbgit
     C                   parm      jdbgqt        xxbgqt
     C     jdvol         parm      0             xxvol
     C     jdcpuam       parm      0             xxcpuam
     C     jdfbam        parm      0             xxfbam
      *
     C                   update    jdrec
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve Creation Schedule Description
      *---------------------------------------------------------------
      *
     C     $csds         begsr
      *
      * Using the Budget Template value, retrieve the Template header
      * to get the Creation Schedule code--which you will use to get
      * the Creation Schedule description.
      *
     C     l1btcd        chain     hsp185                             92
     C                   if        *in92 = *off
      *
     C     thcscd        chain     hsp183                             92
     C                   if        *in92 = *off
     C                   move      cscsds        l1csds
     C                   else
     C                   movel     thcscd        l1csds
     C                   endif
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxprtfl
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    jhfscd
     C                   kfld                    jhbtcd
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Get the system date into CCYYMMDD format.
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wksysdt
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           30 'LISTING:'
     O                                           45 ' CLOSE/ACTIVATE'
     O                                           57 ' ALL BUDGETS'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            5 'Farm'
     O                                           15 'Budget'
     O                                           23 'Budget'
     O                                           36 'Creation'
      *
     O          e            h1hdr       0  1
     O                                            5 'Site'
     O                                           15 'Number'
     O                                           25 'Template'
     O                                           36 'Schedule'
      *
     O          e            h1hdr       0  1
     O                       dash                80
      *
     O          e            h2hdr       2  2
     O                       h1desc              53
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       l1fscd        zb     5
     O                       l1fbsn        zb    15
     O                       l1btcd         b    23
     O                       l1csds         b    48
      *
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1
     O                                           16 'No budgets were '
     O                                           33 'closed/activated.'
