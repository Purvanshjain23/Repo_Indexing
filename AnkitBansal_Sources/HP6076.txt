      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6076
      * TITLE:       Listing of Farms/Bins
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/06/00
      *
      * FUNCTION:  Batch program to list the farms with bins.
      *            This listing is submitted from HP4076-Specify Options for
      *            Farms/Bins Listing which is called with a function key
      *            from HP450-Work with Farms
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/27/01  LeAnne Fedor
      *           Added another listing selection: 'Shared Bins Only'
      *
      * 01/28/02  LeAnne Fedor
      *           Added more 'shared bin' logic. The logic now looks at 'active' rooms only..
      *           omitting inactive rooms. We also now call a generic program to make this
      *           shared determination. And, I added a new report selector option for 'room
      *           status'. You can now generate the report for active rooms only, inactive
      *           rooms only, or all rooms.
      *
      * 04/22/02  LeAnne Fedor
      *           We now use CELL as the primary sort field with a page break on Cell.
      *
      * 01/09/03  LeAnne Fedor
      *           Added a new selection on Farm Site status (active/inactive).
      *
      * 01/16/06  LeAnne Fedor
      *           Added logic to retrieve 'Bin Sets' for a Room from the new
      *           HSP113-Rooms/Bins Sets file.
      *
      * 03/12/09  LeAnne Ramsey
      *           Added a blank line between Buildings to improve readability.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 04/06/20  Danny Nguyen - P405 - Farm Number Increase
      *           Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *           Moved bnbncd 1 space to the right in the Output spec.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl018j   if   e           k disk
      *   Farm Site                (records selected by open query)
      *
      *
     Fhsp020    if   e           k disk
      *   Building/rooms           (records selected by open query)
      *
      *
     Fhsp036    if   e           k disk
      *   Bin set bins
      *
      *
     Fhsl036b   if   e           k disk    rename(bnrec:bnrecb) prefix(p1)
      *   Bin set bins
      *
      *
     Fhsp113    if   e           k disk
      *  Rooms/Bin Sets
      *
      *
     Fqprint    o    f   94        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             94
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D h1fsbo          s                   like(ldptcd)
     D h1ptcd          s                   like(ldptcd)
     D h1ppcd          s                   like(ldppcd)
     D h1yesno         s              3
      *
      *
      * Parm fields
      *
     D xxshared        s              1
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svcell          s                   like(fscell)
     D svfscd          s                   like(fsfscd)
     D svblcd          s                   like(brblcd)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfsbo                 1      5
     D  ldptcd                 6     10
     D  ldppcd                11     15
     D  ldcell                16     17  0
     D  ldfscd                18     22  0
      *
     D  ldbods                23     52
     D  ldptds                53     62
     D  ldppds                63     92
     D  ldfsnm                93    117
      *
     D  ldynfl               118    118
     D  ldrmst               119    119
     D  ldrmstds             120    139
     D  ldfsst               140    140
     D  ldfsstds             141    160
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read Farm Site records selected by the open query.
      *
     C     *loval        setll     hsl018j
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl018j                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        first = yes
     C                   seton                                        of
     C                   z-add     fscell        svcell
     C                   z-add     fsfscd        svfscd
     C                   move      no            first
     C                   endif
      *
     C                   seton                                        87
     C                   move      *blank        svblcd
      *
      * For this Farm:
      *   Process each "active" Bldg/Room selected by the Open Query.
      *     For each Room, process each Bin Set assigned to the Room.
      *
     C     fsfscd        setll     hsp020
     C                   dou       *in91 = *on                                  Do rooms
     C     fsfscd        reade     hsp020                                 91
     C                   if        *in91 = *off                                 If active room
     C                   exsr      $binsets
     C                   endif                                                  If active room
     C                   enddo                                                  Do rooms
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Process each Bin Set assigned to the Room
      *---------------------------------------------------------------
      *
     C     $binsets      begsr
      *
      * Read each record in the 'Rooms/Bin Sets' file for the Farm/Bldg/Room:
      *       For each record, go retrieve/print Bins.
      *
     C     key02         setll     hsp113
      *
     C                   dou       *in93 = *on                                  Do bin sets
     C     key02         reade     hsp113                                 93
     C                   if        *in93 = *off                                 If bin set
     C                   exsr      $bins
     C                   endif                                                  If bin set
     C                   enddo                                                  Do bin sets
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print bins
      *---------------------------------------------------------------
      *
     C     $bins         begsr
      *
     C     key01         setll     hsp036
     C                   dou       *in94 = *on                                  Do bins
     C     key01         reade     hsp036                                 94
     C                   if        *in94 = *off                                 If bins
      *
      * Determine if bin is direct or shared
      *
     C                   exsr      $shared
      *
      * Print the deatil line if the user:
      *  a) selected all bins, or
      *  b) selected only shared bins and this bin is shared
      *
     C                   if        ldynfl = no or                               If print
     C                             (ldynfl = yes and *in97 = *on)
      *
      * Insert a blank line before each Farm and/or Building..except when you are already
      * going to a new page.
      *
     C                   select
     C                   when      *inof = *on or
     C                             fscell <> svcell
     C                   except    h1hdr
     C                   seton                                        87
      *
     C                   when      fsfscd <> svfscd
     C                   except    blank
      *
     C                   when      rbblcd <> svblcd
     C                   except    blank
     C                   endsl
      *
     C                   z-add     fscell        svcell
     C                   z-add     fsfscd        svfscd
     C                   move      rbblcd        svblcd
      *
     C                   except    l1dtl
     C                   setoff                                       87
     C                   move      yes           prtfl
     C                   endif                                                  If print
      *
     C                   endif                                                  If bins
     C                   enddo                                                  Do rooms
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Determine if bin is shared or direct
      *---------------------------------------------------------------
      *
      *
     C     $shared       begsr
      *
      * Set off the 'shared' indicator.
      *
     C                   setoff                                       97
      *
      * Call the generic program to determine if the bin is shared.
      *
     C                   call      'HP8004'
     C                   parm                    fsfscd
     C                   parm                    bnbncd
     C                   parm      no            xxshared
      *
     C                   if        xxshared = yes
     C                   seton                                        97
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    rbfscd
     C                   kfld                    rbbicd
      *
     C     key02         klist
     C                   kfld                    brfscd
     C                   kfld                    brblcd
     C                   kfld                    brrmcd
      *
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up headings
      *
     C                   if        ldfsbo = *blank
     C                   movel     'ALL'         h1fsbo
     C                   else
     C                   move      ldfsbo        h1fsbo
     C                   endif
      *
     C                   if        ldptcd = *blank
     C                   movel     'ALL'         h1ptcd
     C                   else
     C                   move      ldptcd        h1ptcd
     C                   endif
      *
     C                   if        ldppcd = *blank
     C                   movel     'ALL'         h1ppcd
     C                   else
     C                   move      ldppcd        h1ppcd
     C                   endif
      *
     C                   if        ldcell = 0
     C                   seton                                        88
     C                   endif
      *
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   endif
      *
     C                   select
     C                   when      ldynfl = no
     C                   move      'No '         h1yesno
     C                   when      ldynfl = yes
     C                   move      'Yes'         h1yesno
     C                   endsl
      *
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           76 'DATE'
     O                       udate         y     86
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           52 'LISTING OF FARMS/BINS'
     O                                           76 'TIME'
     O                       rtime               86 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           76 'PAGE'
     O                       page          z     86
      *
      *
     O          e            h1hdr          1
     O                                           20 'Business office ...:'
     O                       h1fsbo              27
     O                       ldbods              59
      *
     O          e            h1hdr          1
     O                                           20 'Production type ...:'
     O                       h1ptcd              27
     O                       ldptds              39
      *
      *
     O          e            h1hdr          1
     O                                           20 'Production phase ..:'
     O                       h1ppcd              27
     O                       ldppds              59
      *
      *
     O          e            h1hdr          1
     O                                           20 'Cell ..............:'
     O              N88      ldcell        z     24
     O               88                          25 'ALL'
      *
      *
     O          e            h1hdr          1
     O                                           20 'Farm status .......:'
     O                       ldfsstds            42
      *
      *
     O          e            h1hdr          1
     O                                           20 'Farm site .........:'
     O              N89      ldfscd        z     27
     O              N89      ldfsnm              54
     O               89                          25 'ALL'
      *
     O          e            h1hdr          1
     O                                           20 'Room status .......:'
     O                       ldrmstds            42
      *
     O          e            h1hdr          2
     O                                           20 'Shared bins only ..:'
     O                       h1yesno             25
      *
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           11 'FARM'
     O                                           18 'FARM'
     O                                           39 'ROOM'
     O                                           45 'BIN'
     O          e            h1hdr          1
     O                                            4 'CELL'
     O                                           11 'SITE'
     O                                           19 'STATUS'
     O                                           25 'BLDG'
     O                                           32 'ROOM'
     O                                           40 'STATUS'
     O                                           45 'SET'
     O                                           52 'BIN'
     O                                           65 'FARM NAME'
      *
     O          e            h1hdr          1
     O                       dash                94
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       fscell        z      3
     O               87      fsfscd        z     11
     O                       fsaist              17
     O                       rbblcd              26
     O                       rbrmcd              33
     O                       brrmst              38
     O                       rbbicd              47
P405 O**                     bnbncd              54
P405 O                       bnbncd              55
     O               87      fsfsnm              81
     O               97                          94 'Shared bin'
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
