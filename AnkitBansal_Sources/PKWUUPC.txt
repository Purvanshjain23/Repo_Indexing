/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PKWUUPC                                       */
/*  PROGRAM NAME:    PAYMENT PRODUCER EDIT UNDO                    */
/*  PROGRAMMER:      ROSE CENTONZE                                 */
/*  CREATE DATE:     02/13/01                                      */
/*                                                                 */
/*  NOTE  ***** MUST BE COMPILED USER PROFILE(*OWNER)   *****      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE MPR PAYMENT EDIT*/
/*                   ROUTINE AT 5 A.M.                             */
/*                   IT WILL UNDO THE MPR PAYMENT EDIT SO THEY CAN */
/*                   BEGIN THEIR DAILY PROCESSING AS IF IT DIDNT   */
/*                   HAPPEN.                                       */
/* STEPS:                                                          */
/* 1) DELETE CARCASS DETAIL WITH TIME STAMP 'DATE ADDED' = TODAY   */
/* 2) CHANGE PROCESS CODE TO "RECV" FOR ALL TATTOOS FOR KILL DATE  */
/* 3) CHANGE PROCESS CODE TO "RECV" FOR TATTOOS HELD OVER F/PREV DY */
/* 4) DELETE COMPANY KILL RECAP WITH THIS KILL DATE               */
/* 5) DELETE COMPANY KILL EXPENSE RATE WITH THIS KILL DATE         */
/* 6) CHANGE PAYMENT STEP TO 1.  JANCIE CAN DO WHAT SHE NEEDS NOW. */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*   RMC 4/11/01  IF FRIDAY, RUN UNDO FOR SATURDAY TOO.            */
/*   RMC 4/18/01  IN TATTOO HEADER, ZERO OUT THE HELD OVER CMPLT DT */
/*                & ADD FLAG TO SEE IF SATURDAY SHOULD BE UNDONE TOO */
/*                BECAUSE THIS IS ALSO CALLED FROM A PROMPT, WHERE */
/*                ONLY FRIDAY WOULD NEED TO BE UNDONE              */
/*   RMC 4/27/01  UNDO LOSS TRANS -  DELETE RCDS ADDED LIKE CARCDTL*/
/*   RMC 1/04/06  ADDED COMPANY TO COMP RECAP SELECT, PASS IN JOBD  */
/*   RMC 1/12/15  ADDED CODE FOR SUNDAY UNDO: RAN FOR SUNDAY 1/11/15  */
/*                TAG : SUNUNDO                E3801               */
/*   RMC 1/18/15  DO SUNDAY CODE FOR 1/18/15      E3801               */
/*   RMC 1/19/15  COMMENT  SUNDAY CODE FOR 1/18/15      E3801               */
/*   RMC 4/10/15  DO SUNDAY CODE FOR 4/12/15      C4074               */
/*   RMC 4/10/15  COMMENT  SUNDAY CODE FOR 4/12/15      C4074               */
/*   RMC 8/20/15  DO SUNDAY CODE FOR 8/23/15      C4173               */
/*   RMC 8/21/15  COMMENT  SUNDAY CODE FOR 8/23/15      C4173               */
/*   RMC 10/04/17 PKB1CPP IS NOW IN QTEMP SO DONT HAVE TO UNDO IT HERE  */
/*                -- T11807                                             */
/*   RMC 10/19/17 DO SUNDAY CODE FOR 10/22/17     S11915              */
/*   RMC 10/23/17 COMMENT  SUNDAY CODE FOR 10/22/17     S11915              */
/*   RMC 01/19/18 DO SUNDAY CODE FOR 01/21/18     S12359              */
/*   RMC 01/22/18 COMMENT  SUNDAY CODE FOR 01/21/18 S12359            */
/*   RMC 12/30/18 DO SUNDAY CODE FOR 12/30/18     S12359              */
/*   RMC 12/30/18 COMMENT  SUNDAY CODE FOR 12/30/18 S12359            */
/*******************************************************************/
             PGM        PARM(&RTN &COMP &KILLDT &SATUNDO &JOBD)
/* PRINT OPTION PARMS                                             */
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)

/* FUNCTION SPECIFIC PARMS                                        */
             DCL        VAR(&XCOMP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SEL1) TYPE(*CHAR) LEN(26)
             DCL        VAR(&SEL2) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SEL3) TYPE(*CHAR) LEN(12)
             DCL        VAR(&SEL4) TYPE(*CHAR) LEN(12)
             DCL        VAR(&SEL5) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SEL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&JOBD) TYPE(*CHAR) LEN(10)  /* JOBD FOR SETLIB  */

/* NUMERIC VARIABLES                                             */
             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&KILLDT) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&PREVDT) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&SATDAT) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&SATKLL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SATUNDO) TYPE(*CHAR) LEN(1)

             ADDLIBLE   LIB(SEQUEL)
             MONMSG     MSGID(CPF2103)

/* *********************************************************/
  /* SET LIBL PER JOB DESC                               11/18/05       */

   SETLIB:     SETLIBL    JOBD(&JOBD)

/* *********************************************************/

/* 1) DELETE CARCASS DETAIL WITH TIME STAMP 'DATE ADDED' = TODAY   */

  /*         DELETE     SQL('from pkb1cpp where +
                          cvtdate(b1abdt,cymd)=CURRENT DATE')    */

/* 1B) DELETE LOSS TRANS WITH TIME STAMP 'DATE ADDED' = TODAY   */
  /*    AND TYPE IS 3  */
             DELETE     SQL('from pkbSCpp where +
                          cvtdate(bSabdt,cymd)=CURRENT DATE AND +
                          BSBWCD=3')

/* 2) CHANGE PROCESS CODE TO "RECV" FOR ALL TATTOOS FOR KILL DATE  */
   /* IF FRIDAY KILL DATE AND SAT. WAS A KILL DAY, UNDO SAT. TH 1ST */
             IF         COND(&SATUNDO *EQ 'Y') THEN(DO)
             CALL       PGM(PDSUXFR) PARM('' &COMP &KILLDT &SATDAT)
SATURDY:     IF         COND(&KILLDT *NE &SATDAT) THEN(DO)
             CALL       PGM(PDWBXFR) PARM('' &COMP &SATDAT &SATKLL)
KILLDY:      IF         COND(&SATKLL *EQ 'N') THEN(DO)
             CHGVAR     VAR(&SEL1) VALUE('FROM PKA1CPP WHERE A1B0DT=')
             CHGVAR     VAR(&SEL2) VALUE(&SATDAT)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2)
             UPDATE     SET((A1CSCD '"RECV "') (A1BSDT 0)) SQL(&SEL)
             ENDDO
             ENDDO
             ENDDO

WEEKDAYS:    CHGVAR     VAR(&SEL1) VALUE('FROM PKA1CPP WHERE A1B0DT=')
             CHGVAR     VAR(&SEL2) VALUE(&KILLDT)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2)
             UPDATE     SET((A1CSCD '"RECV "') (A1BSDT 0)) SQL(&SEL)

/* 3) CHANGE PROCESS CODE TO "RECV" FOR TATTOOS HELD OVER F/PREV DY */

             CALL       PGM(PDRUXFR) PARM('' &COMP &KILLDT &PREVDT) +
                          /* GET PREVIOUS KILL DATE */
             CHGVAR     VAR(&SEL1) VALUE('FROM PKA1CPP WHERE A1B0DT=')
PREVDAY:     CHGVAR     VAR(&SEL2) VALUE(&PREVDT)
HELDOVER:    CHGVAR     VAR(&SEL3) VALUE('AND A1H9NB>0')
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2 *BCAT &SEL3)
             UPDATE     SET((A1CSCD '"RECV "') (A1BSDT 0)) SQL(&SEL)

/* 4) DELETE COMPANY KILL RECAP WITH THIS KILL DATE    */
/*       AND COMPANY SINCE PKDCCPP IS IN PRKFLIB NOW  1.04.06 */

             CHGVAR     VAR(&SEL1) VALUE('from pkdccpp where DCCRDT=')
             CHGVAR     VAR(&SEL2) VALUE(&KILLDT)
             CHGVAR     VAR(&SEL4) VALUE('AND DCHONB=')
             CHGVAR     VAR(&SEL5) VALUE(&COMP)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2 *BCAT &SEL4 *CAT &SEL5)
             DELETE     SQL(&SEL)

SATRECAP:    IF         COND(&SATUNDO *EQ 'Y') THEN(DO)
             IF         COND(&KILLDT *NE &SATDAT) THEN(DO)
             IF         COND(&SATKLL *EQ 'N') THEN(DO)
             CHGVAR     VAR(&SEL1) VALUE('from pkdccpp where DCCRDT=')
             CHGVAR     VAR(&SEL2) VALUE(&SATDAT)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2 *BCAT &SEL4 *CAT &SEL5)
             DELETE     SQL(&SEL)
             ENDDO
             ENDDO
             ENDDO

/* 5) DELETE COMPANY KILL EXPENSE RATE WITH THIS KILL DATE        */

             CHGVAR     VAR(&SEL1) VALUE('from pkddcpp where DDCSDT=')
             CHGVAR     VAR(&SEL2) VALUE(&KILLDT)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2)
             DELETE     SQL(&SEL)

SATEXPNS:    IF         COND(&SATUNDO *EQ 'Y') THEN(DO)
             IF         COND(&KILLDT *NE &SATDAT) THEN(DO)
             IF         COND(&SATKLL *EQ 'N') THEN(DO)
             CHGVAR     VAR(&SEL1) VALUE('from pkddcpp where DDCSDT=')
             CHGVAR     VAR(&SEL2) VALUE(&SATDAT)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2)
             DELETE     SQL(&SEL)
             ENDDO
             ENDDO
             ENDDO
 SUNUNDO:      GOTO SKIPSUN    /*  SUNDAY CODE */
             IF         COND(&COMP *EQ 360) THEN(DO)
             UPDATE     SET((A1CSCD '"RECV "') (A1BSDT 0)) SQL('FROM +
                          PKA1CPP WHERE A1B0DT=1181230')

SUNOVER:     UPDATE     SET((A1CSCD '"RECV "') (A1BSDT 0)) SQL('FROM +
                          PKA1CPP WHERE A1B0DT=1181229  AND A1H9NB>0')

SUNRECAP:    CHGVAR     VAR(&SEL1) VALUE('from pkdccpp where DCCRDT=')
             CHGVAR     VAR(&SEL2) VALUE(1181230)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2 *BCAT &SEL4 *CAT &SEL5)
             DELETE     SQL(&SEL)
             CHGVAR     VAR(&SEL1) VALUE('from pkddcpp where DDCSDT=')
             CHGVAR     VAR(&SEL2) VALUE(1181230)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2)
             DELETE     SQL(&SEL)
             ENDDO
SKIPSUN:
/* 6) CHANGE PAYMENT STEP TO 1.  JANCIE CAN DO WHAT SHE NEEDS NOW. */

             CHGVAR     VAR(&XCOMP) VALUE(&COMP)
             CHGVAR     VAR(&SEL1) VALUE('FROM PKC4REP WHERE C4HONB=')
             CHGVAR     VAR(&SEL2) VALUE(&XCOMP)
             CHGVAR     VAR(&SEL) VALUE(&SEL1 *CAT &SEL2)
             CHGVAR     VAR(&SEL2) VALUE(&KILLDT)
             UPDATE     SET((C4M1NB 1) (C4CKDT &SEL2)) SQL(&SEL)


 END:
             ENDPGM
