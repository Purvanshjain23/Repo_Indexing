      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3702-Default Some Budget Items into a Template
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/04/04
      *
      * Function:    This function writes some Budget Items into the Budget Template
      *              Detail file for a specified Budget Template code. This is our
      *              attempt to give the users a head-start on setting up a new
      *              template.
      *
      *              The records defaulted depends on which Item Types the users have
      *              elected to default.
      *
      *              If you trace back far enough, you will find that this is
      *              conditionally called from the Work With Budget Template function.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp3702   if   e           k disk
      *    Temporary Item Type file in QTEMP with user's selections
      *
      *
     Fhsl181c   if   e           k disk
      *    Items (logical selects only Active records with a Budget Flag value of YES)
      *
      *
     Fhsp186    o    e           k disk
      *    Budget template detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Control fields
      *
     D svbgit          s                   like(tdbgit)
      *
      *
      * Parms
      *
     D xxbtcd          s                   like(tdbtcd)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Set up default values for the output record.
      *
     C                   move      xxbtcd        tdbtcd
     C                   z-add     0             tdbtqt
      *
      *
      * Write a record for each unique Budget Item to the Budget Template
      * detail file for this Template Code---if it has an Item Type that
      * matches one of the user's selections.
      *
     C     *loval        setll     hsl181c
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl181c                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        imbgit <> svbgit                             If different
     C     imittycd      chain     hsp3702                            92
     C                   if        *in92 = *off                                 If user wants
     C                   move      imbgit        tdbgit
     C                   write     tdrec
     C                   endif                                                  If user wants
     C                   endif                                                  If different
      *
     C                   move      imbgit        svbgit
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxbtcd
      *
     C                   endsr
      /eject
