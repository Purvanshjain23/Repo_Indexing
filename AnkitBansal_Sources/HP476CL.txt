/***************************************************************** */
/*  PROGRAM NUMBER --> HP476CL                                     */
/*  PROGRAM NAME ----> ACTUAL MOVEMENT LOG                         */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT AN       */
/*                     ACTUAL MOVEMENT LOG.                        */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                     SPECIFY HP476.                              */
/*                                                                 */
/*  PROGRAMMER ------> SHARON ZURN               06/09/94          */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*  08/09/94  R. MARTIN                                            */
/*            ADDED PRINT PARMS FOR HOLD, OUTQ, COPYS, ETC.        */
/*                                                                 */
/*  09/01/94  LEANNE FEDOR                                         */
/*            ADDED MONMSG ON CLRPFM COMMANDS.                     */
/*                                                                 */
/*  09/16/94  MARY PALMER                                          */
/*            ADDED OPNQRY FOR "CU" REPORT SELECTION AND ABILITY TO*/
/*            RUN FOR A SPECIFIC CUSTOMER.                         */
/*                                                                 */
/*  10/06/94  LEANNE FEDOR                                         */
/*            AS REQUESTED BY ED BROWN, CHANGED THE KEY SEQUENCE   */
/*            FOR THE 'CUSTOMER' REPORT TO BE CUSTOMER AND DATE.   */
/*                                                                 */
/*  11/08/94  MARY PALMER                                          */
/*            ADD NEW REPORT OPTION TO PROCESS AS CUSTOMER REPORT, */
/*            AND ONLY INCLUDE MOVEMENTS THAT ARE NOT FULLY PAID.  */
/*                                                                 */
/*  12/05/94  LEANNE FEDOR                                         */
/*            CHANGED WORKFILE LOGIC. WE WILL NOW CREATE WORKFILES */
/*            IN QTEMP INSTEAD OF CLEARING THEM IN PRODUCTION.     */
/*                                                                 */
/*  04/25/95  LEANNE FEDOR                                         */
/*            CHANGED FARM SITE FROM ALPHA TO NUMERIC.             */
/*                                                                 */
/*  08/11/95  LEANNE FEDOR                                         */
/*            REWRITTEN FOR MOVEMENT REWRITE.                      */
/*                                                                 */
/*  08/31/95  LEANNE FEDOR                                         */
/*            MADE BUSINESS OFFICE OPTIONAL.                       */
/*                                                                 */
/*  03/07/96  LEANNE FEDOR                                         */
/*            INCREASED PAGESIZE OVERRIDE ON QPRINT FROM 195 TO    */
/*            198.                                                 */
/*                                                                 */
/* 04/24/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 03/19/97  LEANNE FEDOR                                          */
/*           USER CAN NOW SUBMIT THE REPORT FOR 1) A FROM/TO DATE  */
/*           RANGE OR 2) THE NEW UPDATE DATE THAT CORRESPONDS TO   */
/*           THE UPDATE DATE/TIME STAMP THAT HAS BEEN ADDED TO THE */
/*           MOVEMENT EVENT FILE.                                  */
/*                                                                 */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*                                                                 */
/* 06/08/00  LEANNE FEDOR                                          */
/*           ADDED CELL AS A REPORT GENERATION OPTION.             */
/*                                                                 */
/* 05/31/07 LEANNE RAMSEY                                          */
/*          REPLACED QPRINT WITH PRINT198. REPLACED OLD CUSTOMIZED */
/*          PRINT OVERRIDE COMMAND WITH NEW STANDARD COMMAND.      */
/*                                                                 */
/* 06/24/09 LEANNE RAMSEY                                          */
/*          ADDED LOGIC TO DOWNLOAD/EMAIL A SPREADSHEET INSTEAD OF */
/*          PRINTING THE REPORT.                                   */
/*‚06/11/24  Jagdish Mistry (JM-WI628),                            */
/*‚         Project 628-RabApp Bypass to Agview                    */
/*‚         a. Added parameter to get count for HP370              */
/*‚         b. Comment call of program HP374.                      */
/*‚            We will no longer use work file HSP374,count of     */
/*‚            records is fetched using HP370.Hence we will not use*/
/*‚            HP374 & HSP374 from now onwards.                    */
/*‚          c. Deactivate ESND email notifications                */
/*‚07/24/24  Jagdish Mistry (JM-WI628),                            */
/*‚         Project 628-RabApp Bypass to Agview                    */
/*‚         a. Fixed not getting csv in email scenario             */
/*‚         b. Fixed to separate csv data by business location     */
/*‚08/21/24  Jagdish Mistry (JM-S027448) Reactivate ESND           */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(250)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDUYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDMRCD)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDCVNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FLAG)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSEL)   TYPE(*CHAR) LEN(130)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
/*‚JM-WI628-START                                                        */
             DCL        VAR(&TOTALCNT) TYPE(*DEC) LEN(8 0)
/*‚JM-WI628-END                                                          */

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDUYMD) VALUE(%SST(*LDA 226 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDMRCD) VALUE(%SST(*LDA 39 2))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 41 7))
             CHGVAR     VAR(&LDCVNO) VALUE(%SST(*LDA 48 8))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 234 2))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 236 50))
             CHGVAR     VAR(&LDSEL)  VALUE(%SST(*LDA 451 130))

/*-------------------------------------------------------------------*/
/* CREATE WORKFILES                                                  */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP370) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*‚JM-WI628-START-Comment use of work file HSP374                        */
/*           CRTDUPOBJL OBJ(HSP374) FROMLIB(*LIBL) OBJTYPE(*FILE) +      */
/*                        TOLIB(QTEMP) /* For Download                   */
/*‚JM-WI628-END                                                          */

/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE MOVEMENT EVENT FILE                         */
/*-------------------------------------------------------------------*/

/*  SELECT ALL SALES AND TRANSFER EVENT RECORDS FOR EITHER           */
/*  1) THE DATE RANGE OR 2) THE UPDATE DATE                          */

             IF         COND(&LDFYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MEMEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MEMEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)
             CHGVAR     VAR(%SST(&QDATA 46 40)) VALUE('*AND (MEMTCD +
                          *EQ "T" *OR MEMTCD *EQ "S")')
             ENDDO


             IF         COND(&LDUYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MEUPDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDUYMD)
             CHGVAR     VAR(%SST(&QDATA 21 40)) VALUE('*AND (MEMTCD +
                          *EQ "T" *OR MEMTCD *EQ "S")')
             ENDDO


             OVRDBF     FILE(HSP058) SHARE(*YES)
             OPNQRYF    FILE((HSP058)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  BUILD THE WORKFILE                                               */
/*-------------------------------------------------------------------*/

/*‚JM-WI628-START                                                        */
/*           CALL PGM(HP370)                                             */
/*           CALL       PGM(HP370) PARM((&TOTALCNT))                     */
/*‚JM-WI628-END                                                          */

/*           CLOF       OPNID(HSP058)                                    */
/*           DLTOVR     FILE(HSP058)                                     */

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*-------------------------------------------------------------------*/
/* ORIGIN REPORT                                                     */
/*-------------------------------------------------------------------*/

             IF         COND(&LDMRCD *EQ 'OR') THEN(DO)

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('WFORBO *NE +
                          " "')

/*  IF BUSINESS OFFICE                                               */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('WFORBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 18 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                              */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 17)) VALUE('*AND WFORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 62 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 67 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                     */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 69 16)) VALUE('*AND WFORFS +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 85 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                                                     */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 93 16)) VALUE('*AND WFORSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 109 7)) VALUE(&LDHGSN)
             ENDDO

/*  IF CELL                                                          */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 117 16)) VALUE('*AND WFORCL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 133 2)) VALUE(&LDCELL)
             ENDDO

/*  OPEN QUERY FILE FOR ORIGIN REPORT                            */

/*           OVRDBF     FILE(HSP370) SHARE(*YES)                     */
/*           OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) +              */
/*                        KEYFLD((WFORPP) (WFORFS) (WFRCDT) +        */
/*                        (WFORHG)) SEQONLY(*YES)                    */
             OVRDBF     FILE(HSP370) TOFILE(QTEMP/HSP370) SHARE(*YES)
             OPNQRYF    FILE((HSP370)) OPTION(*ALL) QRYSLT(&QDATA) +
                          KEYFLD((WFORPP) (WFORFS) (WFRCDT) +
                          (WFORHG)) SEQONLY(*YES)
             ENDDO

/*-------------------------------------------------------------------*/
/* DESTINATION FARM REPORT                                           */
/*-------------------------------------------------------------------*/

             IF         COND(&LDMRCD *EQ 'DE') THEN(DO)

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('WFDNBO *NE +
                          " "')

/* IF BUSINESS OFFICE                                                */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('WFDNBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 18 1)) VALUE('"')
             ENDDO

/* IF PRODUCTION PHASE                                               */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 17)) VALUE('*AND WFDNPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 62 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 67 1)) VALUE('"')
             ENDDO

/* IF FARM SITE                                                      */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 69 16)) VALUE('*AND WFDNFS +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 85 5)) VALUE(&LDFSCD)
             ENDDO

/* IF HOG GROUP                                                      */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 93 16)) VALUE('*AND WFDNSN +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 109 7)) VALUE(&LDHGSN)
             ENDDO

/* ONLY INCLUDE TRANSFER MOVEMENTS                                   */

             CHGVAR     VAR(%SST(&QDATA 117 19)) VALUE('*AND WFMTCD +
                          *EQ "T"')

/*  IF CELL                                                          */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 137 16)) VALUE('*AND WFDNCL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 153 2)) VALUE(&LDCELL)
             ENDDO

/*  OPEN QUERY FILE FOR DESTINATION FARM REPORT                      */

/*           OVRDBF     FILE(HSP370) SHARE(*YES)                     */
/*           OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) +              */
/*                        KEYFLD((WFDNPP) (WFDNFS) (WFRCDT) +        */
/*                        (WFDNHG)) SEQONLY(*YES)                    */
             OVRDBF     FILE(HSP370) TOFILE(QTEMP/HSP370) SHARE(*YES)
             OPNQRYF    FILE((HSP370)) OPTION(*ALL) QRYSLT(&QDATA) +
                          KEYFLD((WFDNPP) (WFDNFS) (WFRCDT) +
                          (WFDNHG)) SEQONLY(*YES)
             ENDDO

/*-------------------------------------------------------------------*/
/* CUSTOMER REPORT                                                   */
/*-------------------------------------------------------------------*/

             IF         COND(&LDMRCD *EQ 'CU') THEN(DO)

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('WFORBO *NE +
                          " "')

/* IF BUSINESS OFFICE                                                */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('WFORBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 18 1)) VALUE('"')
             ENDDO

/* IF PRODUCTION PHASE                                               */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 20 17)) VALUE('*AND WFORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 37 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 42 1)) VALUE('"')
             ENDDO

/* IF CUSTOMER NUMBER                                                */

             IF         COND(&LDCVNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 44 16)) VALUE('*AND WFCVNO +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 60 8)) VALUE(&LDCVNO)
             ENDDO

/* IF FARM SITE                                                      */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 69 16)) VALUE('*AND WFORFS +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 85 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                                                     */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 16)) VALUE('*AND WFORSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 107 7)) VALUE(&LDHGSN)
             ENDDO

/* ONLY INCLUDE SALES MOVEMENTS                                      */

             CHGVAR     VAR(%SST(&QDATA 115 19)) VALUE('*AND WFMTCD +
                          *EQ "S"')

/*  IF CELL                                                          */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 135 16)) VALUE('*AND WFORCL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 151 2)) VALUE(&LDCELL)
             ENDDO

/*  OPEN QUERY FILE FOR THE CUSTOMER REPORT                      */

/*           OVRDBF     FILE(HSP370) SHARE(*YES)                     */
/*           OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) +              */
/*                        KEYFLD((WFCVNO) (WFRCDT)) SEQONLY(*YES)    */
             OVRDBF     FILE(HSP370) TOFILE(QTEMP/HSP370) SHARE(*YES)
             OPNQRYF    FILE((HSP370)) OPTION(*ALL) QRYSLT(&QDATA) +
                          KEYFLD((WFCVNO) (WFRCDT)) SEQONLY(*YES)
             ENDDO

/*-------------------------------------------------------------------*/
/* NOT FULLY PAID SALES REPORT                                       */
/*-------------------------------------------------------------------*/

             IF         COND(&LDMRCD *EQ 'NS') THEN(DO)

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('WFORBO *NE +
                          " "')

/* IF BUSINESS OFFICE                                                */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('WFORBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 18 1)) VALUE('"')
             ENDDO

/* IF PRODUCTION PHASE                                               */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 20 17)) VALUE('*AND WFORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 37 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 42 1)) VALUE('"')
             ENDDO

/* IF CUSTOMER NUMBER                                                */

             IF         COND(&LDCVNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 44 16)) VALUE('*AND WFCVNO +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 60 8)) VALUE(&LDCVNO)
             ENDDO

/* IF FARM SITE                                                      */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 69 16)) VALUE('*AND WFORFS +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 85 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                                                     */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 16)) VALUE('*AND WFORSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 107 7)) VALUE(&LDHGSN)
             ENDDO

/* ONLY INCLUDE SALES MOVEMENTS                                      */

             CHGVAR     VAR(%SST(&QDATA 115 19)) VALUE('*AND WFMTCD +
                          *EQ "S"')

/*SELECT ONLY SALES THAT HAVE A STATUS OF SHIPPED OR NOT COMPLETE    */

             CHGVAR     VAR(%SST(&QDATA 135 42)) VALUE('*AND (WFMSCD +
                          *EQ "SH" *OR WFMSCD *EQ "NC")')

/*  IF CELL                                                          */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 178 16)) VALUE('*AND WFORCL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 194 2)) VALUE(&LDCELL)
             ENDDO

/*  OPEN QUERY FILE FOR NOT COMPLETELY PAID SALES REPORT         */

/*           OVRDBF     FILE(HSP370) SHARE(*YES)                     */
/*           OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) +              */
/*                        KEYFLD((WFCVNO) (WFRCDT)) SEQONLY(*YES)    */
             OVRDBF     FILE(HSP370) TOFILE(QTEMP/HSP370) SHARE(*YES)
             OPNQRYF    FILE((HSP370)) OPTION(*ALL) QRYSLT(&QDATA) +
                          KEYFLD((WFCVNO) (WFRCDT)) SEQONLY(*YES)
             ENDDO

/*-------------------------------------------------------------------*/
/* TRUCKER REPORT                                                    */
/*-------------------------------------------------------------------*/

             IF         COND(&LDMRCD *EQ 'TK') THEN(DO)

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('WFORBO *NE +
                          " "')

/* IF BUSINESS OFFICE                                                */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 13)) VALUE('(WFORBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 14 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 19 1)) VALUE('"')
             CHGVAR     VAR(%SST(&QDATA 21 16)) VALUE('*OR WFDNBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 37 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 42 2)) VALUE('")')
             ENDDO

/* IF PRODUCTION PHASE                                               */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 18)) VALUE('*AND (WFORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             CHGVAR     VAR(%SST(&QDATA 70 16)) VALUE('*OR WFDNPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 86 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 91 2)) VALUE('")')
             ENDDO

/* IF FARM SITE                                                      */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 94 17)) VALUE('*AND (WFORFS +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 111 5)) VALUE(&LDFSCD)
             CHGVAR     VAR(%SST(&QDATA 117 15)) VALUE('*OR WFDNFS +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 132 5)) VALUE(&LDFSCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE(')')
             ENDDO

/* IF HOG GROUP                                                      */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 16)) VALUE('*AND (WFORSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 156 7)) VALUE(&LDHGSN)
             CHGVAR     VAR(%SST(&QDATA 164 14)) VALUE('*OR WFDNSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 179 7)) VALUE(&LDHGSN)
             CHGVAR     VAR(%SST(&QDATA 186 1)) VALUE(')')
             ENDDO

/* IF CELL                                                           */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 188 17)) VALUE('*AND (WFORCL +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 205 2)) VALUE(&LDCELL)
             CHGVAR     VAR(%SST(&QDATA 208 15)) VALUE('*OR WFDNCL +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 223 2)) VALUE(&LDCELL)
             CHGVAR     VAR(%SST(&QDATA 225 1)) VALUE(')')
             ENDDO

/* OPEN QUERY FILE FOR TRUCKER                                       */

/*           OVRDBF     FILE(HSP370) SHARE(*YES)                     */
/*           OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) +              */
/*                        KEYFLD((WFTICD) (WFORPP) (WFORFS) +        */
/*                        (WFRCDT) (WFORHG)) SEQONLY(*YES)           */
             OVRDBF     FILE(HSP370) TOFILE(QTEMP/HSP370) SHARE(*YES)
             OPNQRYF    FILE((HSP370)) OPTION(*ALL) QRYSLT(&QDATA) +
                          KEYFLD((WFTICD) (WFORPP) (WFORFS) +
                          (WFRCDT) (WFORHG)) SEQONLY(*YES)
   ENDDO

/*-------------------------------------------------------------------*/
/* RECEIVED DATE REPORT                                              */
/*-------------------------------------------------------------------*/

             IF         COND(&LDMRCD *EQ 'DT') THEN(DO)

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('WFORBO *NE +
                          " "')

/* IF BUSINESS OFFICE                                                */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 13)) VALUE('(WFORBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 14 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 19 2)) VALUE('")')
/*‚JM-WI628-START                                                        */
/*           CHGVAR     VAR(%SST(&QDATA 19 1)) VALUE('"')                */
/*           CHGVAR     VAR(%SST(&QDATA 21 16)) VALUE('*OR WFDNBO +      */
/*                        *EQ "')                                        */
/*           CHGVAR     VAR(%SST(&QDATA 37 5)) VALUE(&LDFSBO)            */
/*           CHGVAR     VAR(%SST(&QDATA 42 2)) VALUE('")')               */
/*‚JM-WI628-END                                                          */
             ENDDO

/* IF PRODUCTION PHASE                                               */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 18)) VALUE('*AND (WFORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             CHGVAR     VAR(%SST(&QDATA 70 16)) VALUE('*OR WFDNPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 86 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 91 2)) VALUE('")')
             ENDDO

/* IF FARM SITE                                                      */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 94 17)) VALUE('*AND (WFORFS +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 111 5)) VALUE(&LDFSCD)
             CHGVAR     VAR(%SST(&QDATA 117 15)) VALUE('*OR WFDNFS +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 132 5)) VALUE(&LDFSCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE(')')
             ENDDO

/* IF HOG GROUP                                                      */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 143 16)) VALUE('*AND (WFORSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 160 7)) VALUE(&LDHGSN)
             CHGVAR     VAR(%SST(&QDATA 168 14)) VALUE('*OR WFDNSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 183 7)) VALUE(&LDHGSN)
             CHGVAR     VAR(%SST(&QDATA 190 1)) VALUE(')')
             ENDDO


/* IF CELL                                                           */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 192 17)) VALUE('*AND (WFORCL +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 209 2)) VALUE(&LDCELL)
             CHGVAR     VAR(%SST(&QDATA 212 15)) VALUE('*OR WFDNCL +
                            *EQ ')
             CHGVAR     VAR(%SST(&QDATA 227 2)) VALUE(&LDCELL)
             CHGVAR     VAR(%SST(&QDATA 229 1)) VALUE(')')
             ENDDO

/* OPEN QUERY FILE BY RECEIVED DATE                                  */

/*           OVRDBF     FILE(HSP370) SHARE(*YES)                     */
/*           OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) +              */
/*                        KEYFLD((WFORPP) (WFRCDT)) SEQONLY(*YES)    */
             OVRDBF     FILE(HSP370) TOFILE(QTEMP/HSP370) SHARE(*YES)
             OPNQRYF    FILE((HSP370)) OPTION(*ALL) QRYSLT(&QDATA) +
                          KEYFLD((WFORPP) (WFRCDT)) SEQONLY(*YES)
             ENDDO

/*‚JM-WI628-START                                                        */
             CALL       PGM(HP370) PARM((&TOTALCNT))
             CLOF       OPNID(HSP058)
             DLTOVR     FILE(HSP058)
/*‚JM-WI628-END                                                          */

/*------------------------------------------------------------------*/
/* PRINT THE REPORT                                                 */
/*------------------------------------------------------------------*/

             IF         COND(&LDEMAIL *EQ ' ') THEN(DO)

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

             CALL       PGM(HP676)
             ENDDO

/*------------------------------------------------------------------*/
/* EMAIL A SPREADSHEET                                              */
/*------------------------------------------------------------------*/

             IF         COND(&LDEMAIL *NE ' ') THEN(DO)

/*‚JM-WI628-START-Comment call of HP374                                 */
/*           CALL PGM(HP374) PARM(&FLAG)                                */
/*‚JM-WI628-END                                                          */

/*‚JM-WI628-START                                                        */
/*           IF         COND(&FLAG = 'Z')     THEN(DO)                   */
             IF         COND(&TOTALCNT *EQ 0) THEN(DO)
/*‚JM-WI628-END                                                          */
/*‚JM-WI628-START  Deactivate notifications                              */
/*‚JM-S027448-START -Reactivate notifications                            */
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('HPS: +
                          HP676-Actual Movement Log') MSG('Your +
                          selections on HP476-Specify Actual +
                          Movement Log did not extract any data for +
                          the spreadsheet. Please change your +
                          selections and submit again.')
             MONMSG     MSGID(CPF0000)
/*‚JM-S027448-END                                                        */
/*‚JM-WI628-END                                                    */
             ENDDO

/*‚JM-WI628-START                                                        */
/*           IF         COND(&FLAG = 'T')     THEN(DO)                   */
/*           ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('HPS: +              */
/*                        HP676-Actual Movement Log') MSG('Your +        */
/*                        selections on HP476-Specify Actual +           */
/*                        Movement Log extracted over 66,000 +           */
/*                        records--which is too many records for +       */
/*                        the spreadsheet to handle. Please change +     */
/*                        your selections to extract fewer records +     */
/*                        and submit again.')                            */
/*           MONMSG     MSGID(CPF0000)                                   */
/*           ENDDO                                                       */
/*‚JM-WI628-END                                                          */

/*‚JM-WI628-START                                                        */
             DLTF       FILE(QTEMP/HSP3701)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(HSP370) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(HSP3701) CST(*NO) +
                          TRG(*NO)
             CPYFRMQRYF FROMOPNID(HSP370) TOFILE(QTEMP/HSP3701) +
                          MBROPT(*ADD) CRTFILE(*NO)
             CLOF       OPNID(HSP370)
             DLTOVR     FILE(*ALL)
/*‚JM-WI628-END                                                          */
/*‚JM-WI628-START                                                        */
/*           IF         COND(&FLAG = ' ')     THEN(DO)                   */
             IF         COND(&TOTALCNT *GT 0) THEN(DO)
/*           EXECUTE    SQL('SELECT * FROM HSP374') PCFMT(*XLS) +        */
/*           EXECUTE    SQL('SELECT * FROM HSP370') +                    */
             EXECUTE    SQL('SELECT * FROM HSP3701') PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&LDEMAIL) +
                          EMLMSG(&LDSEL) TEXT('HP676-Actual +
                          Movement Log')
             MONMSG     MSGID(CPF0000)
             ENDDO
/*‚JM-WI628-END                                                          */

             ENDDO

/*‚JM-WI628-START                                                        */
/*           CLOF       OPNID(HSP370)                                    */
/*           DLTOVR     FILE(*ALL)                                       */
/*‚JM-WI628-END                                                          */

             ENDPGM

