      *****************  RPG PROGRAM HEADING  ***********************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP1067
      * TITLE:       UT: Edit (Maintain) Uploaded CullSales
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     08/18/09
      *
      * FUNCTION: This program uses a subfile to maintain uploaded Cull Sale movements.
      *
      *           This program is called straight from the Menu Option.
      *
      *           It does not have selectors/positioners.
      *           It has F4-Prompt logic.
      *           It uses the standard message handling program.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/20/17  Brad Baden (E7986)
      *           Added select screen for Business Office prior to displaying edit screen.
      *           If the Business Office is 'OKLIV', Guymon records are displayed.  If the
      *           Business Office is 'IFLIV', Iowa records are displayed.  Also removed
      *           processing of data area DAAPER to get the period information and replaced
      *           it with getting the information from the Company Control file (HSP0071).
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd1067   cf   e             workstn sfile(hp1067s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsp019    if   e           k disk
      *  Farm buildings
      *
      *
     Fhsp020    if   e           k disk
      *  Building rooms
      *
      *
     Fhsp034    if   e           k disk
      *  Hog groups
      *
      *
     Fhsl034d   if   e           k disk    rename(hgrec:hgrecd)
      *  Hog groups
      *
      *
     Fhsl034p   if   e           k disk    rename(hgrec:hgrecp) prefix(p1)
      *  Hog groups (open groups only)
      *
      *
     Fhsp067    if   e           k disk
      *    Load type
      *
      *
     Fhsp0071   if   e           k disk
      *    Company Control
      *
      *
     Fhsp2067   uf a e           k disk
      *  Editing file for Guymon
      *
      *
     Fhsp2069   uf a e           k disk
      *  Editing file for Iowa
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      ****************************************************************
      * Named Constants
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2    inz('00')
     D svrrn1          s                   like(hdrrn1)
      *
     D eeditfl         s                        like(e1editfl)
     D emvsn           s                        like(e1mvsn)
     D ecvno           s                        like(e1cvno)
     D eticd           s                        like(e1ticd)
     D eltcd           s                        like(e1ltcd)
     D emdy            s                        like(e1mdy)
     D escdt           s                        like(e1scdt)
     D efscd           s                        like(e1fscd)
     D eblcd           s                        like(e1blcd)
     D ermcd           s                        like(e1rmcd)
     D ehgsn           s                        like(e1hgsn)
     D ehd             s                        like(e1hd)
     D esplit          s                        like(e1split)
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
     D sdbodv          s              5
      *
      *
      * Control and save fields
      *
     D svmvsn          s                   like(e1mvsn)
     D sveditfl        s                   like(e1editfl)
     D svmdy           s                   like(e1mdy)
     D svfscd          s                   like(hgfscd)
     D svhgcd          s                   like(hghgcd)
     D svltcd          s                   like(d1ltcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Parm fields
      *
     D xxcocd          s              3    inz('350')
     D xxhgcd          s                   like(hghgcd)
     D xxptcd          s                   like(hgptcd)
     D xxppcd          s                   like(hgppcd)
     D xxfscd          s                   like(hgfscd)
     D xxrmcd          s                   like(hgrmcd)
     D xxgscd          s                   like(hggscd)
     D xxalphfscd      s              5
     D xxfsnm          s                   like(fsfsnm)
     D xxaist          s                   like(fsaist)
     D xxblcd          s                   like(fbblcd)
     D xxblds          s                   like(fbblds)
     D xxltcd          s                   like(ltltcd)
     D xxltds          s                   like(ltltds)
     D xxalphmdy       s              6
     D xxdt06          s              6  0
      *
     D jdefl           s              1
     D inat1           s              3    inz('C  ')
     D oualph          s             40
     D inajd           s                   like(e1cvno)
     D ouajd           s                   like(e1cvno)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *-----------------------------------------------------------------
      * Definition for external data area 'DAAPER' for accounting period
      *  NOTE: The use of this data area commented out and replaced
      *        with checking file HSP0071 for the period and dates.
      *        E7986 Brad Baden
      *-----------------------------------------------------------------
      *
     D*daaper          ds                  dtaara(daaper)
     D* daccyy                        4s 0
     D* daper                         2s 0
     D* dabpdt                        8s 0
     D* daepdt                        8s 0
     D* dapgfl                        1a
     D* dappfl                        1a
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear and fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      * (Note: If this is the first time into this function for the upload data,
      *  we want to force the data thru the Edit routine to pick up/populate the
      *  Group. If we don't do this here, we need to add/duplicate this Group
      *  pop logic in the program that writes the Receiving data to the Editing
      *  file.)
     C                   if        sveditfl = *blank and norecs = no
     C                   eval      routne = edit1
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Perform operator I/O for screen 1
      *---------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Set the page of the subfile to display
      *
     C                   select
     C                   when      wkflow = 0 and error = 0
     C                   z-add     1             hdpag1
      *
     C                   when      reset = yes and wkflow <= hdrrn1
     C                   z-add     wkflow        hdpag1
     C                   endsl
      *
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * This subfile has Fold/Unfold capabilities.  The DDS uses the keywords
      * (SFLDROP, SFLFOLD) to determine whether the user is folded or unfolded
      * and to redisplay the screen in the same mode as when he last saw it.
      * Indicators 78 and 79 are assigned in the DDS to the keywords.
      *
     C                   setoff                                       7879
     C                   if        fold = '0'
     C                   seton                                        78
     C                   else
     C                   seton                                        79
     C                   endif
      *
      * Write screen 1 to crt
      *
     C                   write     hp1067k1
     C                   write     hp1067ec
     C                   exfmt     hp1067c1
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   z-add     idflow        wkflow
     C                   move      no            reset
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      routne = exit
      *
     C                   when      norecs = yes                                 No data
     C                   eval      routne = set1
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
     C                   eval      routne = scrn1
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      reset = yes
     C                   eval      routne = set1
      *
     C                   when      *in09 = *on                                  F9-accept
     C                   exsr      $edit1
     C                   if        error = 0
     C                   exsr      $accept
     C                   endif
     C                   other
      *
      * User pressed ENTER
      *
     C                   exsr      $edit1
     C                   movel     scrn1         routne
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Subroutine to edit input fields on screen 1
      *---------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * Edit the Control Record
      *
     C                   exsr      $edc1
      *
      * Edit the subfile if there are no errors in the control record.
      * If there are errors in the control record, you need to rewrite
      * any subfile records with the error indicators off.
      *
     C                   if        error = 0
     C                   exsr      $eds1
     C                   else
     C                   exsr      $sfloff
     C                   endif
      *
     C                   movel     scrn1         routne
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Edit the control record info on screen 1
      *---------------------------------------------------------------
      *
     C     $edc1         begsr
      *
      * Customer number
     C                   exsr      $cvno
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate customer
      *----------------------------------------------------------------
      *
     C     $cvno         begsr
      *
     C                   move      *blank        c1cnam
      *
     C                   select
     C                   when      c1cvno = 0
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
     C                   z-add     c1cvno        inajd
     C                   movel(p)  'C'           inat1
     C                   exsr      $jdev
      *
     C                   if        jdefl = yes
     C                   seton                                        32
     C                   else
     C                   movel     oualph        c1cnam
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Call the external program to validate JDE Address Numbers
      *----------------------------------------------------------------
      *
     C     $jdev         begsr
      *
     C                   move      no            jdefl
      *
     C                   call      'HPJDEV'
     C                   parm                    mgi                            MSG ID'S
     C                   parm                    mgd                            MSG DATA FLD
     C                   parm                    error                          # ERR MSGS
     C                   parm                    inajd                          JDE ADDR #
     C                   parm                    inat1                          JDE ADDR TYP
     C                   parm                    jdefl                          ERROR FLAG
     C                   parm      *blank        oualph                         JDE NAME
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Edit subfile on screen 1
      *--------------------------------------------------------------------------------------
      *
     C     $eds1         begsr
      *
      * Set/initialize control/save fields.
      *
     C                   z-add     0             svmvsn
     C                   z-add     0             svmdy
     C                   z-add     0             svfscd
     C                   move      *blank        svhgcd
     C                   move      *blank        svltcd
      *
      * Read/process all changed subfile records
      *
     C                   dou       *in80 = *on                                  Do process
     C                   readc     hp1067s1                               80
     C                   if        *in80 = *off                                 If rec read
      *
      * Set on MDT
      * Set off error indicators in the subfile
      *
     C                   seton                                        85
     C                   exsr      $offin
      *
      * If there are no user entries for the changed subfile record,
      * clear all fields on the line....except Movement Number.
      *
     C                   if        d1mdy  = 0 and                               If entry
     C                             d1fscd = 0 and
     C                             d1blcd = *blank and
     C                             d1rmcd = *blank and
     C                             d1hgcd = *blank and
     C                             d1schd = 0 and
     C                             d1ltcd = *blank
     C                   exsr      $clrln
     C                   else
      *
      * Scheduled Ship Date.
     C                   exsr      $mdy
      *
      * If Group is blank, clear output only Group fields.
      *
     C                   if        d1hgcd = *blank
     C                   move      *blank        d1gscd
     C                   endif
      *
      * Validate Farm
     C                   exsr      $fscd
     C                   if        *in31 = *off                                 If Farm OK
      *
      * The user must enter Farm/Building or Group.
      * If Farm is valid but Building/Group are both blank, issue a message;
      * otherwise retrieve defaults for missing values.
      *
     C                   select
     C                   when      d1blcd = *blank and
     C                             d1hgcd = *blank
     C                   seton                                        3435
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09132'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   other
      *
      * Validate Building
     C                   exsr      $blcd
      *
      * Edit room if a) it is not blank and b) it was not edited previously
      * in the logic that attempted to default a Group for a building.
      * (The check on indicator 30 keeps you from issuing a second invalid
      * room message.)
      *
     C                   if        d1rmcd <> *blank and *in30 = *off
     C                   exsr      $rmcd
     C                   endif
      * Hog Group
     C                   exsr      $hgcd
     C                   endsl
     C                   endif                                                  If Farm OK
      * Validate:
      *  1) Scheduled head
      *  2) Load type
     C                   exsr      $schd
     C                   exsr      $ltcd
      *
      * If there are multiple lines for the same movement, make additional
      * checks.
     C                   if        d1mvsn = svmvsn
     C                   exsr      $multline
     C                   endif
      *
     C                   z-add     d1mvsn        svmvsn
     C                   z-add     d1mdy         svmdy
     C                   z-add     d1fscd        svfscd
     C                   move      d1hgcd        svhgcd
     C                   move      d1ltcd        svltcd
     C                   endif                                                  If entry
      *
     C                   update    hp1067s1
     C                   exsr      $offin
     C                   endif                                                  If rec read
     C                   enddo                                                  Do process
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Scheduled Ship Date
      *----------------------------------------------------------------
      *
     C     $mdy          begsr
      *
      * Clear the hidden date field
      *
     C                   z-add     0             hdscdt
      *
     C                   select
     C                   when      d1mdy = 0
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   move      'HS09001'     mgid(e)
     C                   endif
      *
     C                   when      d1mdy < 0
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   move      'HS09000'     mgid(e)
     C                   endif
     C                   other
      *
     C     *mdy          test(d)                 d1mdy                  92
     C                   if        *in92 = *on                                  If bad date
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09004'
     C                   endif
     C                   else
      *
      * Flip the date from MMDDYY to CCYYMMDD.
      *
     C     *mdy          move      d1mdy         wkisodate
     C                   move      wkisodate     hdscdt
     C                   endif                                                  If bad date
      *
      * If the Date is valid,
      *     Check that it is not in a Closed Period
      *     If it is in the Current Period,
      *         Check that the Period Close programs are not running
      *
     C                   if        *in37 = *off                                 If good date
      *
     C*                  in        daaper
     C                   if        sdbobo = 'OKLIV'
     C                   eval      cccocd = '350'
     C                   else
     C                   eval      cccocd = '340'
     C                   endif                                                  if sdbobo = 'OKLIV'

     C     cccocd        chain     hsp0071
      *
     C                   select
     C                   when      not %found(hsp0071)
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09246'     mgid(e)
     C                   endif

     C                   when      hdscdt < ccbpdt
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09007'     mgid(e)
     C                   endif
      *
     C                   when      hdscdt >= ccbpdt and
     C                             hdscdt <= ccepdt and
     C                             ccppfl <> *blank
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09016'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If good date
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit Farm Site
      *----------------------------------------------------------------
      *
     C     $fscd         begsr
      *
     C                   move      *blank        d1fsnm
      *
     C                   select
     C                   when      d1fscd   = 0
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
      * Check that the Farm exists in the Farm Site master file
      *
     C     d1fscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If good farm
     C                   movel     fsfsnm        d1fsnm
     C                   movel     fsfsbo        d1fsbo
     C                   else
      *
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS00131'     mgid(e)
     C                   clear                   mgw
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If good farm
     C                   endsl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Validate building
      *--------------------------------------------------------------------------------------
      *
     C     $blcd         begsr
      *
      * If building is blank and hog group is entered, default building and
      * room from hog group.
      *
     C                   if        d1blcd = *blank and d1hgcd <> *blank
     C     d1hgcd        chain     hsp034                             92
     C                   if        *in92 = *off
     C                   move      hgblcd        d1blcd
     C                   move      hgrmcd        d1rmcd
     C                   endif
     C                   endif
      *
      * Validations:
      *  1) building is required
      *  2) building must exist on the specified farm site
      *
     C                   select
     C                   when      d1blcd = *blank
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     key01         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     d1blcd        mgw(1)
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
     C                   endsl
      *
      * Attempt to default in a group when:
      *   1) building is valid and group is blank and
      *   2) room is blank or valid
      *
     C                   if        *in35 = *off and d1hgcd = *blank             If valid building
      *
     C                   if        d1rmcd <> *blank
     C                   exsr      $rmcd
     C                   endif
      *
     C                   if        *in30 = *off
     C                   exsr      $group
     C                   endif
     C                   endif                                                  If valid building
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------------
      * Validate room
      *---------------------------------------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $rmcd         begsr
      *
     C                   select
     C                   when      d1rmcd   = *blank
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     ‚*
     C     key02         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     d1rmcd        mgw(1)
     C                   movea     d1blcd        mgw(6)
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     ‚*
      * Room must have an 'active' status
     ‚*
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     d1rmcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------------
      * Attempt to find a default group in the building
      *---------------------------------------------------------------------------------------------
      *
      * If only one group is open in that building or building/room, default
      * it in. But, if multiple groups are open, issue a message to the
      * user to select a group.
      *
     C     $group        begsr
      *
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        d1rmcd   = *blank
     C     key01         chain     hsl034p                            92
     C                   else
     C     key02         chain     hsl034p                            92
     C                   endif
      *
     C                   if        *in92 = *off                                 If open group
      *
      * Check to see if there is another open group.
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        d1rmcd   = *blank
     C     key01         reade     hsl034p                                92
     C                   else
     C     key02         reade     hsl034p                                92
     C                   endif
      *
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movea     d1blcd        mgw(1)
      *
     C                   select
     C                   when      d1rmcd   = *blank
     C                   movel     'HS00721'     mgid(e)
     C                   other
     C                   movel     'HS00765'     mgid(e)
     C                   movea     d1rmcd        mgw(6)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Only one group is open; default in its values.
      *
     C                   movel     p1hghgcd      d1hgcd
     C                   movel     p1hgrmcd      d1rmcd
     C                   endif                                                  If mult group
     C                   else
      *
      * No groups are open in this building/room
      *
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     d1blcd        mgw(6)
      *
     C                   select
     C                   when      d1rmcd   = *blank
     C                   movel     'HS00722'     mgid(e)
     C                   other
     C                   movel     'HS00766'     mgid(e)
     C                   movea     d1rmcd        mgw(11)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If open group
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit Hog Group
      *----------------------------------------------------------------
      *
     C     $hgcd         begsr
      *
     C                   z-add     0             hdhgsn
     C                   move      *blank        d1gscd
      *
     C                   select                                                 Select 1
     C                   when      d1hgcd   = *blank
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   other
      *
     C     d1hgcd        chain     hsp034                             92
     C                   if        *in92 = *on                                  If bad group
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00261'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
     C                   move      hggscd        d1gscd
      *
      * Group must be on the Farm
      *
     C                   if        hgfscd <> d1fscd                             If diff farm
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00468'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   move      hgfscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(8)
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If diff farm
      *
      * Group building must match subfile building---if there is a building
      * on the subfile.
     C                   select
     C                   when      d1blcd   = *blank
     C                   movel     hgblcd        d1blcd
     C                   other
     C                   if        d1blcd   <> hgblcd                           If mismatch building
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     d1blcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch building
     C                   endsl
      *
      * Group room must match subfile room---if there is a room
      * on the subfile.
     C                   select
     C                   when      d1rmcd   = *blank
     C                   movel     hgrmcd        d1rmcd
     C                   other
     C                   if        d1rmcd   <> hgrmcd                           If mismatch room
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     d1rmcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch room
     C                   endsl
      *
      * Group must always have a status of Opened and the Open Date must be
      * on/before the Scheduled Ship Date.
      *
     C                   select
     C                   when      hggscd <> 'OP'
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00759'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     hggscd        mgw(8)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
      *
     C                   when      hggscd = 'OP' and hgopdt > hdscdt and
     C                             *in37 = *off
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00488'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
      *
     C                   move      hgopdt        wkisodate
     C     *mdy          move      wkisodate     xxdt06
     C                   move      xxdt06        xxalphmdy
     C                   movea     xxalphmdy     mgw(8)
      *
     C                   move      d1mdy         xxalphmdy
     C                   movea     xxalphmdy     mgw(14)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If bad group
     C                   endsl                                                  Select 1
      *
      * If group is OK, set up hidden subfile field values.
      *
     C                   if        *in34 = *off
     C                   z-add     hghgsn        hdhgsn
     C                   endif
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Validate Scheduled Head
      *--------------------------------------------------------------------------------------
      *
     C     $schd         begsr
      *
     C                   select
     C                   when      d1schd = 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   exsr      $err1
     C                   endif
      *
     C                   when      d1schd < 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate Load Type
      *----------------------------------------------------------------
      *
      * Validations:
      *   1) required
      *   2) must be valid in load type master
      *
     C     $ltcd         begsr
      *
     C                   eval      d1ltcd = %triml(d1ltcd)
      *
     C                   select
     C                   when      d1ltcd = *blank
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
     C     d1ltcd        chain     hsp067                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00188'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1ltcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no hit
     C                   endsl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Check for a consistent Farm on Movements with Multiple Lines
      *--------------------------------------------------------------------------------------
      *
     C     $multline     begsr
      *
      * The Date must be the same for multiple lines on the same Movement.
      *
     C                   if        svmdy <> d1mdy and *in37 = *off
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01135'     mgid(e)
     C                   clear                   mgw
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
      * The Farm must be the same for multiple lines on the same Movement.
      *
     C                   if        svfscd <> d1fscd and *in31 = *off
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01134'     mgid(e)
     C                   clear                   mgw
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
      * A Group can only be on a movement one time.
      *
     C                   if        svhgcd = d1hgcd and *in34 = *off
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00640'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
      * Load Type must be the same for multiple lines on the same Movement.
      *
     C                   if        svltcd <> d1ltcd and *in33 = *off
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01133'     mgid(e)
     C                   clear                   mgw
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear the subfile on Screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   hp1067s1
     C                   seton                                        83
     C                   write     hp1067c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Always load the entire subfile.
      *
     C                   if        sdbobo = 'OKLIV'
     C     *start        setll     hsp2067
     C                   else
     C     *start        setll     hsp2069
     C                   endif
     C                   exsr      $load1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load the subfile on screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   z-add     svrrn1        hdrrn1
      *
      * Fill the subfile until the end of the database file.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on                                  Do fill
     C                   if        sdbobo = 'OKLIV'
     C                   read      hsp2067                                82
     C                   else
     C                   read      hsp2069                                82
     C                   endif                                                  If sdbobo = 'OKLIV'
      *
     C                   if        *in82 = *off                                 If not EOF
      *
     C                   if        sdbobo = 'OKLIV'
     C                   exsr      $loadguy
     C                   else
     C                   exsr      $loadiowa
     C                   endif                                                  If sdbobo = 'OKLIV'
      *
     C                   move      eeditfl       sveditfl
      *
      * Retrieve Customer Name
     C                   if        c1cvno = 0
     C                   z-add     ecvno         c1cvno
     C                   z-add     ecvno         inajd
     C                   movel(p)  'C'           inat1
     C                   exsr      $jdev
     C                   movel     oualph        c1cnam
     C                   endif
      *
     C                   move      esplit        hdsplit
     C                   z-add     emvsn         d1mvsn
     C                   z-add     emdy          d1mdy
     C                   move      eltcd         d1ltcd
     C                   z-add     efscd         d1fscd
     C                   move      eblcd         d1blcd
     C                   move      ermcd         d1rmcd
     C                   z-add     ehd           d1schd
      * Farm
     C     d1fscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel     fsfsnm        d1fsnm
     C                   movel     fsfsbo        d1fsbo
     C                   else
     C                   move      *blank        d1fsnm
     C                   endif
      *
     C                   move      *blank        d1hgcd
     C                   move      *blank        d1gscd
      *
     C                   if        ehgsn <> 0
     C     ehgsn         chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hghgcd        d1hgcd
     C                   move      hggscd        d1gscd
     C                   endif
     C                   endif
      *
     C                   add       1             hdrrn1
      *
     C                   seton                                        85
     C                   write     hp1067s1
     C                   exsr      $sfloff
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into
      * the sflrcdnbr field (hdpag1)
      *
     C                   z-add     hdrrn1        hdpag1
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do fill loop
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   movel     yes           norecs
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
     C                   else
      *
     C                   movel     no            norecs
     C                   endif                                                  If no recs
      *
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Move Guymon fields to universal fields
      *---------------------------------------------------------------
      *
     C     $loadguy      begsr
      *
     C                   eval      eeditfl = e1editfl
     C                   eval      emvsn   = e1mvsn
     C                   eval      ecvno   = e1cvno
     C                   eval      eticd   = e1ticd
     C                   eval      eltcd   = e1ltcd
     C                   eval      emdy    = e1mdy
     C                   eval      escdt   = e1scdt
     C                   eval      efscd   = e1fscd
     C                   eval      eblcd   = e1blcd
     C                   eval      ermcd   = e1rmcd
     C                   eval      ehgsn   = e1hgsn
     C                   eval      ehd     = e1hd
     C                   eval      esplit  = e1split
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Move Iowa fields to universal fields
      *---------------------------------------------------------------
      *
     C     $loadiowa     begsr
      *                    00
     C                   eval      eeditfl = e2editfl
     C                   eval      emvsn   = e2mvsn
     C                   eval      ecvno   = e2cvno
     C                   eval      eticd   = e2ticd
     C                   eval      eltcd   = e2ltcd
     C                   eval      emdy    = e2mdy
     C                   eval      escdt   = e2scdt
     C                   eval      efscd   = e2fscd
     C                   eval      eblcd   = e2blcd
     C                   eval      ermcd   = e2rmcd
     C                   eval      ehgsn   = e2hgsn
     C                   eval      ehd     = e2hd
     C                   eval      esplit  = e2split
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile line
      *---------------------------------------------------------------
      *
     C     $clrln        begsr
      *
     C                   move      *blank        hdsplit
     C                   z-add     0             hdscdt
     C                   z-add     0             hdhgsn
      *
     C                   z-add     0             d1mdy
     C                   z-add     0             d1schd
      *
     C                   z-add     0             d1fscd
     C                   move      *blank        d1hgcd
     C                   move      *blank        d1blcd
     C                   move      *blank        d1rmcd
     C                   move      *blank        d1gscd
     C                   move      *blank        d1fsnm
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Rewrite the Subfile records with the indicators off
      *---------------------------------------------------------------
      *
      * This subroutine keeps the Cursor from being in the subfile when
      * the displayed error message is for the control record.
      *
     C     $sfloff       begsr
      *
      * Read/Process the changed subfile records
      *
     C                   dou       *in80 = *on                                  Do process
     C                   readc     hp1067s1                               80
     C                   if        *in80 = *off                                 If rec read
      *
      * Set off all subfile indicators
      *
     C                   exsr      $offin
      * Seton MDT
     C                   seton                                        85
      *
     C                   update    hp1067s1
     C                   endif                                                  If rec read
     C                   enddo                                                  Do process
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Set off all subfile indicators
      *----------------------------------------------------------------
      *
     C     $offin        begsr
      *
     C                   setoff                                       303133
     C                   setoff                                       3435
     C                   setoff                                       3738
     C                   setoff                                       505152
     C                   setoff                                       535455
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * F4 Prompt for Screen 1
      *----------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Set off all indicators in all subfile records.
      *
     C                   exsr      $sfloff
      *
      *----------------------------------------------------------------
      * Prompted fields in the Control Record
      *----------------------------------------------------------------
      *
     C                   select
      * Customer
     C                   when      fld = 'C1CVNO'
     C                   call      'HP513'
     C                   parm                    xxcocd
     C                   parm                    inat1
     C                   parm      0             ouajd
     C                   parm      *blank        oualph
      *
     C                   seton                                        52
     C                   if        ouajd <> 0
     C                   z-add     ouajd         c1cvno
     C                   movel     oualph        c1cnam
     C                   endif
      *
      *----------------------------------------------------------------
      * Prompted fields in the subfile
      *----------------------------------------------------------------
      *
      *-------------------
      * FARM SITE
      *-------------------
      *
     C                   when      fld = 'D1FSCD  '
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP5040'
     C                   parm      'A'           xxaist
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        d1fscd
     C                   movel     xxfsnm        d1fsnm
     C                   endif
     C                   seton                                        8551
     C                   update    hp1067s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *-------------------
      * BUILDING
      *-------------------
      *
     C                   when      fld = 'D1BLCD  '
     C                   exsr      $rrn1
     C                   if        *in97 = *off
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   call      'HP523'
     C                   parm      d1fscd        xxfscd
     C                   parm      *blank        xxblcd
     C                   parm      *blank        XXblds
     C                   if        xxblcd <> *blank
     C                   movel     xxblcd        d1blcd
     C                   endif
     C                   seton                                        8555
     C                   update    hp1067s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *-------------------
      * ROOM
      *-------------------
     C                   when      fld = 'D1RMCD  '
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP524'
     C                   parm                    d1fscd
     C                   parm                    d1blcd
     C                   parm      *blank        xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        d1rmcd
     C                   endif
     C                   seton                                        8550
     C                   update    hp1067s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *-----------------
      * HOG GROUP
      *-----------------
     C                   when      fld = 'D1HGCD  '
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP5950'
     C                   parm                    d1fscd
     C                   parm                    d1fsnm
     C                   parm      d1blcd        xxblcd
     C                   parm      d1rmcd        xxrmcd
     C                   parm      *blank        xxhgcd
     C                   parm      *blank        xxgscd
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxppcd
     C                   if        xxhgcd <> *blank
     C                   movel     xxhgcd        d1hgcd
     C                   movel     xxblcd        d1blcd
     C                   movel     xxrmcd        d1rmcd
     C                   endif
     C                   seton                                        8554
     C                   update    hp1067s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *-----------------
      * LOAD TYPE
      *-----------------
     C                   when      fld = 'D1LTCD  '
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP5012'
     C                   parm      *blank        xxltcd
     C                   parm      *blank        xxltds
     C                   if        xxltcd <> *blank
     C                   movel     xxltcd        d1ltcd
     C                   endif
     C                   seton                                        8553
     C                   update    hp1067s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
     C                   other
      *
      * F4 not valid for this field; so, issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------------------
      * Set the subfile to redisplay at the first screen that has errors
      *----------------------------------------------------------------------------
      *
     C     $err1         begsr
      *
     C                   if        error = 1
     C                   z-add     hdrrn1        hdpag1
     C                   endif
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * User took F9 to accept his entries
      *--------------------------------------------------------------------------------------
      *
      * When the user takes F9, we will:
      *  1) delete the records in the Editing file
      *  2) write all subfile records to the editing file
      *
     C     $accept       begsr
      *
      * Delete records
      *
     C                   if        sdbobo = 'OKLIV'
      * Delete Guymon records
     C     *start        setll     hsp2067
     C                   dou       *in92 = *on
     C                   read      hsp2067                                92
     C                   if        *in92 = *off
     C                   delete    e1rec
     C                   endif
     C                   enddo
      *
     C                   else
      * Delete Iowa records
     C     *start        setll     hsp2069
     C                   dou       *in92 = *on
     C                   read      hsp2069                                92
     C                   if        *in92 = *off
     C                   delete    e2rec
     C                   endif                                                  if *in92 = *off
     C                   enddo                                                  dou *in92 = *on
     C                   endif                                                  if sdbobo = 'OKLIV'
      *
      * Write records
     C                   dou       *in80 = *on                                  Do loop
     C                   readc     hp1067s1                               80
     C                   if        *in80 = *off and d1schd <> 0                 If read
      *
     C                   if        sdbobo = 'OKLIV'
      * write to Guymon records
     C                   move      yes           e1editfl
     C                   move      'SBD  '       e1ticd
     C                   z-add     c1cvno        e1cvno
      *
     C                   z-add     d1mdy         e1mdy
     C                   z-add     hdscdt        e1scdt
     C                   z-add     d1fscd        e1fscd
     C                   move      d1blcd        e1blcd
     C                   move      d1rmcd        e1rmcd
     C                   z-add     hdhgsn        e1hgsn
     C                   z-add     d1schd        e1hd
     C                   move      d1ltcd        e1ltcd
     C                   z-add     d1mvsn        e1mvsn
     C                   move      hdsplit       e1split
      *
     C                   write     e1rec

     C                   else
      * write to Iowa records
     C                   move      yes           e2editfl
     C                   move      'UNK  '       e2ticd
     C                   z-add     c1cvno        e2cvno
      *
     C                   z-add     d1mdy         e2mdy
     C                   z-add     hdscdt        e2scdt
     C                   z-add     d1fscd        e2fscd
     C                   move      d1rmcd        e2rmcd
     C                   z-add     hdhgsn        e2hgsn
     C                   z-add     d1schd        e2hd
     C                   move      d1ltcd        e2ltcd
     C                   z-add     d1mvsn        e2mvsn
     C                   move      hdsplit       e2split
      *
     C                   write     e2rec

     C                   endif                                                  If sdbobo = 'OKLIV'
     C                   endif                                                  If read
     C                   enddo                                                  Do loop
      *
      * Set next routine
     C                   move      exit          routne
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Set on LR and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
      *
     C                   seton                                        lr
      *
     C                   endsr
      *
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF Command Key
      * Record will be set on. So, redisplay screen to user.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *---------------------------------------------------------------
      * Clear Messages
      *---------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF Command Key
      * Record will be set on. So, redisplay screen to user.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Find/Retrieve the subfile record to update when returning from a Select Program
      *---------------------------------------------------------------------------------------
      *
      * HDRR1 is the hidden field associated with the Keyword SFLCSRRRN on the Control Record
      * HDRRN1 is the value associated with the Keyword 'KSFILE' in the F-Specs for the Workstation.
      *
     C     $rrn1         begsr
      *
     C                   setoff                                       97
      *
     C                   if        hdrr1 <> 0
     C                   z-add     hdrr1         hdrrn1
     C     hdrrn1        chain     hp1067s1                           97
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Initally call the Business Office selection screen
     C                   call      'HP503'
     C                   parm      *blanks       sdbobo
     C                   parm      *blanks       sdbods
     C                   parm      'LIVE '       sdbodv
      *
      * If user does not select a business office, exist program
     C                   if        sdbobo = *blanks
     C                   exsr      $exit
     C                   endif
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    d1fscd
     C                   kfld                    d1blcd
      *
     C     key02         klist
     C                   kfld                    d1fscd
     C                   kfld                    d1blcd
     C                   kfld                    d1rmcd
      *
      *
      * You are always in REVISE mode in this porgram.
      *
     C                   movel     'REVISE'      d1mode
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     12            wkplin
     C     wkplin        sub       1             wkrlin
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
