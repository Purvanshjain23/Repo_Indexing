/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4484CL                                      */
/*  PROGRAM NAME:    COPY QUARTERLY PREDICTION VALUES              */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     01/31/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4484-SPECIFY QUARTERLY PREDICTION VALUES    */
/*                   TO COPY.                                      */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(72)

             DCL        VAR(&LDFCDYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDFCDQR) TYPE(*DEC) LEN(1)
             DCL        VAR(&LDFCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDFGTCD) TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDFCDYR) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDFCDQR) VALUE(%SST(*LDA 5 1))
             CHGVAR     VAR(&LDFCELL) VALUE(%SST(*LDA 6 2))
             CHGVAR     VAR(&LDFGTCD) VALUE(%SST(*LDA 8 1))


/*-------------------------------------------------------------------*/
/* SET UP/RUN OPEN QUERY TO SELECT THE 'FROM' RECORDS TO COPY        */
/*-------------------------------------------------------------------*/

/*  ALWAYS SELECT RECORDS FOR THE SPECIFIED YEAR                 */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('QPCDYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDFCDYR)


/*  CONDITIONALLY SELECT RECORDS FOR THE SPECIFIED QUARTER        */

             IF         COND(&LDFCDQR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND QPCDQR +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 33 1)) VALUE(&LDFCDQR)
             ENDDO


/*  CONDITIONALLY SELECT RECORDS FOR THE SPECIFIED CELL  */

             IF         COND(&LDFCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 35 15)) VALUE('*AND QPCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 51 2)) VALUE(&LDFCELL)
             ENDDO


/*  CONDITIONALLY SELECT RECORDS FOR THE SPECIFIED GROUP TYPE */

             IF         COND(&LDFGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 54 17)) VALUE('*AND QPGTCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 71 1)) VALUE(&LDFGTCD)
             CHGVAR     VAR(%SST(&QDATA 72 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSP083) SHARE(*YES)
             OPNQRYF    FILE((HSP083)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*------------------------------------------------------------------*/
/*  CALL COPY PROGRAM                                                */
/*------------------------------------------------------------------*/

             CALL       PGM(HP383)

             CLOF       OPNID(HSP083)
             DLTOVR     FILE(HSP083)


             ENDPGM
