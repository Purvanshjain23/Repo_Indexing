/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4021CL                                      */
/*  PROGRAM NAME:    LISTINGS OF BGF WEEKLY WEAN PROJECTIONS       */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     07/07/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM:       */
/*                   HP4021-WORK WITH BGF WEEKLY WEAN PROJECTIONS  */
/*                   HP283CL-SAFEDATA BGF: UPLOAD BGF WEEKLY WEAN  */
/*                     PROJECTIONS.                                */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*                                                                 */
/* 07/11/07  LEANNE RAMSEY                                         */
/*           TO SAVE TIME FOR USER STEVE BROWN (WHO COMES IN VERY, */
/*           VERY EARLY ON TUESDAY MORNINGS TO RUN HIS WEAN PIG    */
/*           PROJECTION REPORT), WE WILL NOW BE SUBMITTING HIS     */
/*           REPORT AT THE END OF THE SAFEDATA UPLOAD (WHICH RUNS  */
/*           ON MONDAY NIGHTS.) SO, THIS CL IS NOW CALLED FROM:    */
/*             HP4021-WW BGF WEEKLY WEAN PROJECTIONS (ON-DEMAND)   */
/*             HP283CL-SAFEDATA BGF: UPLOAD BGF WEEKLY WEAN        */
/*                     PROJECTIONS                                 */
/*           WE HAVE ADDED CODE THAT PERTAINS ONLY TO THE UPLOAD   */
/*           CALL. (NOTE: WE HAVE HARDCODED THE OUTQs FOR THIS.)   */
/*                                                                 */
/* 01/20/12  LEANNE RAMSEY (E1909)                                 */
/*           SAMI WILSON REQUESTED DWL/EMAIL CAPABILITY ON THE     */
/*           ON-DEMAND LISTING FOR MULTIPLE SITES. SO, I HAD TO    */
/*           GO TO A WORKFILE APPROACH.                            */
/*  02/28/23  ERIC LOUCHEZ    TICKET 120610                        */
/*            TRYING TO GET THE REPORT ATTACHED TO THE EMAILS      */
/*            MOVE ESNDMAIL TO THIS CL FROM FTPSDIRTVP             */
/*******************************************************************/

             PGM  &TSTPRDFL

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(70)

             DCL        VAR(&LDODUP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDLIST) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDRPWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFRPWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTRPWEDT) TYPE(*DEC) LEN(8)

             DCL        VAR(&RCDCNT)   TYPE(*DEC)  LEN(10 0)
             DCL        VAR(&LDEMAIL)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&LDRPFL)   TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

/* We need parms for date stuff when this CL is called from the    */
/* Safedata upload. For the on-demand version, we will have the    */
/* users selections; but, from the upload, we have to use the      */
/* system date to retrieve the "current" week info.                */

             DCL        VAR(&DT08)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&DTCHAR) TYPE(*CHAR) LEN(8)
             DCL        VAR(&YRCHAR) TYPE(*CHAR) LEN(4)
             DCL        VAR(&WKCHAR) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SATDT)  TYPE(*DEC)  LEN(8)
             DCL        VAR(&SATYR)  TYPE(*DEC)  LEN(4)
             DCL        VAR(&SATWK)  TYPE(*DEC)  LEN(2)

             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)  /* 120610 */
             DCL        VAR(&EMAIL  )  TYPE(*CHAR) LEN(38) /* 120610 */

             CHGVAR     VAR(&LDLIST)  VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDODUP)  VALUE(%SST(*LDA 70 1))
             CHGVAR     VAR(&LDRPFL)  VALUE(%SST(*LDA 71 1))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 72 50))

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/********************************************************************/
/* LOGIC FOR PRINTING A SINGLE RECORD WHEN USER TOOK OPTION 6=PRINT */
/********************************************************************/

             IF         COND(&LDLIST *EQ 'S') THEN(DO)
             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) LANDSCAPE(*NO) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)
             CALL       PGM(HP6021)
             ENDDO


/********************************************************************/
/* LOGIC FOR MULTIPLE PROJECTIONS WHEN USER TOOK F7=LISTING         */
/********************************************************************/

             IF         COND(&LDLIST *EQ 'M') THEN(DO)        /* Multiple */

/*-------------------------------------------------------------------------*/
/* If you are generating this at the end of the Safedata upload, you must: */
/*  1) retrieve a week-ending date to use for data extraction              */
/*  2) set LDA values                                                      */

             IF         COND(&LDODUP *EQ 'U') THEN(DO)             /* Upload */

/* Retrieve System Date and convert to 8,0 date field   */

             RTVDAT     CCYYMMDD(&DTCHAR)
             CHGVAR     VAR(&DT08) VALUE(&DTCHAR)

/* Pass System Date to generic date program to get Saturday week-ending */
/* info                                                                 */
             CALL       PGM(HP8007) PARM(&DT08 &SATDT &SATYR &SATWK)

/* Using "Saturday" values retrieved above, set the LDA for the report  */

       /* LDA: From/To Week-Ending Date */

             CHGVAR     VAR(&DTCHAR) VALUE(&SATDT)
             CHGDTAARA  DTAARA(*LDA (48 8)) VALUE(&DTCHAR)
             CHGDTAARA  DTAARA(*LDA (62 8)) VALUE(&DTCHAR)

       /* LDA: From/To Year */

             CHGVAR     VAR(&YRCHAR) VALUE(&SATYR)
             CHGDTAARA  DTAARA(*LDA (42 4)) VALUE(&YRCHAR)
             CHGDTAARA  DTAARA(*LDA (56 4)) VALUE(&YRCHAR)

       /* LDA: From/To Week */

             CHGVAR     VAR(&WKCHAR) VALUE(&SATWK)
             CHGDTAARA  DTAARA(*LDA (46 2)) VALUE(&WKCHAR)
             CHGDTAARA  DTAARA(*LDA (60 2)) VALUE(&WKCHAR)

/* Also set LDA for Farm/Cell to be for all Farms/Cells                    */
/*      set the EMail address to blank and the Report/DWL Flag to R=Report */

             CHGDTAARA  DTAARA(*LDA (2 5))   VALUE('00000') /* Farm Site */
             CHGDTAARA  DTAARA(*LDA (40 2))  VALUE('00')    /* Cell      */
             CHGDTAARA  DTAARA(*LDA (71 1))  VALUE(' ')     /* Report    */
             CHGDTAARA  DTAARA(*LDA (72 50)) VALUE(' ')     /* EMail     */

             ENDDO                                                  /* Upload*/

/* End of Special Logic needed for call from Safedata Upload       */
/*-----------------------------------------------------------------*/

/* Continue with normal, on-demand logic                           */

             CHGVAR     VAR(&LDFSCD)    VALUE(%SST(*LDA 2 5))
             CHGVAR     VAR(&LDFRPWEDT) VALUE(%SST(*LDA 48 8))
             CHGVAR     VAR(&LDTRPWEDT) VALUE(%SST(*LDA 62 8))

/*-------------------------------------------------------*/
/* QUERY THE BGF WEEKLY WEAN PROJECTIONS FILE            */
/*-------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('PWRPWEDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 14 8)) VALUE(&LDFRPWEDT)
             CHGVAR     VAR(%SST(&QDATA 23 17)) VALUE('*AND PWRPWEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 41 8)) VALUE(&LDTRPWEDT)


             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 50 15)) VALUE('*AND PWFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 66 5)) VALUE(&LDFSCD)
             ENDDO

             OVRDBF     FILE(HSL086A) SHARE(*YES)
             OPNQRYF    FILE((HSL086A)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-----------------------*/
/* Create/Build Workfile */
/*-----------------------*/

             CRTDUPOBJ  OBJ(HSP3022) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL PGM(HP3022)
             CLOF       OPNID(HSL086A)


/*---------------------------*/
/* ON-DEMAND: PRINTED REPORT */
/*---------------------------*/

             IF         COND(&LDODUP *EQ 'D' *AND &LDRPFL *EQ 'R') +
                          THEN(DO)
             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)
             CALL       PGM(HP6022)
             ENDDO

/*----------------------*/
/* ON-DEMAND: DWL EMAIL */
/*----------------------*/

             IF         COND(&LDODUP *EQ 'D' *AND &LDRPFL *EQ 'D') +
                          THEN(DO)                                   /* EMail */

/* Retrieve the number of records in the workfile                             */

             RTVMBRD    FILE(HSP3022) NBRCURRCD(&RCDCNT)

             IF         COND(&RCDCNT = 0) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('HPS: +
                          HP6022-Listing of BGF Weekly Wean +
                          Projections---XLS Download') MSG('Your +
                          selections on HP4022-Specify Options for +
                          BGF Weekly Wean Projections did not +
                          extract any data for the spreadsheet +
                          download. Please change your selections +
                          and submit the Download again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT >= 66000) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('HPS: +
                          HP6022-Listing of BGF Weekly Wean +
                          Projections---XLS Download') MSG('Your +
                          selections on HP4022-Specify Options for +
                          BGF Weekly Wean Projections extracted +
                          over 66,000 records--which is too many +
                          records for the spreadsheet to handle. +
                          Change your selections to extract fewer +
                          records and submit your Download request +
                          again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT > 0 *AND &RCDCNT < 66000) THEN(DO)
             EXECUTE    SQL('SELECT * FROM HSP3022') NBRRCDS(*ALL) +
                          PCFMT(*XLS) RECIPIENT(&LDEMAIL) +
                          EMLMSG('HPS: HP6022-Listing of BGF Weekly +
                          Wean Projections') TEXT('HPS: +
                          HP6022-Listing of BGF Weekly Wean +
                          Projections')
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO                                                   /* EMail */

/*----------------------*/
/* FROM SAFEDATA UPLOAD */
/*----------------------*/
/* When printing from the upLoad, we need to print a copy on 2 different */
/* printers.                                                            */

             IF         COND(&LDODUP *EQ 'U') THEN(DO)

 /* Guymon: GUYPRT06 */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(GUYPRT06) +
                          WIDTH(198) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(1) HOLD(*NO)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)
             CALL       PGM(HP6022)
             DLTOVR     FILE(QPRINT)

 /* Optima: GUYPRT311 */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(GUYPRT311) +
                          WIDTH(198) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(1) HOLD(*NO)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)
             CALL       PGM(HP6022)
             DLTOVR     FILE(QPRINT)

/*--------------------------------------------------------------*/ /* 120610 */
/*     SENDING EMAILS:                                          */ /* 120610 */
/*--------------------------------------------------------------*/ /* 120610 */

  IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)                      /* 120610 */
   CHGVAR VAR(&EMAIL) VALUE('hps weekly wean projections')         /* 120610 */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Wkly Wean +
                          Projections') MSG('Distribution +
                          List: HPS Safedata Weekly Wean Projections +
                          ') ATTLIST((QPRINT *PDF *N *ALL +
                          * *LAST *ONLY))
    MONMSG     MSGID(CPF0000)                                      /* 120610 */
    ENDDO                                                          /* 120610 */

  IF         COND(&TSTPRDFL *EQ 'T') THEN(DO)                      /* 120610 */
   CHGVAR     VAR(&EMAIL) VALUE('hps safedata test')               /* 120610 */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('TEST: Wkly Wean +
                          Projections') +
                          MSG('Distribution List: TEST: HPS +
                          Safedata Weekly Wean Projections +
                          ') ATTLIST((QPRINT +
                          *PDF *N *ALL * *LAST *ONLY))
   MONMSG     MSGID(CPF0000)                                       /* 120610 */
   ENDDO                                                           /* 120610 */
             ENDDO

             ENDDO                                                /* Multiple */

             DLTOVR     FILE(*ALL)

             ENDPGM

