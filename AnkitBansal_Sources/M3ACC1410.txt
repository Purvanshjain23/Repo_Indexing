      *****************************************************************
      *
      *  Program number:  M3ACC1410
      *  Program name:    Accum mult 1410 on 1 inv to one 1410 rcd
      *  Programmer:      Rose Centonze
      *  Date created:    06/14/2016
      *
      *  Program purpose: Only 1 1410 per invoice can be interfaced to M3, so accum
      *                   additional ones for allowances, taxes to the --0-- seq 1410
      *                   and delete the other ones.
     A*  RMC 1/20/17  Project  E8287
     A*               Recompile for omp300 change
     A*      3/15/17  Add new fields to rename also
      *
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FOML300D   UF   E           K DISK
     F* OM Workfile            (Key: Co#, invoice, order , seq , sec seq)
     Foml300i   uf   e           K disk    rename(omp300r:oml300ar)
      * OM Invoicing workfile   (key: co, gl acct id, ord , seq)
      *
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D return          s              7
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      * Save fields
      *
      * Workfields
      *
      *
     D svco#           s                   like(omco#)
     D svaid           s                   like(omaid)
     D svinv#          s                   like(ominv#)
     D svord#          s                   like(omord#)
     D svseq#          s                   like(omseq#)
     D svsec#          s                   like(omsec#)
      *
      *
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
     Ioml300ar
      * omp300 with diff access path for update
      * Renamed input format fields
     I              OMCO#                       DSCO#
     I              OMORD#                      DSORD#
     I              OMSEQ#                      DSSEQ#
     I              OMSEC#                      DSSEC#
     I              OMOTYP                      DSOTYP
     I              OMINV#                      DSINV#
     I              OMCUST                      DSCUST
     I              OMSLS#                      DSSLS#
     I              OMSITM                      DSSITM
     I              OMIGRP                      DSIGRP
     I              OMICLS                      DSICLS
     I              OMITXT                      DSITXT
     I              OMASDT                      DSASDT
     I              OMFTYP                      DSFTYP
     I              OMCODE                      DSCODE
     I              OMTEXT                      DSTEXT
     I              OMAMT                       DSAMT
     I              OMUNIT                      DSUNIT
     I              OMDOC#                      DSDOC#
     I              OMAID                       DSAID
     I              OMMCU                       DSMCU
     I              OMOBJ                       DSOBJ
     I              OMSUB                       DSSUB
     I              OMSBLTY                     DSSBLTY
     I              OMSBL                       DSSBL
     I              OMSBLFL                     DSSBLFL
     i              OMM3GLLV                    dsm3gliv
     i              OMM3STS                     dsm3sts
      *
      /eject
      *-------------------------------------------------------------------------------------
      * Mainline
      *-------------------------------------------------------------------------------------
      *
      * Read omp300
      *
      *
     C     *IN50         doweq     *OFF                                         HAVE RECORD
     C                   READ      OMp300r                                50
      * Break
     C                   if        ominv#<> svinv#  or
     C                             *in50 = '1'
      *
      * upd saved invoice with new amt if changed
      *
     c     wkamt         ifne      svamt
     c     key01         chain     oml300ar                           51
     c     *IN51         ifeq      '0'
     c                   z-add     wkamt         dsamt
     c                   update    oml300ar
     c                   end
     c                   end
     c                   end                                                   end break
      *
     C     *IN50         IFEQ      *OFF                                         HAVE RECORD
     C                   if        omobj =  '1410  '  and
     c                             omseq#=  0
     c                   z-add     OMCO#         svCO#
     c                   z-add     OMINV#        svINV#
     c                   z-add     OMORD#        svORD#
     c                   z-add     OMSEQ#        svSEQ#
     c                   z-add     OMSEC#        svSEC#
     c                   movel     omaid         svaid
     c                   z-add     omamt         svamt            11 2
     c                   z-add     omamt         wkamt            11 2
     c                   end
      *
     C                   if        omobj =  '1410  '  and
     c                             omseq#>  0
     c     svco#         ifeq      omco#
     c     svinv#        andeq     ominv#
     c     svord#        andeq     omord#
     c                   add       omamt         wkamt
     c                   delete    omp300r
     c                   end
     c                   end
      *
     c                   end                                                     in50
     c                   enddo
     c                   seton                                        lr
      *----------------------------------------------------------------
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    return
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    svco#                          Co # passed in
     C                   kfld                    svaid                          gl aid
     C                   kfld                    svord#
     C                   kfld                    svseq#
      *
      *
     C                   endsr
      /eject
