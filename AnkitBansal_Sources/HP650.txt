      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP650
      * TITLE:       LISTING OF FARM SITES
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     4/04/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE FARM SITES.
      *           THIS PROGRAM IS SUBMITTED FROM HP450 - WORK WITH
      *           FARM SITES.
      *
      *           The restrictors/positioners/selectors on the Work With will be used
      *           as the report selections.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           Added a) square feet values and b) hog capacity which is calculated from
      *           square feet values.
      *
      * 02/23/02  LeAnne Fedor
      *           Added call to a new generic program to retrieve 'total square feet per farm'.
      *
      * 09/12/02  LeAnne Fedor
      *           Added following fields:
      *             county
      *             group type
      *             pod
      *             type of farm
      *             consolidated farm number
      *             company relationship
      *
      * 01/14/03  LeAnne Fedor
      *           The restrictors/positioners/selectors on the Work With will be used
      *           as the report selections.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL018A   IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
      *
     Fhsp011    IF   E           K DISK
      *    Production phase
      *
      *
     FHSP012    IF   E           K DISK
      *    Company relationship
      *
      *
     Fhsp016    IF   E           K DISK
      *    Managers
      *
      *
     FHSP025    IF   E           K DISK
      *    Group type
      *
      *
     Fhsp101    IF   E           K DISK
      *    Type of farm
      *
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDRPFL                 1      1
     D  LDFSBO                 2      6
     D  LDFSCD                 7     11  0
      *
     D  ldcnfscd              12     16  0
     D  ldtfcd                17     21
     D  ldgtcd                22     22
     D  ldpod                 23     25  0
     D  ldcell                26     27  0
     D  ldptcd                28     32
     D  ldppcd                33     37
     D  ldpmgcd               38     42
     D  ldcmgcd               43     47
     D  ldmmgcd               48     52
     D  ldaist                53     53
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE FARM SITE FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     LDFSBO        SETLL     HSL018A
     C                   EXCEPT    HRHDR
      *
      * READ AND PRINT RECORDS IN THE FARM SITE MASTER WITH THE
      *    BUSINESS OFFICE SENT THROUGH THE PARM IF NOT BLANK
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL018A                                90
     C                   if        *in90 = *off and                             IF NOT EOF
     C                             (ldfsbo = *blank or fsfsbo = ldfsbo) and
     C                             (ldfscd = 0 or ldfscd = fsfscd) and
     C                             (ldcnfscd = 0 or ldcnfscd = fscnfscd) and
     C                             (ldtfcd = *blank or fstfcd = ldtfcd) and
     C                             (ldgtcd = *blank or fsgtcd = ldgtcd) and
     C                             (ldpod = 0 or ldpod = fspod) and
     C                             (ldcell = 0 or ldcell = fscell) and
     C                             (ldptcd = *blank or fsptcd = ldptcd) and
     C                             (ldppcd = *blank or fsppcd = ldppcd) and
     C                             (ldpmgcd = *blank or fspmgcd = ldpmgcd) and
     C                             (ldcmgcd = *blank or fscmgcd = ldcmgcd) and
     C                             (ldmmgcd = *blank or fsmmgcd = ldmmgcd) and
     C                             (ldaist = *blank or fsaist = ldaist)
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT BUSINESS OFFICE WHEN A NEW ONE IS READ
      *
     C     FSFSBO        IFNE      SVFSBO
     C                   EXCEPT    HRHDR2
     C                   MOVEL     FSFSBO        SVFSBO
     C                   ENDIF
      *
      *
      * Put 'farm description' into a shortened print field.
      *
     C                   movel(p)  fsfsnm        d1fsnm
      *
      * RETRIEVE PRODUCTION TYPE DESCRIPTION
      *
     C     FSPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *ON
     C                   MOVEL     'Unknonw'     PTPTDS
     C                   ENDIF
      *
      * RETRIEVE GROUP TYPE DESCRIPTION
      *
     C     FSGTCD        CHAIN     HSP025                             91
     C     *IN91         IFEQ      *ON
     C                   MOVEL     'Unknown'     D1GTDS
     C                   ELSE
     C                   MOVEL     GTGTDS        D1GTDS
     C                   ENDIF
      *
      *
      * Retrieve Company Relationship description
      *
     C     FSCRCD        CHAIN     HSP012                             91
     C     *IN91         IFEQ      *ON
     C                   MOVEL     'Unknown'     D1CRDS
     C                   ELSE
     C                   MOVEL     CRCRDS        D1CRDS
     C                   ENDIF
      *
      * Call the generic program to retrieve total square feet on the farm.
      *
     C                   call      'HP8003'
     C                   parm                    fsfscd
     C                   parm      0             xxfssqft
      *
      * CALCULATE 'HOG CAPACITY'
      *
     C                   IF        FSHDSQFT <> 0
     C     XXFSSQFT      DIV(H)    FSHDSQFT      D1CPHD
     C                   ENDIF
      *
      * PRINT FARM SITE LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      *  EOF PROCESSING
      *---------------------------------------------------------------
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   MOVE      LDFSBO        FSFSBO
     C                   EXCEPT    HRHDR2
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    FSFSBO        SVFSBO
     C                   z-add     0             d1cphd            5 0
      *
     C                   z-add     0             xxfssqft          7 0
     C                   move      *blank        d1gtds           15
     C                   move      *blank        d1crds           10
     C                   move      *blank        d1fsnm           20
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVE      NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH            198
     C                   TIME                    RTIME             6 0
      *
      * Set up report selections in headings
      *
     C     *like         define    ldfsbo        hrfsbo
     C     *like         define    tftfds        hrtfds
     C     *like         define    gtgtds        hrgtds
      *
     C     *like         define    ldpmgcd       hrpmgcd
     C     *like         define    mgmgnm        hrpmgnm
      *
     C     *like         define    ldcmgcd       hrcmgcd
     C     *like         define    mgmgnm        hrcmgnm
      *
     C     *like         define    ldmmgcd       hrmmgcd
     C     *like         define    mgmgnm        hrmmgnm
      *
     C     *like         define    ldptcd        hrptcd
     C     *like         define    ptptds        hrptds
      *
     C     *like         define    ldppcd        hrppcd
     C     *like         define    ppppds        hrppds
      *
     C                   if        ldfsbo = *blank
     C                   movel     'All'         hrfsbo
     C                   else
     C                   movel     ldfsbo        hrfsbo
     C                   endif
      *
     C                   if        ldfscd = 0
     C                   seton                                        60
     C                   endif
      *
     C                   if        ldcnfscd = 0
     C                   seton                                        61
     C                   endif
      *
     C                   if        ldaist = *blank
     C                   seton                                        62
     C                   endif
      *
     C                   if        ldtfcd = *blank
     C                   movel     'All'         hrtfds
     C                   else
     C     ldtfcd        chain     hsp101                             92
     C                   if        *in92 = *off
     C                   movel     tftfds        hrtfds
     C                   else
     C                   movel     'Unknown'     hrtfds
     C                   endif
     C                   endif
      *
     C                   if        ldgtcd = *blank
     C                   movel     'All'         hrgtds
     C                   else
     C     ldgtcd        chain     hsp025                             92
     C                   if        *in92 = *off
     C                   movel     gtgtds        hrgtds
     C                   else
     C                   movel     'Unknown'     hrgtds
     C                   endif
     C                   endif
      *
     C                   if        ldcell = 0
     C                   seton                                        63
     C                   endif
      *
     C                   if        ldpod = 0
     C                   seton                                        64
     C                   endif
      *
      *
     C                   if        ldptcd = *blank
     C                   movel     'All'         hrptcd
     C                   else
     C                   movel     ldptcd        hrptcd
     C     ldptcd        chain     hsp010                             92
     C                   if        *in92 = *off
     C                   movel     ptptds        hrptds
     C                   else
     C                   movel     'Unknown'     hrptds
     C                   endif
     C                   endif
      *
      *
     C                   if        ldppcd = *blank
     C                   movel     'All'         hrppcd
     C                   else
     C                   movel     ldppcd        hrppcd
     C     ldppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   movel     ppppds        hrppds
     C                   else
     C                   movel     'Unknown'     hrppds
     C                   endif
     C                   endif
      *
      *
     C                   if        ldpmgcd = *blank
     C                   movel     'All'         hrpmgcd
     C                   else
     C                   movel     ldpmgcd       hrpmgcd
     C     ldpmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   movel     mgmgnm        hrpmgnm
     C                   else
     C                   movel     'Unknown'     hrpmgnm
     C                   endif
     C                   endif
      *
      *
     C                   if        ldcmgcd = *blank
     C                   movel     'All'         hrcmgcd
     C                   else
     C                   movel     ldcmgcd       hrcmgcd
     C     ldcmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   movel     mgmgnm        hrcmgnm
     C                   else
     C                   movel     'Unknown'     hrcmgnm
     C                   endif
     C                   endif
      *
      *
     C                   if        ldmmgcd = *blank
     C                   movel     'All'         hrmmgcd
     C                   else
     C                   movel     ldmmgcd       hrmmgcd
     C     ldmmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   movel     mgmgnm        hrmmgnm
     C                   else
     C                   movel     'Unknown'     hrmmgnm
     C                   endif
     C                   endif
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          100 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          100 'LISTING OF FARM SITES'
     O                                          188 'TIME'
     O                       RTIME              198 '  :  :  '
      *
      *
     O          E            HRHDR          2
     O                                          188 'PAGE'
     O                       PAGE          Z    198
      *
      *-------------------------------------------------------------
      * Report selections
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                           26 'Business office .........:'
     O                       hrfsbo              33
      *
     O                                           52 'Farm type ..:'
     O                       hrtfds              79
      *
     O                                          100 'Production type .....:'
     O                       hrptcd             107
     O                       hrptds             119
      *
      *
     O          E            HRHDR          1
     O                                           26 'Farm site ...............:'
     O              n60      ldfscd        z     33
     O               60                          31 'All'
      *
     O                                           52 'Group type .:'
     O                       hrgtds              69
     O                                          100 'Production phase ....:'
     O                       hrppcd             107
     O                       hrppds             139
      *
      *
     O          E            HRHDR          1
     O                                           26 'Consolidated farm site ..:'
     O              n61      ldcnfscd      z     33
     O               61                          31 'All'
      *
     O                                           52 'Cell .......:'
     O              n63      ldcell        z     56
     O               63                          57 'All'
      *
     O                                          100 'Cell manager ........:'
     O                       hrcmgcd            107
     O                       hrcmgnm            134
      *
      *
     O          E            HRHDR          1
     O                                           26 'Active/inactive status ..:'
     O              n62      ldaist              29
     O               62                          31 'All'
      *
     O                                           52 'Pod ........:'
     O              n64      ldpod         z     57
     O               64                          57 'All'
      *
     O                                          100 'Production manager ..:'
     O                       hrpmgcd            107
     O                       hrpmgnm            134
      *
      *
     O          E            HRHDR          2
     O                                          100 'Multisite manager ...:'
     O                       hrmmgcd            107
     O                       hrmmgnm            134
      *
      *
      *
      *-------------------------------------------------------------
      * Column Headings
      *-------------------------------------------------------------
     O          E            HRHDR          1
     O                                           36 'ACTIVE/'
     O                                           42 'CONS'
     O                                           96 'TYPE'
     O                                          178 'TOTAL'
     O                                          188 'TARGET'
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           36 'INACTIVE'
     O                                           42 'FARM'
     O                                           89 '---- PRODUCTION ----'
     O                                           95 'OF'
     O                                          124 'COMPANY'
     O                                          178 'SQ FT'
     O                                          188 'SQ FT'
     O                                          196 'HOG'
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           18 'DESCRIPTION'
     O                                           35 'STATUS'
     O                                           42 'SITE'
     O                                           48 'CELL'
     O                                           53 'POD'
     O                                           67 'COST CENTER'
     O                                           75 'TYPE'
     O                                           89 'PHASE'
     O                                           96 'FARM'
     O                                          110 'GROUP TYPE'
     O                                          129 'RELATIONSHIP'
     O                                          140 'STATE'
     O                                          149 'COUNTY'
     O                                          178 'FOR FARM'
     O                                          188 'PER PIG'
     O                                          198 'CAPACITY'
      *
     O          E            HRHDR          0
     O                       DASH               198
      *
     O          E            HRHDR2      2  1
     O                                           16 'BUSINESS OFFICE:'
     O                       FSFSBO              23
      *
      *-------------------------------------------------------------
      * FARM SITE DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FSFSCD        z      5
     O                       d1fsnm         b    27
     O                       fsaist              32
     O                       FSCNFSCD      z     42
     O                       FSCELL        z     48
     O                       FSPOD         z     54
     O                       FSCJD               67
     O                       PTPTDS         b    79
     O                       fsppcd              89
     O                       fstfcd              97
     O                       d1gtds         b   115
     O                       d1crds         b   127
     O                       fssacd             140
     O                       fscnty             168
     O                       xxfssqft      k    179
     O                       fshdsqft      k    188
     O                       d1cphd        kb   198
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
