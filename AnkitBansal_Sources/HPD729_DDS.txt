     A*%%TS  SD  20030106  161732  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD729
     A*  TITLE:         VIEW SCHEDULED TRANSFER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/06/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  90  THE TRANSFER THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP729S1                   SFL
     A*%%TS  SD  19991109  141520  ISLRAMS     REL-V4R2M0  5769-PW1
     A            HDRRN1         4Y 0H
     A            DFORBL    R        O 17  2REFFLD(@@BLCD)
     A            DFORRM    R        O 17  9REFFLD(@@RMCD)
     A            DFORHG    R        O 17 16REFFLD(HGHGCD)
     A            DFSCHD    R        O 17 24REFFLD(MDSCHD)
     A                                      EDTCDE(K)
     A            DFORGS    R        O 17 34REFFLD(HGGSCD)
     A            DFDNBL    R        O 17 46REFFLD(@@BLCD)
     A            DFDNRM    R        O 17 54REFFLD(@@RMCD)
     A            DFDNHG    R     A  O 17 62REFFLD(HGHGCD)
     A            DFDNGS    R        O 17 73REFFLD(HGGSCD)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP729C1                   SFLCTL(HP729S1)
     A*%%TS  SD  20030106  161732  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A N90N82                               ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 25'VIEW SCHEDULED TRANSFER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number ......:'
     A            C1MVSN    R        O  4 27REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  5  2'Scheduled ship date ..:'
     A            C1MDY     R     S  O  5 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      TEXT('received date')
     A                                  6  2'Trucker ..............:'
     A            C1TICD    R        O  6 27REFFLD(MHTICD)
     A            C1TINM    R        O  6 38REFFLD(TITINM)
     A                                  8  2'Total scheduled head .:'
     A            C1SCHD    R        O  8 27REFFLD(MDSCHD)
     A                                      EDTCDE(K)
     A                                 10  2'-------------- ORIGIN -------------
     A                                      --'
     A                                      DSPATR(HI)
     A                                 10 43'------------ DESTINATION ----------
     A                                      ---'
     A                                      DSPATR(HI)
     A                                 11  2'Farm .:'
     A            C1ORFS    R        O 11 10REFFLD(FSFSCD)
     A                                      EDTCDE(Z)
     A                                 11 43'Farm .:'
     A            C1DNFS    R        O 11 52REFFLD(FSFSCD)
     A                                      EDTCDE(Z)
     A            C1ORNM    R        O 12 12REFFLD(FSFSNM)
     A            C1DNNM    R        O 12 54REFFLD(FSFSNM)
     A                                 13 32'                                  -
     A                                                    '
     A                                      DSPATR(HI)
     A                                 14 25'Sched'
     A                                      DSPATR(HI)
     A                                 14 33'Group'
     A                                      DSPATR(HI)
     A                                 14 71'Group'
     A                                      DSPATR(HI)
     A                                 15  2'Bldg'
     A                                      DSPATR(HI)
     A                                 15  9'Room'
     A                                      DSPATR(HI)
     A                                 15 16'Group'
     A                                      DSPATR(HI)
     A                                 15 26'Head'
     A                                      DSPATR(HI)
     A                                 15 32'Status'
     A                                      DSPATR(HI)
     A                                 15 46'Bldg'
     A                                      DSPATR(HI)
     A                                 15 54'Room'
     A                                      DSPATR(HI)
     A                                 15 62'Group'
     A                                      DSPATR(HI)
     A                                 15 71'Status'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP729K1
     A*%%TS  SD  19951006  132726  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP729ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP729EC                   SFLCTL(HP729ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
