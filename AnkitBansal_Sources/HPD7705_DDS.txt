     A*%%TS  SD  20040802  084150  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7705
     A*  TITLE:         VIEW BUDGET ITEM ON INACTIVE/ACTIVE FARM BUDGETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/11/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7705S1                  SFL
     A*%%TS  SD  20040802  084150  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O 13  2REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1BGQT    R        O 13  9REFFLD(HSRREF/@@BGQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1FBAM    R        O 13 25REFFLD(HSRREF/@@FBAM *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1FBSCD   R        O 13 42REFFLD(@@FBSCD)
     A            D1FBSN    R        O 13 47REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1BTCD    R        O 13 57REFFLD(@@BTCD)
     A            D1BTQT    R        O 13 64REFFLD(HSRREF/@@BTQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7705C1                  SFLCTL(HP7705S1)
     A*%%TS  SD  20040802  084150  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 12'VIEW BUDGET ITEM ON INACTIVE/ACTIV-
     A                                      E FARM BUDGETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Budget item:'
     A                                      DSPATR(HI)
     A            C1BGIT    R        O  4 15REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A                                  5  2'Description:'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  5 15REFFLD(HSRREF/@@ITDS *LIBL/HSPREF)
     A                                  6  2'UOM .......:'
     A                                      DSPATR(HI)
     A            C1UOM     R        O  6 15REFFLD(HSRREF/@@UOM *LIBL/HSPREF)
     A                                  7  2'UOW .......:'
     A                                      DSPATR(HI)
     A            C1UOW     R        O  7 15REFFLD(HSRREF/@@UOW *LIBL/HSPREF)
     A                                  9  3'?'
     A                                      DSPATR(HI)
     A                                  9 42'?'
     A                                      DSPATR(HI)
     A                                  9 59'?'
     A                                      DSPATR(HI)
     A                                 10  2'Farm'
     A                                      DSPATR(HI)
     A                                 10  9'---- Farm Site Budgeted ----'
     A                                      DSPATR(HI)
     A                                 10 40'Budget'
     A                                      DSPATR(HI)
     A                                 10 48'Budget'
     A                                      DSPATR(HI)
     A                                 10 57'-- Budget Template --'
     A                                      DSPATR(HI)
     A                                 11  2'Site'
     A                                      DSPATR(HI)
     A                                 11 15'Quantity'
     A                                      DSPATR(HI)
     A                                 11 31'Amount'
     A                                      DSPATR(HI)
     A                                 11 40'Status'
     A                                      DSPATR(HI)
     A                                 11 48'Number'
     A                                      DSPATR(HI)
     A                                 11 57'Code'
     A                                      DSPATR(HI)
     A                                 11 70'Quantity'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B 12  2REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1FBSCD   R        B 12 42REFFLD(@@FBSCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1BTCD    R        B 12 57REFFLD(@@BTCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7705K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7705ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7705EC                  SFLCTL(HP7705ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
