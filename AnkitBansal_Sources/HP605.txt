      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP605
      * TITLE:       LISTING OF FEED MANUFACTURING TYPES
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     4/05/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           FEED MANUFACTURING TYPE MASTER FILE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 04/20/95  LEANNE FEDOR
      *           CHANGED TO USE PROGRAM STATUS DATA STRUCTURE OF
      *           REPORT HEADING VALUES.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP024    IF   E           K DISK
      *    FEED TYPES                  (KEY: FTFTCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE FEED TYPES MASTER FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSP024
     C                   EXCEPT    HRHDR
      *
      * READ AND PRINT ALL RECORDS IN THE FEED TYPE MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP024                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT FEED TYPE DETAIL LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           41 'LISTING OF FEED MANUF'
     O                                           55 'ACTURING TYPES'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          2
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                            9 'FEED TYPE'
     O                                           24 'DESCRIPTION'
     O                                           34 'STATUS'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FEED TYPES DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FTFTCD               5
     O                       FTFTDS              23
     O                       FTFTST              31
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
