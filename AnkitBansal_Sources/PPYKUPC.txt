/*********************************************************************/
/*  PROGRAM  - PPYKUPC                                               */
/*  SYNOPSIS - Print Internal Company Edit, Calculate Price, and     */
/*             then update the price in the Order Detail.            */
/*             ALSO, PRINTS A REPORT OF ALL 3RD PARTY INOICES        */
/*             THAT STILL NEED TO BE PRINTED.                        */
/*                                                                   */
/*  SYSTEM   - ORDER MANAGEMENT SYSTEM                               */
/*  PROGRAM  - PRINT ERROR REPORT AND UPDATE TRANSFER PRICE IN       */
/*             ORDER DETAIL                                          */
/*             THIS PROGRAM CALLS:                                   */
/*                PPUBPFR - PRT INTER-CO TRANSPRICPF                 */
/*                          PRINTS REPORT OF 3RD PARTY NOT INVOICED  */
/*                PPUCPFR - PRT INTER-CO PRICE/UPDPF                 */
/*                          PRINTS REPORT OF TRANSFER PRICE EDIT     */
/*                         ERRORS, THEN MAKES THE PRICING CHANGES    */
/*                PPXZPFR - PRT INVOICE EXCPT RPT UP                 */
/*                          SUBMITS THE INVOICE EXCEPTION REPORT     */
/*                                                                   */
/*  DATE     - 05/09/2006                                            */
/*                                                                   */
/*      PARAMETERS:                                                  */
/*                                                                   */
/*     O    &RETURN     - RETURN CODE                                */
/*     I    &APPLIC     - APPLICATION CODE                           */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT      */
/*                         SCREEN)                                   */
/*     B    &OUTQ       - OUTPUT QUEUE                               */
/*                        IF THIS IS BLANK AT START, PROMPT          */
/*                        PGM IS CALLED; OTHERWISE, THE PRINT CL     */
/*                        IS SUBMITTED WITHOUT PROMPTING             */
/*     B    &HOLD       - HOLD STATUS                                */
/*     B    &SAVE       - SAVE STATUS                                */
/*     B    &NQSTAT     - INDICATOR WHETHER MUST RUN IN NIGHT Q      */
/*     B    &NIGHTQ     - NIGHT JOB QUEUE                            */
/*     B    &FDATEL     - FROM DATE      (PASSED)                    */
/*     B    &TDATEL     - THRU DATE      (PASSED)                    */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                    */
/*     I    &COMPNYN    - COMPANY NUMBER (PASSED AS 15.5)            */
/*     I    &PRCUSTT    - PROCESS CUSTOMER TYPES - IC OR TP          */
/*     I    &STATUSYN   - PRINT AMOUNTS STATUS FLAG                  */
/*     I    &JOBNME     - JOB NAME                                   */
/*********************************************************************/
/* DN  03/27/17  R9700 - STF Inter Company & Affliated Sales Pricing */
/*               Added Inter Company Group flag as input parm.       */
/*********************************************************************/

 PPYKUPC:    PGM        PARM(&RETURN &DESC &OUTQ &HOLD &SAVE &NQSTAT +
                          &NIGHTQ &COMPNYN &FDATEL &TDATEL &COPYN +
                          &STATUSYN &PRCUSTT &JOBNME &ICGROUP)
/* PARMS                                                                      */
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7) /* Return +
                          Code               */
             DCL        VAR(&DESC) TYPE(*CHAR) LEN(25) /* PRT Pgm +
                      Description       */
             DCL        VAR(&COPYN) TYPE(*DEC) /* No. of +
                          Copies             */
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10) /* Output +
                          Queue              */
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4) /* Hold +
                          Status               */
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4) /* Save +
                          Status               */
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10) /* Night +
                          Job Queue           */
             DCL        VAR(&BEGTO) TYPE(*DEC) LEN(7 0) /*  +
                          BEGINNING TO   */
             DCL        VAR(&FDATE) TYPE(*DEC) LEN(7 0) /*  FROM +
                          DATE      */
             DCL        VAR(&FDATEL) TYPE(*DEC) /*  FOR PASSING    */
             DCL        VAR(&TDATE) TYPE(*DEC) LEN(7 0) /*  THROUGH +
                          DATE   */
             DCL        VAR(&TDATEL) TYPE(*DEC) /*  FOR PASSING    */
             DCL        VAR(&TYPRUN) TYPE(*CHAR) LEN(1) /*  TYPE OF +
                          RUN           */
             DCL        VAR(&ERROR1) TYPE(*CHAR) LEN(1) /*  ERROR ON +
                          REPORT 1     */
             DCL        VAR(&ERROR2) TYPE(*CHAR) LEN(1) /*  ERROR PM +
                          REPORT 2     */
             DCL        VAR(&COMPNYN) TYPE(*DEC) /*  FOR PASSING    */
             DCL        VAR(&LOADN) TYPE(*DEC) /*  FOR PASSING    */
             DCL        VAR(&COMMAND) TYPE(*CHAR) LEN(512)
             DCL        VAR(&APPLIC) TYPE(*CHAR) LEN(6) VALUE(OMS)
             DCL        VAR(&NQSTAT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&ORDRN) TYPE(*DEC)
             DCL        VAR(&BILTYP) TYPE(*CHAR) VALUE('1')
             DCL        VAR(&JOBNME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&STATUSYN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PRCUSTT) TYPE(*CHAR) LEN(2)
/*R9700*/    DCL        VAR(&ICGROUP) TYPE(*CHAR) LEN(1)

/* INTERNAL FIELDS                                                            */
             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0) /* Number of +
                          copies          */

/* ERROR MESSAGE FIELDS                                                       */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512) /* +
                          message                   */
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7) /* message +
                          id                */
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

/*------------------------------------------------------------------*/
/*   INITIALIZE VARIABLES                                           */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&FDATE) VALUE(&FDATEL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&TDATE) VALUE(&TDATEL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&BEGTO) VALUE(&TDATEL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&BILTYP) VALUE(1)
             CHGVAR     VAR(&ORDRN) VALUE(0)

/*------------------------------------------------------------------*/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/*------------------------------------------------------------------*/
             OVRPRTF    FILE(PPUBPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*------------------------------------------------------------------*/
/* PRINT REPORT OF ALL 3RD PARTY INVOICES THAT STILL NEED TO BE     */
/* PRINTED.                                                         */
/* R9700 - Added &ICGROUP as parm.                                  */
/*------------------------------------------------------------------*/
 CALL1:      CALL       PGM(PPUBPFR) PARM(' ' &BEGTO &FDATE &TDATE +
                          &ERROR1 &TYPRUN &ICGROUP)

             DLTOVR     FILE(PPUBPFR$)

/*------------------------------------------------------------------*/
/* PRINT REPORT OF TRANSFER PRICE UPDATE RECORDS AND ERORORS        */
/*------------------------------------------------------------------*/
             OVRPRTF    FILE(PPUCPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* R9700 - Added &ICGROUP as parm.                                  */
 CALL2:      CALL       PGM(PPUCPFR) PARM(' ' &BEGTO &FDATE &TDATE +
                          &ERROR2 &TYPRUN &ICGROUP)

             DLTOVR     FILE(PPUCPFR$)

/*------------------------------------------------------------------*/
/* PRINT INTER-COMPANY EXCEPTION REPORT                             */
/* PPYZXFR DOES NOT RECEIVE A COMPANY NUMBER - IT LOOPS FOR IT      */
/* R9700 - Added &ICGROUP as parm.                                  */
/*------------------------------------------------------------------*/
 CALL3:      CALL       PGM(PPYZXFR) PARM(&RETURN &APPLIC &BILTYP +
                          &DESC &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COMPNYN &TDATEL &ORDRN &LOADN &COPYN +
                          &STATUSYN &PRCUSTT &JOBNME &ICGROUP)

             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))
/*------------------------------------------------------------------*/

 END:        ENDPGM
/*------------------------------------------------------------------*/
