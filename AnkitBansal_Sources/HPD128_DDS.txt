     A*%%TS  SD  19950301  094446  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD128
     A*  TITLE:         MAINTAIN AUTHORIZED FUNCTIONS FOR A USER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/13/95
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE USER THAT WAS SELECTED FROM THE
     A*               WORK WITH HAS BEEN DELETED BY ANOTHER PERSON
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  61                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP128S1                   SFL
     A*%%TS  SD  19950216  084253  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 11  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N90                            DSPATR(HI)
     A  60N30N90                            DSPATR(UL)
     A            DFFNCD    R        B 11  8REFFLD(FNFNCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A N31N60                               DSPATR(HI)
     A  61N31N90                            DSPATR(UL)
     A            DFFNDS    R        O 11 21REFFLD(FNFNDS)
     A            DFFNTY    R        O 11 58REFFLD(FNFNTY)
     A            DFACT          8A  O 11 66
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP128C1                   SFLCTL(HP128S1)
     A*%%TS  SD  19950301  094446  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP128'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 17'MAINTAIN AUTHORIZED FUNCTIONS FOR -
     A                                      A USER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'User ..:'
     A            C1USCD    R        O  4 12REFFLD(AUUSCD)
     A            C1USNM    R        O  4 25REFFLD(AUUSNM)
     A N61                              6  2'4=Delete'
     A  61                              7 12'?'
     A                                      DSPATR(HI)
     A N61                              8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  8'Function'
     A                                      DSPATR(HI)
     A                                  8 54'Function'
     A                                      DSPATR(HI)
     A                                  9 10'Code'
     A                                      DSPATR(HI)
     A                                  9 21'Description'
     A                                      DSPATR(HI)
     A                                  9 56'Type'
     A                                      DSPATR(HI)
     A  60        C1FNCD    R        B 10  8REFFLD(FNFNCD)
     A                                      DSPATR(HI)
     A  60        C1FNTY    R        B 10 58REFFLD(FNFNTY)
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP128K1
     A*%%TS  SD  19950213  131521  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A  61                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F6=Create'
     A                                 23 34'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP128ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP128EC                   SFLCTL(HP128ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
