     h option(*SRCSTMT:*NODEBUGIO)
      *
      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      TF Fees & Payments
      * PROGRAM:     Margin: Update Item Structure Fields in Revenue Detail
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     09/25/06
      *
      * Function: Before running the Margin Adjustment and Weekly Inventory Close
      *           functions, we will update the Item Structure fields in the
      *           Revenue Detail file for the Week being processed.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/13/06  LeAnne Ramsey
      *           Changed LDA positions.
      *
      * 02/14/07  LeAnne Ramsey
      *           Recompile only. New field "STD Other Cost/LB" was added to
      *           Weekly Product Revenue Detail file.
      *
      * 03/26/07  LeAnne Ramsey
      *           Added logic to retrieve the Capability Flag and populate this
      *           new field in the Weekly Product Revenue Detail file.
      *
      * 04/05/07  LeAnne Ramsey
      *           We just added the Exclude from Mix and Exclude from Volume Flags to the
      *           Weekly Product Revenue Detail file. So, I expanded the logic that
      *           retrieves the new Capablity Flag to retrieve these two "exclude" flags.
      *
      * 04/16/07  LeAnne Ramsey
      *           On the call to TF809:
      *           Added parms: Meat Cost Group Code
      *           Changed parms: Exclude from Mix Flag     to  Mix Flag
      *                          Exclude from Volume Flag  to  Volume Flag
      *                          Capability Flag           to  Inventory Flag
      *
      * 05/14/07  LeAnne Ramsey
      *           Added new parms on calls to generic programs:
      *              TF Percent Owned  (TF809)
      *              Co-owned Flag     (TF813)
      *              Producing Company (TF813)
      *           BUT, we will not update the TFP010 records with these values
      *           since all the "co-owned" logic is executed at the time
      *           the Weekly Revenue Close is run.
      *
      * 08/14/07  LeAnne Ramsey
      *           Recompile only. New field 'Item Type Code' added to
      *           TFP010.
      *
      * 07/24/08  LeAnne Ramsey
      *           We are now calling this program from the Meat Cost function as well
      *           as the TFS Weekly Inventory Close function. Since the LDAs were
      *           radically different between these two functions, we switched this
      *           program to use an incoming parm for WeekEnding Date instead of the LDA.
      *
      * 08/15/08  LeAnne Ramsey
      *           On the call to TF809:
      *           Added parm: Producing Company (3A)
      *
      * 09/11/08  LeAnne Ramsey
      *           Added parm to the call to TF813:
      *               Export/Domestic Flag
      *
      * 11/14/17  Danny Nguyen  (R12011A-Weekly Product Revenue)
      *           Recompile only. Added 25 STF fields to TFP010 file.
      *
      * 05/17/22  Danny Nguyen  (DO2484 - WI 479 STF Variance Reporting)
      *           Recompile only. Added 2 STF fields to TFP010 file.
      *           PRXYPC   - STF STD YIELD %
      *           PRXPMPPC - STF PUMP %
      *
      * 10/10/22  R Centonze   W105621 - Increase price fields to handle > 999.99
      *                        CHANGE @@ANPRPR TO SIZE 10.6  --> pranprpr tfp010
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl010a   uf   e           k disk
      *  Weekly Product Revenue Detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parm fields
      *
     D xxwedt          s              8  0
      *
     D xxprcd          s                   like(prprcd)
     D xxistycd        s                   like(pristycd)
     D xxisgrcd        s                   like(prisgrcd)
     D xxisclcd        s                   like(prisclcd)
     D xxtfclcd        s                   like(prtfclcd)
     D xxtfcgcd        s                   like(prtfcgcd)
     D xxglsub         s                   like(prglsub)
     D xxitycd         s                   like(pritycd)
     D xxinvfl         s                   like(prinvfl)
     D xxmixfl         s                   like(prmixfl)
     D xxvolfl         s                   like(prvolfl)
     D xxcgcd          s                   like(prcgcd)
     D xxcoownfl       s                   like(prcoownfl)
     D xxprdcmp        s                   like(prprdcmp)
     D xxtownpc        s                   like(prtownpc)
     D xxedfl          s                   like(predfl)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *************************************************************************************
      * Mainline
      *************************************************************************************
      *
      * Read all records this this week and update them.
      *
     C     xxwedt        setll     tfl010a
      *
     C                   dou       *in91 = *on                                  Main do
     C     xxwedt        reade     tfl010a                                91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Retrieve TF Classification and Item Structure fields
      *
     C                   call      'TF813'
     C                   parm      prprcd        xxprcd
     C     pristycd      parm      0             xxistycd
     C     prisgrcd      parm      0             xxisgrcd
     C     prisclcd      parm      0             xxisclcd
     C     prtfclcd      parm      *blank        xxtfclcd
     C     prtfcgcd      parm      *blank        xxtfcgcd
     C     prglsub       parm      *blank        xxglsub
     C     pritycd       parm      *blank        xxitycd
     C                   parm      *blank        xxcoownfl
     C                   parm      *blank        xxprdcmp
     C     predfl        parm      *blank        xxedfl
      *
      *
      * Retrieve Values associated with Item Structure
      *
     C                   call      'TF809'
     C                   parm      prtfclcd      xxtfclcd
     C                   parm      pristycd      xxistycd
     C                   parm      prisgrcd      xxisgrcd
     C                   parm      prisclcd      xxisclcd
     C     prinvfl       parm      *blank        xxinvfl
     C     prmixfl       parm      *blank        xxmixfl
     C     prvolfl       parm      *blank        xxvolfl
     C     prcgcd        parm      *blank        xxcgcd
     C                   parm      0             xxtownpc
     C                   parm      *blank        xxprdcmp
      *
     C                   update    prrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxwedt
      *
     C                   endsr
      /eject
