/***************************************************************** */
/*                                                                   */
/*  PROGRAM NUMBER:  HP477CL                                         */
/*  PROGRAM NAME:    EOP: COST MOVEMENTS, CLOSE GROUPS, CLOSE PERIOD */
/*  PROGRAMMER:      LEANNE FEDOR                                    */
/*  CREATE DATE:     06/15/94                                        */
/*                                                                   */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM          */
/*                   HP477-SPECIFY PERIOD CLOSE.                     */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.             */
/*                                                                   */
/*********************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                   */
/* 09/23/2003  Barb Gutierrez                                      */
/*             Removed the datamart HPS EOP processing from this   */
/*             CL and added it to HPBLDCG$.                        */
/*                                                                 */
/* 02/02/06    LeAnne Fedor                                        */
/*             Added new Feed Report for Created Groups.           */
/*                                                                 */
/* 02/26/16    Rose Centonze    E5290                              */
/*             Added company code TO LDA and company ctl file update */
/*             when the dtaara DAAPER is updated. Add company as parm to clps */
/*******************************************************************/

             PGM

             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)

             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&REOPEN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEFFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDACYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDACPE) TYPE(*DEC) LEN(2)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&ERROR) VALUE('N')
             CHGVAR     VAR(&REOPEN) VALUE('N')
             CHGVAR     VAR(&LDEFFL) VALUE(%SST(*LDA 19 1))
             CHGVAR     VAR(&LDACYR) VALUE(%SST(*LDA 20 4))
             CHGVAR     VAR(&LDACPE) VALUE(%SST(*LDA 17 2))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 200 3))

/*-------------------------------------------------------------------*/
/*  CHANGE DATA AREA "DAAPER" TO INDICATE THAT THE PERIOD CLOSE      */
/*  FUNCTION IS BEING RUN                                            */
/*-------------------------------------------------------------------*/

             CHGDTAARA  DTAARA(DAAPER (24 1)) VALUE('E')
             CALL       PGM(HP4772) PARM(&LDCOCD)           /* E5290 */
/*-------------------------------------------------------------------*/
/* ALWAYS REOPEN ALL CLOSED GROUPS...JUST TO BE SAFE                 */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP374CL) PARM(&REOPEN &LDCOCD)      /* E5290 */

/*-------------------------------------------------------------------*/
/*  PRELIMINARY EDITS AND EXCEPTION REPORTS                          */
/*-------------------------------------------------------------------*/

/*  PERIOD CLOSE EDITS                                               */

             CALL       PGM(HP373CL) PARM(&ERROR &LDCOCD)       /* E5290 */


/*  DISPOSED GROUP EDITS                                             */

             IF         COND(&ERROR *EQ 'N') THEN(DO)
             CALL       PGM(HP372CL) PARM(&ERROR &LDCOCD)       /* E5290 */
             ENDDO

/*  NEGATIVE INVENTORY EXCEPTION REPORT                              */

             CALL       PGM(HP3081CL) PARM(&LDCOCD)            /* E5290 */

/*  LOW INVENTORY REPORT                                             */

             CALL       PGM(HP3084CL) PARM(&LDCOCD)            /* E5290 */

/*  G/L ACCOUNT/HPS EXPENSE CODE EXCEPTION REPORT                    */

             CALL       PGM(HP3082CL)  PARM(&LDCOCD)            /* E5290 */


/* FEED REPORT FOR CREATED GROUPS                                    */

             CALL       PGM(HP3085CL)  PARM(&LDCOCD)            /* E5290 */

/*-------------------------------------------------------------------*/
/*  COST MOVEMENTS IF THERE WERE NO PRELIMINARY ERRORS               */
/*-------------------------------------------------------------------*/

             IF         COND(&ERROR *EQ 'N') THEN(DO)
             CALL       PGM(HP375CL) PARM(&ERROR &LDCOCD)  /* E5290  */
             ENDDO

/*-------------------------------------------------------------------*/
/* IF THERE ARE NO ERRORS,                                           */
/*  1) CLOSE GROUPS                                                  */
/*  2) CLOSE PERIOD                                                  */
/*-------------------------------------------------------------------*/

             IF         COND(&ERROR *EQ 'N') THEN(DO)
             CALL       PGM(HP376CL) PARM(&LDCOCD)            /* E5290 */
             CALL       PGM(HP377CL) PARM(&ERROR &LDCOCD) /* close +
                          period,     E5290  ADD COMPANY CODE  */
             ENDDO

/*-------------------------------------------------------------------*/
/* IF THIS WAS A 'FINAL' RUN WITH ERRORS OR                          */
/*    THIS WAS AN 'EDIT' RUN:                                        */
/*            1) REOPEN ALL GROUPS THAT WERE CLOSED                  */
/*            2) DELETE PERIOD COST DATA                             */
/*-------------------------------------------------------------------*/

             IF         COND((&LDEFFL *EQ 'F' *AND &ERROR *EQ 'Y') +
                          *OR &LDEFFL *EQ 'E') THEN(DO)

             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** The EOP edits have completed. +
                          Reopening is in progress. ****') TOUSR(&USER)

             CALL       PGM(HP374CL) PARM(&REOPEN &LDCOCD) /* E5290  */
             CALL       PGM(HP8013) PARM(&LDACYR &LDACPE &LDCOCD) /*E5290 */
             ENDDO

/*-------------------------------------------------------------------*/
/*  CHANGE DATA AREA "DAAPER" TO INDICATE THAT THE PERIOD CLOSE      */
/*  FUNCTION IS FINISHED                                             */
/*-------------------------------------------------------------------*/

 EXIT:       CHGDTAARA  DTAARA(DAAPER (24 1)) VALUE(' ')
             CALL       PGM(HP4772) PARM(&LDCOCD)           /* E5290 */

  END:       ENDPGM

