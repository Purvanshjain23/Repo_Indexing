      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3719-Workfile: Farm Budget Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     08/05/04
      *
      * Function:    Build a workfile of Approved/Used Quantities for a
      *              Budget Item/Farm Budget.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      * 11/18/04  LeAnne Fedor
      *           Added Total Cost Amount from Request Detail file into workfile.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsj193i   if   e           k disk
      *    Request detail + Item Master
      *
      *
     Fhsp3719   uf a e           k disk
      *    Workfile
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkapqt          s                   like(qdapqt)
     D wkusqt          s                   like(qdusqt)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldreport               1      1
     D  ldfbsn                 2      8  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdusr = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read each Request Detail record for this Farm Budget. (Do not
      * process records that have a status of Hold, Deleted, Error.)
      *
     C     ldfbsn        setll     hsj193i
     C                   dou       *in90 = *on                                  Do detail
     C     ldfbsn        reade     hsj193i                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             qdrqscd <> 'H' and
     C                             qdrqscd <> 'D' and
     C                             qdrqscd <> 'E'
      *
      * We need the request quantities in the same units they budget in since
      * we compare them to the 'budget quantity' on the listing. So, since
      * quantites in the Request Detail record are in the UOM units, you need
      * to flip to UOW units if an Item has 'volume'.
      *
     C                   if        imvol = 0
     C                   z-add     qdapqt        wkapqt
     C                   z-add     qdusqt        wkusqt
     C                   else
     C     imvol         mult      qdapqt        wkapqt
     C     imvol         mult      qdusqt        wkusqt
     C                   endif
      *
      *
      * Update or write a workfile record.
      *
     C     key01         chain     hsp3719                            92
     C                   if        *in92 = *off                                 If exists
     C                   add       wkapqt        w1apqt
     C                   add       wkusqt        w1usqt
     C                   add       qdtcam        w1tcam
     C                   update    w1rec
     C                   else
      *
     C                   z-add     ldfbsn        w1fbsn
     C                   move      imbgit        w1bgit
     C                   z-add     wkapqt        w1apqt
     C                   z-add     wkusqt        w1usqt
     C                   z-add     qdtcam        w1tcam
     C                   write     w1rec
     C                   endif                                                  If exists
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do Detail
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldfbsn
     C                   kfld                    imbgit
      *
     C                   endsr
      /eject
