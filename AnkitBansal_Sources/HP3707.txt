      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3707-Tag a Request for Deletion
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/16/04
      *
      * Function:    The actual deletion is done by John L. from the p.c. side after
      *              he has done his thing with the records we have tagged as D=Delete.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp192    uf   e           k disk
      *    Request header
      *
      *
     Fhsp193    uf   e           k disk
      *    Request detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               d   datfmt(*iso)
     D wkupdt          s              8  0
      *
      *
      * Parms
      *
     D xxrqsn          s                   like(qhrqsn)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdusr = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Tag each Detail record for the incoming Request number
      *
     C     xxrqsn        setll     hsp193
     C                   dou       *in91 = *on                                  Do detail
     C     xxrqsn        reade     hsp193                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   move      'D'           qdrqscd
     C                   update    qdrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do Detail
      *
      * Tag the Request Header record.
      *
     C     xxrqsn        chain     hsp192                             92
     C                   if        *in92 = *off                                 If got it
     C                   move      'D'           qhrqscd
     C                   z-add     wkupdt        qhupdt
     C                   time                    qhuptm
     C                   move      sdusr         qhuscd
     C                   update    qhrec
     C                   endif                                                  If got it
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxrqsn
      *
      * Save date for record stamp
      *
     C     *mdy          move      udate         wkcymdiso
     C                   move      wkcymdiso     wkupdt
      *
      *
     C                   endsr
      /eject
