     A*%%TS  SD  20171110  092801  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD483
     A*  TITLE:         SPECIFY CHECK REGISTER EDIT OPTIONS
     A*  PROGRAMMER:    ROSE MARTIN
     A*  DATE:          08/10/94
     A*
     A*****************************************************************
     A*MODIFIED
     A* 09/02/94  LEANNE FEDOR
     A*           REWRITTEN.
     A*
     A* 06/30/95  LEANNE FEDOR
     A*           CHANGED 'KILL DATE' TO 'SALES ACCRUAL DATE'.
     A*
     A* 11/10/17  Brad Baden   E11947 and E010812
     A*           Added Business Office to screen.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP483R1
     A*%%TS  SD  20040602  090629  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'SPECIFY CHECK REGISTER EDIT OPTION-
     A                                      S'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business Office ..........'
     A                                  4 29'?'
     A            DFBOCD    R        B  4 31REFFLD(@@BOCD)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 42REFFLD(@@DS30)
     A                                  5  2'From sales accrual date ..'
     A            DFFMDY    R     Y  B  5 31REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To sales accrual date ....'
     A            DFTMDY    R     Y  B  6 31REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Customer A/R number ......'
     A                                  7 29'?'
     A            DFCVNO    R        B  7 31REFFLD(HSRREF/CDCVNO *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFCNAM        23A  O  7 42
     A                                  7 66'(Blank for all)'
     A                                  8  2'Check number .............'
     A            DFCHNO    R        B  8 31REFFLD(HSRREF/CDCHNO *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  8 66'(Blank for all)'
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP483K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP483ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP483EC                   SFLCTL(HP483ES)
     A*%%TS  SD  19940810  085301  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
