/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4011CL                                      */
/*  PROGRAM NAME:    LISTINGS FROM WEEKLY SIRE LINE DOSES          */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     11/09/01                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4011-WORK WITH WEEKLY SIRE LINE DOSES.      */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(97)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&LDLIST) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDCDYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDCDWK) TYPE(*DEC) LEN(2)

             CHGVAR     VAR(&LDLIST) VALUE(%SST(*LDA 27 1))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 2 5))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 13 2))
             CHGVAR     VAR(&LDCDYR) VALUE(%SST(*LDA 7 4))
             CHGVAR     VAR(&LDCDWK) VALUE(%SST(*LDA 11 2))

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD PRINT AT 10 CPI:  SO, ON A LASER PRINTER */
/* IT CAN PRINT 'PORTRAIT' ON LETTER PAPER                          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) LANDSCAPE(*NO) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/********************************************************************/
/* LOGIC FOR LISTING OF WEEKLY SIRE LINE DOSES                      */
/********************************************************************/

             IF         COND(&LDLIST *EQ 'D') THEN(DO) /* Doses */

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('SWFSCD *NE 0')

/*  IF FARM                                                      */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND SWFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 30 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF CALENDAR YEAR                                             */

             IF         COND(&LDCDYR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 36 15)) VALUE('*AND SWCDYR +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 52 4)) VALUE(&LDCDYR)
             ENDDO

/*  IF CALENDAR WEEK                                             */

             IF         COND(&LDCDWK *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 57 15)) VALUE('*AND SWCDWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 73 2)) VALUE(&LDCDWK)
             ENDDO


/*------------------------------------------------*/
/* RUN QUERY OVER THE WEEKLY SIRE LINE DOSES FILE */
/*------------------------------------------------*/

             OVRDBF     FILE(HSL081A) SHARE(*YES)
             OPNQRYF    FILE((HSL081A)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------*/
/*  CALL LISTING PROGRAM  */
/*------------------------*/

             CALL       PGM(HP6011)

             CLOF       OPNID(HSL081A)
             DLTOVR     FILE(HSL081A)
             ENDDO      /* Doses */


/********************************************************************/
/* LOGIC FOR LISTING OF MISSING SIRE LINE DOSES                     */
/********************************************************************/

             IF         COND(&LDLIST *EQ 'M') THEN(DO) /* Missing */

/*------------------------------------------------------------------*/
/* CREATE/BUILD A WORKFILE OF ALL WEEKS BETWEEN THE FROM/TO DATES   */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3019) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(HP3019)

/*------------------------------------------------------------------*/
/* BUILD/RUN QUERY OVER FARM SITE MASTER FILE                       */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  IF USER SELECTED A SINGLE FARM                               */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FSFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF USER DID NOT LIMIT THE REPORT TO A SINGLE FARM            */

             IF         COND(&LDFSCD *EQ 0) THEN(DO)

             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('FSPPCD *EQ +
                          "BGF  "')

             CHGVAR     VAR(%SST(&QDATA 20 19)) VALUE('*AND FSAIST +
                          *EQ "A"')

             CHGVAR     VAR(%SST(&QDATA 40 19)) VALUE('*AND FSGTCD +
                          *NE "B"')

             CHGVAR     VAR(%SST(&QDATA 60 19)) VALUE('*AND FSGTCD +
                          *NE "G"')
/*  IF CELL                                                      */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 80 15)) VALUE('*AND FSCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 96 2)) VALUE(&LDCELL)
             ENDDO
             ENDDO

             OVRDBF     FILE(HSP018)  SHARE(*YES)
             OPNQRYF    FILE((HSP018)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------*/
/*  CALL LISTING PROGRAM  */
/*------------------------*/

             CALL       PGM(HP6019)

             CLOF       OPNID(HSP018)
             ENDDO      /* Missing */


             DLTOVR     FILE(*ALL)

             ENDPGM

