      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PROCUREMENT
      * PROGRAM:     CONVTT
      * TITLE:       CONVERT OLD/NEW TATTOOS
      * PROGRAMMER:  ALICE
      * CREATED:     05/02/95
      *
      *  FUNCTION:   THIS PROGRAM CONVERTS THE OLD CARCASS DETAIL
      *              FILE IN ALBERT LEA (HEP006) INTO THE NEW CARCASS
      *              DETAIL UPLOAD FILE FOR HPE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 06/06/95  CHANGE HARD CODED DATE TO 950703
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FCONVTT  IF  E           K        DISK
      *   OLD/NEW TATTOOS            (KEY: OLDTT)
      *
     FPKCPCPP O   E           K        DISK
      *  NEW CARCASS DETAIL UPLOAD FILE
      *
     FHEP006  IF  E           K        DISK
      *  OLD CARCASS DETAIL FILE
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
     I            DS
     I                                        1   1 DUMMYD
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * SETUP HARD CODED FIELDS FOR OUTPUT RECORD
      *
     C                     Z-ADD360       CPHKST
     C                     Z-ADD0950704   CPB7DT
     C                     Z-ADD0         CPKINB
     C                     MOVE '1'       CPHLST
     C                     MOVE 'U'       CPHNST
     C                     MOVE 'P'       CPHTST
     C                     MOVE 'N'       CPHUST
     C                     Z-ADDUDATE     CPB8DT
     C                     Z-ADD*ZEROS    CPAOTM
     C                     Z-ADD*ZEROS    COUNT   20
      *
      * READ/PROCESS ALL OLD CARCASS DETAIL RECORDS
      *
     C           *LOVAL    SETLLHEP006
     C                     SETOF                     90
     C           *IN90     DOUEQ*ON                        DO FARM
     C                     READ HEP006                   90
     C           *IN90     IFEQ *OFF                       IF NOT EOF
     C           TDKLDT    ANDEQ931109                     DATE OK
     C           TDADJF    ANDNE'A'                        NO ADDS
     C                     Z-ADDTDTATO    TATO4   40
     C           TATO4     CHAINCONVTT               91
     C           *IN91     IFEQ *OFF
     C                     ADD  1         CPKINB
     C                     Z-ADDNEWTT     CPKHNB
     C                     Z-ADDNEWTT     CPLNNB
     C                     Z-ADDTDHWT     CPKJNB
     C           TDBFM     MULT .0393701  CPKKNB
     C           TDLEM     MULT .0393701  CPBQPC
     C                     Z-ADDTDRVP     CPBRPC
      *
      *  SETUP TO SEND SOME TRIM CODES EVERY 20 RECORDS
      *
     C                     ADD  1         COUNT
     C           COUNT     IFEQ 20
     C                     Z-ADD0         COUNT
     C                     MOVE 'T'       CPHMST
     C                     ENDIF                           COUNT = 20
      *                                                     + DATE OK
     C                     WRITE@CPCPMV
     C                     MOVE *BLANK    CPHMST
     C                     ENDIF                           HAVE RECORD
      *                                                     + DATE OK
     C                     ENDIF                           IF NOT EOF
     C                     ENDDO                           DO UNTIL EOF
      *
     C                     SETON                     LR
