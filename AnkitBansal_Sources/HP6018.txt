      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6018
      * TITLE:       Listing of Farm Health Status
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/18/03
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4018-Work With Farm Health
      *            Status.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl112b   if   e           k disk
      *    Farm health status      (records selected by open query)
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D procfl          s              1    inz('Y')
     D contfl          s              1    inz('Y')
      *
      * Save fields
      *
     D svfscd          s                   like(fsfscd)
     D svfscd2         s                   like(fsfscd)
     D svhccd2         s                   like(hfhccd)
      *
      * Print fields
      *
     D h1fsnm          s                   like(fsfsnm)
     D h1yesno         s              3
     D l1mdyefdt       s              6  0
     D l1cell          s                   like(fscell)
      *
      *
      * Work fields for date manipulation
      *
     D wkmdy           s               D   datfmt(*mdy)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldynfl                 1      1
     D  ldfscd                 2      6  0
     D  ldhccd                 7     11
     D  ldhsfl                12     12
     D  ldcell                13     14  0
     D  ldfmdy                15     20  0
     D  ldfcymd               21     28  0
     D  ldtmdy                29     34  0
     D  ldtcymd               35     42  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print headings the first time thru.
      *
     C                   except    h1hdr
      *
      * Read all records in the file that were selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl112b                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Default the 'continue' flag to YES.
      *
     C                   move      yes           contfl
      *
      * If the user selected a single cell and this farm is in that cell,
      * then continue.
      *
     C                   if        ldcell <> 0                                  If 1 cell
     C     hffscd        chain     hsp018                             92
     C                   if        *in92 = *on or fscell <> ldcell
     C                   move      no            contfl
     C                   endif
     C                   endif                                                  If 1 cell
      *
     C                   if        contfl = yes                                 If continue
      *
     C                   select
     C                   when      first = yes
     C                   z-add     hffscd        svfscd2
     C                   move      hfhccd        svhccd2
     C                   move      no            first
     C                   move      yes           procfl
      *
     C                   when      ldynfl = no
     C                   move      yes           procfl
      *
     C                   when      ldynfl = yes and
     C                             (hffscd <> svfscd2 or hfhccd <> svhccd2)
     C                   z-add     hffscd        svfscd2
     C                   move      hfhccd        svhccd2
     C                   move      yes           procfl
     C                   other
      *
     C                   move      no            procfl
     C                   endsl
      *
      * If you need to process this record.
      *
     C                   if        procfl = yes                                 If process
      *
      * Print 1) report headings or 2) a blank line between farm sites
      *
     C                   select
     C                   when      *inof = *on
     C                   except    h1hdr
      *
     C                   when      hffscd <> svfscd
     C                   except    blank
     C                   z-add     hffscd        svfscd
     C                   endsl
      *
      * Flip effective date from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 hfefdt                 92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             l1mdyefdt
     C                   else
     C     *iso          move      hfefdt        wkmdy
     C                   movel     wkmdy         l1mdyefdt
     C                   endif                                                  If bad date
      *
      * Retrieve cell for farm
      *
     C     hffscd        chain     hsp018                             92
     C                   if        *in92 = *on
     C                   z-add     0             l1cell
     C                   else
     C                   z-add     fscell        l1cell
     C                   endif
      *
      * Print detail line
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If continue
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      *
      * Setup the user selections in the headings.
      *
      * Print last status only
      *
     C                   if        ldynfl = yes
     C                   move      'Yes'         h1yesno
     C                   else
     C                   move      ' No'         h1yesno
     C                   endif
      *
      * Date range
     C                   if        ldfmdy = 0
     C                   seton                                        88
     C                   endif
      *
      * Health code
     C                   if        ldhccd = *blank
     C                   seton                                        87
     C                   endif
      *
      * Health status
     C                   if        ldhsfl = *blank
     C                   seton                                        85
     C                   endif
      *
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        86
     C                   endif
      *
      * Farm site
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   else
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel(p)  fsfsnm        h1fsnm
     C                   else
     C                   movel(p)  'Unknown'     h1fsnm
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           49 'LISTING OF FARM HEALTH'
     O                                           56 'STATUS'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                           25 'Show last status only .:'
     O                       h1yesno             30
      *
      *
     O          e            h1hdr          1
     O                                           25 'From effective date ...:'
     O              N88      ldfmdy              35 '  /  /  '
     O               88                          30 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'To effective date ......'
     O              N88      ldtmdy              35 '  /  /  '
     O               88                          30 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'Health code ...........:'
     O              N87      ldhccd              32
     O               87                          30 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'Health status .........:'
     O              N85      ldhsfl              28
     O               85                          30 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'Cell ..................:'
     O              N86      ldcell        z     29
     O               86                          30 'All'
      *
     O          e            h1hdr          2
     O                                           25 'Farm site .............:'
     O              N89      ldfscd        z     32
     O              N89      fsfsnm              60
     O               89                          30 'All'
      *
      *
     O          e            h1hdr          1
     O                                            5 'FARM'
     O                                           16 'EFFECTIVE'
     O                                           24 'HEALTH'
     O                                           32 'HEALTH'
     O          e            h1hdr          1
     O                                            5 'SITE'
     O                                           14 'DATE'
     O                                           22 'CODE'
     O                                           32 'STATUS'
     O                                           38 'CELL'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       hffscd        z      5
     O                       l1mdyefdt           15 '  /  /  '
     O                       hfhccd              23
     O                       hfhsfl              29
     O                       l1cell        z     36
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
