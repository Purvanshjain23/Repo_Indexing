/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF430CL                                       */
/*  PROGRAM NAME:    WORK WITH CHARGE CODES                        */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     03/02/05                                      */
/*                                                                 */
/*  PRINTS LISTING(S) FROM WORK WITH.                              */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 05/06/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(223)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDCCCD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LDCONO) TYPE(*DEC) LEN(3)
             DCL        VAR(&LDGLDBCD) TYPE(*CHAR) LEN(8)
             DCL        VAR(&LDGLCRCD) TYPE(*CHAR) LEN(8)
             DCL        VAR(&LDCFQCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDUCFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDINFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSMCD)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDAIST)  TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDCCCD) VALUE(%SST(*LDA 1 10))
             CHGVAR     VAR(&LDCONO) VALUE(%SST(*LDA 11 3))
             CHGVAR     VAR(&LDGLDBCD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDGLCRCD) VALUE(%SST(*LDA 22 8))
             CHGVAR     VAR(&LDCFQCD) VALUE(%SST(*LDA 30 1))
             CHGVAR     VAR(&LDUCFL) VALUE(%SST(*LDA 31 1))
             CHGVAR     VAR(&LDINFL) VALUE(%SST(*LDA 32 1))
             CHGVAR     VAR(&LDSMCD) VALUE(%SST(*LDA 33 2))
             CHGVAR     VAR(&LDAIST) VALUE(%SST(*LDA 35 1))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE CHARGE CODE MASTER FILE                   */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('CCCCCD *NE " "')

/* IF COMPANY                                                       */

             IF         COND(&LDCONO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 15)) VALUE('*AND CCCONO *EQ')
             CHGVAR     VAR(%SST(&QDATA 32 3)) VALUE(&LDCONO)
             ENDDO

/* IF CHARGE CODE                                                   */

             IF         COND(&LDCCCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 36 17)) VALUE('*AND CCCCCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 53 10)) VALUE(&LDCCCD)
             CHGVAR     VAR(%SST(&QDATA 63 1)) VALUE('"')
             ENDDO

/* IF GL CODE-DEBIT                                                 */

             IF         COND(&LDGLDBCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 65 19)) VALUE('*AND CCGLDBCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 84 8)) VALUE(&LDGLDBCD)
             CHGVAR     VAR(%SST(&QDATA 92 1)) VALUE('"')
             ENDDO

/* IF GL CODE-CREDIT                                                */

             IF         COND(&LDGLCRCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 94 19)) VALUE('*AND CCGLCRCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 113 8)) VALUE(&LDGLCRCD)
             CHGVAR     VAR(%SST(&QDATA 121 1)) VALUE('"')
             ENDDO

/* IF CHARGE FREQUENCY CODE                                         */

             IF         COND(&LDCFQCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 123 18)) VALUE('*AND CCCFQCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 141 1)) VALUE(&LDCFQCD)
             CHGVAR     VAR(%SST(&QDATA 142 1)) VALUE('"')
             ENDDO

/* IF USER CONTROLLED FLAG                                          */

             IF         COND(&LDUCFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 144 17)) VALUE('*AND CCUCFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 161 1)) VALUE(&LDUCFL)
             CHGVAR     VAR(%SST(&QDATA 162 1)) VALUE('"')
             ENDDO

/* IF INCLUDE IN INVOICE TOTAL FLAG                                 */

             IF         COND(&LDINFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 164 17)) VALUE('*AND CCINFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 181 1)) VALUE(&LDINFL)
             CHGVAR     VAR(%SST(&QDATA 182 1)) VALUE('"')
             ENDDO

/* IF SYSTEM MODULE CODE                                            */

             IF         COND(&LDSMCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 184 17)) VALUE('*AND CCSMCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 201 2)) VALUE(&LDSMCD)
             CHGVAR     VAR(%SST(&QDATA 203 1)) VALUE('"')
             ENDDO

/* IF ACTIVE/INACTIVE STATUS                                             */

             IF         COND(&LDAIST *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 205 17)) VALUE('*AND CCAIST *EQ "')
             CHGVAR     VAR(%SST(&QDATA 222 1)) VALUE(&LDAIST)
             CHGVAR     VAR(%SST(&QDATA 223 1)) VALUE('"')
             ENDDO

/* OVERRIDE CHARGE CODES FILE                                         */

             OVRDBF     FILE(TFP005)  SHARE(*YES)
             OPNQRYF    FILE((TFP005)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/*---------------------------------------------------------------------*/
/* PRINT LISTING                                                       */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

 /* CALL PRINT PROGRAM                                                    */

             CALL       PGM(TF630)

             CLOF       OPNID(TFP005)
             DLTOVR     FILE(TFP005)
             DLTOVR     FILE(*ALL)

             ENDPGM

