     A*%%TS  SD  20050321  133958  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD773
     A*  TITLE:         VIEW FARM COSTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/25/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP773S1                   SFL
     A*%%TS  SD  20050321  133958  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1MDY     R        O  9  2REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  -')
     A            D1FSCD    R        O  9 14REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            D1CELL    R        O  9 23REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1HDRT    R        O  9 30REFFLD(FCREC/FCHDRT *LIBL/HSP028)
     A                                      EDTCDE(K)
     A            D1LBRT    R        O  9 44REFFLD(FCREC/FCLBRT *LIBL/HSP028)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP773C1                   SFLCTL(HP773S1)
     A*%%TS  SD  20050321  133958  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 29'VIEW FARM COSTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ..'
     A N99                              4 21'?'
     A            C1FSBO    R        B  4 23REFFLD(FSFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N99                               DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A N99                              6 17'?'
     A                                      DSPATR(HI)
     A                                  6 33'Cost'
     A                                      DSPATR(HI)
     A                                  6 45'Cost'
     A                                      DSPATR(HI)
     A                                  7  4'Date'
     A                                      DSPATR(HI)
     A                                  7 15'Farm'
     A                                      DSPATR(HI)
     A                                  7 22'Cell'
     A                                      DSPATR(HI)
     A                                  7 31'Per Head'
     A                                      DSPATR(HI)
     A                                  7 43'Per Pound'
     A                                      DSPATR(HI)
     A N99        C1MDY     R        B  8  2REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A  53                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A N99        C1FSCD    R        B  8 14REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A N99        C1CELL    R        B  8 23REFFLD(FSCELL)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP773K1
     A*%%TS  SD  20000905  091220  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A N99                             23 22'F5=Refresh'
     A N99                             23 65'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP773ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP773EC                   SFLCTL(HP773ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
