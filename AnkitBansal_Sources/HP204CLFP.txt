/******************************************************************* */
/*                                                                   */
/*  PROGRAM NUMBER:  HP204CLFP                                       */
/*  PROGRAM NAME:    BUILD JDE UPLOAD FILE: HPS FEED PICK-UP DOLLARS */
/*  PROGRAMMER:      LEANNE FEDOR                                    */
/*  CREATE DATE:     10/22/01                                        */
/*                                                                   */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE DRIVER CL         */
/*                   HP404CL.                                        */
/*                                                                   */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.             */
/*                   AN OPNQRY STATEMENT IS USED TO SELECT           */
/*                   RECORDS.                                        */
/*                                                                   */
/*                                                                   */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 04/16/05  LEANNE FEDOR                                          */
/*           CHANGED THE QUERY STATEMENT TO SELECT ONLY "PRODUCED" */
/*           FEED TICKETS.                                         */
/*                                                                 */
/* 04/11/16  BARB GUTIERREZ                                        */
/*           CHANGED THE QUERY STATEMENT TO USE A JOIN LOGICAL     */
/*           INSTEAD OF JUST ON HSL037I.  WE NEED TO PROCESS BY    */
/*           COMPANY BECAUSE OF THE NEW IOWA FARMS.  JOINING THE   */
/*           FARM MASTER TO THE FEED TICKET HEADER TO ACCOMPLISH   */
/*           THIS.  E005290                                        */
/*                                                                 */
/* 10/16/18  Danny Nguyen  (S13682)                                */
/*           Retrieve Company Code from LDA. Change OPNQRYF logic  */
/*           for HSJ018A to select by Company. Increase &QDATA     */
/*           from 90 to 110.                                       */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(110)

             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 25 3))


/*********************************************************************/
/*  SETUP OPNQRY STATEMENTS                                          */
/*********************************************************************/

/*  ALWAYS SELECT FEED TICKET HEADER RECORDS FOR DATE RANGE OF THE       */
/*  CURRENTLY OPEN PERIOD                                                */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FHTKDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND FHTKDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

/*  ALWAYS SELECT ONLY PICKUP TICKETS                                    */

             CHGVAR     VAR(%SST(&QDATA 46 19)) VALUE('*AND FHTRCD +
                          *EQ "P"')

/*  ALWAYS SELECT ONLY TICKETS THAT ARE TAGGED AS "PRODUCED" FEED        */

             CHGVAR     VAR(%SST(&QDATA 66 19)) VALUE('*AND FHPRFL +
                          *EQ "Y"')

/*  S13682-ALWAYS SELECT BY COMPANY CODE.                        */

             CHGVAR     VAR(%SST(&QDATA 86 17)) VALUE('*AND FSCOCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 103 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA 106 1)) VALUE('"')


/*********************************************************************/
/*  SET UP FILE OVERRIDES AND PERFORM QUERIES                        */
/*********************************************************************/

/*  FARM MASTER + FEED HEADER RECORDS                                */

             OVRDBF     FILE(HSJ018A) SHARE(*YES)
             OPNQRYF    FILE((HSJ018A)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/*********************************************************************/
/*  CALL PROGRAM TO BUILD UPLOAD OF FILE FEED PICKUP TICKETS         */
/*********************************************************************/

             CALL       PGM(HP204FP)

/*******************************************************************/
/*  DELETE FILE OVERRIDES                                          */
/*******************************************************************/

             CLOF       OPNID(HSJ018A)
             DLTOVR     FILE(HSJ018A)

             ENDPGM

