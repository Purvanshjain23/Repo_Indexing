     A*%%TS  SD  20090707  103212  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD440
     A*  TITLE:         WORK WITH HOG GROUPS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/04/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99 88                               CF06(06 'CREATE')
     A N99                                  CF07(07 'ADJUSTMENT REPORTS')
     A N99 88                               CF10(10 'UPDATE ADOI')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP440S1                   SFL
     A*%%TS  SD  20090707  103212  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(@@HGSN)
     A            DFOPT          2A  B 14  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFHGCD    R        O 14  7REFFLD(@@HGCD)
     A            DFFSCD    R        O 14 16REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            DFBLCD    R        O 14 23REFFLD(@@BLCD)
     A            DFRMCD    R        O 14 29REFFLD(@@RMCD)
     A            DFCELL    R        O 14 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            DFPTCD    R        O 14 41REFFLD(@@PTCD)
     A            DFPPCD    R        O 14 47REFFLD(@@PPCD)
     A            DFGTCD    R        O 14 55REFFLD(@@GTCD)
     A            DFSDCD    R        O 14 59REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A            DFSMCD    R        O 14 63REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A            DFGSCD    R        O 14 69REFFLD(@@GSCD)
     A            DFOPDT    R        O 14 72REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFCFFL    R        O 14 66REFFLD(HSRREF/@@CFFL *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP440C1                   SFLCTL(HP440S1)
     A*%%TS  SD  20090707  103212  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0007)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(79)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1VERSION R        O  2  2REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH ACTIVE HOG GROUPS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .. ?'
     A            C1FSBO    R        B  4 23REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A  88                              6  3'2=Revise Group'
     A                                  6 28'5=View Group'
     A                                  6 50'6=Print Reports/Downloads'
     A  88                              7  3'9=Reopen Group'
     A                                  7 28'7=View Movements'
     A  88                              8  2'11=Maintain Adjustments'
     A                                  8 27'12=View Adjustments'
     A                                 10 19'?'
     A                                      DSPATR(HI)
     A                                 10 43'?'
     A                                      DSPATR(HI)
     A                                 10 49'?'
     A                                      DSPATR(HI)
     A                                 10 55'?'
     A                                      DSPATR(HI)
     A                                 10 59'?'
     A                                      DSPATR(HI)
     A                                 10 63'?'
     A                                      DSPATR(HI)
     A                                 10 66'?'
     A                                      DSPATR(HI)
     A                                 10 69'?'
     A                                      DSPATR(HI)
     A                                 11 41'Production'
     A                                      DSPATR(HI)
     A                                 11 54'Grp'
     A                                      DSPATR(HI)
     A                                 11 58'Stk'
     A                                      DSPATR(HI)
     A                                 11 62'SM'
     A                                      DSPATR(HI)
     A                                 11 65'Con'
     A                                      DSPATR(HI)
     A                                 11 74'Date'
     A                                      DSPATR(HI)
     A                                 12  2'Opt'
     A                                      DSPATR(HI)
     A                                 12  7'Group'
     A                                      DSPATR(HI)
     A                                 12 17'Farm'
     A                                      DSPATR(HI)
     A                                 12 23'Bldg'
     A                                      DSPATR(HI)
     A                                 12 29'Room'
     A                                      DSPATR(HI)
     A                                 12 35'Cell'
     A                                      DSPATR(HI)
     A                                 12 41'Type'
     A                                      DSPATR(HI)
     A                                 12 47'Phase'
     A                                      DSPATR(HI)
     A                                 12 54'Typ'
     A                                      DSPATR(HI)
     A                                 12 58'Den'
     A                                      DSPATR(HI)
     A                                 12 62'Cd'
     A                                      DSPATR(HI)
     A                                 12 65'Flo'
     A                                      DSPATR(HI)
     A                                 12 69'St'
     A                                      DSPATR(HI)
     A                                 12 73'Opened'
     A                                      DSPATR(HI)
     A            C1HGCD    R        B 13  7REFFLD(@@HGCD)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A N31N51N52
     A N53N54N55
     A N57N58                               DSPATR(PC)
     A            C1FSCD    R        B 13 16REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1BLCD    R        B 13 23REFFLD(@@BLCD)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A            C1RMCD    R        B 13 29REFFLD(@@RMCD)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A            C1CELL    R        B 13 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1PTCD    R        B 13 41REFFLD(@@PTCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1PPCD    R        B 13 47REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1GTCD    R        B 13 55REFFLD(@@GTCD)
     A                                      DSPATR(HI)
     A  58                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1SDCD    R        B 13 59REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  59                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1SMCD    R        B 13 63REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  79                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1CFFL    R        B 13 66REFFLD(HSRREF/@@CFFL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1GSCD    R        B 13 69REFFLD(@@GSCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP440K1
     A*%%TS  SD  20081031  103539  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  1'F3=Exit'
     A                                 22 10'F4=Prompt'
     A                                 22 26'F5=Refresh'
     A  88                             22 38'F6=Create'
     A                                 22 57'F7=Adjustment Reports'
     A  88                             23 10'F10=Update ADOI'
     A                                 23 38'F17=Print Options'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP440ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP440EC                   SFLCTL(HP440ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
