     A*%%TS  SD  19950421  084653  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD576 - SELECT DISPOSED HOG GROUP
     A*  RPG PROGRAM:   HP576  - SELECT DISPOSED HOG GROUP
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          06/17/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP576R1
     A*%%TS  SD  19950421  083926  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 20 57)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP576R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP576R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP576S1                   SFL
     A*%%TS  SD  19950421  081649  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFHGCD    R        O 10  1REFFLD(HGHGCD)
     A            DFFSCD    R        O 10 10REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFMDY     R        O 10 17REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            DFFSBO    R        O 10 29REFFLD(HSRREF/HGFSBO)
     A            DFPPCD    R        O 10 40REFFLD(HGPPCD)
     A            DFPTCD    R        O 10 51REFFLD(HGPTCD)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP576C1                   SFLCTL(HP576S1)
     A*%%TS  SD  19950421  084653  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP576R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 16'SELECT DISPOSED HOG GROUP'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'From disposed date ..'
     A            C1FMDY    R     Y  B  4 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                  5  1'To disposed date ....'
     A            C1TMDY    R     Y  B  5 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                  7  1'Hog'
     A                                      DSPATR(HI)
     A                                  7 10'Farm'
     A                                      DSPATR(HI)
     A                                  7 17'Disposed'
     A                                      DSPATR(HI)
     A                                  7 29'Business'
     A                                      DSPATR(HI)
     A                                  7 40'-- Production --'
     A                                      DSPATR(HI)
     A                                  8  1'Group'
     A                                      DSPATR(HI)
     A                                  8 10'Site'
     A                                      DSPATR(HI)
     A                                  8 19'Date'
     A                                      DSPATR(HI)
     A                                  8 29'Office'
     A                                      DSPATR(HI)
     A                                  8 40'Phase'
     A                                      DSPATR(HI)
     A                                  8 51'Type'
     A                                      DSPATR(HI)
     A            C1FSCD    R     Y  B  9 10REFFLD(HGFSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1FSBO    R        B  9 29REFFLD(HGFSBO)
     A                                      DSPATR(HI)
     A            C1PPCD    R        B  9 40REFFLD(HGPPCD)
     A                                      DSPATR(HI)
     A            C1PTCD    R        B  9 51REFFLD(HGPTCD)
     A                                      DSPATR(HI)
     A          R HP576K1
     A*%%TS  SD  19940506  091739  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP576R1)
     A                                      OVERLAY
     A                                 19  1'F12=Previous'
