     A*%%TS  SD  20050314  165843  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD515 - SELECT INVOICE CHARGE--STATUS OPTIONS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/14/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      PRINT
     A                                      CF04(04 'PROMPT')
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF515S1                   SFL
     A*%%TS  SD  20050314  165843  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1ICDTMDY R        O  9  1REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1ICSN    R        O  9 13REFFLD(@@ICSN)
     A                                      EDTCDE(M)
     A            D1CFQCD   R        O  9 26REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A            D1INSTCD  R        O  9 38REFFLD(@@INSTCD)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF515C1                   SFLCTL(TF515S1)
     A*%%TS  SD  20050314  165843  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0008)
     A                                      WINDOW(TF515EC)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 13'SELECT INVOICE CHARGE'
     A                                      DSPATR(HI)
     A            C1DESC        30A  O  2 13DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  4 26'?'
     A                                      DSPATR(HI)
     A N62                              4 38'?'
     A                                      DSPATR(HI)
     A                                  5  1'Invoice'
     A                                      DSPATR(HI)
     A                                  5 14'Invoice'
     A                                      DSPATR(HI)
     A                                  5 35'Invoice'
     A                                      DSPATR(HI)
     A                                  6  1'Charge'
     A                                      DSPATR(HI)
     A                                  6 14'Charge'
     A                                      DSPATR(HI)
     A                                  6 24'Charge'
     A                                      DSPATR(HI)
     A                                  6 36'Charge'
     A                                      DSPATR(HI)
     A                                  7  2'Date'
     A                                      DSPATR(HI)
     A                                  7 14'Number'
     A                                      DSPATR(HI)
     A                                  7 23'Frequency'
     A                                      DSPATR(HI)
     A                                  7 36'Status'
     A                                      DSPATR(HI)
     A            C1ICDTMDY R        B  8  1REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1ICSN    R        B  8 13REFFLD(@@ICSN)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CFQCD   R        B  8 26REFFLD(@@CFQCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A N62        C1INSTCD  R        B  8 38REFFLD(TFREF/@@INSTCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R TF515K1
     A                                      WINDOW(TF515EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF515ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R TF515EC                   SFLCTL(TF515ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 43)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
