      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH203 - Datamart Sales: Build Customer
      * PROGRAMMER:  Alice
      * CREATED:     03/24/00
      *
      * FUNCTION: This program consolidates a number of files
      *           in the OMS-Order Management System into a Customer
      *           Master for Datamart.
      *
      *           This file in the Data Mart will be re-created with
      *           each load of the Data Mart.
      *
      ****************************************************************
      * Date      Programmer
      * 07/01/02  Barb Gutierrez
      *           Recompiled only.  Increased territory code from 3a to 6a.
      *           Was already doing a move left.
      *
      * 07/01/02  Barb Gutierrez
      *           Bring in new distributor flag to datamart.
      *
      * 10/09/03  Barb Gutierrez
      *           If the customer was coded as an outside warehouse, we
      *           did not write the customer to the datamart.  However,
      *           if the order for the customer was coded as an "OR', we included
      *           the order in the shipping detail.  We've come across an
      *           instance where the orders are an "OR" but the customer is flagged
      *           as an outside warehouse.  We will now include these customers
      *           in the data warehouse customer master.
      *
      * 03/10/04  Barb Gutierrez
      *           Added zip code, and broker code to the customer master.
      *
      * 07/15/05  Barb Gutierrez
      *           Added sales region code to the customer master.  This
      *           region code will be populated from the customer extension file.
      *           Also added sales region description.
      *
      * 07/31/07  Todd Johnson
      *           Added logic to add the following fields
      *            1) Exempt/Non-Exempt Flag
      *            2) Buyer Group Code
      *            3) Buyer Group Name
      *            4) Shipping Zone
      *
      * 08/16/07  LeAnne Ramsey
      *           Purva D. wants this installed now. But, the Dailys files are not
      *           installed. So, I commented out some of Todd Johnson's changes and
      *           compiled over PRKFLIB instead of PRKTESTF on the install to force
      *           it in. After we install, we will check it back out. Scan for
      *           "temporary" to find the Todd code to reinstate.
      *
      * 09/04/07  LeAnne Ramsey
      *           Recompiled in Production.
      *
      * 09/05/07  LeAnne Ramsey
      *           Added "Company Organization" as an incoming parm.
      *           We will no longer populate "broker code"; it is going away
      *           in the next phase.
      *
      * 12/06/07  LeAnne Ramsey
      *           We are removing the B=Both for Company Organization.
      *           We will now write a Customer record for both Seaboard and Dailys.
      *           We are doing this because the "broker code" is back in play.
      *           We are now populating Broker Code and 2 new fields:
      *                    Broker Name
      *                    Broker G/L Vendor Number
      *
      * 12/31/07  LeAnne Ramsey
      *           Added new field: Market Code.
      *           Changed retrieval logic for: Broker, Buyer Group, Product Rebate Group
      *
      * 01/11/08  LeAnne Ramsey
      *           Added new fields:  Market Name
      *                              Market Salesperson Code
      *                              Market Salesperson Name
      *                              Market Broker Code
      *                              Market Broker Name
      *
      * 10/21/08  LeAnne Ramsey
      *           Recompile only. New field "Commission Schedule" added to PMDDREP.
      *
      * 08/06/09  LeAnne Ramsey
      *           Added new field 'Commodity Market Code' to SHP203-Datamart: Customer.
      *
      * 07/23/15  Lara Buser
      *           Added Field vs. In-House Managed flag to SHP203-Datamart: Customer
      *
      * 07/08/21  Brad Baden
      *           Added a call to program PPH6XFR to retrieve the Parent Customer
      *           Name for adding it to the new field in file SHP203.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcaajrel1  if   e           k disk
      *  Type Codes
      *
      *
     Fcaaprel1  if   e           k disk
      *  Salesperson
      *
      *
     Fcabbrep   if   e             disk
      *  Customer
      *
      *
     Fcaemcpl3  if   e           k disk
      *  Contact attach customer
      *
      *
     Fcaeorel3  if   e           k disk
      *  Company/Customer
      *
      *
     Fpdnerel1  if   e           k disk
      *  Customer extension
      *
      *
     Fpdjxrel1  if   e           k disk
      *  Customer Group Master
      *
      *
     Fpdlvrel1  if   e           k disk
      *  Territory
      *
      *
     Fpdltrel1  if   e           k disk
      *  Shipping Zone
      *
      *
     Fpdlwrel3  if   e           k disk
      *  Territory State
      *
      *
     Fpmddrel1  if   e           k disk
      *  Cusomter market
      *
      *
     Fpoc5rel1  if   e           k disk
      *  Broker
      *
      *
     Fpdlurel1  if   e           k disk
      *  Destination-Zone
      *
      *
     Fshp203    uf a e           k disk
      *    Datamart: Customer
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D region          c                   'REGION'
      *
      *---------------------------------------------------------------
      *  Standalone fields
      *---------------------------------------------------------------
      *
      * Workfields
      *
     D wkpacuno        s              7  0
     D wkcunoalph      s              7a
     D wknonzero       s              7  0
     D wktype          s              6a
     D wkretrn         s              7a
      *
      *
      * Parms
      *
     D xxcorg          s                   like(cucorg)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read each record in the OMS Customer file.
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      cabbrep                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Do not process customers that:
      *   1) are not set up in the company/customer file
      *
     C     bbbkc7        chain     caeorel3                           92
     C                   if        *in92 = *off                                  If OK customer
      *
     C                   move      xxcorg        cucorg
      *
      * Set up Datamart fields that are populated from:
      *  1) OMS Company/customer file
      *  2) OMS Customer file
      *  3) Customer Extension file
      *
     C                   exsr      $compcust
     C                   exsr      $directmap
     C                   exsr      $custext
      *
      * Populate:
      *  Shipping Zone
      *  Buyer Group Rebate Group
      *  Product Rebate Group
      *  Sales Territory
      *  Broker
      *  Market
     C                   exsr      $zone
     C                   exsr      $buygrp
     C                   exsr      $prdgrp
     C                   exsr      $slsterr
     C                   exsr      $broker
     C                   exsr      $market
      *
     C                   write     curec
     C                   clear                   curec
      *
     C                   endif                                                  If OK customer
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------
      * Populate fields that are a direct map from the customer master
      *----------------------------------------------------------------
      *
     C     $directmap    begsr
      *
      * Customer number
     C                   z-add     bbbkc7        cucuno
     C                   move      bbvsst        custatus
      * Address
     C                   move      bbacna        cuadd1
     C                   move      bbadna        cuadd2
     C                   move      bbaena        cuadd3
      *
      * City/state/country
     C                   move      bbajna        cucity
     C                   move      bbbgcd        custate
     C                   move      bbbttx        cuzip
     C                   move      bbkxcd        cucntry
      *
      * Field vs. In-House Managed flag
     C                   move      bbrkst        cumanaged
      *
      * A/R customer
      * Parent customer
     C                   z-add     bbanc7        cuarcuno
     C                   z-add     bbbtc7        cupacuno
      *
      * Ship to customer flag
      *
     C                   move      bbrjst        cushcufl
      *
      * Determine A/R and Parent Flags
      *
     C                   if        cucuno = cuarcuno
     C                   move      yes           cuarcufl
     C                   else
     C                   move      no            cuarcufl
     C                   endif
      *
     C                   if        cucuno = cupacuno
     C                   move      yes           cupacufl
     C                   else
     C                   move      no            cupacufl
     C                   endif
      *
      *
      * Concatenate name/number/city into customer name field.
      * (Note: Stip leading zeros from customer number before concatenating.)
      *
     C                   move(p)   cucuno        wkcunoalph
     C     '0'           check     wkcunoalph    wknonzero
     C                   eval      wkcunoalph = %subst(wkcunoalph:wknonzero)
      *
     C                   eval      cucunm = %trim(bbaytx) + '-' +
     C                                      %trimr(wkcunoalph) + '-' +
     C                                      %trim(cucity)
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Populate fields that are valued from the Company/customer file
      *----------------------------------------------------------------
      *
     C     $compcust     begsr
      *
      * Credit contact:
     C                   move      eonjna        cuccnm                         Name
     C                   move      eoi0tx        cucctel                        Telephone
     C                   move      eoi1tx        cuccfax                        Fax
      *
      * Populate the customer group code, then retrieve its description from the
      * Customer Group Master.  Concatenate the code/description into a single field.
      *
     C                   move      eot9cd        cucugrcd
     C     eot9cd        chain     pdjxrel1                           92
     C                   if        *in92 = *off
     C                   eval      cucugrds =  %trim(cucugrcd) + '-' +
     C                                         %trim(jxndna)
     C                   endif
      *
      *
      * Populate the Customer Type code, then retrieve its description
      * from the Customer Type Master.  Concatenate the code/description
      * into a single field.
      *
     C                   move      eobocd        cucutycd
     C     eobocd        chain     caajrel1                           92
     C                   if        *in92 = *off
     C                   eval      cucutyds =  %trim(cucutycd) + '-' +
     C                                         %trim(ajauna)
     C                   endif
      *
      * Distributor Y/N Flag
      * Brokerage flag
      * Sales route
      * Commodity market code
      *
     C                   move      eoq6st        cudistfl
     C                   move      eollst        cubkfl
     C                   move      eor4cd        cusrcd
     C                   move      eolqcd        cucmmkcd
      *
      * Parent Customer Name
      *
     C                   eval      wkpacuno = bbbtc7
      *
     C                   call      'PPH6XFR'
     C                   parm      *blank        wkretrn
     C                   parm                    wkpacuno
     C                   parm      *blank        cupacunm
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------------
      * Populate the "Buyer Group Rebate Group" fields
      *-----------------------------------------------------------------------------------------
      *
     C     $buygrp       begsr
      *
      * Retrieve the Buyer Group associated with this Customer from the Contact
      * Attach Customer file using a key of: Customer Number
      *                                      Type (hardcoded to 'BUYGRP')
      *
     C                   eval      wktype = 'BUYGRP'
     C     key03         chain     caemcpl3                           92
     C                   if        *in92 = *off                                 If hit
     C                   move      emhhaa        cubgrgcd
      *
     C                   if        cubgrgcd <> *blank
     C     cubgrgcd      chain     poc5rel1                           92
     C                   if        *in92 = *off
     C                   eval      cubgrgnm = c5c4t1
     C                   endif
     C                   endif
      *
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------------
      * Populate the "Product Rebate Group" fields
      *-----------------------------------------------------------------------------------------
      *
     C     $prdgrp       begsr
      *
      * Retrieve the Product Rebate Group associated with this Customer from the Contact
      * Attach Customer file using a key of: Customer Number
      *                                      Type (hardcoded to 'PRDGRP')
      *
     C                   eval      wktype = 'PRDGRP'
     C     key03         chain     caemcpl3                           92
     C                   if        *in92 = *off                                 If hit
     C                   move      emhhaa        cuprgcd
      *
     C                   if        cuprgcd <> *blank
     C     cuprgcd       chain     poc5rel1                           92
     C                   if        *in92 = *off
     C                   eval      cuprgnm = c5c4t1
     C                   endif
     C                   endif
      *
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------
      * Sales Territory
      *--------------------------------------------------------------------------------
      *
     C     $slsterr      begsr
      *
      * Using the State from the OMS Customer file,
      * retrieve the associated territory code from the OMS Territory State file.
      *
     C     bbbgcd        chain     pdlwrel3                           92
     C                   if        *in92 = *off                                 If state hit
     C                   movel     lwx3cd        custcd
      *
      * Now, use the territory code just retrieved to get its description
      * from the OMS Territory file. Concatenate code/description into 1 field.
      *
     C     lwx3cd        chain     pdlvrel1                           92
     C                   if        *in92 = *off
     C                   eval      custds =  %trim(custcd) + '-' +
     C                                       %trim(lvokna)
     C                   endif
      *
     C                   endif                                                  If state hit
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------
      * Retrieve broker info
      *--------------------------------------------------------------------------------
      *
     C     $broker       begsr
      *
      * Retrieve the Broker associated with this Customer from the Contact Attach Customer
      * file using a key of: Customer Number
      *                      Type (hardcoded to 'BROKER')
      *
     C                   eval      wktype = 'BROKER'
     C     key03         chain     caemcpl3                           92
     C                   if        *in92 = *off                                 If broker hit
     C                   move      emhhaa        cubkcd
      *
      * From the Broker Master, retrieve:
      *  1) Broker Name
      *  2) Broker GL Vendor Number
      *
     C                   if        cubkcd <> *blank
     C     cubkcd        chain     poc5rel1                           92
     C                   if        *in92 = *off
     C                   eval      cubknm = c5c4t1
     C                   z-add     c5dat1        cubkglno
     C                   endif
     C                   endif
      *
     C                   endif                                                  If broker hit
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------
      * Retrieve values associated with the "Market"
      *--------------------------------------------------------------------------------
      *
     C     $market       begsr
      *
      * Retrieve info for the Market Code from Customer Market file.
      *   1) Market Description
      *   2) Market Salesperson Code
      *   3) Market Broker Code
      *
     C     cumkcd        chain     pmddrel1                           92
     C                   if        *in92 = *off
     C                   move      ddr3na        cumkds
     C                   move      ddbccd        cumkspcd
     C                   move      ddhhaa        cumkbkcd
     C                   endif
      *
      * Retrieve Market Salesperson Name from Salesperson file
      *
     C                   if        cumkspcd <> *blank
     C     cumkspcd      chain     caaprel1                           92
     C                   if        *in92 = *off
     C                   eval      cumkspnm = apaltx
     C                   endif
     C                   endif
      *
      * Retrieve Market Broker Name from Broker file
      *
     C                   if        cumkbkcd <> *blank
     C     cumkbkcd      chain     poc5rel1                           92
     C                   if        *in92 = *off
     C                   eval      cumkbknm = c5c4t1
     C                   endif
     C                   endif
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------
      * Retrieve values from the Customer Extension file.
      *--------------------------------------------------------------------------------
      *
     C     $custext      begsr
      *
     C     bbbkc7        chain     pdnerel1                           92
     C                   if        *in92 = *off                                 If hit
      * Market Code
     C                   move      nep6sx        cumkcd
      * TF Exempt Code
     C                   move      nep3sx        cutfexcd
      *
      * If Retailer/Foodservice Customer, set up:
      *   1) Sales Region Code
      *   2) Sales Region Description
      *
     C                   if        cucutycd = 'FS' or cucutycd = 'RT'           If FS/RT
     C                   movel     nep5sx        curgcd
     C                   if        curgcd <> *blank
     C     region        cat(p)    curgcd:1      curgds
     C                   endif
     C                   endif                                                  If FS/RT
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Retrieve Shipping Zone Code and Description
      *----------------------------------------------------------------
      *
     C     $zone         begsr
      *
     C     key01         chain     pdlurel1                           92
     C                   if        *in92 = *off                                 If zone
     C                   eval      cushzone = lux1cd
      *
     C     key02         chain     pdltrel1                           92
     C                   if        *in92 = *off
     C                   eval      cushzoneds = ltauna
     C                   endif
     C                   endif                                                  If zone
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      *  Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parms
     C     *entry        plist
     C                   parm                    xxcorg
      *
      * Key list
      *
     C     key01         klist
     C                   kfld                    eoaic3                         Company No
     C                   kfld                    bbbgcd                         State Code
     C                   kfld                    bbajna                         Destination City
      *
     C     key02         klist
     C                   kfld                    luaic3                         Company No
     C                   kfld                    lux1cd                         State Code
      *
     C     key03         klist
     C                   kfld                    bbbkc7                         Customer
     C                   kfld                    wktype                         Type
      *
     C                   endsr
      /EJECT
