      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP963
      * TITLE:       Purge - Delete/Void Old Groups with Create Status
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     06/05/97
      *
      *  FUNCTION:   Users may have created groups that they never used.
      *              This program attempts to Delete/Void these groups
      *              when their Created Date is on/before the Purge Date.
      /EJECT
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/15/08  LeAnne Ramsey
      *           Cleaned up code.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 03/27/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034o   uf   e           k disk
      *  Hog group (LF omits Closed/Voided/Disposed groups)
      *
      *
     Fhsl038e   if   e           k disk
      *  Feed ticket detail
      *
      *
     Fhsl048b   if   e           k disk
      *  Expense ticket
      *
      *
     Fhsl058d   if   e           k disk
      *  Movement event
      *
      *
     Fhsp034pr  o    e           k disk    rename(hgrec:hgrecpr)
      *  Hog group
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D dltfl           s              1    inz('Y')
     D voidfl          s              1    inz('Y')
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  lddt                   1      8  0
     D  ldyr                   9     12  0
     D  ldpe                  13     14  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **************************************************************************************
      * Mainline
      **************************************************************************************
      *
      * Process each Hog Group with 1) a status of "created" and
      *                             2) a Created Date on/before the Purge Date
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl034o                                90
     C                   if        *in90 = *off and
     C                             hggscd = 'CR' and
     C                             hgcrdt <= lddt                               If not EOF
      *
      * Initialize processing flag
      *
     C                   move      yes           dltfl
     C                   move      yes           voidfl
      *
      * If a Group has Feed Tickets, it must be Voided instead of Deleted.
      *
     C     hghgsn        chain     hsl038e                            92
     C                   if        *in92 = *off
     C                   move      no            dltfl
     C                   endif
      *
      * If a Group has Expense Tickets, it must be Voided instead of Deleted.
      *
     C     hghgsn        chain     hsl048b                            92
     C                   if        *in92 = *off
     C                   move      no            dltfl
     C                   endif
      *
      *
      * A Group cannot be Deleted/Voided if it has any Movements.
      * (Note: The Group Status should not be "created" if it as any
      * movements; this is just a double check.)
      *
     C     hghgsn        chain     hsl058d                            92
     C                   if        *in92 = *off
     C                   move      no            dltfl
     C                   move      no            voidfl
     C                   endif
      *
     C                   select
     C                   when      dltfl = yes
     C                   write     hgrecpr
     C                   delete    hgrec
      *
     C                   when      voidfl = yes
     C                   move      'VD'          hggscd
     C                   z-add     hgcrdt        hgvddt
     C                   update    hgrec
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C                   endsr
      /EJECT
