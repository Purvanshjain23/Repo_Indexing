/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT SALES REPORT BY CO/DATE RANGE/ITEM CLASS/      */
/*             ITEM GROUP/ITEM TYPE.                                */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - SHEILA SCHERTZ                                      */
/*  DATE      - 07/19/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &TYPE       - SALES TYPE                                */
/*          &GRUP       - SALES GROUP                               */
/*          &CLSS       - SALES CLASS                               */
/*          &DTLSUM     - DETAIL OR SUMMARY                         */
/*          &EXPSLS     - EXPORT SALES                              */
/*          &FLAG1      - INCLUDE ALL PRODUCTS                      */
/*          &FLAG2      - INCLUDE CM/DM                             */
/********************************************************************/
/* RMC  8/16/00  ADDED FTP STATUS AND DOCUMENT TO PARMS             */
/*                 AND TO FTP TO /DOWNLOAD.PORK                     */
/* RMC   2/21/02   ADD WHSE TYPE AND WHSE CODE PARMS                */
/* RMC  10/03/03   ADD ITEM CODE                                    */
/* RMC  08/20/04   ADD COMPANY PARM TO THE CALL RPG OMNPPFR         */
/* SLM  10/05/04   SELECTION ON COMPANY WITH &C IF > 000 ELSE       */
/*                 THEN SELECT WITH COMPANY NUMBER.                 */
/* RMC  02/08/06   ADDED &PRTCUS PARM. IF NO DONT PRINT CUST INFO   */
/* RMC  01/02/08   PASS IN EMAIL ADDRESS - CONCAT 2-25 POS FIELDS   */
/*                 USE &FTP PARM TO BUILD THE WORKF FOR FTP & EMAIL */
/* SLM  07/03/08   ERROR ON BIG BYTE &E WAS DROPING STUFF FROM      */
/*                 &I COUNT A, B, C, D, AND I = 120                 */
/* LJB  01/11/10   2 FIELDS ADDED TO POA1CPP. CHANGED TO USE SEQUEL */
/*                 VIEW INSTEAD OF DEFINING SELECT STMT HERE.       */
/* PIO  05/29/23   REPLACE OPNQRYF WITH OPNSQLF FOR OMHSTPP FILE    */
/*                 ADDED NEEDED OVRDBF AROUND THE COMMAND.          */
/*                 CHANGE *AND TO AND, *EQ TO =, *GE TO >=, *LE TO  */
/*                 <=, *GT TO > IN &E                               */
/*                                                                  */
/* *-*-*-*- NOTE TO PROGRAMMERS  -*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-  */
/*          THERE IS A 16 MG LIMIT FOR EMAILING OUTSIDE SBF. IF     */
/*          USERS ARE NOT RECEIVING EMAILS, THIS MAY BE WHY.        */
/********************************************************************/
 OMNPPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &TYPEL +
                          &GRUPL &CLSSL &ITEML &OUTQ &HOLD &SAVE +
                          &COPIESL &NIGHTQ &DTLSUM &EXPSLS &FLAG1 +
                          &FLAG2 &SALRT &FTP &DOCUM &WHSTYP &WHSCOD +
                          &PRTCUS &EMAIL1 &EMAIL2)
        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &TYPE      *DEC    (3 0)          /*  SALES TYPE     */
        DCL    &TYPEL     *DEC                   /*  FOR PASSING    */
        DCL    &GRUP      *DEC    (3 0)          /*  SALES GROUP    */
        DCL    &GRUPL     *DEC                   /*  FOR PASSING    */
        DCL    &CLSS      *DEC    (3 0)          /*  SALES CLASS    */
        DCL    &CLSSL     *DEC                   /*  FOR PASSING    */
        DCL    &ITEM      *DEC    (7 0)          /*  ITEM CODE      */
        DCL    &ITEML     *DEC                   /*  FOR PASSING    */
        DCL    &OUTQ      *CHAR   10             /*  OUTPUT QUEUE   */
        DCL    &NIGHTQ    *CHAR   10             /*  NIGHT JOB QUEUE*/
        DCL    &SAVE      *CHAR   4              /*  SAVE SPOOL     */
        DCL    &HOLD      *CHAR   4              /*  HOLD SPOOL     */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &DTLSUM    *CHAR   1               /*  DETAIL/SUMMARY*/
        DCL    &EXPSLS    *CHAR   1               /*  EXPORT SALES  */
        DCL    &FLAG1     *CHAR   1               /*  INCL. ALL PROD*/
        DCL    &PRTCUS    *CHAR   1               /*  PRT CUST INFO */
        DCL    &FLAG2     *CHAR   1               /*  INCL. CM/DM   */
        DCL    &SALRT     *CHAR   3
        DCL    &FTP        *CHAR    (1)
        DCL    &DOCUM      *CHAR    (8)
        DCL    &DOCUMENT  *CHAR   (10)
        DCL    &WHSTYP    *CHAR   (2)
        DCL    &WHSCOD    *CHAR    (3)           /*  FOR PASSING    */
        DCL    &PCFILE    *CHAR   (32)
        DCL    &SEL       *CHAR    (1500)        /*  FOR PASSING    */
        DCL    &COMMAND    *CHAR   (512)
        DCL    &DATA      *CHAR   10             /* DATA FILES      */
        DCL    &DATAH     *CHAR   10             /* HIST FILES      */
        DCL    &EMAIL1     *CHAR   (25)          /* EMAIL ADDR - 1ST 25 POS.  */
        DCL    &EMAIL2     *CHAR   (25)          /* EMAIL ADDR - 2ND 25 POS.  */
        DCL    &EMAIL      *CHAR   (50)          /* EMAIL ADDR - BOTH FIELDS  */

/* SEND PROGRAM MESSAGE PARAMETERS  */
        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY FROM DATE  */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY EXPORT     */
        DCL     &I        *CHAR   25              /* QRY WHSE CODE   */
        DCL     &E        *CHAR  120              /* QRY ALL        */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TYPE) VALUE(&TYPEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&GRUP) VALUE(&GRUPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&CLSS) VALUE(&CLSSL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&ITEM) VALUE(&ITEML) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */
             CHGVAR     VAR(&EMAIL) VALUE(&EMAIL1 *CAT &EMAIL2)

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/
             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR AREA/BRK WORK FILE BUILD  */
/*********************************************************************/
  /*PIO      CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT + +
                         &FDATEA)                         */
  /*PIO*/    CHGVAR     VAR(&A) VALUE('F4GNDT >=  ' *CAT +
                         &FDATEA)

 /*PIO       CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT + +
                         &TDATEA)                              */
 /*PIO*/     CHGVAR     VAR(&B) VALUE(' AND F4GNDT  <= ' *CAT +
                         &TDATEA)

 /*PIO       CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT + +
                         &COMPA)                               */
 /*PIO*/     CHGVAR     VAR(&C) VALUE(' AND F4AIC3  =  ' *CAT +
                         &COMPA)

             IF         COND(&EXPSLS *EQ 'N') THEN(DO)
 /*PIO       CHGVAR     VAR(&D) VALUE('*AND F4U2ST *NE ' *CAT + +
                         '"Y"')                                */
 /*PIO*/     CHGVAR     VAR(&D) VALUE(' AND F4U2ST  <> ' *CAT +
                         '"Y"')
             ENDDO
             IF         COND(&EXPSLS *EQ 'E') THEN(DO)
 /*PIO       CHGVAR     VAR(&D) VALUE('*AND F4U2ST *EQ ' *CAT + +
                         '"Y"')                                */
 /*PIO*/     CHGVAR     VAR(&D) VALUE(' AND F4U2ST  =  ' *CAT +
                         '"Y"')
             ENDDO

WHSCOD:      IF         COND(&WHSCOD *NE '   ') THEN(DO)
 /*PIO       CHGVAR     VAR(&I) VALUE('*AND F4AJCD *EQ "' *CAT + +
                         &WHSCOD *CAT '"')                     */
/*PIO*/      CHGVAR     VAR(&I) VALUE(' AND F4AJCD =   "' *CAT +
                         &WHSCOD *CAT '"')
             ENDDO

/***************************************************************************/
/**** COMPANY NE 000 SELECT ON SPECIFIC COMPANY  USING &C               ****/
/***************************************************************************/
             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&E) VALUE(&A *BCAT &B *BCAT &C *BCAT &D *BCAT &I)
             ENDDO

/***************************************************************************/
/**** COMPANY EQ 000 SELECT ALL COMPANIES                               ****/
/***************************************************************************/
             IF         COND(&COMPA *EQ '000') THEN(DO)
             CHGVAR     VAR(&E) VALUE(&A *BCAT &B *BCAT &D *BCAT &I)
             ENDDO

   /*PIO     OVRDBF     FILE(OMHSTPO3) SHARE(*YES)   +
                                                     +
             OPNQRYF    FILE((OMHSTPO3)) OPTION(*ALL) + +
                          QRYSLT(&E) KEYFLD(*FILE)    */

   /*PIO*/   OVRDBF     FILE(OMHSTPP) TOFILE(OMHSTPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from OMHSTPP Where  ' *BCAT &E +
                          *BCAT ' ORDER BY F4RGNB ASC, F4RHNB ASC, +
                          F4RINB ASC, F4S5NB ASC, F4AIC3 ASC') +
                          OPNID(OMHSTPO3) OPTION(*ALL) SEQONLY(*NO) +
                          RCDFMT(@HSTPEN) ALWCPY(*IFRQD) /*PIO*/
   /*PIO*/   OVRDBF     FILE(OMHSTPO3) TOFILE(OMHSTPP) LVLCHK(*NO) +
                           OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)

/********************************************************************/
/* CRTDUP OUTFILE IN QTEMP IF FTP OR EMAIL IS REQUESTED                      */
/********************************************************************/
             IF         COND(&FTP *EQ 'Y') THEN(DO)
             CALL      PGM(CAQXXFR) PARM(' ' &DATA &DATAH)
             CRTDUPOBJ  OBJ(POA1CPP) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ OBJ(POA1CPL0) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ OBJ(POA1CPL1) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/
/*  THIS SPECIFIES  DRAWER(2)  AGAIN  2/10/06                              */
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          DRAWER(2) COPIES(&COPIES) HOLD(&HOLD) +
                          SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/*********************************************************************/
/* PRINT THE REPORT                                                 */
/********************************************************************/
             CALL       PGM(&PGM) PARM('' &FDATE &TDATE &TYPE &GRUP +
                          &CLSS &FLAG1 &FLAG2 &DTLSUM &EXPSLS +
                          &SALRT &FTP &COMP &ITEM &PRTCUS &WHSTYP &WHSCOD)
/*****       CPYF       FROMFILE(QTEMP/POA1CPP) +
                          TOFILE(OMSDEVGEN/POA1CPP) MBROPT(*REPLACE) */
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(OMHSTPO3)
    /*PIO    DLTOVR     FILE(OMHSTPO3)   */
             DLTOVR     FILE(OMHSTPO3) LVL(*JOB) /*PIO*/

 /*------------------------------------------------------------------*/
 /* RUN SQL TO PUSH document to /DOWNLOAD.PORK                       */
 /*------------------------------------------------------------------*/
             IF         COND(&DOCUM *GT ' ') THEN(DO)
             ADDLIBLE LIB(SEQUEL) POSITION(*LAST)
             MONMSG MSGID(CPF2103)

 /*------------------------------------------------------------------*/
 /* EXECUTE SEQUEL TO SELECT THE WORKF FILE AND FTP IT               */
 /*------------------------------------------------------------------*/
 /* SALES BY ITEM - DETIAL                                           */
             IF         COND(&DTLSUM *EQ 'D') THEN(EXECUTE +
                          VIEW(SLSBYITM) MBROPT(*REPLACE) +
                          PCFMT(*XLS) TOFLR('GUY.IS') TODOC(&DOCUM) +
                          REPLACE(*YES) OPTIMIZE(*TOTAL) MSG(*YES) +
                          DTSTYLE(*FIELD '/' *FIELD ':') +
                          TEXT('Sales Realization CM/DM'))

 /* SALES BY ITEM - SUMMARY                                          */
             IF         COND(&DTLSUM *EQ 'S') THEN(EXECUTE +
                          VIEW(SLSBYITMSM) MBROPT(*REPLACE) +
                          PCFMT(*XLS) TOFLR('GUY.IS') TODOC(&DOCUM) +
                          REPLACE(*YES) OPTIMIZE(*TOTAL) +
                          DTSTYLE(*FIELD '/' *FIELD ':') +
                          TEXT('Sales Realization CM/DM-Summary'))

 /*------------------------------------------------------------------*/
 /* PUSH documents to /DOWNLOAD.PORK                                 */
 /*------------------------------------------------------------------*/
             CHGVAR VAR(&DOCUMENT) VALUE(&DOCUM)
             CHGVAR VAR(&PCFILE)   VALUE(&DOCUM *TCAT '.CSV')

             CALL PGM(FTPTEST) PARM(&DOCUMENT &PCFILE)

             ENDDO

 /********************************************************************/
 /* RUN SQL TO EMAIL document to EMAIL ADDRESS                       */
 /*------------------------------------------------------------------*/
             IF         COND(&EMAIL *GT ' ') THEN(DO)
             ADDLIBLE LIB(SEQUEL) POSITION(*LAST)
             MONMSG MSGID(CPF2103)

 /*------------------------------------------------------------------*/
 /* EXECUTE SEQUEL VIEW TO EMAIL SPREADSHEET OF THE WORKFILE        -*/
 /*------------------------------------------------------------------*/
 /* SALES BY ITEM - DETAIL                                           */
             IF         COND(&DTLSUM *EQ 'D') THEN(EXECUTE +
                          VIEW(SLSBYITM) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG('Sales By +
                          Item-Detail') TEXT('Sales By Item-Detail'))

 /* SALES BY ITEM - SUMMARY                                          */
             IF         COND(&DTLSUM *EQ 'S') THEN(EXECUTE +
                          VIEW(SLSBYITMSM) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG('Sales By +
                          Item-Summary') TEXT('Sales By Item-Summary'))
             ENDDO
 END:        ENDPGM

/*********************************************************************/
/************  COMMENTED OUT OLD CODE  *******************************/
/*********************************************************************/
/* THE FOLLOWING ARE THE SAVED SELECT STATEMENTS                     */
/*********************************************************************/

/* THIS GOES AFTER: IF &DTLSUM *EQ 'D' ...  (BOTH PLACES)            */
             CHGVAR     VAR(&SEL) VALUE('SELECT A1AIC3 +
                          COLHDG("Company"), a1ncnx colhdg("Date +
                          Range"), a1I6Sx colhdg("Select" +
                          "Report"),a1rhnb colHDg("Group"), a1r4cD +
                          COLHDG("Sales" "Route"), a1s5nb  +
                          colhdg("Shipped Item"),a1bkc7 +
                          COLHDG("Ship to" "Cust"), a1phna +
                          colhdg("Ship to" "Name"), a1dbcd +
                          colhdg("Dlv" "State"), a1conb +
                          colhdg("Invoice"), a1gndt colhdg("Date" +
                          "Shipped"),a1fdpc colhdg("FOB" +
                          "Price/CWT"),a1czvl colhdg("Mkt + +
                          Ovr/CWT"), a1cyvl +
                          colhdg("Var/CWT"),a1nfnx +
                          colhdg("Invoiced" "$"), a1ngnx +
                          colhdg("Absorbed" "Freight"), a1grvl +
                          colhdg("Accruals" "$"), a1nhnx +
                          colhdg("Net $"), a1ijwg COLHDG("Shipped" +
                          "Net Weight"),  a1ninx colhdg("Absorbed" +
                          "Frt/CWT"), a1m3qt colhdg("Quantity" +
                          "Billed"), a1rlcd colhdg("Order" +
                          "Reason"), A1JXAA COLHDG("Price" "Type")  +
                          FROM    QTEMP/POA1CPP')

/* THIS GOES AFTER: IF &DTLSUM *EQ 'S' ...    (BOTH PLACES)          */
             CHGVAR     VAR(&SEL) VALUE('SELECT A1AIC3 +
                          COLHDG("Company"), a1ncnx colhdg("Date +
                          Range"), a1I6Sx colhdg("Select" +
                          "Report"),a1rhnb colHDg("Group"), a1s5nb  +
                          colhdg("Shipped Item"), a1fdpc +
                          colhdg("FOB" "Price/CWT"),a1czvl +
                          colhdg("Mkt + Ovr/CWT"), a1cyvl +
                          colhdg("Var/CWT"),a1nfnx +
                          colhdg("Invoiced" "$"), a1ngnx +
                          colhdg("Absorbed" "Freight"), +
                          a1grvl colhdg("Accruals" "$"), +
                          a1nhnx COLHDG("NeT $"), +
                          a1ijwg COLHDG("Shipped" +
                          "Net Weight"),  a1ninx colhdg("Absorbed" +
                          "Frt/CWT"), a1m3qt colhdg("Quantity" +
                          "Billed"), a1rlcd colhdg("Order" +
                          "Reason") FROM    QTEMP/POA1CPP')

/* THIS GOES AFTER THE &DTLSUM *EQ 'D' AND 'S' ...   (FTP ONLY)      */
             EXECUTE    SQL(&SEL) MBROPT(*REPLACE) NBRRCDS(*ALL) +
                          KEYFLDCNT(*ALL) ALWNULL(*NO) +
                          TOFLR('GUY.IS') TODOC(&DOCUM) +
                          PCFMT(*XLS) REPLACE(*YES) +
                          TEXT('Sales Realization CM/DM') +
                          OPTIMIZE(*TOTAL) ALWCPY(*YES) MSG(*YES) +
                          UNIQUEKEY(*NONE) IGNDECERR(*NO) +
                          DTSTYLE(*FIELD '/' *FIELD ':')

