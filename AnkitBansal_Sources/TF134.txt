      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF134
      * TITLE:       Assign/Unassign Charges to an Invoice
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/10/05
      *
      * FUNCTION: This function allows the user to remove Charges from an Invoice and go
      *           to a select function to select charges to be assigned to an Invoice.
      *
      *           Restrictors:
      *             Charge frequency code
      *             Invoice number
      *
      *           Positioners:
      *               None
      *
      *           Selectors:
      *               Include in invoice total flag
      *               System module
      *               Charge code
      *               Invoice charge date
      *               Invoice charge number
      *               Charge code description
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfd134    cf   e             workstn sfile(tf134s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Ftfp020    uf   e           k disk
      *  Invoice header
      *
      *
     Ftfj021f   if   e           k disk
      *   Invoice charges + Charge Codes
      *
      *
     Ftfl021c   if   e           k disk    rename(icrec:icrecc) prefix(p2)
      *   Invoice charges
      *
      *
     Ftfp021    uf   e           k disk    rename(icrec:icrecp1) prefix(p1)
      *   Invoice charges
      *
      *
     Ftfp097    if   e           k disk
      *    Status codes
      *
      *
     Ftfp098    if   e           k disk
      *    Charge frequencies
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
     D upper           c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     D lower           c                   'abcdefghijklmnopqrstuvwxyz'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2
     D svrrn1          s                   like(hdrrn1)
      *
      *
      * Control fields
      *
     D procfl          s              1
     D unassignfl      s              1
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Workfields
      *
     D wkdesc          s                   like(c1ccds)
     D wkccds          s                   like(c1ccds)
     D nbrchar         s              2  0
      *
      *
      * Parms
      *
     D xxaist          s              1
     D xxucfl          s                   like(ccucfl)
     D xxynfl          s                   like(ccinfl)
      *
     D xxcccd          s                   like(cccccd)
     D xxccds          s                   like(ccccds)
      *
     D xxsmcd          s                   like(ccsmcd)
     D xxsmds          s             20
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
      *
      *
      * Save fields for positioners and selectors
      *
      *
     D svicsn          s                   like(c1icsn)
     D svicdtmdy       s                   like(c1icdtmdy)
     D svcccd          s                   like(c1cccd)
     D svccds          s                   like(c1ccds)
     D svsmcd          s                   like(c1smcd)
     D svinfl          s                   like(c1infl)
      *
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *---------------------------------------------------------------
      * Standard subfile option conversion
      *---------------------------------------------------------------
     D                 ds                  inz
     D d1opt                          2
     D   wkopt1                       1    overlay(d1opt)
     D   wkopt2                       1    overlay(d1opt:2)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Retrieve Invoice Header stuff for Control Record
      *
     C                   exsr      $invoice
      *
      * Clear and fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
     C                   if        norecs = yes
     C                   seton                                        90
     C                   else
     C                   setoff                                       90
     C                   endif
      *
      * Write screen 1 to crt
      *
     C                   write     tf134k1
     C                   write     tf134ec
     C                   exfmt     tf134c1
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   eval      wkflow = idflow
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   move      '03'          rtncd
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $edit1
     C                   if        error <> 0
     C                   movel     scrn1         routne
     C                   else
     C                   exsr      $f4s1
     C                   exsr      $chksv
     C                   endif
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   move      yes           reset
     C                   movel     set1          routne
      *
     C                   when      *in10 = *on                                  F10-select
     C                   exsr      $edit1
     C                   if        error <> 0
     C                   movel     scrn1         routne
     C                   else
     C                   move      no            reset
     C                   movel     set1          routne
     C                   call      'TF135'
     C                   parm                    c1insn
     C                   parm                    rtncd
     C                   endif
      *
      * User has changed a positioners/selectors.
      *
     C                   when      c1icsn <> svicsn or                          Selectors
     C                             c1infl <> svinfl or
     C                             c1icdtmdy <> svicdtmdy or
     C                             c1cccd <> svcccd or
     C                             c1ccds <> svccds or
     C                             c1smcd  <> svsmcd
     C                   exsr      $edit1
     C                   if        error <> 0
     C                   movel     scrn1         routne
     C                   else
     C                   exsr      $chksv
     C                   endif
      *
     C                   when      *in25 = *on                                  F25-rollup
     C                   exsr      $load1
     C                   movel     scrn1         routne
     C                   other
      *
      * User pressed 'ENTER'
      *
     C                   if        hdrrn1 = 0                                   If nothing
     C                   else
     C                   exsr      $edit1
     C                   endif                                                  If nothing
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *
     C     $chksv        begsr
      *
     C                   movel     no            reset
     C                   eval      routne = set1
      *
     C                   z-add     c1icsn        svicsn
     C                   z-add     c1icdtmdy     svicdtmdy
     C                   move      c1smcd        svsmcd
     C                   move      c1infl        svinfl
      *
      * Trim leading blank on Charge Code
      *
     C                   eval      c1cccd = %triml(c1cccd)
     C                   move      c1cccd        svcccd
      *
      * Prepare to scan on Charge Code Description
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      c1ccds = %trim(c1ccds)
     C     ' '           checkr    c1ccds        nbrchar
     C                   movel     c1ccds        svccds
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Invoice Header stuff for Control Record
      *---------------------------------------------------------------
      *
     C     $invoice      begsr
      *
      * Retrieve Invoice values from Invoice Header record
      *
     C     c1insn        chain(n)  tfp020                             92
     C                   if        *in92 = *off                                 If hit
      *
      * Flip Invoice Date from CCYYMMDD to MMDDYY for display
      *
     C     *iso          test(d)                 ihindt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihindt        wkisodate
     C     *mdy          move      wkisodate     c1indtmdy
     C                   endif                                                  If OK date
      *
      * Revtieve description for Charge Frequency Code
      *
     C     ihcfqcd       chain     tfp098                             92
     C                   if        *in92 = *off
     C                   move      cfcfqds       c1cfqds
     C                   endif
      *
      * Revtieve description for Invoice charge status
      *
     C     ihinstcd      chain     tfp097                             92
     C                   if        *in92 = *off
     C                   move      ststds        c1stds
     C                   endif
      *
      * Revtieve amounts
     C                   call      'TF806'
     C                   parm                    c1insn
     C                   parm      0             c1inam
     C                   parm      0             c1memoam
     C                   endif                                                  If hit
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   tf134s1
     C                   seton                                        83        sflclr
     C                   write     tf134c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C     c1insn        setll     tfj021f
      *
      *
      * The field 'reset' is a flag field to indicate whether you are
      * attempting to reposition the user close to where he was in the
      * subfile.
      * The lowest subfile RRN on the screen from the DSPF feedback
      * area (IDFLOW) was retrieved earlier and put in WKFLOW.
      * Add to WKFLOW the number of lines on your subfile page or the
      * number less 1 to determine a subfile RRN to reposition to.
      *
      *
     C                   if        reset = yes
     C                   eval      maxrrn = wkflow + wkrlin
     C                   else
      *
      * Don't try to reposition the user, just start the subfile over
      *
     C                   eval      maxrrn = wkplin
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       svrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   z-add     svrrn1        hdrrn1
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   eval      count = 0
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
     C     c1insn        reade     tfj021f                                82
     C                   if        *in82 = *off                                 If read
     C                   move      yes           procfl
      *
     C                   move      icinfl        d1infl
     C                   z-add     icicsn        d1icsn
     C                   move      iccccd        d1cccd
     C                   z-add     icicam        d1icam
     C                   move      icsmcd        d1smcd
     C                   move      ccccds        d1ccds
      *
      * Flip Invoice Charge Date from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 icicdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      icicdt        wkisodate
     C     *mdy          move      wkisodate     d1icdtmdy
     C                   endif                                                  If OK date
      *
      * Only include records that match the user's selectors
      *
      * Include in Invoice Total
     C                   if        c1infl <> *blank and
     C                             c1infl <> d1infl
     C                   move      no            procfl
     C                   endif
      *
      * Invoice Charge Number
      *
     C                   if        c1icsn <> 0 and
     C                             c1icsn <> d1icsn
     C                   move      no            procfl
     C                   endif
      *
      * Invoice Charge Date
      *
     C                   if        c1icdtmdy <> 0 and
     C                             c1icdtmdy <> d1icdtmdy
     C                   move      no            procfl
     C                   endif
      * Charge code
     C                   if        c1cccd <> *blank and
     C                             c1cccd <> d1cccd
     C                   move      no            procfl
     C                   endif
      *
      * Charge Code Description
      *
     C                   if        c1ccds <> *blank                             If desc
     C     lower:upper   xlate     c1ccds        wkdesc
     C     lower:upper   xlate     ccccds        wkccds
     C     wkdesc:nbrcharscan      wkccds                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif                                                  If desc
      *
      * System Module
     C                   if        c1smcd <> *blank and
     C                             c1smcd <> d1smcd
     C                   move      no            procfl
     C                   endif
      *
      *
     C                   if        procfl = yes                                 If process
      *
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   move      *blank        d1opt
     C                   write     tf134s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into
      * the sflrcdnbr field (hdpag1)
      *
     C                   z-add     hdrrn1        hdpag1
      *
     C                   endif                                                  If process
     C                   endif                                                  If read
     C                   enddo                                                  Do fill loop
      *
      * If no records were written to the subfile, then issue
      * a message telling the user to take F10 to assign charges
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   movel     yes           norecs
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
      *
     C                   else
     C                   movel     no            norecs
     C                   endif                                                  If no recs
      *
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
     C                   z-add     0             error
      *
     C                   if        norecs = no                                  If have recs
     C                   exsr      $edop1
      *
      * If there are no errors, process for updates.
      *
     C                   if        error = 0                                    If no error
     C                   exsr      $proc1
     C                   movel     set1          routne
     C                   else
     C                   movel     scrn1         routne
     C                   endif                                                  If no error
     C                   endif                                                  If have recs
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Read/edit changed subfile records for valid options on Screen 1
      *--------------------------------------------------------------------------------------
      *
     C     $edop1        begsr
      *
     C                   z-add     0             error
      *
      * Read all changed subfile records, editing for valid options
      *
     C                   setoff                                       80
     C                   dou       *in80 = *on                                  Do readc
     C                   readc     tf134s1                                80
     C                   if        *in80 = *off                                 If rec read
      *
      * Set off error indicator for subfile option field
      *
     C                   setoff                                       30
      *
      * Convert option user entered to standard format. For example,
      * a ' 1', '01', and '1 ' must be standardized to ' 1'.
      *
     C                   if        wkopt2 = *blank                              If 2 = blank
     C                   eval      wkopt2 = wkopt1
     C                   eval      wkopt1 = *blank
     C                   endif                                                  If 2 = blank
      *
     C                   if        wkopt1 = *zero                               If 1 is zero
     C                   eval      wkopt1 = *blank
     C                   endif                                                  If 1 is zero
      *
      * Test for valid subfile option entry on all changed records
      *
     C                   if        d1opt <> ' 4' and                            If bad opt
     C                             d1opt <> '  '
     C                   seton                                        3085
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09006'
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      *
      * Rewrite the subfile record with the modified data tag on so it
      * will be reread and processed.
      *
     C                   if        d1opt <> *blank                              If good option
     C                   seton                                        85
     C                   else
     C                   setoff                                       85
     C                   endif                                                  If good option
     C                   endif                                                  If bad opt
      *
     C                   update    tf134s1
     C                   setoff                                       8530
     C                   endif                                                  If rec read
     C                   enddo                                                  Do readc
      *
     C                   if        error = 00
     C                   z-add     1             hdpag1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Read subfile records and process user selections for Screen 1
      *---------------------------------------------------------------
      *
     C     $proc1        begsr
      *
     C                   move      no            unassignfl
      *
      * Read all the changed subfile records processing the subfile selections
      *
     C                   dou       *in80 = *on                                  Do process
     C                   readc     tf134s1                                80
     C                   if        *in80 = *off                                 If rec read
      *
      * If user is removing this charge from the Invoice
      *
     C                   if        d1opt = ' 4'
     C                   move      yes           unassignfl
     C                   exsr      $update
     C                   endif
      *
      * Upon returning from the called program,
      * clear the subfile option selected by the user and rewrite
      * the subfile record.
      *
     C                   setoff                                       85
     C                   update    tf134s1
     C                   endif                                                  If rec read
     C                   enddo                                                  Do process
      *
      *
      * If you unassigned some charges,
      *  1) Check to see if the Invoice Status should be reset to Open
      *
     C                   if        unassignfl = yes                             If removed some
     C     c1insn        chain(n)  tfl021c                            92
     C                   if        *in92 = *on                                  If all gone
     C     c1insn        chain     tfp020                             92
     C                   if        *in92 = *off                                 If header hit
     C                   move      'O'           ihinstcd
     C                   update    ihrec
     C                   endif                                                  If header hit
     C                   endif                                                  If all gone
     C                   endif                                                  If removed some
      *
     C                   move      yes           reset
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Include in invoice total flag
      *
     C                   select
     C                   when      fld = 'C1INFL'
     C                   call      'HP522'
     C                   parm      *blank        xxynfl
     C                   seton                                        53
     C                   if        xxynfl <> *blank
     C                   movel     xxynfl        c1infl
     C                   endif
      *
      * Charge code
     C                   when      fld = 'C1CCCD'
     C                   call      'TF513'
     C                   parm      ' '           xxaist
     C                   parm      ' '           xxucfl
     C                   parm      *blank        xxcccd
     C                   parm      *blank        xxccds
     C                   seton                                        51
     C                   if        xxcccd <> *blank
     C                   move      xxcccd        c1cccd
     C                   endif
      *
      * System module
     C                   when      fld = 'C1SMCD'
     C                   call      'TF509'
     C                   parm      *blank        xxsmcd
     C                   parm      *blank        xxsmds
     C                   seton                                        52
     C                   if        xxsmcd <> *blank
     C                   movel     xxsmcd        c1smcd
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------
      * Update database files
      *---------------------------------------------------------------------------------
      *
     C     $update       begsr
      *
      * Retrieve the Invoice Charge record and:
      *  1) set its Invoice Number field to zero
      *  2) change its Status back to Open
      *
     C     d1icsn        chain     tfp021                             92
     C                   if        *in92 = *off                                 If found
     C                   z-add     0             p1icinsn
     C                   move      'O'           p1icinstcd
     C                   update    icrecp1
     C                   endif                                                  If found
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------
      * Set the subfile to redisplay at the first page that has errors on screen 1
      *---------------------------------------------------------------------------------
      *
     C     $err1         begsr
      *
     C                   if        error = 1
     C                   eval      hdpag1 = hdrrn1
     C                   endif
      *
     C                   endsr
      *
      *---------------------------------------------------------------
      * Set on last record and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    c1insn
     C                   parm                    rtncd
      *
      * Key lists
      *  None
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     11            wkplin                         sflpag
     C     wkplin        sub       1             wkrlin                         sflpag - 1
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
