/***************************************************************** */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF483CL                                       */
/*  PROGRAM NAME:    CASH DISTRIBUTION BALANCE LISTING/DOWNLOAD    */
/*  PROGRAMMER:      ERIC LOUCHEZ                                  */
/*  CREATE DATE:     09/30/22                                      */
/*  FUNCTION:        AUTO/DOWNLOAD TFP011 FILE DATA                */
/*******************************************************************/
PGM
/*---- REPORT ALWAYS HAS 01/01/2006 AS FROM DATE------------------- */
  DCL VAR(&FRDATE)  TYPE(*CHAR) LEN(8) VALUE('20060101')
  DCL VAR(&JULDEC)  TYPE(*DEC ) LEN(7)
  DCL VAR(&TOJUL )  TYPE(*CHAR) LEN(7)
  DCL VAR(&TODAY )  TYPE(*CHAR) LEN(6)
  DCL VAR(&TODATE)  TYPE(*CHAR) LEN(8)
  DCL VAR(&LDEMAIL) TYPE(*CHAR) LEN(50) VALUE('Cash_Dist_Bal')
  DCL VAR(&QDATA)   TYPE(*CHAR) LEN(100)
  DCL VAR(&RCDCNT)  TYPE(*DEC)  LEN(10)

/*---- RTV TODAY'S DATE & PREVIOUS SATURDAY ------------------------- */
/*---- REPORT ALWAYS RUNS ON WEDNESDAY WITH THE PREVIOUS SATURDAY AS A TODATE */
  RTVSYSVAL SYSVAL(QDATE) RTNVAR(&TODAY)
  CVTDAT DATE(&TODAY) TOVAR(&TOJUL) FROMFMT(*MDY) TOFMT(*LONGJUL) TOSEP(*NONE)
  CHGVAR &JULDEC &TOJUL
  CHGVAR &JULDEC (&JULDEC -4)
  CHGVAR &TOJUL  &JULDEC
  CVTDAT DATE(&TOJUL) TOVAR(&TODATE) FROMFMT(*LONGJUL) TOFMT(*YYMD) TOSEP(*NONE)

/***********************************************************************/
/* EXTRACT THE DATA FROM THE CASH DISTRIBUTION BALANCE FILE            */
/***********************************************************************/
  CHGVAR VAR(&QDATA) VALUE(' ')
/* FROM/TO DATE                                                           */
  CHGVAR (%SST(&QDATA 1 10))  ('CBWEDT *GE')
  CHGVAR (%SST(&QDATA 12 8))  (&FRDATE )
  CHGVAR (%SST(&QDATA 21 15)) ('*AND CBWEDT *LE')
  CHGVAR (%SST(&QDATA 37 8))  (&TODATE )

/*---------------------------------------------------------------------*/
/* EXTRACT DATA                                                        */
/*---------------------------------------------------------------------*/
  OVRDBF FILE(TFP011) SHARE(*YES)
  OPNQRYF FILE((TFP011)) QRYSLT(&QDATA) KEYFLD(*FILE) SEQONLY(*YES)

/* DOWNLOAD                                                             */
             CRTDUPOBJ  OBJ(TFP011) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(TFP011DWL) DATA(*NO)
     CPYFRMQRYF FROMOPNID(TFP011) TOFILE(QTEMP/TFP011DWL) MBROPT(*REPLACE)

/* RETRIEVE THE NUMBER OF RECORDS IN THE DOWNLOAD FILE                  */
     RTVMBRD    FILE(TFP011DWL) NBRCURRCD(&RCDCNT)
       IF COND(&RCDCNT = 0) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Cash +
                          Distribution Balance Download') MSG('Your +
                          selections on TF403-Specify Options for +
                          Cash Distribution Balance +
                          Listing/Download did not extract any data +
                          for the spreadsheet download. Please +
                          change your selections and submit the +
                          Download again.')
                   MONMSG MSGID(CPF0000)
       ENDDO
       IF COND(&RCDCNT >= 66000) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Cash +
                          Distribution Balance Download') MSG('Your +
                          selections on TF403-Specify Options for +
                          Cash Distribution Balance +
                          Listing/Download extracted over 66,000 +
                          records--which is too many records for +
                          the spreadsheet to handle. Change your +
                          selections to extract fewer records and +
                          submit your Download request again.')
                   MONMSG MSGID(CPF0000)
       ENDDO
       IF COND(&RCDCNT > 0 *AND &RCDCNT < 66000) THEN(DO)
             EXECUTE    SQL('SELECT * FROM TFP011DWL') NBRRCDS(*ALL) +
                          PCFMT(*XLS) RECIPIENT(&LDEMAIL) +
                          EMLMSG('Cash Distribution Balance +
                          Download') TEXT('Cash Distribution +
                          Balance Download')
           MONMSG MSGID(CPF0000)
       ENDDO

  CLOF OPNID(TFP011)
  DLTOVR FILE(*ALL)
ENDPGM
