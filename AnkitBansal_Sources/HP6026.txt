      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP6026
      * TITLE:       Listing of Companies
      * PROGRAMMER:  Shefali Goyal
      * CREATED:     6/18/25
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4026-Work with Company.
      *
      *            All the company records will be printed irrespective of the
      *            positioner record value.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/18/25  Shefali Goyal
      *           Initial creation
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP062    IF   E           K DISK
      *    Company
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Print fields
      *
     D dash            s             80    inz(*all'-')
      *
     D h1company       s             75
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * Prepare to read the Company file and print headings the first time through.
      *
     C     *loval        setll     hsp062
     C                   EXCEPT    h1hdr
      *
      * Read and print all records in the file that match the user's selection.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp062                                 90
      *
     C                   if        *in90 = *off                                 If not EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    h1hdr
     C                   ENDIF
      *
      * Print detail line
     C                   EXCEPT    d1dtl
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * End of File
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
     C                   endsr
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            h1hdr          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           66 'DATE'
     O                       UDATE         Y     76
      *
     O          E            h1hdr          1
     O                       SDUSR               10
     O                                           47 ' LISTING OF COMPANIES'
     O                                           66 'TIME'
     O                       RTIME               76 '  :  :  '
      *
     O          E            h1hdr          2
     O                                           66 'PAGE'
     O                       PAGE          Z     71
      *
      *
     O          e            h1hdr          1
     O                       h1company           75
      *
     O          E            h1hdr          1
     O                                            7 'COMPANY'
     O                                           16 'COMPANY'
      *
     O          E            h1hdr          1
     O                                            5 'CODE'
     O                                           14 'NAME'
      *
     O          E            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            d1dtl       1
     O                       cococd               5
     O                       coconm              39
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
