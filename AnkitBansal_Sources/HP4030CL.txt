/***************************************************************** */
/*  PROGRAM NUMBER --> HP4030CL                                    */
/*  PROGRAM NAME ----> REPORTED DAILY ACTIVITY LISTING             */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 10/22/02                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT A        */
/*                     LISTING OF RECORDS IN THE 'REPORTED DAILY   */
/*                     ACTIVITY' FILE.                             */
/*                                                                 */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/*  04/06/11  LeAnne Ramsey                                        */
/*            The CHGVAR command for LDCELL was wrong. It was:     */
/*                 VALUE(%SST(*LDA 64 5))                          */
/*            I changed it to be 2 positions in length:            */
/*                 VALUE(%SST(*LDA 64 2))                          */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(90)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 64 2))

/*******************************************************************/
/*  SELECT ALL GROW FINISH AND NURSERY FARMS                       */
/*******************************************************************/

             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('FSPPCD *NE "BGF  "')

/*  IF CELL                                                  */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 20 16)) VALUE('*AND FSCELL +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 36 5)) VALUE(&LDCELL)
             ENDDO

             OVRDBF     FILE(HSL018J) SHARE(*YES)
             OPNQRYF    FILE((HSL018J)) QRYSLT(&QDATA) KEYFLD(*FILE)

/***************************************************************** */
/*  SELECT ALL RECORDS WITHIN DATE RANGE                             */
/***************************************************************** */

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('RDDYDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND RDDYDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE                                       */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND RDFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 70 15)) VALUE('*AND RDFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 86 5)) VALUE(&LDFSCD)
             ENDDO


             OVRDBF     FILE(HSP103) SHARE(*YES)
             OPNQRYF    FILE((HSP103)) QRYSLT(&QDATA) KEYFLD(*FILE)

/*------------------------------------------------------------------*/
/* SET UP PRINT PARMS                                               */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD 10 CPI WITH 100 COLUMNS OF PRINT          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/* RUN LISTING                                                     */
/*------------------------------------------------------------------*/

             CALL PGM(HP6030)

             CLOF       OPNID(HSL018J)
             CLOF       OPNID(HSP103)
             DLTOVR     FILE(*ALL)

             ENDPGM

