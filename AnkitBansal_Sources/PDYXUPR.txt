/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION USED BY ANY PROGRAM      */
/*              PASSING ITEM TYPE, CONVERTED/BASE ITEM, ITEM CLASS, */
/*              PRODUCTION DATE CONTROLLED, CATCHWEIGHT STATUS,     */
/*              PRIMARY U/M, AND RECORD STATUS.                     */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 04/30/97                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     B    &COMPN      - COMPANY NUMBER (PASSED)                   */
/*     B    &ITMTYPE    - ITEM TYPE(PASSED)                         */
/*     B    &CNVBASITM  - CONVERTED/BASE ITEM(PASSED)               */
/*     B    &ITMCLS     - ITEM CLASS(PASSED)                        */
/*     B    &CWSTS      - CATCHWEIGHT STATUS(PASSED)                */
/*     B    &PRIMUM     - PRIMARY U/M(PASSED)                       */
/*     B    &PRDDT      - PRODUCTION DATE CONTROLLED(PASSED)        */
/*     B    &STATUS     - STATUS(PASSED)                            */
/*     B    &COPYN      -  NO. OF COPIES (PASSED)                   */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*                                                                  */
/********************************************************************/

 PDYXUPR:    PGM        PARM(&RETURN &APPLIC &PGM &COMPN &ITMTYPE +
                          &CNVBASITM &ITMCLS &CWSTS &PRIMUM &PRDDT +
                          &STATUS &COPYN &DESC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &COMPN      *DEC                  /* Company Number (PASSED)   */
        DCL    &ITMTYPE    *CHAR   (3)           /* Item Type (PASSED)        */
        DCL    &CNVBASITM  *CHAR   (2)           /* Bas/Cnv Item(PASSED)      */
        DCL    &ITMCLS     *DEC    (15 5)        /* Item Class (PASSED)       */
        DCL    &PRDDT      *CHAR   (1)           /* Prod Date  (PASSED)       */
        DCL    &CWSTS      *CHAR   (1)           /* Catchweight Status(PASSED)*/
        DCL    &PRIMUM     *CHAR   (2)           /* Primary U/M(PASSED)       */
        DCL    &STATUS     *CHAR   (1)           /* Status (PASSED)           */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&APPLIC &COMPN +
                          &ITMTYPE &CNVBASITM &ITMCLS &PRDDT &CWSTS +
                          &PRIMUM &STATUS &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ)) JOB(&PGM)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

