/*********************************************************************/
/*  SYSTEM      - ORDER MANAGEMENT SYSTEM                            */
/*  PROGRAM     - PUB9UPC                                            */
/*  DESCRIPTION - EXC AR MISC POST LSTRPTCL EXECUTE USER PROGRAM     */
/*  DATE        - 04/24/2017                                         */
/*  BY          - DANNY NGUYEN                                       */
/*  PROJECT     - R9995 - A/R CASH APPLIED MISC POST                 */
/*  PURPOSE     - Created process to build/print A/R Misc Post       */
/*                Summary report for OA & CM.                        */
/*                Process can be called from either Menu=6.2.4 (     */
/*                Deposit Date will be 0.)  OR                       */
/*                Menu=6.3.21 (Deposit Date will be populated.)      */
/*                                                                   */
/*  PARAMETERS:                                                      */
/*     I    &DEPDTE     - DEPOSIT DATE                               */
/*********************************************************************/
/* PROJECT  BY  DATE     DESCRIPTION                                 */
/*   S13572 RMC  8/14/18 ADDED LF PMACCPL4                           */
/*********************************************************************/
             PGM        PARM(&RETURN &DEPDTE &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &COPYN)

             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DEPDTE) TYPE(*CHAR) LEN(7) /* Deposit Date */
             DCL        VAR(&DEPDTE2) TYPE(*DEC) LEN(7 0) /* Deposit Date */
             DCL        VAR(&COPYN) TYPE(*DEC) /* # of Copies Passed */

    /* For Printer File Overrides. */
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10) /* Output Queue */
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4) /* Hold Status */
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4) /* Save Status */
             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0) /* # of Copies */
             DCL        VAR(&NQSTAT) TYPE(*CHAR) LEN(1) /* If MUST +
                          run in Night Q */
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10) /* Night Job Queue */

    /* Error Message Fields. */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512) /* Message */
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7) /* Message ID */
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/* MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                        */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/* R9995 - Build/Print A/R Misc Post Summary Report                 */
/* Build/Override WF A/R Detail file in QTEMP.                      */
/********************************************************************/
             CHKOBJ     OBJ(QTEMP/PMACCPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* Object Not Found */
             CRTDUPOBJ  OBJ(PMACCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMACCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMACCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMACCPL3) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMACCPL4) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO

             CLRPFM     FILE(QTEMP/PMACCPP)

             OVRDBF     FILE(PMACCPP) TOFILE(QTEMP/PMACCPP)
             OVRDBF     FILE(PMACCPL0) TOFILE(QTEMP/PMACCPL0)
             OVRDBF     FILE(PMACCPL1) TOFILE(QTEMP/PMACCPL1)
             OVRDBF     FILE(PMACCPL3) TOFILE(QTEMP/PMACCPL3)
             OVRDBF     FILE(PMACCPL4) TOFILE(QTEMP/PMACCPL4)

             IF         COND(&DEPDTE *NE ' ') THEN(DO)
    /* Build by Deposit Date for Posted Transactions. */
             CHGVAR     VAR(&DEPDTE2) VALUE(&DEPDTE)
             CALL       PGM(PUCCXFR) PARM(&RETURN &DEPDTE2)
             ENDDO
             ELSE       CMD(DO)
    /* Build for Available to Post Transactions; Prior to Posting. */
             CALL       PGM(PUB1XFR) PARM(&RETURN)
             ENDDO

    /* Print A/R Misc Post Summary Report. */
             OVRPRTF    FILE(PUB3PFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(PUB3PFR) PARM(&RETURN &DEPDTE2)

             DLTOVR     FILE(*ALL)


             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
