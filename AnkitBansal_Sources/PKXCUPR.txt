/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PKXCUPR                                       */
/*  PROGRAM NAME:    PRINT CARCASS SUMMARY &/OR DETAIL             */
/*  PROGRAMMER:      ROSE M. CENTONZE                                */
/*  CREATE DATE:     06/27/95                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH EXECUTE MESSAGE     */
/*                   FROM PMT PRT CRC SUM,DTL.                     */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  ROSE CENTONZE  10/18/95 ADD PARM FOR CARCASS SUMMARY FORMTYPE  */
/*                          AND CALL THE CORRECT PRTF FUNCTION     */
/*  ROSE CENTONZE  03/14/96 ADD PARM TO RUN PAYMENT TYPES LIVE AS  */
/*                          CARCASS.                               */
/*                                                                 */
/*  SUSAN MASON  06/27/96   CARCASS DETAIL SBDOVRPRTF CHANGE       */
/*                          TO PRINT ON  8 1/2 BY 11               */
/*                          BY LETTER YES IF USER WANTS            */
/*                          TO GO BACK TO LAZER THIS PARM          */
/*                          WILL BE IGNORED.                       */
/*                                                                 */
/*  ROSE CENTONZE 9/17/96   CARCASS DETAIL IS NOW CALLED FROM THE  */
/*                          RETRIEVE THAT CALLS THE CARC SUMM SO    */
/*                          THEY ARE PRINTED CONSECUTIVELY FOR A   */
/*                          TATTOO.  ONE OR BOTH ARE PRINTED PER   */
/*                          THE &DTLSUM PARM.                      */
/*                                                                 */
/*  SUSAN MASON  10/04/96   CRTDUPOBJ TO CRTDUPOBJL *LIBL          */
/*                                                                 */
/*  MARK ARENSBERG 2/27/97  ADDED THE IGNORE FAX Y/N OPTION        */
/*                          IF THE FLAG IS YES THEN DO NOT FAX     */
/*                          ANY CARCASS SUMMARYS OR DETAILS. IF    */
/*                          THE FLAG IS NO THEN FAX ANY CARCASS    */
/*                          SUMMARY OR DETAIL WERE THE PRODUCER    */
/*                          FAX STATUS IS YES.                     */
/*                                                                 */
/*  SUSAN MASON   04/28/97  ADD PAYMENT TYPE TO SELECTION          */
/*                                                                 */
/*  ROSE CENTONZE 10/16/97  CALL PKMWXFR TO BUILD THE FAX DRIVER   */
/*                        INSTEAD OF PKTAXFR TO BUILD THE TH DRIVER.*/
/*                        IF IGNFAX IS 'YES':                       */
/*                         CALL PKMOXFR TO PRINT INSTEAD OF        */
/*                         PKT2XFR.                                */
/*                        IF IGNFAX IS 'NO':                       */
/*                         CALL PKHWUPR TO FAX.                    */
/*                                                                 */
/*  ROSE CENTONZE 05/28/03  ADDED PARM FOR SORT LOSS PRINT        */
/*  SUSAN MASON   04/16/04  TFM - REPLACE PKMLPFR WITH PLEMPFR     */
/*  SUSAN MASON   12/17/07  FP1049 EMAIL PROCESSING                */
/*                          ADD NEW PARM IGNEMAIL                  */
/*                          ADD NEW FILES FOR FAX DRIVER           */
/*  SUSAN MASON   01/19/08  COPY TATTOO FAX DRIVER TO PRKFSAV      */
/*                          SO THAT IF THEY HAVE ISSUES A PROGRAMER*/
/*                          CAN REVIEW WHAT WAS SELECTED           */
/*  SUSAN MASON   01/20/08  ADD USER CLASS           *PGMR         */
/*  SUSAN MASON   05/15/08  ADD VARIABLE ON CRTFILE DUE TO         */
/*                          WHEN A PROGRAMMER RUNS A PRODUCTION    */
/*                          RUN A FILE WAS BEING SAVED FOR THE     */
/*                          PROGRAMMER TO VIEW.                    */
/*                          WAS GETTING HALT DUE TO PRKFSAV        */
/*                          HAD BEEN CLEARED OUT.                  */
/*  ROSE CENTONZE 04/24/17  COMMENT OVR TO PKTCPFR$-- DOESNT EXIST */
/*  JULIUS HIGH   05/09/18  Add MONMSG CPF2817 after CPYF          */
/*                          FROMFILE(PKD7CPP) TOFILE(PRKFSAV/PKD7CPP) */
/*                          SNDMSG NO RECORDS SELECTED FOR PRINT   */
/*                          IF NO CARCASS SUMMARY/DETAIL TO PRINT  */
/*******************************************************************/
             PGM        PARM(&RTN &PDEV &HOLD &SAVE &XCOPIES &XCOMP +
                          &XTTT &XVCDT &PRDCR &SRTYP &DTLSUM &PMTYP +
                          &FORM &LIVCRC &IGNFAX &SORT &IGNEMAIL &APP)
/* PRINT OPTION PARMS                                             */
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDEV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)

/* FUNCTION SPECIFIC PARMS                                        */
             DCL        VAR(&XCOMP) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XVCDT) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XTTT) TYPE(*CHAR) LEN(5)
             DCL        VAR(&PRDCR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SRTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DTLSUM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PMTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FORM) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LIVCRC) TYPE(*CHAR) LEN(1)
             DCL        VAR(&APP) TYPE(*CHAR) LEN(33)
             DCL        VAR(&LPP) TYPE(*CHAR) LEN(40)
             DCL        VAR(&IGNFAX) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SORT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IGNEMAIL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&NODATA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)

/* NUMERIC VARIABLES                                             */
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&VDTE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&TTT) TYPE(*DEC) LEN(5 0)

/* SET POST_OR_SELECT_RPTS_STATUS TO = SELECT FROM PROMPT */
             DCL        VAR(&POST) TYPE(*CHAR) LEN(1) VALUE(S)
/* SET VALUE ON A CREATE OF FILE OR NOT TO CREATE FILE    */
             DCL        VAR(&CRTFILE) TYPE(*CHAR) LEN(10) VALUE(*NO)

/* SBDPRTOVR VARIABLE                                          */
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

/* USER CLASS                                                  */
             DCL        VAR(&USRCLS) TYPE(*CHAR) LEN(10)
/* PROFILE ONLY FOR PRKTEST                                    */
             DCL        VAR(&PRFNME) TYPE(*CHAR) LEN(10)
/* CHANGE ALPHA PARMS TO NUMERIC VARIABLES                       */
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)
             CHGVAR     VAR(&COMP) VALUE(&XCOMP)
             CHGVAR     VAR(&VDTE) VALUE(&XVCDT)
             CHGVAR     VAR(&TTT) VALUE(&XTTT)
             CHGVAR     VAR(&LPP) VALUE(&APP)

             RTVUSRPRF  USRCLS(&USRCLS)
             RTVJOBA    USER(&PRFNME)

/* CREATE TATTOO HEADER DRIVER FILE IN QTEMP                    */
             CRTDUPOBJL OBJ(PKD7CPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL3) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL4) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL5) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL6) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL7) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
/* ADD 122607 SLM                                                  */
             CRTDUPOBJL OBJ(PKD7CPL8) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL OBJ(PKD7CPL9) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)

/* BUILD TATTOO FAX DRIVER FILE      10/16/97               */
/* ADD 122607 SLM   IGNEMAIL AS A PARM                             */
             CALL       PGM(PKMWXFR) PARM(' ' &COMP &TTT &VDTE +
                          &SRTYP &PRDCR &DTLSUM &PMTYP &IGNFAX +
                          &SORT &IGNEMAIL)

/* SAVE THE TATTOO FAX DRIVER FOR A PROGRAMMER WHO IS TESTING */
/* SLM 07/01/08 DO NOT DO THIS ON PRKTEST */
             IF         COND(&PRFNME *NE 'PRKTEST') THEN(DO)
/* CAN LOOK AT THE DATA THAT WAS JUST CREATED                 */
             IF         COND(&USRCLS *EQ '*PGMR') THEN(DO)
/* SLM 05/15/08  */
             CHKOBJ     OBJ(PRKFSAV/PKD7CPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&CRTFILE) +
                          VALUE(*YES))
             CPYF       FROMFILE(PKD7CPP) TOFILE(PRKFSAV/PKD7CPP) +
                          MBROPT(*REPLACE) CRTFILE(&CRTFILE) +
                          FMTOPT(*MAP *DROP)

             MONMSG     MSGID(CPF2817)

/* SLM 05/15/08  */
             ENDDO
/* SLM 07/01/08  */
             ENDDO
/*******************************************************************/
/*     PRINT CARCASS SUMMARY AND/OR DETAIL PER THE &DTLSUM PARM    */
/*******************************************************************/


FORMSUM:     IF         COND(&FORM *EQ 'PRD') THEN(DO)
             OVRPRTF    FILE(PKW9PFR$) PAGESIZE(*N 96) CPI(12) +
                          OUTQ(&PDEV) COPIES(&COPIES) HOLD(&HOLD) +
                          SAVE(&SAVE) FORMTYPE(CARCSUM)

CRDTL:  /*   SBDPRTOVR  CMDVAR(&CMD) FILE(PKTCPFR$) OUTQ(&PDEV) +
                            COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE) +
                            LETTER(*YES)                           +
               CALL       PGM(QCMDEXC) PARM(&CMD 512)             */

/* PRINT CRC SUM ON FORM AND CARC DTL FROM TTT HEADER DRIVER       */

             CALL       PGM(PKXDXFR) PARM(' ' &LIVCRC &LPP)

             ENDDO

STDSUM:      IF         COND(&FORM *NE 'PRD') THEN(DO)

/* IF IGNFAX IS YES, PRINT CARC SUM/DTL                */
 PRT:        IF         COND(&IGNFAX *EQ 'Y')   THEN(DO)
             IF         COND(&IGNEMAIL *EQ 'Y') THEN(DO)
             SBDPRTOVR  CMDVAR(&CMD) FILE(PLEMPFR$) OUTQ(&PDEV) +
                            COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE) +
                            LETTER(*YES)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(PKMOXFR) PARM(' ' &POST &LIVCRC &SORT +
                          &LPP &NODATA)

             IF         COND(&NODATA *EQ 'Y') THEN(DO)
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('*** There were NO Carcass +
                          Summary/Detail records selected to print. +
                          ***') TOUSR(&USER)
             ENDDO
             ENDDO
             ENDDO

/* IF IGNFAX IS NO , FAX CARC SUM/DTL                */
 FAX:        IF         COND(&IGNFAX *EQ 'N')  THEN(DO)
             CALL       PGM(PKHWUPR) PARM(' ' &POST &LIVCRC &SORT &LPP)
             GOTO       ENDTAG
             ENDDO
/* IF IGNEMAIL IS NO, FAX CARC SUM/DTL               */
EMAIL:       IF         COND(&IGNEMAIL *EQ 'N') THEN(DO)
             CALL       PGM(PKHWUPR) PARM(' ' &POST &LIVCRC &SORT &LPP)
             GOTO       ENDTAG
             ENDDO
ENDTAG:
             ENDDO


END:         DLTOVR *ALL

/**********  DMPCLPGM   ************/

             ENDPGM
