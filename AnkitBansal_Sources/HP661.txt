      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP661
      * TITLE:       LISTING OF FEED MANUFACTURING TYPE COSTS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     4/14/94
      *
      * FUNCTION: BATCH PROGRAM SUBMITTED FROM SPECIFY FEED TYPE MANU-
      *           FACTURING COSTS TO MAINTAIN (HP461).  THE
      *           COSTS WILL BE PRINTED FOR A SPECIFIC FEED MILL
      *           BUSINESS OFFICE AND EFFECTIVE DATE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 12/09/94  LEANNE FEDOR
      *           RECOMPILE ONLY. FIELD SIZE OF FEED TYPE WAS CHANGED
      *           FROM 5A TO 1A.
      *
      * 06/18/97  LEANNE FEDOR
      *           REPLACED ENTRY PARM LOGIC WITH LDA VALUES.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE                (KEY: BOBOBO)
      *
     FHSP024    IF   E           K DISK
      *    FEED TYPE                      (KEY: FTFTCD)
      *
     FHSL030A   IF   E           K DISK
      *    FEED TYPE MANUFACTURING COSTS  (KEY: MCFMBO MCMCDT MCFTCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDMCDT                 1      8  0
     D  LDMDY                  9     14  0
     D  LDFMBO                15     19
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PRINT HEADINGS FOR FIRST PAGE AND THEN
      * READ/PROCESS ALL FEED COST RECORDS FOR SELECTION CRITERIA
      *
     C     K1P030        SETLL     HSL030A
     C                   EXCEPT    HDRPT
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     K1P030        READE     HSL030A                                92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE FEED TYPE DESCRIPTION FROM FEED TYPE MASTER.
      *
     C     MCFTCD        CHAIN     HSP024                             93
     C     *IN93         IFEQ      *OFF
     C                   MOVEL     FTFTDS        WKFTDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WKFTDS
     C                   ENDIF
      *
      * TEST FOR OVERFLOW AND PRINT HEADINGS IF ON
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
      * PRINT DETAIL LINE
      *
     C                   EXCEPT    DTCOST
     C                   MOVE      YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * PRINT STANDARD 'NO DATA...' ERROR MESSAGE IF NO DATA MET THE
      * USER'S SELECTION CRITERIA
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR FEED MANUFACTURING COST FILE
      *
     C     K1P030        KLIST
     C                   KFLD                    LDFMBO
     C                   KFLD                    LDMCDT
      *
     C     *LIKE         DEFINE    BOBODS        WKBODS
     C     *LIKE         DEFINE    FTFTDS        WKFTDS
      *
      * CHAIN TO BUSINESS OFFICE FILE FOR DESCRIPTION IN HEADING
      *
     C     LDFMBO        CHAIN     HSP009                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     BOBODS        WKBODS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WKBODS
     C                   ENDIF
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP DASHED LINE THAT APPEARS UNDER HEADINGS
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVE      NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HDRPT          1 01
     O                       SDPGM               10
     O                                           49 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           42 'LISTING OF FEED MANUFACT'
     O                                           58 'URING TYPE COSTS'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HDRPT          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HDRPT          1
     O                                           16 'BUSINESS OFFICE:'
     O                       LDFMBO              23
     O                       BOBODS              55
      *
     O          E            HDRPT          2
     O                                           15 'EFFECTIVE DATE:'
     O                       LDMDY         Y     26
      *
     O          E            HDRPT          1
     O                                            9 'FEED TYPE'
     O                                           25 'DESCRIPTION'
     O                                           44 'COST PER POUND'
      *
     O          E            HDRPT          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FEED TYPE MANUFACTURING COST DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTCOST      1
     O                       MCFTCD               5
     O                       WKFTDS              24
     O                       MCFLRT        K     40
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /EJECT
