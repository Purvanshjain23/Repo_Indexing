/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF4140CL                                      */
/*  PROGRAM NAME:    SPECIFY CASH DISTRIBUTION BALANCE ADJUSTMENT  */
/*                   LISTING OPTIONS                               */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     11/06/06                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                     */
/* 05/07/09  LEANNE RAMSEY                                             */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.                */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(66)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFDT)    TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTDT)    TYPE(*DEC) LEN(8)
             DCL        VAR(&LDRCEXFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDDTFL)   TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDFDT) VALUE(%SST(*LDA  7 8))
             CHGVAR     VAR(&LDTDT) VALUE(%SST(*LDA 21 8))
             CHGVAR     VAR(&LDRCEXFL) VALUE(%SST(*LDA 35 1))
             CHGVAR     VAR(&LDDTFL)   VALUE(%SST(*LDA 36 1))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE ADJUSTMENTS FILE                          */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* FROM DATE                                                            */
             IF         COND(&LDDTFL *EQ 'W')  THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BAWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFDT)
             ENDDO

             IF         COND(&LDDTFL *EQ 'A')  THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BAAJDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFDT)
             ENDDO

/* TO DATE                                                              */
             IF         COND(&LDDTFL *EQ 'W')  THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND BAWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTDT)
             ENDDO

             IF         COND(&LDDTFL *EQ 'A')  THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND BAAJDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTDT)
             ENDDO

/* IF RECORD EXEMPT FLAG                                               */

             IF         COND(&LDRCEXFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 19)) VALUE('*AND +
                          BARCEXFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 65 1)) VALUE(&LDRCEXFL)
             CHGVAR     VAR(%SST(&QDATA 66 1)) VALUE('"')
             ENDDO

/* OVERRIDE PRINT FILE                                                */

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* WHEN USER ELECTED TO EXTRACT DATA BASED ON WEEKENDING DATE           */

             IF         COND(&LDDTFL *EQ 'W')  THEN(DO)
             OVRDBF     FILE(TFL023B) SHARE(*YES)
             OPNQRYF    FILE((TFL023B)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)
             CALL       PGM(TF604)
             CLOF       OPNID(TFL023B)
             DLTOVR     FILE(TFL023B)
             ENDDO

/* WHEN USER ELECTED TO EXTRACT DATA BASED ON ADJUSTMENT DATE           */

             IF         COND(&LDDTFL *EQ 'A')  THEN(DO)
             OVRDBF     FILE(TFL023C) SHARE(*YES)
             OPNQRYF    FILE((TFL023C)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)
             CALL       PGM(TF604)
             CLOF       OPNID(TFL023C)
             DLTOVR     FILE(TFL023C)
             ENDDO

/*---------------------------------------------------------------------*/
/* END OF PROCESSING                                                   */
/*---------------------------------------------------------------------*/

             DLTOVR     FILE(*ALL)

             ENDPGM

