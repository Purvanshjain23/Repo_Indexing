     A*%%TS  SD  20111221  105111  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4074
     A*  TITLE:         SPECIFY COMMUTER HERD MOVEMENT LOG OPTIONS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          12/21/11
     A*****************************************************************
     A*  MODIFICATIONS
     A*  10/19/2016     LARA BUSER   E7606
     A*  DESCRIPTION:   ADD A DATE TYPE SELECTOR FLAG AND DESCRIPTION
     A*                 PLACE AFTER BUSINESS OFFICE AND MOVE EVERYTHING
     A*                 ELSE DOWN. CHANGE DATE DESCRIPTIONS TO JUST
     A*                 SAY 'FROM DATE' OR 'TO DATE'
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP4074R1
     A*%%TS  SD  20111221  105111  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(48)
     A                                      SETOF(51)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 18'SPECIFY COMMUTER HERD MOVEMENT LOG-
     A                                       OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ................'
     A                                  4 35'?'
     A            DFFSBO    R        B  4 37REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 46REFFLD(BOBODS)
     A*
     A                                  5  2'Select Date Type ...............'
     A            DFDTSEL   R        B  5 37REFFLD(@@FLAG)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  5 46'R=Received, S=Scheduled'
     A                                  6  5'From Date ...................'
     A            DFFMDY    R     Y  B  6 37REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  5'To Date .....................'
     A            DFTMDY    R     Y  B  7 37REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  8  2'Transfers across a state line ..'
     A                                  8 35'?'
     A            DFASLFL   R        B  8 37REFFLD(@@FLAG)
     A N48                                  DSPATR(HI)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFASLDS   R        O  8 46REFFLD(@@DS30)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP4074K1
     A*%%TS  SD  19941101  163520  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP4074ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP4074EC                  SFLCTL(HP4074ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
