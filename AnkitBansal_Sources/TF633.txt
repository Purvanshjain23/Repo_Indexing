      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF633
      * TITLE:       Listing of Invoices
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/16/05
      *
      * FUNCTION:  Batch program to print a record(s) in the Invoice file with the
      *            associated Invoice Charges.
      *
      *            This listing is submitted from W/W Invoices when the user takes:
      *                1) F7=Listing
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp005    if   e           k disk
      *    Charge codes
      *
      *
     Ftfp006    if   e           k disk
      *    System modules
      *
      *
     Ftfp020    if   e           k disk
      *    Invoice header
      *
      *
     Ftfl021c   if   e           k disk
      *    Invoice charges
      *
      *
     Ftfp097    if   e           k disk
      *    Status codes
      *
      *
     Ftfp098    if   e           k disk
      *    Charge frequency codes
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D firstchg        s              1    inz('Y')
      *
     D smtotfl         s              1    inz('Y')
     D inctotfl        s              1    inz('Y')
      *
     D countsm         s              3  0
     D countinc        s              3  0
      *
     D countinv        s              3  0
     D svinfl          s                   like(ccinfl)
     D svsmcd          s                   like(ccsmcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      *
      * Print fields
      *
     D h1cfqds         s                   like(cfcfqds)
     D h1stds          s                   like(ststds)
     D h1ynds          s              3
      *
     D l1indtmdy       s              6  0
     D l1podtmdy       s              6  0
     D l1dudtmdy       s              6  0
      *
     D l2icdtmdy       s              6  0
     D l2ucfl          s                   like(ccucfl)
     D l2cfqcd         s                   like(cccfqcd)
      *
     D t1inclinv       s             13
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D aram            s             11  2 dim(4)
     D arunit          s             11  4 dim(4)
      *
      *
      * Array index
      *
     D x               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldtask                 1      1
     D  ldinsn                 2      8  0
     D  ldynfl                 9      9
     D  ldfmdy                10     15  0
     D  ldfcymd               16     23  0
     D  ldtmdy                24     29  0
     D  ldtcymd               30     37  0
     D  ldinstcd              38     38
     D  ldcfqcd               39     39
     D  ldpmdy                40     45  0
     D  ldpcymd               46     53  0
     D  lddmdy                54     59  0
     D  lddcymd               60     67  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read each record selected by the open query.
      *
     C     *loval        setll     tfp020
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp020                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      *
      * Flip Invoice Dates and Print Invoice Header
      *
     C                   exsr      $invdate
     C                   exsr      $l1dtl
      *
      * Retrieve/print Invoice Charges
      *
     C                   exsr      $charges
      *
      * If there were no charges, print a blank line; otherwise, print
      * totals for the invoice.
      *
     C                   if        firstchg = yes
     C                   except    blank
     C                   else
     C                   exsr      $smtot
     C                   exsr      $inctot
     C                   exsr      $invtot
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
      * Print total lines:
      *  1) For report
      *
     C                   exsr      $rpttot
     C                   endsl
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print Invoice Header line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Invoice Charge Detail Line
      *---------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   except    l2dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/Print Invoice Charges
      *---------------------------------------------------------------
      *
     C     $charges      begsr
      *
     C                   move      yes           firstchg
     C                   z-add     0             countsm
     C                   z-add     0             countinc
     C                   z-add     0             countinv
      *
     C     ihinsn        setll     tfl021c
     C                   dou       *in91 = *on                                  Do charges
     C     ihinsn        reade     tfl021c                                91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Control break
     C                   select
     C                   when      firstchg = yes
     C                   move      no            firstchg
     C                   move      icinfl        svinfl
     C                   move      icsmcd        svsmcd
      *
     C                   when      icinfl <> svinfl
     C                   exsr      $smtot
     C                   exsr      $inctot
     C                   except    blank
      *
     C                   when      icsmcd <> svsmcd
     C                   exsr      $smtot
     C                   except    blank
     C                   endsl
      *
      *
      * Retrieve Charge Code info
      *
     C                   exsr      $cccd
      *
      * Flip Invoice Charge Date from CCYYMMDD to MMDDYY
      *
     C                   if        icicdt <> 0
     C     *iso          test(d)                 icicdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      icicdt        wkisodate
     C     *mdy          move      wkisodate     l2icdtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Detail processing:
      *   1) accumulate totals into arrays
      *
     C                   add       icicam        aram
     C                   add       icunit        arunit
      *
      * Print line
     C                   exsr      $l2dtl
      *
     C                   add       1             countsm
     C                   add       1             countinc
     C                   add       1             countinv
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do charges
      *
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Charge Code Info
      *---------------------------------------------------------------
      *
     C     $cccd         begsr
      *
     C     iccccd        chain     tfp005                             92
     C                   if        *in92 = *off
     C                   move      ccucfl        l2ucfl
     C                   move      cccfqcd       l2cfqcd
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Flip Invoice Dates
      *---------------------------------------------------------------
      *
     C     $invdate      begsr
      *
      * Flip Invoice Date from CCYYMMDD to MMDDYY
      *
     C                   if        ihindt <> 0
     C     *iso          test(d)                 ihindt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihindt        wkisodate
     C     *mdy          move      wkisodate     l1indtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip Posted Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        ihpodt <> 0
     C     *iso          test(d)                 ihpodt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihpodt        wkisodate
     C     *mdy          move      wkisodate     l1podtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip Due Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        ihdudt <> 0
     C     *iso          test(d)                 ihdudt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihdudt        wkisodate
     C     *mdy          move      wkisodate     l1dudtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Total Line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Invoice Total Line
      *---------------------------------------------------------------
      *
     C     $invtot       begsr
      *
     C                   z-add     3             x
      *
     C                   if        countinv = 1                                 If only 1
     C                   z-add     0             aram(x)
     C                   z-add     0             arunit(x)
     C                   else
      *
      * If you did not print a System Module Total or an Include in Invoice
      * Total, then you need to print underscores.
      *
     C                   if        smtotfl = no and inctotfl = no
     C                   except    t1under
     C                   endif
      *
     C                   seton                                        80
     C                   exsr      $t1tot
     C                   setoff                                       80
     C                   endif                                                  If only 1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Include in Invoice Total Line
      *---------------------------------------------------------------
      *
     C     $inctot       begsr
      *
     C                   z-add     2             x
      *
     C                   if        countinc = 1                                 If only 1
     C                   move      no            inctotfl
     C                   z-add     0             aram(x)
     C                   z-add     0             arunit(x)
     C                   else
      *
     C                   move      yes           inctotfl
      *
      * If you did not print a System Module Total, you need to print
      * underscores.
      *
     C                   if        smtotfl = no
     C                   except    t1under
     C                   endif
      *
     C                   if        svinfl = yes
     C                   eval      t1inclinv = 'Incl Inv-Yes:'
     C                   else
     C                   eval      t1inclinv = ' Incl Inv-No:'
     C                   endif
      *
     C                   seton                                        87
     C                   exsr      $t1tot
     C                   setoff                                       87
     C                   endif                                                  If only 1
      *
     C                   z-add     0             countinc
     C                   move      icinfl        svinfl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print System Module total line
      *---------------------------------------------------------------
      *
     C     $smtot        begsr
      *
     C                   z-add     1             x
      *
     C                   if        countsm = 1                                  If only 1
     C                   z-add     0             aram(x)
     C                   z-add     0             arunit(x)
     C                   move      no            smtotfl
     C                   else
      *
     C                   move      yes           smtotfl
      *
      * Print underscores, then total line.
      *
     C                   except    t1under
     C                   seton                                        86
     C                   exsr      $t1tot
     C                   setoff                                       86
     C                   endif                                                  If only 1
      *
     C                   z-add     0             countsm
     C                   move      icsmcd        svsmcd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Report Total line
      *---------------------------------------------------------------
      *
     C     $rpttot       begsr
      *
     C                   z-add     4             x
      *
     C                   if        aram(x) <> 0 or arunit(x) <> 0               If anything
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   seton                                        79
     C                   except    blank
     C                   exsr      $t1tot
     C                   endif                                                  If anything
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Set up heading selections:
      *
      * Include Posted Invoices flag
      *
     C                   select
     C                   when      ldynfl = yes
     C                   eval      h1ynds = 'Yes'
     C                   other
     C                   eval      h1ynds = 'No '
     C                   endsl
      *
      * From date
     C                   if        ldfmdy = 0
     C                   seton                                        88
     C                   endif
      *
      * To date
     C                   if        ldtmdy = 0
     C                   seton                                        89
     C                   endif
      *
      * Due date
     C                   if        lddmdy = 0
     C                   seton                                        83
     C                   endif
      *
      * Posted date
     C                   if        ldpmdy = 0
     C                   seton                                        85
     C                   endif
      *
      * Invoice number
     C                   if        ldinsn = 0
     C                   seton                                        82
     C                   endif
      *
      *
      * Invoice Status
     C                   if        ldinstcd = *blank
     C                   seton                                        81
     C                   else
      *
     C     ldinstcd      chain     tfp097                             92
     C                   if        *in92 = *off
     C                   move      ststds        h1stds
     C                   endif
     C                   endif
      *
      * Charge Frequency Code
      *
     C                   if        ldcfqcd = *blank
     C                   seton                                        84
     C                   else
      *
     C     ldcfqcd       chain     tfp098                             92
     C                   if        *in92 = *off
     C                   move      cfcfqds       h1cfqds
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           70 'LISTING OF INVOICES'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          122 'PAGE'
     O                       page          z    132
      *
     O          e            h1hdr          1
     O                                           26 'Include posted invoices ..'
     O                       h1ynds              31
      *
     O                                           60 'Due date .....:'
     O              n83      lddmdy              70 '  /  /  '
     O               83                          65 'All'
      *
     O                                           99 'Invoice status code .:'
     O               81                         104 'All'
     O              n81      ldinstcd           102
     O              n81      h1stds             114
      *
      *
     O          e            h1hdr          1
     O                                           26 'From invoice date ........'
     O              n88      ldfmdy              36 '  /  /  '
     O               88                          31 'All'
      *
     O                                           60 'Posted date ..:'
     O              n85      ldpmdy              70 '  /  /  '
     O               85                          65 'All'
      *
     O                                           99 'Frequency code ......:'
     O               84                         104 'All'
     O              n84      ldcfqcd            102
     O              n84      h1cfqds            114
      *
     O          e            h1hdr          2
     O                                           26 'To invoice date ..........'
     O              n89      ldtmdy              36 '  /  /  '
     O               89                          31 'All'
      *
     O                                           60 'Invoice number:'
     O               82                          65 'All'
     O              n82      ldinsn        z     69
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           57 'Invoice'
     O                                           65 'Includ'
     O                                           92 'Invoice'
     O                                          107 'Invoice'
     O                                          132 'User'
      *
     O          e            h1hdr       0  1
     O                                            7 'Invoice'
     O                                           12 'Inv'
     O                                           23 'Invoice'
     O                                           30 'Due'
     O                                           42 'Posted'
     O                                           49 'Freq'
     O                                           57 'Charge'
     O                                           65 'In Inv'
     O                                           70 'Sys'
     O                                           78 'Charge'
     O                                           91 'Charge'
     O                                          107 'Charge'
     O                                          132 'Cont'
      *
     O          e            h1hdr       0  1
     O                                            7 'Number'
     O                                           13 'Stat'
     O                                           22 'Date'
     O                                           31 'Date'
     O                                           41 'Date'
     O                                           49 'Code'
     O                                           57 'Number'
     O                                           65 'Total'
     O                                           70 'Mod'
     O                                           76 'Code'
     O                                           90 'Date'
     O                                          107 'Amount'
     O                                          125 'Units'
     O                                          132 'Flag'
      *
     O          e            h1hdr          0
     O                       dash               132
      *
      *
      *-------------------------------------------------------------
      * Detail line for Invoice Header
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  1
     O                       ihinsn        z      7
     O                       ihinstcd            11
     O                       l1indtmdy      b    23 '  /  /  '
     O                       l1dudtmdy      b    33 '  /  /  '
     O                       l1podtmdy      b    43 '  /  /  '
     O                       ihcfqcd        b    47
      *
      *-------------------------------------------------------------
      * Detail line for Invoice Charges
      *-------------------------------------------------------------
      *
     O          e            l2dtl          1
     O                       iciccom             43
     O                       l2cfqcd        b    47
     O                       icicsn        z     57
     O                       icinfl              62
     O                       icsmcd              69
     O                       iccccd              82
     O                       l2icdtmdy      b    92 '  /  /  '
     O                       icicam        k    108
     O                       icunit        k    126
     O                       l2ucfl         b   131
      *
      *
      *
      *-------------------------------------------------------------
      * Total lines
      *-------------------------------------------------------------
      *
     O          e            t1under     0  1
     O              n88                         107 '--------------'
     O              n88                         125 '--------------'
      *
     O          e            t1tot       0  1
     O               86      svsmcd              83
     O               86                          92 'Sys Mod:'
      *
     O               87      t1inclinv      b    92
      *
     O               80                         129 '**'
     O               80                          92 'Invoice:'
     O               79                          92 'Report Total:'
     O                       aram(x)       kb   108
     O                       arunit(x)     kb   126
      *
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     o          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
