      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP394
      * TITLE:       BUILD WORKFILES--PRODUCTION ANALYSIS REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     01/30/95
      *
      *  FUNCTION:   THIS PROGRAM BUILDS TWO WORKFILES THAT WILL BE
      *              USED IN HP694 TO PRINT THE PRODUCTION ANALYSIS
      *              REPORT. USER SELECTIONS ARE PASSED THROUGH THE
      *              LDA FROM PROGRAM HP494 WHICH ALSO SUBMITS THE
      *              CLP HP494CL THROUGH QCMDEXC.
      *
      *              THIS PROGRAM FIRST CREATES WORKFILE HSP393
      *              WHICH SIMPLY ASSOCIATES EACH GROUP SELECTED
      *              BY THE OPEN QUERY WITH THE PRODUCTION
      *              ANALYSIS FACTOR SELECTED BY THE USER.  THE
      *              REST OF THE PROCESSING USES THIS WORKFILE AS
      *              THE PRIMARY INPUT FILE FOR EXTRACTING DATA
      *              AND ACCUMULATING IT BY ANALYSIS FACTOR.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/14/95  LEANNE FEDOR
      *           ADDED LOGIC FOR THE TWO NEW PRODUCTION ANALYSIS
      *           FACTORS: ORIGIN BGF FARM SITE AND ORIGIN NURSERY
      *           FARM SITE.
      *
      * 04/13/00  LeAnne Fedor
      *           Recompile only. The 'closed group pounds' field in the
      *           Closed Hog Group file was increased to 11,2.
      *
      * 08/17/00  LeAnne Fedor
      *           Added logic to populate the new 'adjusted transferred in' fields.
      *           Also, changed mortality % calc to use 'adjusted transferred in' head
      *           instead of the original 'transferred in' head.
      *
      * 05/02/01  LeAnne Fedor
      *           Changed logic that calculates 'average pounds placed' to used
      *           the 'adjusted' transferred in head and pounds.
      *
      * 04/04/02  LeAnne Fedor
      *           Per Jeff Sherboundy, removed all special logic for FHAUL/CHAUL.
      *           They will now be treated as the 'expenses' that they truly are.
      *
      * 08/26/02  LeAnne Fedor
      *           Added 'group type' as a new Production Analysis Factor.
      *
      * 01/29/03  LeAnne Fedor
      *           Removed all hardcoded special logic for SHAUL.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL006B   IF   E           K DISK
      *    REPORTING CODES
      *
     FHSP033    IF   E           K DISK
      *    CLOSED GROUP SUMMARY
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP
      *    (RECORDS SELECTED BY OPEN QUERY)
      *
     FHSP039    IF   E           K DISK
      *    CLOSED GROUP EQUIPMENT
      *
     FHSP044    IF   E           K DISK
      *    PREMUIM/DISCOUNT
      *
     FHSP045    IF   E           K DISK
      *    DEDUCTIONS/ADDONS
      *
     FHSP393    UF A E           K DISK
      *    WORKFILE OF GROUP
      *
     FHSP394    O  A E             DISK
      *    WORKFILE OF ANALYSIS FACTOR TOTALS
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDPACD                32     36
     D  LDFMDY                37     42  0
     D  LDTMDY                43     48  0
     D  LDICFL                49     49
     D  LDETFL                50     50
      *
     D  LDPPDS                51     80
     D  LDPADS                81    100
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D GF              C                   CONST('GF   ')
      *
      * REPORT CODES
      *
     D FPS             C                   CONST('FPS  ')
     D COS             C                   CONST('COS  ')
     D BTCAR           C                   CONST('BTCAR')
     D BTLIV           C                   CONST('BTLIV')
     D WGAIN           C                   CONST('WGAIN')
     D TRANA           C                   CONST('TRANA')
     D TRANI           C                   CONST('TRANI')
     D TRANO           C                   CONST('TRANO')
     D FCONS           C                   CONST('FCONS')
     D DEAD            C                   CONST('DEAD ')
     D INADJ           C                   CONST('INADJ')
     D CULLS           C                   CONST('CULLS')
     D CYIEL           C                   CONST('CYIEL')
     D BFIN            C                   CONST('BFIN ')
     D LEAN            C                   CONST('LEAN ')
     D PDAYS           C                   CONST('PDAYS')
     D FCAL            C                   CONST('FCAL ')
     D FPRO            C                   CONST('FPRO ')
      *
      * REPORT CATEGORIES
      *
     D OPCST           C                   CONST('OPCST')
     D OTCST           C                   CONST('OTCST')
     D SALES           C                   CONST('SALES')
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * STEP ONE:
      *   CREATE A WORKFILE RECORD FOR EACH HOG GROUP SELECTED
      *   BY THE OPEN QUERY.  ASSOCIATE EACH GROUP WITH THE
      *   ANALYSIS FACTOR SELECTED BY THE USER.
      *
     C                   EXSR      $GROUP
      *
      * STEP TWO:
      * PROCESS ALL HOG GROUPS IN THE WORKFILE JUST CREATED
      *
     C                   SETOFF                                       99
     C     *LOVAL        SETLL     HSP393
      *
     C     *IN99         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP393                                 99
     C     *IN99         IFEQ      *OFF                                         IF NOT EOF
      *
     C     WGHGCD        CHAIN     HSP034                             97
     C     *IN97         IFEQ      *OFF                                         IF GROUP HIT
      *
     C     FIRST         IFEQ      YES
     C                   MOVEL     NO            FIRST
     C                   MOVEL     WGSORT        SVSORT
     C                   ENDIF
      *
      * IF THERE IS A CHANGE IN THE ANALYSIS FACTOR,
      *    WRITE FACTOR TOTAL RECORD TO WORKFILE
      *    SAVE CONTROL VALUE
      *    CLEAR WORKFIELDS AND ACCUMULATORS FOR THE FACTOR
      *
     C     WGSORT        IFNE      SVSORT                                       IF FACTOR
     C                   EXSR      $WFTOT
     C                   MOVEL     WGSORT        SVSORT
     C                   EXSR      $CLRAF
     C                   ENDIF                                                  IF FACTOR
      *
      * SETUP AND FORMAT FIELDS FOR THE HOG GROUP RECORD
      *
     C                   EXSR      $SETUP
      *
      * RETRIEVE THE FOLLOWING INFORMATION FOR THE HOG GROUP.
      *    MOVEMENTS
      *    PERFORMANCE
      *    SALES
      *    COSTS (INCLUDING CULL SALES AS COST)
      *    OTHER COSTS
      *
     C                   EXSR      $MOVE
     C                   EXSR      $PERF
     C                   EXSR      $SALE
     C                   EXSR      $COST
     C                   EXSR      $OTCST
      *
      * UPDATE THE GROUP RECORD IN THE WORKFILE AND THEN
      * CLEAR WORKFIELDS AND ALSO FIELDS IN THE GROUP WORKFILE
      *
     C                   UPDATE    WGREC
     C                   EXSR      $CLRHG
      *
     C                   ENDIF                                                  IF GROUP HIT
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *
     C     FIRST         IFEQ      NO
     C                   EXSR      $WFTOT
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * WRITE A RECORD TO THE WORKFILE FOR EACH GROUP
      *---------------------------------------------------------------
      *
     C     $GROUP        BEGSR
      *
     C                   SETOFF                                       98
     C     *LOVAL        SETLL     HSP034
      *
     C     *IN98         DOUEQ     *ON                                          DO GROUP
     C                   READ      HSP034                                 98
     C     *IN98         IFEQ      *OFF                                         IF NOT EOF
     C                   Z-ADD     HGHGSN        WGHGSN
     C                   MOVEL     HGHGCD        WGHGCD
      *
     C     LDETFL        IFEQ      NO                                           IF NOT EQUIP
     C                   SELECT
      *
     C     LDPACD        WHENEQ    'BREED'
     C                   MOVEL     HGBSCD        WGSORT
      *
     C     LDPACD        WHENEQ    'COM  '
     C                   MOVEL     HGCRCD        WGSORT
      *
     C     LDPACD        WHENEQ    'FARM '
     C                   MOVE      HGFSCD        WGSORT
      *
     C     LDPACD        WHENEQ    'GEN  '
     C                   MOVEL     HGGNCD        WGSORT
      *
     C     LDPACD        WHENEQ    'OBGF '
     C                   MOVE      HGOBFS        WGSORT
      *
     C     LDPACD        WHENEQ    'ONUR '
     C                   MOVE      HGONFS        WGSORT
      *
     C     LDPACD        WHENEQ    'SEX  '
     C                   MOVEL     HGSXCD        WGSORT
      *
     C     LDPACD        WHENEQ    'GTYPE'
     C                   MOVEL     HGGTCD        WGSORT
     C                   ENDSL
     C                   ELSE
      *
      * ANALYSIS FACTOR IS EQUIPMENT TYPE, SO RETRIEVE
      * THE EQUIPMENT CODE FOR THE GROUP FOR THE SELECTED
      * EQUIPMENT TYPE AND USE IT AS THE ANALYSIS FACTOR.
      *
     C     K2P039        CHAIN     HSP039                             97
     C     *IN97         IFEQ      *OFF
     C                   MOVEL     CEEQCD        WGSORT
     C                   ENDIF
     C                   ENDIF                                                  IF NOT EQUIP
      *
     C                   WRITE     WGREC
     C                   CLEAR                   WGREC
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO GROUP
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * SETUP AND FORMAT FIELDS FOR THE HOG GROUP DETAIL RECORD
      ****************************************************************
      *
     C     $SETUP        BEGSR
      *
      * MOVE GROUPS FIELDS TO THE GROUP WORKFILE RECORD
      * AND, SET UP A KEY FIELD FOR THE HOG GROUP
      *
     C                   MOVEL     HGPPCD        WGPPCD
     C                   MOVEL     HGFSBO        WGFSBO
     C                   Z-ADD     HGFSCD        WGFSCD
     C                   Z-ADD     HGOBFS        WGOBFS
     C                   Z-ADD     HGONFS        WGONFS
     C                   MOVEL     HGHGCD        WGHGCD
     C                   MOVEL     HGSXCD        WGSXCD
     C                   MOVEL     HGGNCD        WGGNCD
     C                   Z-ADD     HGCLDT        WGCLDT
     C                   Z-ADD     HGDSDT        WGDSDT
      *
     C                   Z-ADD     HGHGSN        KYHGSN
      *
      * CALCULATE THE DAYS IN THE PHASE & CONVERT TO WEEKS - DAYS
      *
     C                   Z-ADD     HGOPDT        PFRM8
     C                   Z-ADD     HGDSDT        PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAY
     C                   MOVEL     'W'           PCODE
     C                   MOVE      *BLANKS       PRTRN
      *
     C                   EXSR      $DATE
      *
      * CONVERT ELASPED DAYS TO WEEKS AND DAYS
      *
     C                   Z-ADD     PDAY          WGPDAY
     C                   Z-ADD     0             WGWK
     C                   Z-ADD     0             WGDAY
      *
     C     PDAY          IFGT      0
     C     PDAY          DIV       7             WGWK
     C                   MVR                     WGDAY
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * MOVEMENTS INFORMATION
      ****************************************************************
      *
     C     $MOVE         BEGSR
      *
      *---------------------------------------
      * GET THE ADJUSTED TRANSFERRED IN HEAD
      *----------------------------------------
      *
     C                   MOVEL     TRANA         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         If adjusted
     C                   Z-ADD     CGCGHD        WGAJTIHD
     C                   ADD       CGCGHD        AFAJTIHD
     C                   ADD       CGCGLB        AFAJTILB
      *
      * CALCULATE THE AVERAGE POUNDS PLACED = ADJ TRANI LBS / ADJ TRANI HEAD
      *
     C     CGCGHD        IFNE      0
     C     CGCGLB        DIV(H)    CGCGHD        WGAPLB
     C                   ENDIF
     C                   ENDIF                                                  If adjusted
      *
      *-------------------------------
      * GET THE TRANSFERRED IN HEAD
      *-------------------------------
      *
     C                   MOVEL     TRANI         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF TRAN IN
     C                   Z-ADD     CGCGHD        WGTIHD
     C                   Z-ADD     CGCGAM        WKTIAM
      *
      *
     C                   ADD       CGCGHD        WFTIHD
     C                   ADD       CGCGAM        AFTIAM
     C                   ENDIF                                                  IF TRAN IN
      *
      *---------------------------------
      * GET THE TRANSFERRED OUT HEAD
      *----------------------------------
      *
     C                   MOVEL     TRANO         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF TRAN OUT
      *
     C                   Z-ADD     CGCGHD        WGTOHD
     C                   Z-ADD     CGCGLB        WKTOLB
     C                   ADD       CGCGHD        WFTOHD
     C                   ENDIF                                                  IF TRAN OUT
      *
      *-------------------------------------------------
      * GET THE DEAD FOR USE IN CALCULATING DEATH LOSS %
      *-------------------------------------------------
      *
     C                   MOVEL     DEAD          KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF DEAD
     C                   ADD       CGCGHD        WKDDHD
     C                   ADD       CGCGHD        AFDDHD
     C                   ENDIF                                                  IF DEAD
      *
      *------------------------------------------------------
      * GET THE ADJUSTMENTS AND SUBTRACT FROM THE DEAD COUNT
      *------------------------------------------------------
      *
     C                   MOVEL     INADJ         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF ADJUST
     C                   SUB       CGCGHD        WKDDHD
     C                   SUB       CGCGHD        AFDDHD
     C                   ENDIF                                                  IF ADJUST
      *
      *------------------------------------------------------
      * DEATH LOSS % = DEATHS / ADJUSTED TRANSFERRED IN HEAD
      *------------------------------------------------------
      *
     C     WGAJTIHD      IFNE      0
     C     WKDDHD        DIV(H)    WGAJTIHD      WKFLD3
     C     WKFLD3        MULT(H)   100           WGDLPR
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * PERFORMANCE STATISTICS
      ****************************************************************
      *
     C     $PERF         BEGSR
      *
      *--------------------------------------------------------------
      * RETRIEVE WEIGHT GAINED
      *--------------------------------------------------------------
      *
     C                   MOVEL     WGAIN         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF GAIN
     C                   Z-ADD     CGCGLB        WKWGLB
     C                   ADD       CGCGLB        AFWGLB
     C                   ADD       CGCGLB        AFWXLB
     C                   ENDIF                                                  IF GAIN
      *
      *----------------------------------------------------
      * RETRIEVE TOTAL COST
      *----------------------------------------------------
      *
      *  ACCUMULATE TOTAL COST INTO THE PHASE COST DOLLARS
      *
     C                   MOVEL     COS           KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF
     C                   ADD       CGCGAM        WKP$AM
     C                   ENDIF
      *
      * DEDUCT THE COST OF TRANSFERS IN FROM THE PHASE COST
      * THEN, CALCULATE THE PHASE COST PER POUND OF GAIN
      *
     C     WKP$AM        SUB       WKTIAM        WKP$AM
     C     WKWGLB        IFNE      0
     C     WKP$AM        DIV(H)    WKWGLB        WGP$GN
      *
     C                   ADD       WKP$AM        AFP$AM
     C                   ENDIF
      *
      *----------------------------------------------
      * RETRIEVE PIG DAYS
      *----------------------------------------------
      *
     C                   MOVEL     PDAYS         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF PIG DAYS
     C                   Z-ADD     CGCGQT        WGPIGD
     C                   ADD       CGCGQT        WFPIGD
      *
      * CALCULATE AVERAGE DAILY GAIN = WGT GAIN / TOTAL PIG DAYS
      *
     C     CGCGQT        IFNE      0                                            IF DAYS
     C     WKWGLB        DIV(H)    CGCGQT        WGAGLB
     C                   ENDIF                                                  IF DAYS
     C                   ENDIF                                                  IF PIG DAYS
      *
      *---------------------------------------------------------------
      * RETRIEVE POUNDS OF LYSINE CONSUMED AND CALCULATE CONSUMPTION
      *---------------------------------------------------------------
      *
      * CONSUMPTION OF GRAMS OF LYSINE PER PIG DAY =
      *                    = (PROTEIN LBS * 453.6) / PIG DAYS
      *                    = PROTEIN GRAMS / PIG DAYS
      *
     C                   MOVEL     FPRO          KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     CGCGLB        WGLYLB
     C                   ADD       CGCGLB        WFLYLB
     C     WGPIGD        IFNE      0
     C     CGCGLB        MULT(H)   453.6         WGLYGR
     C     WGLYGR        DIV(H)    WGPIGD        WGLYPD
     C                   ENDIF
     C                   ENDIF
      *
      *---------------------------------------------------------------
      * RETRIEVE CALORIES AND CALCULATE KCAL PER PIG DAY
      *---------------------------------------------------------------
      *
      * CALORIE CONVERSION = (CALORIES / 1000) / PIGDAYS
      *
     C                   MOVEL     FCAL          KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     CGCGQT        WGCAL
     C                   ADD       CGCGQT        WFCAL
     C     WGPIGD        IFNE      0
     C     CGCGQT        DIV(H)    1000          WKKCAL
     C     WKKCAL        DIV(H)    WGPIGD        WGKCPD
     C                   ENDIF
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * THE REMAINING CODE IN THIS SUBROUTINE IS ONLY EXECUTED
      * FOR GROW FINISH GROUPS
      *----------------------------------------------------------------
      *
     C     LDPPCD        IFEQ      GF                                           IF G/F
      *
      *--------------------------
      * RETRIEVE BACKFAT INCHES
      *---------------------------
      *
     C                   MOVEL     BFIN          KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF BACKFAT
     C                   Z-ADD     CGCGQT        WGBFIN
      *
      * CALCULATE BACKFAT QTY =  BACKFAT INCHES * CARCASS POUNDS
      *
     C     CGCGQT        MULT      CGCRLB        WKFLD3
      *
     C                   ADD       WKFLD3        AFTBFI
     C                   ADD       CGCRLB        AFCRLB
     C                   ENDIF                                                  IF BACKFAT
      *
      *------------------------
      * RETRIEVE CARCASS YIELD
      *------------------------
      *
     C                   MOVEL     CYIEL         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF YIELD
     C                   Z-ADD     CGCGQT        WGCRYD
     C                   ADD       CGCGLB        AFCYLB
     C                   ADD       CGCRLB        AFCYCB
     C                   ENDIF                                                  IF YIELD
      *
      *-----------------------------------------------
      * RETRIEVE LEAN PERCENT
      *-----------------------------------------------
      *
     C                   MOVEL     LEAN          KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF LEAN
     C                   Z-ADD     CGCGQT        WGLEAN
      *
      * CALCULATE LEAN QTY = MULTIPLY LEAN PERCENT * CARCASS POUNDS
      *
     C     CGCGQT        MULT      CGCRLB        WKFLD3
      *
     C                   ADD       WKFLD3        AFLNQT
     C                   ADD       CGCRLB        AFLNCB
     C                   ENDIF                                                  IF LEAN
     C                   ENDIF                                                  IF G/F
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $SALE
      ****************************************************************
      *
     C     $SALE         BEGSR
      *
      *-----------------------------------------------------
      * RETRIEVE BUTCHER HOG, LIVE AND FEEDER PIG SALES
      *-----------------------------------------------------
      *
     C     SALES         SETLL     HSL006B
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO SALES
     C     SALES         READE     HSL006B                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
     C     RPRPCD        IFEQ      BTCAR                                        IF RIGHT
     C     RPRPCD        OREQ      BTLIV
     C     RPRPCD        OREQ      FPS
      *
     C                   MOVEL     RPRPCD        KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF HIT
      *
     C                   ADD       CGCGHD        WGSLHD
     C                   ADD       CGCGLB        WKSLLB
     C                   ADD       CGCGAM        WKSLAM
      *
     C                   ADD       CGCGHD        WFSLHD
     C                   ADD       CGCGLB        AFSLLB
     C                   ADD       CGCGAM        AFSLAM
     C                   ENDIF                                                  IF HIT
     C                   ENDIF                                                  IF RIGHT
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO SALES
      *
      * ADD SOLD AND TRANSFERRED OUT VALUES TO GET MOVED OUT
      *
     C     WKSLLB        ADD       WKTOLB        WKMOLB
     C     WGSLHD        ADD       WGTOHD        WKMOHD
      *
     C                   ADD       WKMOHD        AFMOHD
     C                   ADD       WKMOHD        AFMXHD
     C                   ADD       WKMOLB        AFMOLB
     C                   ADD       WKMOLB        AFMXLB
      *
      * CONVERT SOLD POUNDS AND MOVED OUT POUNDS TO CWT
      *
     C     WKSLLB        DIV       100           WKSCWT
     C     WKMOLB        DIV       100           WKMCWT
      *
      *-------------------------------
      * RETRIEVE FEED CONSUMED
      *-------------------------------
      *
     C                   MOVEL     FCONS         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     CGCGLB        WKFCLB
     C                   Z-ADD     CGCGAM        WKFCAM
     C                   ADD       CGCGLB        AFFCLB
     C                   ADD       CGCGAM        AFFCAM
     C                   ENDIF
      *
      * IF FEED POUNDS AND FEED AMOUNT ARE ZERO, BACK OUT
      * WEIGHT GAIN, HEAD MOVED OUT AND POUNDS MOVED OUT FROM THE
      * ACCUMULATORS WITH AN 'X'
      *
     C     *IN92         IFEQ      *ON                                          IF NO FEED
     C     WKFCLB        OREQ      0
     C     WKFCAM        ANDEQ     0
     C     AFWXLB        SUB       WKWGLB        AFWXLB
     C     AFMXHD        SUB       WKMOHD        AFMXHD
     C     AFMXLB        SUB       WKMOLB        AFMXLB
     C                   ENDIF                                                  IF NO FEED
      *
      * CALCULATE AVERAGE POUNDS OF FEED PER HEAD MOVED
      *
     C     WKMOHD        IFNE      0
     C     WKFCLB        DIV(H)    WKMOHD        WGAFLB
     C                   ENDIF
      *
      * IF POUNDS OF WEIGHT GAINED IS NOT ZERO,
      *     CALCULATE FEED CONVERSION
      *     CALCULATE FEED COST PER POUND OF GAIN
      *
     C     WKWGLB        IFNE      0
     C     WKFCLB        DIV(H)    WKWGLB        WGFDCV
     C     WKFCAM        DIV(H)    WKWGLB        WGF$GN
     C                   ENDIF
      *
      * CALCULATE AVERAGE FEED COST PER TON
      *
     C     WKFCLB        DIV(H)    2000          WKTONS
     C     WKTONS        IFNE      0
     C     WKFCAM        DIV(H)    WKTONS        WGFC$T
     C                   ENDIF
      *
      * CALCULATE THE AVERAGE POUNDS PER HEAD MOVED OUT
      *
     C     WKMOHD        IFNE      0
     C     WKMOLB        DIV(H)    WKMOHD        WGAMLB
     C                   ENDIF
      *
      *-----------------------------------------------------------
      * RETRIEVE PREMIUM/DISCOUNT DOLLARS
      *-----------------------------------------------------------
      *
     C     *LOVAL        SETLL     HSP045
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO PREMIUMS
     C                   READ      HSP045                                 91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
     C                   MOVEL     PDRPCD        KYRPCD
      *
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       CGCGAM        WKPDAM
     C                   ADD       CGCGAM        AFPDAM
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO PREMIUMS
      *
      *---------------------------------------------------
      * RETRIEVE DEDUCTION/ADD-ON DOLLARS
      *---------------------------------------------------
      *
     C     *LOVAL        SETLL     HSP044
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO DISCOUNTS
     C                   READ      HSP044                                 91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
     C                   MOVEL     DARPCD        KYRPCD
      *
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF DIS FOUND
     C                   ADD       CGCGAM        WKDAAM
     C                   ADD       CGCGAM        AFDAAM
     C                   ENDIF                                                  IF DIS FOUND
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO DISCOUNTS
      *
      * CALCULATE NET SALES DOLLARS =
      *     SALES + DEDUCTION ADDONS + PREMIUM DISCOUNTS
      *
     C     WKSLAM        ADD       WKDAAM        WGN$AM
     C                   ADD       WKPDAM        WGN$AM
      *
      * CALCULATE NET SALES PER CWT SOLD
      *
     C     WKSCWT        IFNE      0
     C     WGN$AM        DIV(H)    WKSCWT        WGN$CW
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * COST INFORMATION
      ****************************************************************
      *
     C     $COST         BEGSR
      *
     C     WKMCWT        IFNE      0                                            IF MOVED CWT
      *
      * CALCULATE TRANSFER IN DOLLARS PER CWT LIVE MOVED OUT =
      *      TRANSFER IN DOLLARS / HUNDRED WEIGHT MOVED OUT
      *
     C     WKTIAM        DIV(H)    WKMCWT        WGTI$M
      *
      * ADD TRANSFER IN DOLLARS TO TOTAL COST
      *
     C                   ADD       WKTIAM        WKTCAM
      *
      * CALCULATE FEED COST PER CWT LIVE MOVED OUT =
      *     FEED CONSUMED DOLLARS / MOVED OUT LIVE POUNDS CWT
      *
     C     WKFCAM        DIV(H)    WKMCWT        WGFC$M
     C                   ENDIF                                                  IF MOVED CWT
      *
      * ADD FEED DOLLARS INTO TOTAL COST
      *
     C                   ADD       WKFCAM        WKTCAM
      *
      *-------------------------------------
      * RETRIEVE OPERATING COSTS
      *-------------------------------------
      *
     C     OPCST         SETLL     HSL006B
      *
     C     *IN91         DOUEQ     *ON                                          DO OPER COST
     C     OPCST         READE     HSL006B                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   MOVEL     RPRPCD        KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       CGCGAM        WKOCAM
     C                   ADD       CGCGAM        WKTCAM
     C                   ADD       CGCGAM        AFOCAM
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO OPER COST
      *
      *-------------------------------
      * RETREIVE CULL SALES
      *-------------------------------
      *
     C                   MOVEL     CULLS         KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF CULLS
     C                   Z-ADD     CGCGHD        WGCUHD
      *
      * SUBTRACT THE CULL SALES DOLLARS FROM THE COSTS
      *
     C                   SUB       CGCGAM        WKTCAM
     C                   SUB       CGCGAM        WKOCAM
     C                   ADD       CGCGHD        WFCUHD
     C                   SUB       CGCGAM        AFOCAM
     C                   ENDIF                                                  IF CULLS
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $OTCST - OTHER COSTS
      *****************************************************************
      *
      * DO NOT INCLUDE THE TOTAL COST RECORD IN THIS PROCESSING
      *
     C     $OTCST        BEGSR
      *
     C     OTCST         SETLL     HSL006B
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO OTHER
     C     OTCST         READE     HSL006B                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
     C     RPRPCD        ANDNE     COS
      *
     C                   MOVEL     RPRPCD        KYRPCD
     C     K1P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF HIT
     C                   ADD       CGCGAM        WKTCAM
     C                   ADD       CGCGAM        WKOCAM
     C                   ADD       CGCGAM        AFOCAM
     C                   ENDIF                                                  IF HIT
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO OTHER
      *
     C                   ADD       WKTCAM        AFTCAM
      *
      * CALCULATE THE TOTAL COST DOLLARS PER CWT MOVED OUT.
      * CALCULATE THE OTHER COST DOLLARS PER CWT MOVED OUT.
      *
     C     WKMCWT        IFNE      0
     C     WKTCAM        DIV(H)    WKMCWT        WGTC$M
     C     WKOCAM        DIV(H)    WKMCWT        WGOC$M
     C                   ENDIF
      *
      * CALCULATE THE MARGIN PER CWT =
      * NET DOLLARS PER CWT - TOTAL COST DOLLARS PER CWT MOVED OUT
      *
     C     WGN$AM        IFNE      0
     C     WGN$CW        SUB       WGTC$M        WGM$CW
     C                   ENDIF
      *
      * CALCULATE COST OF THE TRANSFER OUTS
      *
     C     WKTOLB        DIV(H)    100           WKFLD3
     C     WKFLD3        MULT(H)   WGTC$M        WK$TO
      *
      * IF NET SALES ARE NOT ZERO
      *
     C     WGN$AM        IFNE      0                                            IF NET NOT 0
      *
      * ACCUMULATE TOTAL COSTS ASSOCATED WITH NET SALES DOLLARS
      *
     C                   ADD       WKTCAM        AFSCAM
      *
      * ACCUMULATE TRANSFER OUT COSTS
      *
     C                   ADD       WK$TO         AF$TO
      *
      * ACCUMULATE MOVED OUT POUNDS THAT HAD NET SALES DOLLARS
      *
     C                   ADD       WKMOLB        AFMSLB
      *
      * FOR PRINTING, REDUCE TOTAL COST BY COST OF TRANSFER OUT
      *
     C     WKTCAM        SUB       WK$TO         WGC$AM
      *
      * CALCULATE MARGIN DOLLARS
      *
     C     WGN$AM        SUB       WGC$AM        WGM$AM
     C                   ENDIF                                                  IF NET NOT 0
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $WFTOT - CONTROL BREAK IN ANALYSIS FACTOR
      *****************************************************************
      *
     C     $WFTOT        BEGSR
      *
      * CALCULATE DEATH LOSS % = DEATHS / ADJUSTED TRANSFERRED IN HEAD
      *
     C     AFAJTIHD      IFNE      0                                            If adjusted
     C     AFDDHD        DIV(H)    AFAJTIHD      WKFLD3
     C     WKFLD3        MULT(H)   100           WFDLPR
      *
      * Average pounds placed = adjusted tran in pounds / adjusted tran in head
      *
     C     AFAJTILB      DIV(H)    AFAJTIHD      WFAPLB
     C                   ENDIF                                                  If adjusted
      *
      * CALCULATE THE AVERAGE POUNDS MOVED OUT
      *
     C     AFMOHD        IFNE      0
     C     AFMOLB        DIV(H)    AFMOHD        WFAMLB
     C                   ENDIF
      *
      * CALCULATE FEED POUNDS PER HEAD MOVED THAT ATE FEED
      *
     C     AFMXHD        IFNE      0
     C     AFFCLB        DIV(H)    AFMXHD        WFAFLB
     C                   ENDIF
      *
      * CALCULATE FEED CONVERSION = FEED POUNDS / TOTAL WEIGHT GAIN
      * CALCULATE  FEED COST PER POUNDS OF GAIN =
      *                     TOTAL FEED COST DOLLARS / TOTAL WEIGHT GAIN
      *
     C     AFWXLB        IFNE      0
     C     AFFCLB        DIV(H)    AFWXLB        WFFDCV
     C     AFFCAM        DIV(H)    AFWXLB        WFF$GN
     C                   ENDIF
      *
      * CALCULATE AVERAGE FEED COST PER TON
      *
     C     AFFCLB        DIV       2000          WKTONS
     C     WKTONS        IFNE      0
     C     AFFCAM        DIV(H)    WKTONS        WFFC$T
     C                   ENDIF
      *
      * CALCULATE COST PER LB OF GAIN FOR THIS PHASE
      *
     C     AFWGLB        IFNE      0
     C     AFP$AM        DIV(H)    AFWGLB        WFP$GN
     C                   ENDIF
      *
      * AVERAGE DAILY GAIN = WGT GAIN / TOTAL PIG DAYS
      *
     C     WFPIGD        IFNE      0
     C     AFWGLB        DIV(H)    WFPIGD        WFAGLB
     C                   ENDIF
      *
      * CALCULATE HUNDERED WEIGHT VALUES FOR:
      *   SOLD POUNDS
      *   MOVED POUNDS
      *   MOVED POUNDS ASSOCIATED HOGS THAT ATE FEED
      *
     C     AFSLLB        DIV(H)    100           WKSCWT
     C     AFMOLB        DIV(H)    100           WKMCWT
     C     AFMXLB        DIV(H)    100           WKXCWT
      *
      * CALCULATE TRANSFER IN DOLLARS PER CWT WEIGHT LIVE MOVED OUT =
      *     TOTAL TRANI DOLLARS / MOVED OUT LIVE CWT
      *
     C     WKMCWT        IFNE      0
     C     AFTIAM        DIV(H)    WKMCWT        WFTI$M
     C                   ENDIF
      *
      * CALCULATE FEED COST PER CWT LIVE MOVED OUT
      *
     C     WKXCWT        IFNE      0
     C     AFFCAM        DIV(H)    WKXCWT        WFFC$M
     C                   ENDIF
      *
      * CALCULATE THE OTHER COST PER CWT LIVE MOVED OUT
      *
     C     WKMCWT        IFNE      0
     C     AFOCAM        DIV(H)    WKMCWT        WFOC$M
     C                   ENDIF
      *
      * CALCULATE TOTAL COST PER CWT LIVE MOVED OUT
      *
     C     WKMCWT        IFNE      0
     C     AFTCAM        DIV(H)    WKMCWT        WFTC$M
     C                   ENDIF
      *
      * CALCULATE CWT DOLLAR VALUES FOR:
      *     SALES
      *     DEDUCTIONS AND ADDONS
      *     PREMIUMS AND DISCOUNTS
      *
     C                   Z-ADD     0             WKFLD1
     C     WKSCWT        IFNE      0
     C     AFSLAM        DIV(H)    WKSCWT        WKFLD1
     C     AFDAAM        DIV(H)    WKSCWT        AFDACW
     C     AFPDAM        DIV(H)    WKSCWT        AFPDCW
     C                   ENDIF
      *
      * CALCULATE NET SALES PER CWT SOLD =
      *    SALES PER CWT + DISCOUNTS PER CWT + PREMIUMS PER CWT
      *
     C     WKFLD1        ADD       AFDACW        WFN$CW
     C                   ADD       AFPDCW        WFN$CW
      *
      * CALCULATE NET SALES =
      *    SALES + TOTAL DEDUCTIONS ADDONS + PREMIUMS DISCOUNTS
      *
     C     AFSLAM        ADD       AFDAAM        WFN$AM
     C     WFN$AM        ADD       AFPDAM        WFN$AM
      *
      *
      * CALCULATE TOTAL COSTS PER MOVED OUT CWT WHEN THERE WERE NET
      * SALES DOLLARS.
      * THE FIELDS AFMSLB AND AFSCAM WILL ONLY HAVE ACCUMULATED
      * VALUES WHEN THE NET SALES DOLLARS WERE NOT ZERO.
      *
     C     AFMSLB        DIV(H)    100           WKFLD3
     C                   Z-ADD     0             WKFLD1
     C     WKFLD3        IFNE      0
     C     AFSCAM        DIV(H)    WKFLD3        WKFLD1
     C                   ENDIF
      *
      * CALCULATE MARGIN DOLLARS PER CWT =
      *    NET DOLLARS PER CWT - TOTAL COSTS PER MOVED OUT CWT
      *
     C     WFN$CW        SUB       WKFLD1        WFM$CW
      *
      * SUBTRACT COST OF TRANSFER OUTS FROM THE TOTAL COST DOLLARS
      * ACCUMLATED WHEN NET SALES DOLLARS WERE NOT ZERO.
      *
     C     AFSCAM        SUB       AF$TO         WFC$AM
      *
      * CALCULATE THE MARGIN $ = NET DOLLARS - COST DOLLARS
      *
     C     WFN$AM        SUB       WFC$AM        WFM$AM
      *
      * LYSINE CONSUMPTION = PROTEIN GRAMS / PIG DAYS
      *                    = (PROTEIN LBS * 453.6) / PIG DAYS
      *
     C     WFPIGD        IFNE      0
     C     WFLYLB        MULT(H)   453.6         WFLYGR
     C     WFLYGR        DIV(H)    WFPIGD        WFLYPD
     C                   ENDIF
      *
      * CALORIE CONVERSION = (CALORIES / 1000) / PIGDAYS
      *
     C     WFPIGD        IFNE      0
     C     WFCAL         DIV(H)    1000          WKKCAL
     C     WKKCAL        DIV(H)    WFPIGD        WFKCPD
     C                   ENDIF
      *
      *-------------------------------------------------------------
      * THE FOLLOWING LOGIC IS ONLY PERFORMED FOR GROW FINISH GROUPS
      *-------------------------------------------------------------
      *
     C     LDPPCD        IFEQ      GF                                           IF G/F
      *
      * LEAN % = TOTAL LEAN % / TOTAL CARCASS LBS PER CWT
      *
     C     AFLNCB        IFNE      0
     C     AFLNQT        DIV(H)    AFLNCB        WFLEAN
     C                   ENDIF
      *
      * BACKFAT INCHES
      *
      * ((AVG BF INCHES * CARCASS LBS)ACCUMULATED) / TOTAL CARCASS LBS
      * ((CGCGQT        *  CGCRLB)ACCUMULATED)
      *
     C     AFCRLB        IFNE      0
     C     AFTBFI        DIV(H)    AFCRLB        WFBFIN
     C                   ENDIF
      *
      * CALCULATE CARCASS YIELD = CARCASS POUNDS / LIVE POUNDS
      *
     C     AFCYLB        IFNE      0
     C     AFCYCB        DIV(H)    AFCYLB        WKFLD3
     C     WKFLD3        MULT(H)   100           WFCRYD
     C                   ENDIF
     C                   ENDIF                                                   IF G/F
      *
      * WRITE ANALYSIS FACTOR RECORD
      *
     C                   MOVEL     SVSORT        WFSORT
     C                   WRITE     WFREC
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      ****************************************************************
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAY              4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $CLRHG - CLEAR WORK FIELDS AND WORKFILE FIELDS FOR HOG GROUP
      *****************************************************************
      *
     C     $CLRHG        BEGSR
      *
      * CLEAR WORK FIELDS
     C                   Z-ADD     0             WKTIAM                         T/I $
     C                   Z-ADD     0             WKTOLB                         T/O LBS
     C                   Z-ADD     0             WKDDHD                         DEAD HEAD
     C                   Z-ADD     0             WKP$AM                         PHASE COST
     C                   Z-ADD     0             WKWGLB                         WEIGHT GAIN
     C                   Z-ADD     0             WKDAAM                         DED/ADDON $
     C                   Z-ADD     0             WKPDAM                         PREM/DIS $
     C                   Z-ADD     0             WKSLAM                         SALES $
     C                   Z-ADD     0             WKTCAM                         TOTAL COST $
     C                   Z-ADD     0             WKOCAM                         OTHER COST $
     C                   Z-ADD     0             WKFCAM                         FEED COST $
     C                   Z-ADD     0             WKTONS                         FEED TONS
     C                   Z-ADD     0             WKFCLB                         FEED LBS
     C                   Z-ADD     0             WKSLLB                         SALES LBS
     C                   Z-ADD     0             WKMOLB                         MOVED LBS
     C                   Z-ADD     0             WKMOHD                         MOVED HD
      *
      * CLEAR WORKFILE FIELDS
      *
     C                   CLEAR                   WGREC
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $CLRAF - CLEAR FIELDS FOR ANALYSIS FACTOR
      *****************************************************************
      *
     C     $CLRAF        BEGSR
      *
      * CLEAR ANALYSIS FACTOR ACCUMULATORS
      *
     C                   Z-ADD     0             AFDDHD
     C                   Z-ADD     0             AFTBFI                         BACKFAT IN
     C                   Z-ADD     0             AFCRLB                         BF CARC LBS
     C                   Z-ADD     0             AFOCAM                         OTHER COST
     C                   Z-ADD     0             AFSCAM                         TOTAL COST
     C                   Z-ADD     0             AFTCAM                         TOTAL COST
     C                   Z-ADD     0             AFFCLB                         FEED LBS
     C                   Z-ADD     0             AFWGLB                         WEIGHT GAIN
     C                   Z-ADD     0             AFWXLB                         WEIGHT GAIN
     C                   Z-ADD     0             AFCYLB                         YIELD LBS
     C                   Z-ADD     0             AFCYCB                         YIELD CARC
     C                   Z-ADD     0             AFTIAM                         T/I $
     C                   Z-ADD     0             AFFCAM                         FEED COST $
     C                   Z-ADD     0             AFDAAM                         DED/ADDON $
     C                   Z-ADD     0             AFPDAM                         PREM/DISC $
     C                   Z-ADD     0             AFSLAM                         SALES $$
     C                   Z-ADD     0             AFP$AM                         PHASE COST
     C                   Z-ADD     0             AFDACW                         DED/ADDON CW
     C                   Z-ADD     0             AFPDCW                         PREM/DISC CW
     C                   Z-ADD     0             AFLNCB                         LEAN CARC LB
     C                   Z-ADD     0             AFLNQT                         LEAN %
     C                   Z-ADD     0             AF$TO                          TRAN/OUT $
     C                   Z-ADD     0             AFMOHD                         HEAD MOVED
     C                   Z-ADD     0             AFMXHD                         HEAD MOVED
     C                   Z-ADD     0             AFSLLB                         LBS SOLD
     C                   Z-ADD     0             AFMOLB                         LBS MOVED
     C                   Z-ADD     0             AFMXLB                         LBS MOVED
     C                   Z-ADD     0             AFMSLB                         LBS MOVED
     C                   Z-ADD     0             AFAJTIHD                       ADJ TRAN IN
     C                   Z-ADD     0             AFAJTILB                       ADJ TRAN IN
      *
      * CLEAR WORKFILE FIELDS FOR ANALYSIS FACTOR
      *
     C                   CLEAR                   WFREC
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * *INZSR - INITIALIZATION SUBROUTINE
      ****************************************************************
      *
     C     *INZSR        BEGSR
      *
      * KEY FIELDS
      *
     C     K1P033        KLIST
     C                   KFLD                    KYHGSN
     C                   KFLD                    KYRPCD
      *
     C     *LIKE         DEFINE    HGHGSN        KYHGSN
     C     *LIKE         DEFINE    RPRPCD        KYRPCD
      *
      * KEY FOR CLOSED GROUP EQUIPMENT FILE
      *
     C     K2P039        KLIST
     C                   KFLD                    HGHGSN
     C                   KFLD                    LDPACD
      *
      * DEFINE SAVE FIELDS
      *
     C     *LIKE         DEFINE    WGSORT        SVSORT
      *
      * WORKFIELDS FOR INTERIM CALCULATIONS AND ACCUMULATIONS
      *
     C                   Z-ADD     0             WKFLD1            5 2
     C                   Z-ADD     0             WKFLD2           15 2
     C                   Z-ADD     0             WKFLD3           15 4
      *
     C     *LIKE         DEFINE    WKFLD2        WKFCLB
     C     *LIKE         DEFINE    WKFLD2        WKWGLB
     C     *LIKE         DEFINE    WKFLD2        WKSLLB
     C     *LIKE         DEFINE    WKFLD2        WKMOLB
     C     *LIKE         DEFINE    WKFLD2        WKTOLB
      *
     C     *LIKE         DEFINE    WKFLD3        WKSCWT
     C     *LIKE         DEFINE    WKFLD3        WKMCWT
     C     *LIKE         DEFINE    WKFLD3        WKXCWT
      *
     C     *LIKE         DEFINE    WKFLD2        WK$TO
     C     *LIKE         DEFINE    WKFLD2        WKFCAM
     C     *LIKE         DEFINE    WKFLD2        WKTCAM
     C     *LIKE         DEFINE    WKFLD2        WKOCAM
     C     *LIKE         DEFINE    WKFLD2        WKDAAM
     C     *LIKE         DEFINE    WKFLD2        WKSLAM
     C     *LIKE         DEFINE    WKFLD2        WKPDAM
     C     *LIKE         DEFINE    WKFLD2        WKTIAM
     C     *LIKE         DEFINE    WKFLD2        WKP$AM
      *
     C                   Z-ADD     0             WKMOHD           15 0
     C     *LIKE         DEFINE    WKMOHD        WKDDHD
     C     *LIKE         DEFINE    WKFLD2        WKTONS
     C                   Z-ADD     0             WKKCAL           15 5
      *
      * ACCUMULATORS FOR ANALYSIS FACTOR ACCUMULATIONS
      *
     C                   Z-ADD     0             AFDDHD           15 0
     C     *LIKE         DEFINE    WKFLD2        AFMXHD                                     M
     C     *LIKE         DEFINE    WKFLD2        AFMOHD                                     M
     C     *LIKE         DEFINE    WKFLD2        AFAJTIHD                                   M
      *
     C     *LIKE         DEFINE    WKFLD2        AFMOLB                                     M
     C     *LIKE         DEFINE    WKFLD2        AFAJTILB                                   M
     C     *LIKE         DEFINE    WKFLD2        AFMXLB                                     M
     C     *LIKE         DEFINE    WKFLD2        AFMSLB
     C     *LIKE         DEFINE    WKFLD2        AFSLLB                                     M
     C     *LIKE         DEFINE    WKFLD2        AFFCLB                                     M
     C     *LIKE         DEFINE    WKFLD2        AFWGLB                                     M
     C     *LIKE         DEFINE    WKFLD2        AFWXLB
     C     *LIKE         DEFINE    WKFLD2        AFCRLB
     C     *LIKE         DEFINE    WKFLD2        AFCYLB
     C     *LIKE         DEFINE    WKFLD2        AFCYCB
     C     *LIKE         DEFINE    WKFLD2        AFLNCB
      *
     C     *LIKE         DEFINE    WKFLD2        AFOCAM
     C     *LIKE         DEFINE    WKFLD2        AFSCAM
     C     *LIKE         DEFINE    WKFLD2        AFTCAM
     C     *LIKE         DEFINE    WKFLD2        AFFCAM
     C     *LIKE         DEFINE    WKFLD2        AFTIAM
     C     *LIKE         DEFINE    WKFLD2        AFP$AM
     C     *LIKE         DEFINE    WKFLD2        AFPDAM
     C     *LIKE         DEFINE    WKFLD2        AFDAAM
     C     *LIKE         DEFINE    WKFLD2        AFSLAM
     C     *LIKE         DEFINE    WKFLD2        AF$TO
      *
     C     *LIKE         DEFINE    WKFLD3        AFLNQT
     C     *LIKE         DEFINE    WKFLD3        AFTBFI
     C     *LIKE         DEFINE    WKFLD1        AFDACW
     C     *LIKE         DEFINE    WKFLD1        AFPDCW
      *
      * SET UP A FIRST TIME FLAG
      *
     C                   MOVEL     YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
