/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP3700CL                                      */
/*  PROGRAM NAME:    WEEKLY CLOSE/ACTIVATE BUDGETS                 */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     02/26/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED ON SATURDAY EVENING      */
/*                   FROM THE JOB SCHEDULER.                       */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/* 01/03/04  LEANNE FEDOR                                          */
/*           ALICE DOES NOT WANT THE REPORTS DELETED FROM THE      */
/*           OUTPUT QUEUE. SO, I HAVE COMMENTED OUT THE DLTSPFL    */
/*           COMMANDS THAT LEE CHRISTINA HAD ME PUT IN.            */
/* 08/09/05  BARB GUTIERREZ                                        */
/*           CHANGED EMAIL ADDRESS FROM SEABOARDPORK.COM TO        */
/*           SEABOARDFOODS.COM PER COMPANY NAME CHANGE.            */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(32)
             DCL        VAR(&PRTFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&COPYS) VALUE(1)
             CHGVAR     VAR(&OUTQ) VALUE(*JOB)


             CHGVAR     VAR(&EMAIL) +
                          VALUE(MP2BUDGET@SEABOARDFOODS.COM)


/*------------------------------------------------------------------*/
/* ALL THE LISTINGS WERE DESIGNED TO BE 80 CHARACTERS WIDE. SO,     */
/* YOU JUST NEED TO OVERRIDE THE PRINT FILE ONE TIME.               */
/*------------------------------------------------------------------*/

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/* ADD LIBRARY TO ALLOW YOU TO SEND LISTINGS BY E-MAIL              */
/*------------------------------------------------------------------*/

             ADDLIBLE   LIB(ESEND)
             MONMSG CPF0000


/*------------------------------------------------------------------*/
/* ROTATIONAL BUDGETS: SET TO DATES                                 */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&PRTFL) VALUE(' ')
             CALL       PGM(HP3712) PARM(&PRTFL)

             IF         COND(&PRTFL *EQ 'Y') THEN(DO)
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Rotational +
                          Budgets that had TO dates set') +
                          ATTLIST((* *PDF *N QPRINT *))
             ENDDO
       /***  DLTSPLF    FILE(QPRINT)   ***/

/*------------------------------------------------------------------*/
/* ALL BUDGETS: CLOSE ACTIVE BUDGETS AND ACTIVATE INACTIVE BUDGETS  */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&PRTFL) VALUE(' ')
             CALL       PGM(HP3711) PARM(&PRTFL)

             IF         COND(&PRTFL *EQ 'Y') THEN(DO)
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Closed Budgets +
                          and Activated Budgets') ATTLIST((* *PDF +
                          *N QPRINT *))
             ENDDO
       /***  DLTSPLF    FILE(QPRINT)   ***/

/*------------------------------------------------------------------*/
/* CREATE ACTIVE WEEKLY/ROTATIONAL BUDGETS FROM TEMPLATES           */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&PRTFL) VALUE(' ')
             CALL       PGM(HP3713) PARM(&PRTFL)

             IF         COND(&PRTFL *EQ 'Y') THEN(DO)
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Weekly/Rotational +
                          Budgets that were created from +
                          Templates.') ATTLIST((* *PDF *N QPRINT *))
             ENDDO
       /***  DLTSPLF    FILE(QPRINT)   ***/

/*------------------------------------------------------------------*/
/* LISTING OF DUPLICATE ITEMS FOR FARMS ON ACTIVE BUDGETS           */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3714) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CHGVAR     VAR(&PRTFL) VALUE(' ')
             CALL       PGM(HP3714) PARM(&PRTFL)

             IF         COND(&PRTFL *EQ 'Y') THEN(DO)
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('FARMS that have +
                          the SAME Item on more than one ACTIVE +
                          Budget') ATTLIST((* *PDF *N QPRINT *))
             ENDDO
       /***  DLTSPLF    FILE(QPRINT)   ***/

/*------------------------------------------------------------------*/
/* END OF JOB                                                       */
/*------------------------------------------------------------------*/
             DLTOVR     FILE(*ALL)

             ENDPGM

