/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP204CLCS                                     */
/*  PROGRAM NAME:    BUILD JDE UPLOAD FILE: HPS CULL SALES DOLLARS */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     10/12/01                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE DRIVER CL       */
/*                   HP404CL.                                      */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                   AN OPNQRY STATEMENT IS USED TO SELECT         */
/*                   RECORDS.                                      */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/* 10/16/18  Danny Nguyen  (S13682)                                */
/*           Retrieve Company Code from LDA. Added OPNQRYF logic   */
/*           for HSL018I to select by Company.                     */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA1) TYPE(*CHAR) LEN(112)

             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 25 3))


/*********************************************************************/
/*  SETUP QUERY STATEMENT                                            */
/*********************************************************************/

/*  ALWAYS SELECT ONLY SALES MOVEMENTS THAT HAVE SOME PAYMENT    */

             CHGVAR     VAR(%SST(&QDATA1 1 38)) VALUE('(SHMSCD +
                          *NE "SC" *AND SHMSCD *NE "SH")')

/*  ALWAYS SELECT ONLY SALES MOVEMENTS WITH A RECEIVE DATE IN THE */
/*  USERS REQUESTED DATE RANGE                                    */

             CHGVAR     VAR(%SST(&QDATA1 40 15)) VALUE('*AND SHRCDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA1 56 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA1 65 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA1 81 8)) VALUE(&LDEPDT)

/* ALWAYS SELECT CULL SALES ONLY                                 */

             CHGVAR     VAR(%SST(&QDATA1 90 23)) VALUE('*AND SHSTCD +
                          *EQ "CULLS"')


/*********************************************************************/
/*  SET UP FILE OVERRIDE AND PERFORM QUERY                           */
/*********************************************************************/

/* SALES MOVEMENTS HEADER                                        */

             OVRDBF     FILE(HSL084C) SHARE(*YES)
             OPNQRYF    FILE((HSL084C)) QRYSLT(&QDATA1) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/*********************************************************************/
/* S13682-OPEN QUERY FILE LOGIC SELECTION ON FARM SITE FILE:         */
/*********************************************************************/

/*  S13682-ALWAYS SELECT BY COMPANY CODE.                        */

             CHGVAR     VAR(&QDATA1) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA1 1 12)) VALUE('FSCOCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA1 13 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA1 16 1)) VALUE('"')

             OVRDBF     FILE(HSL018I) SHARE(*YES)
             OPNQRYF    FILE((HSL018I)) QRYSLT(&QDATA1) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*********************************************************************/
/*  CALL PROGRAM TO BUILD UPLOAD OF FILE CULL SALES                  */
/*********************************************************************/

             CALL       PGM(HP204CS)

/*******************************************************************/
/*  DELETE FILE OVERRIDES                                          */
/*******************************************************************/

             CLOF       OPNID(HSL084C)
             DLTOVR     FILE(HSL084C)

             CLOF       OPNID(HSL018I)
             DLTOVR     FILE(HSL018I)

             ENDPGM

