      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP60261
      * TITLE:       Listing of Divisions
      * PROGRAMMER:  Anurag Agrawal
      * CREATED:     07/04/25
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP40261-Work with Division
      *
      *            All the division records will be printed irrespective of the
      *            positioner record value.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP007    IF   E           K DISK
      *    Division
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Print fields
      *
     D dash            s             80    inz(*all'-')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * Prepare to read the Division file and print headings the first time through.
      *
     C     *loval        setll     hsp007
     C                   EXCEPT    h1hdr
      *
      * Read and print all records in the file that match the user's selection.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp007                                 90
      *
     C                   if        *in90 = *off                                 If not EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    h1hdr
     C                   ENDIF
      *
      * Print detail line
     C                   EXCEPT    d1dtl
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * End of File
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
     C                   endsr
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            h1hdr          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           66 'DATE'
     O                       UDATE         Y     76
      *
     O          E            h1hdr          1
     O                       SDUSR               10
     O                                           47 ' LISTING OF DIVISIONS'
     O                                           66 'TIME'
     O                       RTIME               76 '  :  :  '
      *
     O          E            h1hdr          2
     O                                           66 'PAGE'
     O                       PAGE          Z     71
      *
     O          E            h1hdr          1
     O                                            8 'DIVISION'
      *
     O          E            h1hdr          1
     O                                            5 'CODE'
     O                                           22 'DESCRIPTION'
      *
     O          E            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            d1dtl       1
     O                       dvdvcd               6
     O                       dvdvds              31
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
