      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3094
      * TITLE:       Expense Code/Report Code Change
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/30/03
      *
      * FUNCTION:  Batch program to change the Report Code associated with an Expense Code.
      *
      *            This function is ONLY for I.S. personnel.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp052    uf   e           k disk
      *  Expense codes
      *
      *
     Fhsp033    uf a e           k disk
      *  Closed hog group
      *
      *
     Fhsl034i   if   e           k disk
      *  Hog groups (closed groups only)
      *
      *
     Fhsl048j   uf   e           k disk
      *  Expense tickets
      *
      *
     Fhsl048k   if   e           k disk    rename(xtrec:xtreck)
      *  Expense tickets
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Standard fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for Expense Ticket processing
      *
     D wkxtam          s                   like(cgcgam)
     D wkxthd          s                   like(cgcghd)
     D wkxtlb          s                   like(cgcglb)
     D wkxtqt          s                   like(cgcgqt)
      *
      *
      *---------------------------------------------------------------
      * DATA STRUCTURES
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldexcd                 1      5
     D  ldoldrpcd              6     10
     D  ldrpcd                11     15
     D  ldcacd                16     20
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Retrieve the Expense Code record from the Expense Code Master file
      * and update it with its new Report Code.
      *
     C     ldexcd        chain     hsp052                             92
     C                   if        *in92 = *off
     C                   move      ldrpcd        exrpcd
     C                   update    exrec
     C                   endif
      *
      * Read/process all expense ticket records for the Expense Code replacing
      * the old Report Code with the new Report Code.
      *
     C     ldexcd        setll     hsl048j
      *
     C                   dou       *in90 = *on                                  Do tickets
     C     ldexcd        reade     hsl048j                                90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
     C                   move      ldrpcd        xtrpcd
     C                   update    xtrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do tickets
      *
      * If you changed any expense tickets (we expect to always have tickets
      * but there is no point in going forward if you did not change any
      * tickets), then you must process every closed group.
      *
     C                   if        first = no
     C                   exsr      $groups
     C                   endif
      *
      * EOF processing
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------------------------
      *  Process all closed groups
      *---------------------------------------------------------------------------------
      *
     C     $groups       begsr
      *
     C     *loval        setll     hsl034i
      *
     C                   dou       *in90 = *on                                  Do groups
     C                   read      hsl034i                                90
     C                   if        *in90 = *off                                 If read
      *
      * Accumulate values from all Expense Tickets for the group/report code.
      *
     C                   exsr      $expenses
      *
      * If you accumulated anything for the group/report code, then you have
      * to retrieve/fix the Closed Group records for the old/new report codes.
      *
     C                   if        wkxtam <> 0 or                               If anything
     C                             wkxtlb <> 0 or
     C                             wkxthd <> 0 or
     C                             wkxtqt <> 0
     C                   exsr      $fix033
     C                   endif                                                  If anything
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do groups
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------
      *  Process all Expense Tickets for a group/expense code
      *---------------------------------------------------------------------------------
      *
     C     $expenses     begsr
      *
     C                   z-add     0             wkxtam
     C                   z-add     0             wkxthd
     C                   z-add     0             wkxtlb
     C                   z-add     0             wkxtqt
      *
     C     key01         setll     hsl048k
      *
     C                   dou       *in91 = *on                                  Do tickets
     C     key01         reade     hsl048k                                91
     C                   if        *in91 = *off                                 If read
      *
     C                   add       xtxtam        wkxtam
     C                   add       xtxthd        wkxthd
     C                   add       xtxtlb        wkxtlb
     C                   add       xtxtqt        wkxtqt
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do tickets
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------
      *  Fix the old/new Report Code closed group records for a group
      *---------------------------------------------------------------------------------
      *
     C     $fix033       begsr
      *
      * Retrieve the 'old' report code record and subtract out the accumulated
      * values. If this subtraction brings the fields to zero, delete the record;
      * otherwise, update it.
      *
     C     key02         chain     hsp033                             92
     C                   if        *in92 = *off                                 If old
     C                   sub       wkxtam        cgcgam
     C                   sub       wkxthd        cgcghd
     C                   sub       wkxtlb        cgcglb
     C                   sub       wkxtqt        cgcgqt
      *
     C                   if        cgcgam = 0 and
     C                             cgcghd = 0 and
     C                             cgcglb = 0 and
     C                             cgcgqt = 0
     C                   delete    cgrec
     C                   else
     C                   update    cgrec
     C                   endif
     C                   endif                                                  If old
      *
      * Retrieve the 'new' report code record and add in the accumulated values.
      * Write a record if there is not already one to add the values into.
      *
     C     key03         chain     hsp033                             92
     C                   if        *in92 = *off                                 If new
     C                   add       wkxtam        cgcgam
     C                   add       wkxthd        cgcghd
     C                   add       wkxtlb        cgcglb
     C                   add       wkxtqt        cgcgqt
     C                   update    cgrec
     C                   else
      *
     C                   clear                   cgrec
     C                   z-add     hghgsn        cghgsn
     C                   move      ldrpcd        cgrpcd
     C                   move      ldcacd        cgcacd
     C                   z-add     wkxtam        cgcgam
     C                   z-add     wkxthd        cgcghd
     C                   z-add     wkxtlb        cgcglb
     C                   z-add     wkxtqt        cgcgqt
     C                   write     cgrec
     C                   endif                                                  If new
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    hghgsn
     C                   kfld                    ldexcd
      *
     C     key02         klist
     C                   kfld                    hghgsn
     C                   kfld                    ldoldrpcd
      *
     C     key03         klist
     C                   kfld                    hghgsn
     C                   kfld                    ldrpcd
      *
      *
     C                   endsr
      /EJECT
