     A*%%TS  SD  20110107  083608  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD4120
     A*  TITLE:         SPECIFY CLAIM ADJUSTMENTS LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/05/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'SPOOL FILES')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R TF4120R1
     A*%%TS  SD  20110107  083608  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      WINDOW(TF4120EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(52)
     A                                      SETOF(56)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SPECIFY CLAIM ADJUSTMENTS LISTING -
     A                                      OPTIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Report/download ..........'
     A            D1RDFL    R        B  3 30REFFLD(@@FLAG)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A                                  3 41'(R=Report, D=Download)'
     A                                  4  6'E-mail address ...'
     A            D1EMAIL   R        B  4 25REFFLD(TFREF/@@EMAIL *LIBL/TFPREF)
     A                                      CHGINPDFT(UL LC FE)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A                                  5  1'Report sort sequence .....'
     A            D1RPT     R        B  5 30REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A N41                                  DSPATR(HI)
     A                                  5 41'(C=Claim, P=Product)'
     A                                  6  1'Affiliated sales claims ..'
     A            D1IEOFL   R        B  6 30REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  6 41'(I=Include, E=Exclude, O=Only)'
     A                                  7  1'From date ................'
     A            D1FMDY    R        B  7 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  7 41'(Blank for all)'
     A                                  8  1'To date ..................'
     A            D1TMDY    R        B  8 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  8 41'(Blank for all)'
     A                                  9  1'Data source code .........'
     A                                  9 28'?'
     A            D1DSCD    R        B  9 30REFFLD(TFREF/@@DSCD *LIBL/TFPREF)
     A  42
     AO 52                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A N42                                  DSPATR(HI)
     A                                  9 41'(Blank for all)'
     A                                 10  1'Company number ...........'
     A            D1CONO    R        B 10 30REFFLD(@@CONO)
     A                                      EDTCDE(M)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                 10 41'(Blank for all)'
     A                                 11  1'Claim number .............'
     A            D1CLNO    R        B 11 30REFFLD(@@CLNO)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                 11 41'(Blank for all)'
     A                                 12  1'Memo type ................'
     A            D1MMTY    R        B 12 30REFFLD(@@MMTY)
     A                                      DSPATR(HI)
     A                                 12 41'(Blank for all)'
     A                                 13  1'Credit/debit memo number .'
     A            D1MMNO    R        B 13 30REFFLD(@@MMNO)
     A                                      EDTCDE(M)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                 13 41'(Blank for all)'
     A                                 14  1'Claim order number .......'
     A            D1CLORNO  R        B 14 30REFFLD(TFREF/@@CLORNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N40                                  DSPATR(HI)
     A                                 14 41'(Blank for all)'
     A                                 15  1'Product code .............'
     A                                 15 28'?'
     A            D1PRCD    R        B 15 30REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                 15 41'(Blank for all)'
     A                                 16  1'Responsibility flag ......'
     A            D1RPFL    R        B 16 30REFFLD(TFREF/@@RPFL *LIBL/TFPREF)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A                                 16 41'(Blank for all)'
     A                                 17  1'Responsible department ...'
     A            D1RPDEPT  R        B 17 30REFFLD(TFREF/@@RPDEPT *LIBL/TFPREF)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A                                 17 41'(Blank for all)'
     A                                 18  1'Reason code ..............'
     A            D1RSCD    R        B 18 30REFFLD(TFREF/@@RSCD *LIBL/TFPREF)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A                                 18 41'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R TF4120K1
     A*%%TS  SD  20070323  143641  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      WINDOW(TF4120EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             19  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 20  1'F3=Exit'
     A                                 20 10'F4=Prompt'
     A                                 20 21'F8=Submit'
     A                                 20 32'F17=Print Options'
     A                                 20 51'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R TF4120ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(21)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R TF4120EC                  SFLCTL(TF4120ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 22 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
