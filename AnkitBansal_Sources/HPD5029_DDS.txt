     A*%%TS  SD  20030617  105116  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5029- SELECT EXPENSE SUBCLASS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          05/17/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5029S1                  SFL
     A*%%TS  SD  20030617  105116  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1ESCD    R        O  8  2REFFLD(@@ESCD)
     A            D1ESDS    R        O  8 13REFFLD(HSRREF/ESESDS *LIBL/HSPREF)
     A            D1ECCD    R        O  8 38REFFLD(@@ECCD)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5029C1                  SFLCTL(HP5029S1)
     A*%%TS  SD  20030617  105116  ISLRAMS     REL-V5R2M0  5722-WDS
     A  *DS3                                SFLSIZ(0100)
     A  *DS4                                SFLSIZ(0100)
     A  *DS3                                SFLPAG(0009)
     A  *DS4                                SFLPAG(0009)
     A  *DS3                                WINDOW(HP5029EC)
     A  *DS4                                WINDOW(HP5029EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SELECT EXPENSE SUBCLASS'
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  4 40'?'
     A                                      DSPATR(HI)
     A                                  5  2'Subclass'
     A                                      DSPATR(HI)
     A                                  5 38'Expense'
     A                                      DSPATR(HI)
     A                                  6  2'Code'
     A                                      DSPATR(HI)
     A                                  6 13'Description'
     A                                      DSPATR(HI)
     A                                  6 38'Class'
     A                                      DSPATR(HI)
     A            C1ESCD    R        B  7  2REFFLD(HSRREF/@@ESCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ESDS    R        B  7 13REFFLD(HSRREF/ESESDS *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1ECCD    R        B  7 38REFFLD(HSRREF/@@ECCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5029K1
     A                                      WINDOW(HP5029EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5029ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5029EC                  SFLCTL(HP5029ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
