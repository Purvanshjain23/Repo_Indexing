/*                                                                 */
/*  PROGRAM NUMBER --> HP214CL                                     */
/*  PROGRAM NAME ----> DATAMART FIN: UPDATE GROUP HEADER WITH IN   */
/*                     BEFORE OUT FLAG                             */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 08/15/00                                    */
/*                                                                 */
/*  FUNCTION --------> THIS FUNCTION UPDATES THE DATAMART GROUP    */
/*                     HEADER FOR COMMERCIAL/GROW FINISH GROUPS    */
/*                     ONLY.                                       */
/*                                                                 */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/***************************************************************** */

             PGM        PARM(&PROCFL &CURBEGDT)

             DCL        VAR(&PROCFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CURBEGDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&YEAR) TYPE(*CHAR) LEN(4)
             DCL        VAR(&DATE) TYPE(*CHAR) LEN(8)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(125)
             DCL        VAR(&QDATA2) TYPE(*CHAR) LEN(50)


/*-------------------------------------------------------------------*/
/* RETRIEVE THE FINISHER DATA AREA TO GET THE BEGINNING YEAR FOR     */
/* PROCESSING. THEN, ADD ZEROS TO IT TO GET A FIELD OF YYYY0000.     */
/*-------------------------------------------------------------------*/

             RTVDTAARA  DTAARA(DAFIN (1 4)) RTNVAR(&YEAR)
             CHGVAR     VAR(%SST(&DATE 1 4)) VALUE(&YEAR)
             CHGVAR     VAR(%SST(&DATE 5 4)) VALUE(0000)

/*-------------------------------------------------------------------*/
/* SETUP QUERY FOR THE HOG GROUP FILE IN HPS                         */
/*-------------------------------------------------------------------*/

/* ALWAYS OMIT VOIDED GROUPS                                         */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGGSCD *NE "')
             CHGVAR     VAR(%SST(&QDATA 13 2)) VALUE('VD')
             CHGVAR     VAR(%SST(&QDATA 15 1)) VALUE('"')


/* ALWAYS OMIT CREATED GROUPS                                        */

             CHGVAR     VAR(%SST(&QDATA 17 17)) VALUE('*AND HGGSCD *NE "')
             CHGVAR     VAR(%SST(&QDATA 34 2)) VALUE('CR')
             CHGVAR     VAR(%SST(&QDATA 36 1)) VALUE('"')

/* ALWAYS OMIT BGF GROUPS                                            */

             CHGVAR     VAR(%SST(&QDATA 38 17)) VALUE('*AND HGPPCD *NE "')
             CHGVAR     VAR(%SST(&QDATA 55 3)) VALUE('BGF')
             CHGVAR     VAR(%SST(&QDATA 58 1)) VALUE('"')


/* ALWAYS SELECT GROUPS THAT OPENED IN THE YEAR FROM THE DATA AREA   */
/* OR LATER                                                          */

             CHGVAR     VAR(%SST(&QDATA 60 15)) VALUE('*AND HGOPDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 76 8)) VALUE(&DATE)


/* RUN QUERY OVER THE HPS HOG GROUP FILE                             */

             OVRDBF     FILE(HSL034E) SHARE(*YES)
             OPNQRYF    FILE((HSL034E)) QRYSLT(&QDATA) +
                          KEYFLD((HGFSCD) (HGOPDT)) SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  BUILD THE QUERY STATEMENT FOR THE DATAMART HOG GROUP FILE AND   */
/*  RUN QUERY OVER THE DATAMART GROUP HEADER                        */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA2 1 42)) VALUE('HGPTCD *EQ +
                          "COM  " *AND HGPPCD *EQ "GF   "')

             OVRDBF     FILE(HPPF034) SHARE(*YES)
             OPNQRYF    FILE((HPPF034)) OPTION(*INP *UPD) +
                          QRYSLT(&QDATA2)

/*------------------------------------------------------------------*/
/*  UPDATE DATAMART GROUP HEADER                                    */
/*------------------------------------------------------------------*/

             CALL       PGM(HP214) PARM(&PROCFL &CURBEGDT)

/*------------------------------------------------------------------*/
/*  REMOVE FILE OVERRIDES                                           */
/*------------------------------------------------------------------*/
             CLOF       OPNID(HPPF034)
             DLTOVR     FILE(HPPF034)

             CLOF       OPNID(HSL034E)
             DLTOVR     FILE(HSL034E)

             ENDPGM

