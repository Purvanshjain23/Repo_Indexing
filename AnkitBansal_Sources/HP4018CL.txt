/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4018CL                                      */
/*  PROGRAM NAME:    LISTING OF FARM HEALTH STATUS                 */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     02/18/03                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4018-WORK WITH FARM HEALTH STATUS.          */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(128)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDHCCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDHSFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 2 5))
             CHGVAR     VAR(&LDHCCD) VALUE(%SST(*LDA 7 5))
             CHGVAR     VAR(&LDHSFL) VALUE(%SST(*LDA 12 1))
             CHGVAR     VAR(&LDFCYMD) VALUE(%SST(*LDA 21 8))
             CHGVAR     VAR(&LDTCYMD) VALUE(%SST(*LDA 35 8))

/*------------------------------------------------------------------*/
/* BUILD QUERY                                                      */
/*------------------------------------------------------------------*/

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HFFSCD *NE 0')

/*  IF FARM                                                      */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND HFFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 30 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HEALTH CODE                                               */

             IF         COND(&LDHCCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 36 17)) VALUE('*AND HFHCCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 53 5)) VALUE(&LDHCCD)
             CHGVAR     VAR(%SST(&QDATA 58 1)) VALUE('"')
             ENDDO

/*  IF HEALTH STATUS FLAG                                        */

             IF         COND(&LDHSFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 60 17)) VALUE('*AND HFHSFL +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 77 1)) VALUE(&LDHSFL)
             CHGVAR     VAR(%SST(&QDATA 78 1)) VALUE('"')
             ENDDO

/*  SELECT RECORDS WITHIN DATE RANGE                               */

             IF         COND(&LDFCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 80 15)) VALUE('*AND HFEFDT *GE')
             CHGVAR     VAR(%SST(&QDATA 96 8)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA 105 15)) VALUE('*AND HFEFDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 121 8)) VALUE(&LDTCYMD)
             ENDDO


/*------------------------------------------------------------------*/
/* RUN QUERY OVER THE FARM HEALTH STATUS FILE                       */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSL112B)  SHARE(*YES)
             OPNQRYF    FILE((HSL112B)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  CALL LISTING PROGRAM                                             */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6018)

             CLOF       OPNID(HSL112B)
             DLTOVR     FILE(*ALL)

             ENDPGM

