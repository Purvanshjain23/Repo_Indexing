      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP233 - WRITE EXPENSE EDIT FILE TO HPS EXPENSE
      *                      TICKET FILE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     01/05/98
      *
      * FUNCTION: THIS PROGRAM PERFORMS THE FOLLOWING FUNCTIONS:
      *
      *           1) READS THE EXPENSE INTERFACE EDIT FILE
      *
      *           2) MAPS IN PERIOD BEGIN/END DATES AND ASSIGNS A
      *              TICKET NUMBER
      *
      *           3) WRITES AN EXPENSE TICKET RECORD TO THE HPS
      *              EXPENSE TICKET FILE FOR EACH RECORD IN THE EDIT FILE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 01/31/01  LeAnne Fedor
      *           In preparation for allowing groups on a farm to have different
      *           production types, we will no longer default the 'production type' of a farm
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *
      * 07/18/01  LeAnne Fedor
      *           Recompile only.
      *           Expense ticket amount expanded from 7,2 to 9,2.
      *
      * 08/22/01  LeAnne Fedor
      *           When 'cost center' is blank (which we expect will be never),
      *           we will find the first 'live' business office in the Business Office
      *           file and use it for the 'allocation level'.
      *
      * 01/29/03  LeAnne Fedor
      *           Logic will no longer summarize Edit records to the Expense Subclass level.
      *           Instead, logic will now write an HPS Expense Ticket record for EACH
      *           record in the Edit File.
      *
      * 03/02/16  Barb Gutierrez
      *           Retrieve allocated business office code based on company being processed
      *           when the cost center is blank. We were just getting the first record
      *           from the business office file.  Now that we have multiple companies,
      *           we can't do that.  (E5290)
      *
      * 04/06/16  Barb Gutierrez
      *           Changed to use the company number in the LDA to retrieve the allocated business
      *           office. Also added the population of the division code.  eE5290
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl009b   IF   E           K DISK
      *  Business office  K: cocd
      *
      *
     FHSL231B   IF   E           K DISK
      *  EDIT FILE FOR G/L TO HPS EXPENSE INTERFACE
      *
      *
     FHSP048    O    E           K DISK
      *   EXPENSE TICKET
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAXTSN'
      *---------------------------------------------------------------
     D                 DS
     D  DAXTSN                 1      7  0
      *
      *---------------------------------------------------------------
      *  LDA - REPORT OPTIONS
      *---------------------------------------------------------------
      *
     D                UDS
      *
     D  LDBPDT                 1     08  0
     D  LDEPDT                 9     16  0
     D  LDPER                 17     18  0
     D  LDCCYY                19     22  0
     D  LDCOCD                23     25
     D  LDBOBO                26     30
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS EACH RECORD IN THE EDIT FILE.
      *
     C     *LOVAL        SETLL     HSL231B
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL231B                                90
     C                   if        *in90 = *off and eeam <> 0                   IF NOT EOF
      *
     C                   EXSR      $wrt048
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * EOF PROCESSING
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * WRITE AN HPS EXPENSE TICKET
      *----------------------------------------------------------------
      *
     C     $wrt048       BEGSR
      *
     C                   CLEAR                   XTREC
      *
      * MOVE LDA FIELDS TO EXPENSE TICKET RECORD
      *
     C                   MOVEL     LDCOCD        XTCOCD
     C                   Z-ADD     LDBPDT        XTXBDT
     C                   Z-ADD     LDEPDT        XTXEDT
      *
      *  This business office is based on the company being processed.
      *
     C                   MOVEL     LDBOBO        XTBOBO
      *
      *
      * MOVE IN DEFAULTS:
      *   1) TICKET STATUS IS 'ENTERED'
      *   2) ALLOCATION FLAG IS 'TO BE ALLOCATED'
      *
     C                   MOVEL     'EN'          XTXSCD
     C                   MOVEL     'T'           XTALFL
      *
      * If 'cost center' is blank, then you must default in a 'live' business
      * office for the 'allocation level'.
      *
     C                   if        eecjd = *blank                               If no center
     C     ldcocd        setll     hsl009b
      *
     C                   dou       *in91 = *on or xtalbo <> *blank              Do loop
     C     ldcocd        reade     hsl009b                                91
     C                   if        *in91 = *off and bodvcd = 'LIVE '            If not EOF
     C                   move      bobobo        xtalbo                         allocated bus off
     C                   move      bodvcd        xtdvcd                         division code
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endif                                                  If no center
      *
      * SET UP FIELDS
      *
     C                   MOVEL     EEEXCD        XTEXCD
     C                   MOVEL     EEESCD        XTESCD
     C                   MOVEL     EEECCD        XTECCD
     C                   MOVEL     EERPCD        XTRPCD
     C                   MOVEL     EEAMCD        XTAMCD
     C                   MOVEL     EECJD         XTCJD
     C                   Z-ADD     EEAM          XTXTAM
      *
      *
      * RETRIEVE NEXT SYSTEM-ASSIGNED TICKET NUMBER
      *
     C     *LOCK         IN        DAXTSN
     C                   ADD       1             DAXTSN
     C                   OUT       DAXTSN
     C                   Z-ADD     DAXTSN        XTXTSN
      *
     C                   WRITE     XTREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE THE DATA AREA THAT CONTAINS THE EXPENSE TICKET NUMBER
      *
     C     *DTAARA       DEFINE                  DAXTSN
      *
      *
     C                   ENDSR
      /EJECT
