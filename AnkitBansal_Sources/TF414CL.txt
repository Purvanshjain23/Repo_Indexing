/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF414CL                                       */
/*  PROGRAM NAME:    MISSING COST/YIELD REPORT                     */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     09/28/06                                      */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/* 10/17/07  LeAnne Ramsey                                         */
/*           Per Damon Ginther, always exclude items with a        */
/*           TF Classification Code:                               */
/*                        REN=Rendering                            */
/*                        OFF=Offal                                */
/* 10/26/07  LeAnne Ramsey                                         */
/*           Per Damon Ginther, ONLY include Finished Goods        */
/*           items on the "Byproduct" version.                     */
/*                                                                 */
/* 11/21/08  LeAnne Ramsey                                         */
/*           As part of synchronizing the LDAs between the         */
/*           TFS Margin Adjustment Close and the Meat Costing,     */
/*           the LDA position of "report" was moved from 200 to    */
/*           113.                                                  */
/*                                                                 */
/* 02/03/09  LeAnne Ramsey                                         */
/*           We are including WPs on the new Weekly Yield Reports. */
/*           Damon G. needed them on this Report; but they are not */
/*           in the TFP010-Weekly Product Revenue file. These WPs  */
/*           are in the inventory files. So, we had to go to a     */
/*           Workfile to bring all the Items together.             */
/*                                                                 */
/* 02/10/09  LeAnne Ramsey                                         */
/*           From Meat Costing, this CL is called twice back-to-   */
/*           back (once for ByProducts; once for Non-ByProducts).  */
/*           The job abended on the second call because the        */
/*           workfile TFP314 already existed in QTEMP.             */
/*           So, to prevent the abend, we will always delete       */
/*           the file from QTEMP.                                  */
/*                                                                 */
/* 05/06/09  LeAnne Ramsey                                         */
/*           Make Print logic match Meat Costing.                  */
/*                                                                 */
/* 06/17/09  LeAnne Ramsey                                         */
/*           Added logic for ROP102-Component Item Meat Cost.      */
/*           We will be using it to add a new section "Component   */
/*           Items with no Base Price" to TF614 for Tom Dye.       */
/*                                                                 */
/* 11/13/17  Danny Nguyen  R12011A-Weekly Product Revenue          */
/*           File TFP010 was changed. Recompile only.              */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(240)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDRPT)  TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDWEDT) VALUE(%SST(*LDA 29 8))
             CHGVAR     VAR(&LDRPT)  VALUE(%SST(*LDA 113 1))

/*----------------------------------------------------------------*/
/* SET PRINT PARAMETERS FROM LDA                                  */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))
/*---------------------------------------------------------------------*/
/* CREAT WORKFILE                                                      */
/*---------------------------------------------------------------------*/

             DLTF       FILE(QTEMP/TFP314)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(TFP314) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*---------------------------------------------------------------------*/
/* EXTRACT DATA                                                        */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 16)) VALUE('PRPREXFL *EQ +
                          "N"')
             CHGVAR     VAR(%SST(&QDATA 18 15)) VALUE('*AND PRWEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 34 8)) VALUE(&LDWEDT)
             CHGVAR     VAR(%SST(&QDATA 43 56)) VALUE('*AND (PRASLLB +
                          *NE 0 *OR PRSPULB *NE 0 *OR PRTPULB *NE 0)')
             CHGVAR     VAR(%SST(&QDATA 100 23)) VALUE('*AND PRTFCLCD *NE +
                          "REN"')
             CHGVAR     VAR(%SST(&QDATA 124 23)) VALUE('*AND PRTFCLCD *NE +
                          "OFF"')


/* BYPRODUCTS */
             IF         COND(&LDRPT *EQ 'B') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 148 22)) VALUE('*AND PRTFCGCD +
                          *EQ "BP"')
             CHGVAR     VAR(%SST(&QDATA 171 20)) VALUE('*AND PRMIXFL +
                          *EQ "Y"')
             CHGVAR     VAR(%SST(&QDATA 192 21)) VALUE('*AND +
                          PRITYCD *EQ "FG"')
             ENDDO

/* NO BYPRODUCTS */
             IF         COND(&LDRPT *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 148 23)) VALUE('*AND PRTFCLCD +
                          *NE "CVZ"')
             CHGVAR     VAR(%SST(&QDATA 172 68)) VALUE('*AND (PRTFCGCD +
                          *EQ "CV" *OR PRTFCGCD *EQ "NV" *OR PRTFCGCD +
                          *EQ "VA")')
             ENDDO

             OVRDBF     FILE(TFL010F) SHARE(*YES)
             OPNQRYF    FILE((TFL010F)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*---------------------------------------------------------------------*/
/* BUILD THE WORKFILE                                                  */
/*---------------------------------------------------------------------*/

             CALL       PGM(TF314)

             CLOF       OPNID(TFL010F)
             DLTOVR     FILE(*ALL)

/*---------------------------------------------------------------------*/
/* EXTRACT COMPONENT ITEM MEAT COST RECORDS                            */
/*---------------------------------------------------------------------*/
/* Extract records for the WeekEnding Date where:                      */
/*   1) BOM Type is C=Consumed                                         */
/*   2) Item is NOT 99000-Cut Loss                                     */
/*   3) Component Item Base price is zero                              */


             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('CMWEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDWEDT)
             CHGVAR     VAR(%SST(&QDATA 21 20)) VALUE('*AND CMBOMTY +
                          *EQ "C"')
             CHGVAR     VAR(%SST(&QDATA 42 21)) VALUE('*AND CMITCD +
                          *NE 99000')
             CHGVAR     VAR(%SST(&QDATA 64 19)) VALUE('*AND CMCIBSPR +
                          *EQ 0')

             OVRDBF     FILE(ROP102) SHARE(*YES)
             OPNQRYF    FILE((ROP102)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD((CMCIITCD) (CMITCD)) SEQONLY(*YES)

/*---------------------------------------------------------------------*/
/* PRINT THE REPORT                                                    */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF614)

             CLOF       OPNID(ROP102)
             DLTOVR     FILE(*ALL)

             ENDPGM

