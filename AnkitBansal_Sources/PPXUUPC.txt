/********************************************************************/
/*                                                                  */
/*  PROGRAM   - PPXUUPC -  PMT PASS INV PRNT&POST UP                */
/*  SYNOPSIS  - PROMPT JOB CONTROL FOR PRINTER OVERRIDE INFORMATION */
/*              SUBMIT PPXVUPC PRT INVC PRINT & POST CL PROGRAM.    */
/*                                                                  */
/*              FOR THIRD PARTY ONLY, IF AN ORDER NUMBER IS PASSED  */
/*              THEN ONLY THAT ORDER NUMBER WILL BE PROCESSED       */
/*              COMPLETELY THROUGH THE INVOICE PRINT AND POST.      */
/*              THIS IS TO ACCOMMODATE INDIVIDUAL INVOICES THAT     */
/*              MAY BE FAXED OR EMAILED OR EMERGENCIES.             */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 04/17/2006                                          */
/*            - LARA BUSER                                          */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &BILTYP     - BILLING ACTIVITY TYPE (INVOICE, MEMO)     */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     I    &COMPNYN    -  COMPANY NUMBER (PASSED)                  */
/*     B    &THRDATN    -  THRU SHIP DATE (PASSED)                  */
/*     I    &LOADIDN    -  LOAD ID   (PASSED)                       */
/*     I    &ORDRN      -  ORDER NO. (PASSED)                       */
/*     B    &COPYN      -  NO. OF COPIES  (PASSED)                  */
/*     I    &PRCUSTT    -  THRID PARTY OR INTER-COMPANY (PASSED)    */
/*     I    &STATUSYN   -  CONTROL PRINT OF AMOUNTS                 */
/*     I    &JOBNME     -  JOB NAME = INVPRTPOST                    */
/********************************************************************/
/* MODIFICATIONS:                                                   */
/*------------------------------------------------------------------*/
/* RMC  01/11/2010 FP1322 IF OUTQ IS PASSED IN, DONT CALL PRINT PROMPT */
/*                 SCAN: FP1322                                       */
/********************************************************************/
 PPXUUPC:    PGM        PARM(&RETURN &PGM &APPLIC &BILTYP &DESC +
                          &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COMPNYN &THRDATN &LOADIDN &ORDRN &COPYN +
                          &STATUSYN &PRCUSTT &JOBNME)
/* PARMS                                                              */
        DCL    &RETURN     *CHAR   (7)   /* RETURN CODE               */
        DCL    &PGM        *CHAR   (10)  /* CL TO SUBMIT              */
        DCL    &APPLIC     *CHAR   (06)  /* APPLICATION CODE          */
        DCL    &BILTYP     *CHAR   (01)  /* BILL TYPE (INVOICE,MEMO)  */
        DCL    &DESC       *CHAR   (25)  /* PRT PGM DESCRIPTION       */
        DCL    &OUTQ       *CHAR   (10)  /* OUTPUT QUEUE              */
        DCL    &HOLD       *CHAR   (4)   /* HOLD STATUS               */
        DCL    &SAVE       *CHAR   (4)   /* SAVE STATUS               */
        DCL    &NQSTAT     *CHAR   (01)  /* IF MUST RUN IN NIGHT Q    */
        DCL    &NIGHTQ     *CHAR   (10)  /* NIGHT JOB QUEUE           */
        DCL    &COMPNYN    *DEC          /* COMPANY NUMBER (PASSED)   */
        DCL    &THRDATN    *DEC          /* THRU SHIP DATE (PASSED)   */
        DCL    &LOADIDN    *DEC          /* LOAD ID        (PASSED)   */
        DCL    &ORDRN      *DEC          /* ORDER NUMBER   (PASSED)   */
        DCL    &COPYN      *DEC          /* NO. OF COPIES  (PASSED)   */
        DCL    &JOBNME     *CHAR   (10)  /* JOB NAME                  */
        DCL    &PRCUSTT    *CHAR   (02)  /* PROCESS CUST TYPE         */
        DCL    &STATUSYN   *CHAR   (01)  /* PRINT AMOUNT CONTROL      */

/* INTERNAL FIELDS                                                    */
        DCL    &COPY       *DEC    (2 0) /* NUMBER OF COPIES          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/* PROMPT FOR PRINTER OVERRIDE INFORMATION                          */
/********************************************************************/
FP1322:      IF         COND(&OUTQ > '          ') THEN(GOTO +
                          CMDLBL(SKIPPMT))
 PRINTER:    CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

SKIPPMT:     CHGVAR     VAR(&COPYN) VALUE(&COPY)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)

/********************************************************************/
/*   SUBMIT THE INVOICE PRINT AND POST PROGRAM - PPXVUPC            */
/********************************************************************/

 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &LOADIDN +
                          &THRDATN &ORDRN &BILTYP &DESC &OUTQ &HOLD +
                          &SAVE &COPYN &NQSTAT &NIGHTQ &JOBNME +
                          &PRCUSTT &STATUSYN)) JOB(&JOBNME) +
                          JOBQ(&NIGHTQ)

             GOTO       CMDLBL(END)

  /*  SUSAN WAS TESTING                                             */
  /*L:       CALL       PGM(&PGM) PARM(&COMPNYN &LOADIDN &THRDATN + */
  /*                      &ORDRN &BILTYP &DESC &OUTQ &HOLD &SAVE +  */
  /*                      &COPYN &NQSTAT &NIGHTQ &JOBNME &PRCUSTT + */
  /*                      &STATUSYN)                                */
  /*                                                                */
  /*         GOTO       CMDLBL(END)                                 */
  /*                                                                */

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U9999')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             CHGJOB     LOG(4 20 *SECLVL) LOGCLPGM(*YES)

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
