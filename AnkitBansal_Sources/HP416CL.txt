/********************************************************************/
/*  SYSTEM:            HOG PRODUCTION SYSTEM                       */
/*  PROGRAM NUMBER:    HP416CL                                     */
/*  PROGRAM NAME:      DELETED MOVEMENT LISTING                    */
/*  PROGRAMMER:        LEANNE FEDOR                                */
/*  DATE:              04/02/96                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> GENERATE THE DELETED MOVEMENT LISTING.      */
/*                     REPORT VALUES ARE PASSED THRU THE LDA FROM  */
/*                     HP416.                                      */
/*                                                                 */
/***************************************************************** */
/* MODIFICATIONS                                                  ED*/
/* 99/99/99  PROGRAMMER                                            */
/********************************************************************/
/*                                                                 */
             PGM
/*                                                                 */
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(375)
/*                                                                 */
             DCL        VAR(&LDUSCD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LDFDYM) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTDYM) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFSYM) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTSYM) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDMTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDORBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORPT) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORPP) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORFS) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDDNBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNPT) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNPP) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNFS) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDCVNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
/*                                                                 */
             CHGVAR     VAR(&LDUSCD) VALUE(%SST(*LDA 1 10))
             CHGVAR     VAR(&LDFDYM) VALUE(%SST(*LDA 11 8))
             CHGVAR     VAR(&LDTDYM) VALUE(%SST(*LDA 19 8))
             CHGVAR     VAR(&LDFSYM) VALUE(%SST(*LDA 27 8))
             CHGVAR     VAR(&LDTSYM) VALUE(%SST(*LDA 35 8))
             CHGVAR     VAR(&LDMTCD) VALUE(%SST(*LDA 43 1))
             CHGVAR     VAR(&LDORBO) VALUE(%SST(*LDA 44 5))
             CHGVAR     VAR(&LDORPT) VALUE(%SST(*LDA 49 5))
             CHGVAR     VAR(&LDORPP) VALUE(%SST(*LDA 54 5))
             CHGVAR     VAR(&LDORFS) VALUE(%SST(*LDA 59 5))
             CHGVAR     VAR(&LDDNBO) VALUE(%SST(*LDA 64 5))
             CHGVAR     VAR(&LDDNPT) VALUE(%SST(*LDA 69 5))
             CHGVAR     VAR(&LDDNPP) VALUE(%SST(*LDA 74 5))
             CHGVAR     VAR(&LDDNFS) VALUE(%SST(*LDA 79 5))
             CHGVAR     VAR(&LDCVNO) VALUE(%SST(*LDA 84 8))

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('DMMTCD *NE " "')

/*  IF USER                                                      */

             IF         COND(&LDUSCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND DMUSCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 33 10)) VALUE(&LDUSCD)
             CHGVAR     VAR(%SST(&QDATA 43 1)) VALUE('"')
             ENDDO

/*  IF ORIGIN BUSINESS OFFICE                                    */

             IF         COND(&LDORBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 17)) VALUE('*AND DMORBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 62 5)) VALUE(&LDORBO)
             CHGVAR     VAR(%SST(&QDATA 67 1)) VALUE('"')
             ENDDO

/*  IF ORIGIN PRODUCTION TYPE                                    */

             IF         COND(&LDORPT *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 69 17)) VALUE('*AND DMORPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 86 5)) VALUE(&LDORPT)
             CHGVAR     VAR(%SST(&QDATA 91 1)) VALUE('"')
             ENDDO

/*  IF ORIGIN PRODUCTION PHASE                                   */

             IF         COND(&LDORPP *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 93 17)) VALUE('*AND DMORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 110 5)) VALUE(&LDORPP)
             CHGVAR     VAR(%SST(&QDATA 115 1)) VALUE('"')
             ENDDO

/*  IF ORIGIN FARM SITE                                          */

             IF         COND(&LDORFS *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 117 4)) VALUE('*AND')
             CHGVAR     VAR(%SST(&QDATA 122 10)) VALUE('DMORFS *EQ')
             CHGVAR     VAR(%SST(&QDATA 133 5)) VALUE(&LDORFS)
             ENDDO

/*  IF DESTINATION BUSINESS OFFICE                              */

             IF         COND(&LDDNBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 17)) VALUE('*AND DMDNBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 156 5)) VALUE(&LDDNBO)
             CHGVAR     VAR(%SST(&QDATA 161 1)) VALUE('"')
             ENDDO

/*  IF DESTINATION PRODUCTION TYPE                              */

             IF         COND(&LDDNPT *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 163 17)) VALUE('*AND DMDNPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 180 5)) VALUE(&LDDNPT)
             CHGVAR     VAR(%SST(&QDATA 185 1)) VALUE('"')
             ENDDO

/*  IF DESTINATION PRODUCTION PHASE                             */

             IF         COND(&LDDNPP *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 187 17)) VALUE('*AND DMDNPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 204 5)) VALUE(&LDDNPP)
             CHGVAR     VAR(%SST(&QDATA 209 1)) VALUE('"')
             ENDDO

/*  IF DESTINATION FARM SITE                                    */

             IF         COND(&LDDNFS *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 211 4)) VALUE('*AND')
             CHGVAR     VAR(%SST(&QDATA 216 10)) VALUE('DMDNFS *EQ')
             CHGVAR     VAR(%SST(&QDATA 227 5)) VALUE(&LDDNFS)
             ENDDO

/*  IF CUSTOMER NUMBER                                          */

             IF         COND(&LDCVNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 233 4)) VALUE('*AND')
             CHGVAR     VAR(%SST(&QDATA 238 10)) VALUE('DMCVNO *EQ')
             CHGVAR     VAR(%SST(&QDATA 249 8)) VALUE(&LDCVNO)
             ENDDO

/*  IF FROM DELETE DATE                                         */

             IF         COND(&LDFDYM *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 258 15)) VALUE('*AND DMDMDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 274 8)) VALUE(&LDFDYM)
             CHGVAR     VAR(%SST(&QDATA 283 15)) VALUE('*AND DMDMDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 299 8)) VALUE(&LDTDYM)
             ENDDO

/*  IF FROM SCHEDULE DATE                                       */

             IF         COND(&LDFSYM *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 308 15)) VALUE('*AND DMSCDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 324 8)) VALUE(&LDFSYM)
             CHGVAR     VAR(%SST(&QDATA 333 15)) VALUE('*AND DMSCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 349 8)) VALUE(&LDTSYM)
             ENDDO

/*  RUN OPEN QUERY                                              */

             OVRDBF     FILE(HSP080) SHARE(*YES)
             OPNQRYF    FILE((HSP080)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/* SET UP PRINTER OVERRIDES                                     */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/* PRINT LISTING                                                    */
/*------------------------------------------------------------------*/

             CALL       PGM(HP616)

 EXIT:       CLOF       OPNID(HSP080)
             DLTOVR     FILE(*ALL)

             ENDPGM

