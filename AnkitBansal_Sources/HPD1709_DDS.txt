     A*%%TS  SD  20040217  163054  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1709
     A*  TITLE:         DELETE BUDGET ITEMS FOR A TEMPLATE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/17/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF11(11 'DELETE')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1709R1
     A*%%TS  SD  20040217  163054  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 16'DELETE BUDGET ITEMS FOR A BUDGET T-
     A                                      EMPLATE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'This function deletes all Budget I-
     A                                      tems that exist for the Template.'
     A                                  6  2'If you are sure that you want to d-
     A                                      elete these Budget Items from the T-
     A                                      emplate,'
     A                                  7  2'take'
     A                                  7  7'F11=Delete.'
     A                                      DSPATR(HI)
     A                                  9  2'Budget template .........:'
     A            D1BTCD    R        O  9 29REFFLD(@@BTCD)
     A            D1BTDS    R        O  9 38REFFLD(@@BTDS)
     A                                 10  2'Number of budget items ..:'
     A            D1COUNT        5Y 0O 10 29EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1709K1
     A*%%TS  SD  20040217  163054  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F11=Delete'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1709ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP1709EC                  SFLCTL(HP1709ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
