     A*%%TS  SD  20171213  091012  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD453
     A*  TITLE:         SPECIFY KILL DATA TO COPY TO HOG PRODUCTION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/05/94
     A*
     A*****************************************************************
     A* Modifications:
     A* 12/13/2017 JBB  E010812 - Market Hogs to OTC
     A*            Added Plant Code to screen to restrict the records
     A*            selected to a single plant.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP453R1
     A*%%TS  SD  19950907  092020  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 13'SPECIFY KILL DATA TO COPY FROM THE-
     A                                       HOG PROCUREMENT AND'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  3 17'EVALUATION SYSTEM TO THE HOG PRODU-
     A                                      CTION SYSTEM'
     A                                      DSPATR(HI)
     A                                  5  2'Plant Code ............'
     A            DFPLCD    R        B  5 28REFFLD(@@NU30)
     A                                      EDTCDE(Z)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Cut-off kill date .....'
     A            DFMDY     R     Y  B  7 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  9  2'Edit or final .........'
     A                                  9 26'?'
     A            DFEFFL    R        B  9 28REFFLD(@@FLAG)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFEFDS        10A  O  9 32
     A  36                             12 13'YOU ARE GETTING READY TO PROCESS'
     A                                      COLOR(RED)
     A  36                             12 46'KILL DATA FOR COMPANY'
     A                                      COLOR(RED)
     A  36        DFPLT     R        O 12 68REFFLD(@@NU30)
     A                                      COLOR(RED)
     A  36                             13 24'DO YOU WANT TO CONTINUE?'
     A                                      COLOR(RED)
     A  36        DFYESNO   R        B 13 49REFFLD(@@UCFL)
     A N35                                  DSPATR(HI)
     A  35
     AO 36                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A  36                             13 51'(Y or N)'
     A                                      COLOR(RED)
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP453K1
     A*%%TS  SD  20000207  134111  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A  44N99     DFEFDS        10A  O 23 32
     A N99                             23 44'F17=Print Options'
     A N99                             23 63'F20=Spool Files'
      *
      *--------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------
     A          R HP453ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------
     A*
     A          R HP453EC                   SFLCTL(HP453ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
