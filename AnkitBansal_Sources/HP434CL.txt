/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP434CL                                       */
/*  PROGRAM NAME:    SPECIFY FEED TICKETS TO INTERFACE             */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/29/97                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP434-SPECIFY FEED TICKETS TO INTERFACE.      */
/*                   USER SUBMISSIONS ARE PASSED IN THE LDA        */
/*                                                                 */
/*******************************************************************/
/* DATE      PROGRAMMER                                            */
/*                                                                 */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*******************************************************************/

             PGM


/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&LDERFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)


/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE AND CALL BATCH EDIT PROGRAM                  */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */
/* ALSO, SHARE THE OPEN DATA PATH SO ERROR MESSAGES WILL PRINT RIGHT*/
/* THE PAGE BREAKS MAY BE WEIRD SINCE WE HAVE TO 'COUNT' LINES      */
/* IN THE BATCH EDIT PROGRAM TO PRINT ERROR MESSAGES.               */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD) SHARE(*YES)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP238)
             DLTOVR     FILE(QPRINT)

/*-------------------------------------------------------------------*/
/*  IF THERE ARE NO ERRORS,                                          */
/*        CALL THE POST PROGRAM TO WRITE THE INTERFACE RECORDS FROM  */
/*        THE EDITING FILES TO THE HPS FEED TICKET FILES.            */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&LDERFL) VALUE(%SST(*LDA 2 1))
             IF         COND(&LDERFL *EQ 'N') THEN(DO)
             CALL       PGM(HP234)
             MONMSG     MSGID(CPF0000)
             ENDDO


 EXIT:
             CHGDTAARA  DTAARA(DAGROS (2 1)) VALUE(' ')

             ENDPGM

