      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3048
      * TITLE:       Build Workfile: Reported Weekly Activity Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/26/02
      *
      * FUNCTION:  Batch program to build workfile for Reported Weekly Activity Listing.
      *
      *            This listing is submitted from HP4048-Work with Reported Weekly Activity.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fhsl034d   if   e           k disk
      *   Hog group
      *
      *
     Fhsl068d   if   e           k disk
      *    Killed/dead hogs
      *
      *
     Fhsp102    if   e           k disk
      *   Reported weekly activity (records selected in open query)
      *
      *
     Fhsp3048   o    e           k disk
      *  Workfile: Reported weekly activity
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
     D svhgsn          s                   like(rwhgsn)
      *
      * Work fields
      *
     D wkfsnm          s                   like(fsfsnm)
     D wkbegdt         s                   like(rwwedt)
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfscd                 1      5  0
     D  ldhgsn                 6     12  0
     D  ldhgcd                13     19
     D  ldcdyr                20     23  0
     D  ldcdwk                24     25  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read each Reported Weekly Activity record selected by the open query.
      *
     C     *loval        setll     hsp102
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp102                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Get farm info when group changes
      *
     C                   if        rwhgsn <> svhgsn
     C                   exsr      $farm
     C                   endif
      *
     C                   if        procfl = yes
     C                   exsr      $write3048
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------
      * Retrieve Farm info
      *--------------------------------------------------------------------------
      *
     C     $farm         begsr
      *
     C                   move      yes           procfl
      *
      * First, retrieve the Hog Group record to get the Farm Site.
      *
     C     rwhgsn        chain     hsl034d                            92
     C                   select
     C                   when      *in92 = *on
     C                   move      no            procfl
      *
     C                   when      ldfscd <> 0 and ldfscd <> hgfscd
     C                   move      no            procfl
     C                   other
      *
     C     hgfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fsfsnm        wkfsnm
     C                   else
     C                   move      *blank        wkfsnm
     C                   endif
      *
     C                   endsl
      *
     C                   z-add     rwhgsn        svhgsn
      *
     c                   endsr
      /EJECT
      *--------------------------------------------------------------------------
      * Write workfile record
      *--------------------------------------------------------------------------
      *
     C     $write3048    begsr
      *
     C                   move      hghgcd        w1hgcd
     C                   z-add     hgfscd        w1fscd
     C                   move      wkfsnm        w1fsnm
      *
     C                   z-add     rwhgsn        w1hgsn
     C                   z-add     rwbginhd      w1bginhd
     C                   z-add     rwtihd        w1tihd
     C                   z-add     rwtohd        w1tohd
     C                   z-add     rwcuhd        w1cuhd
     C                   z-add     rwmkhd        w1mkhd
     C                   z-add     rwcdyr        w1cdyr
     C                   z-add     rwcdwk        w1cdwk
     C                   z-add     rwwedt        w1wedt
      *
      * Flip week-ending date to MMDDYY format
      *
     C     *iso          test(d)                 rwwedt                 92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             w1wedt
     C                   else
     C                   move      rwwedt        wkisodate
     C     *mdy          move      wkisodate     w1wemdy
     C                   endif                                                  If bad date
      *
      * Retrieve killed/dead head for this group/week
      *
     C                   if        w1wedt <> 0                                  If OK date
     C                   subdur    6:*days       wkisodate
     C                   move      wkisodate     wkbegdt
      *
     C     key01         setll     hsl068d
      *
     C                   dou       *in91 = *on                                  Do dead
     C     rwhgsn        reade     hsl068d                                91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       kdkdhd        w1kdhd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do dead
     C                   endif                                                  If OK date
      *
      * Calculate Ending Inventory
      *
      *
     C                   z-add     w1bginhd      w1eninhd
     C                   add       w1tihd        w1eninhd
     C                   sub       w1tohd        w1eninhd
     C                   sub       w1cuhd        w1eninhd
     C                   sub       w1mkhd        w1eninhd
     C                   sub       w1kdhd        w1eninhd
      *
      * Write workfile record
      *
     C                   write     w1rec
     C                   clear                   w1rec
      *
     c                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    rwhgsn
     C                   kfld                    wkbegdt
      *
      *
     C                   endsr
      /EJECT
