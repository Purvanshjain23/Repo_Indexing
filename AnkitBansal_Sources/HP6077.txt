      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6077
      * TITLE:       Idle Farms Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/11/01
      *
      * FUNCTION:  Batch program to list the farms that had no open groups for the
      *            specified date range.
      *
      *            This listing is submitted from HP4077-Specify Options for
      *            Idle Farms Report.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 02/25/03  LeAnne Fedor
      *           Added a new selector and column for active/inactive status.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site                (records selected by open query)
      *
      *
     Fhsl034a   if   e           k disk
      *   Hog group                (records selected by open query)
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D h1fsbo          s                   like(ldptcd)
     D h1ptcd          s                   like(ldptcd)
     D h1ppcd          s                   like(ldppcd)
     D h1aist          s              3
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfmdy                 1      6  0
     D  ldtmdy                 7     12  0
     D  ldfcymd               13     20  0
     D  ldtcymd               21     28  0
      *
     D  ldfsbo                29     33
     D  ldptcd                34     38
     D  ldppcd                39     43
     D  ldcell                44     45  0
     D  ldaist                46     46
      *
     D  ldbods                51     80
     D  ldptds                81     90
     D  ldppds                91    120
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
     C     *loval        setll     hsp018
     C                   except    h1hdr
      *
      * Read each farm that was selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * If no groups are open for the farm, print the farm on the report.
      *
     C     fsfscd        chain     hsl034a                            92
     C                   if        *in92 = *on                                  If idle
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    l1dtl
     C                   move      yes           prtfl
     C                   endif                                                  If idle
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up headings
      *
     C                   if        ldfsbo = *blank
     C                   movel     'ALL'         h1fsbo
     C                   else
     C                   move      ldfsbo        h1fsbo
     C                   endif
      *
     C                   if        ldptcd = *blank
     C                   movel     'ALL'         h1ptcd
     C                   else
     C                   move      ldptcd        h1ptcd
     C                   endif
      *
     C                   if        ldppcd = *blank
     C                   movel     'ALL'         h1ppcd
     C                   else
     C                   move      ldppcd        h1ppcd
     C                   endif
      *
     C                   if        ldcell = 0
     C                   seton                                        88
     C                   endif
      *
     C                   if        ldaist = *blank
     C                   movel     'ALL'         h1aist
     C                   else
     C                   movel     ldaist        h1aist
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           49 'IDLE FARM REPORT'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                           20 'From date .........:'
     O                       ldfmdy              30 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           20 'To date ...........:'
     O                       ldtmdy              30 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           20 'Business office ...:'
     O                       h1fsbo              27
     O                       ldbods              61
      *
      *
     O          e            h1hdr          1
     O                                           20 'Production type ...:'
     O                       h1ptcd              27
     O                       ldptds              41
      *
      *
     O          e            h1hdr          1
     O                                           20 'Production phase ..:'
     O                       h1ppcd              27
     O                       ldppds              61
      *
      *
     O          e            h1hdr          1
     O                                           20 'Cell ..............:'
     O              N88      ldcell        z     24
     O               88                          25 'ALL'
      *
     O          e            h1hdr          2
     O                                           20 'Active/inactive ...:'
     O                       h1aist              25
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                            6 'FARM'
     O                                           30 '-PRODUCTION-'
     O                                           80 'ACTIVE/'
     O          e            h1hdr          1
     O                                            6 'SITE'
     O                                           13 'CELL'
     O                                           23 'PHASE'
     O                                           30 'TYPE'
     O                                           45 'FARM NAME'
     O                                           80 'INACTIVE'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       fsfscd        z      6
     O                       fscell        z     11
     O                       fsppcd              23
     O                       fsptcd              31
     O                       fsfsnm              61
     O                       fsaist              77
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
