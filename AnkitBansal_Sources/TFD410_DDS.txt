     A*%%TS  SD  20040930  102433  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD410
     A*  TITLE:         SPECIFY WEEKLY REVENUE CLOSE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       09/16/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  44                                  CF08(08 'SUBMIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF410R1
     A*%%TS  SD  20040930  102433  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'SPECIFY WEEKLY REVENUE CLOSE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Year/week:'
     A                                      DSPATR(HI)
     A            D1YR      R        O  4 13REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      COLOR(PNK)
     A            D1WK      R        O  4 19REFFLD(@@WK)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      COLOR(PNK)
     A            D1WBMDY   R     Y  O  4 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  4 37'to'
     A            D1WEMDY   R     Y  O  4 40REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  6  2'Preliminary/final ..'
     A                                  6 23'?'
     A            D1PFCD    R        B  6 25REFFLD(@@PFCD)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1PFDS    R        O  6 28REFFLD(@@PFDS)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF410K1
     A*%%TS  SD  20010220  091710  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A  44                             23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF410ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF410EC                   SFLCTL(TF410ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
