     A*%%TS  SD  20090514  155441  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1030
     A*  TITLE:         REVISE REPORTED DAILY ACTIVITY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/22/02
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  61N62                               CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1030R1
     A*%%TS  SD  20090514  155441  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(39)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 23'REVISE REPORTED DAILY ACTIVITY'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  5  2'Farm site .......'
     A  61                              5 20'?'
     A            D1FSCD    R        B  5 22REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A  61N36                               DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A            D1FSNM    R        O  5 33REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  6  2'Daily date ......'
     A            D1MDY     R        B  6 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  61N34                               DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                  7  2'Dead head .......'
     A            D1DDHD    R        B  7 22REFFLD(@@HD05)
     A                                      EDTCDE(K)
     A N62N35                               DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  8  2'Destroyed head ..'
     A            D1DSHD    R        B  8 22REFFLD(@@HD05)
     A                                      EDTCDE(K)
     A N62N37                               DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  9  2'Manhours ........'
     A            D1MANHR   R        B  9 22REFFLD(HSRREF/@@MANHR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N39                               DSPATR(HI)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1030K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  61N62                          23 11'F4=Prompt'
     A N62N61                          23 23'F5=Refresh'
     A N62N61                          23 35'F11=Delete'
     A                                 23 45'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1030ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1030EC                  SFLCTL(HP1030ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
