     A*%%TS  SD  20040116  160402  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD186
     A*  TITLE:         MAINTAIN POSTED TRANSFER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          08/27/02
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  60  REVISE MODE
     A*  90  THE TRANSFER THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A                                      CA12(12 'PREVIOUS')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP186S1                   SFL
     A*%%TS  SD  20020822  141100  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDLNNO    R        H      REFFLD(MDLNNO)
     A            HDORHGSN  R        H      REFFLD(HGHGSN)
     A            HDSHLB    R        H      REFFLD(MDSHLB)
     A            HDDNHGSN  R        H      REFFLD(HGHGSN)
     A            HDDNOLDHG R        H      REFFLD(HGHGCD)
     A            HDQLLB    R        H      REFFLD(MDQLLB)
     A            HDRJLB    R        H      REFFLD(MDRJLB)
     A            HDDOLB    R        H      REFFLD(MDDOLB)
     A            DFORBLCD  R        B 18  2REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  45
     AO 55                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N45   N90
     A  42                                  DSPATR(HI)
     A N45   N90
     A  42                                  DSPATR(UL)
     A            DFORRMCD  R        B 18  8REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N47   N90
     A  42                                  DSPATR(HI)
     A N47   N90
     A  42                                  DSPATR(UL)
     A            DFORHGCD  R        B 18 14REFFLD(HGHGCD)
     A  44
     AO 54                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N44   N90
     A  42                                  DSPATR(HI)
     A N44   N90
     A  42                                  DSPATR(UL)
     A            DFSCHD         4Y 0B 18 22EDTCDE(M)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N74   N90
     A  42                                  DSPATR(HI)
     A N74   N90
     A  42                                  DSPATR(UL)
     A            DFSHHD         4Y 0B 18 28EDTCDE(M)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N71   N90
     A  42                                  DSPATR(HI)
     A N71   N90
     A  42                                  DSPATR(UL)
     A            DFDNBLCD  R        B 18 34REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  75
     AO 50                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N75   N90
     A  42                                  DSPATR(HI)
     A N75   N90
     A  42                                  DSPATR(UL)
     A            DFDNRMCD  R        B 18 40REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  77
     AO 59                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N77   N90
     A  42                                  DSPATR(HI)
     A N77   N90
     A  42                                  DSPATR(UL)
     A            DFDNHGCD  R     A  B 18 46REFFLD(HGHGCD)
     A  46
     AO 56                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N46   N90
     A  42                                  DSPATR(HI)
     A N46   N90
     A  42                                  DSPATR(UL)
     A            DFQLHD         4Y 0B 18 54EDTCDE(M)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N48   N90
     A  42                                  DSPATR(HI)
     A N48   N90
     A  42                                  DSPATR(UL)
     A            DFDOAHD        4Y 0B 18 60EDTCDE(M)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N70   N90
     A  42                                  DSPATR(HI)
     A N70   N90
     A  42                                  DSPATR(UL)
     A            DFKOAHD        4Y 0B 18 66EDTCDE(M)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N69   N90
     A  42                                  DSPATR(HI)
     A N69   N90
     A  42                                  DSPATR(UL)
     A            DFOPT          2A  B 18 72CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  90                                  DSPATR(ND)
     A N30   N90
     A  42                                  DSPATR(HI)
     A N30   N90
     A  42                                  DSPATR(UL)
     A            DFRJHD         4Y 0B 18 75EDTCDE(M)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N49   N90
     A  42                                  DSPATR(HI)
     A N49   N90
     A  42                                  DSPATR(UL)
     A                                 19  2'Type/phase/stat'
     A            DFORPTCD  R        O 19 18REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFORPPCD  R        O 19 24REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFORGSCD  R        O 19 30REFFLD(HGGSCD)
     A                                 19 35'Type/phase/stat'
     A            DFDNPTCD  R        O 19 51REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFDNPPCD  R        O 19 57REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFDNGSCD  R        O 19 63REFFLD(HGGSCD)
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP186C1                   SFLCTL(HP186S1)
     A*%%TS  SD  20040116  160402  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0002)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(59)
     A                                      SETOF(68)
     A                                      SETOF(69)
     A                                      SETOF(70)
     A                                      SETOF(71)
     A                                      SETOF(72)
     A                                      SETOF(73)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      SETOF(76)
     A                                      SETOF(77)
     A                                      SETOF(79)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 25'MAINTAIN POSTED TRANSFER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number .....:'
     A            C1MVSN    R        O  4 27REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  4 41'Movement status .......'
     A            C1MSCD    R        O  4 67REFFLD(MHMSCD)
     A                                  5  2'Scheduled ship date ..'
     A            C1MDY     R     S  B  5 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N76N90 42                            DSPATR(HI)
     A N76N90 42                            DSPATR(UL)
     A                                  5 41'Trucker ...............'
     A N90                              5 65'?'
     A            C1TICD    R        B  5 67REFFLD(MHTICD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N33N90                            DSPATR(HI)
     A  42N33N90                            DSPATR(UL)
     A                                  6  2'Shipped date .........'
     A            C1SMDY    R     S  B  6 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N38N90 42                            DSPATR(HI)
     A N38N90 42                            DSPATR(UL)
     A                                      TEXT('received date')
     A                                  6 41'Shipped time in/out ...'
     A            C1SITM    R        B  6 67REFFLD(MHSITM)
     A                                      EDTWRD(' 0:  -')
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N35N90 42                            DSPATR(HI)
     A N35N90 42                            DSPATR(UL)
     A            C1SOTM    R        B  6 75REFFLD(MHSOTM)
     A                                      EDTWRD(' 0:  -')
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N34N90 42                            DSPATR(HI)
     A N34N90 42                            DSPATR(UL)
     A                                  7  2'Received date ........'
     A            C1RMDY    R     S  B  7 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N39N90 42                            DSPATR(HI)
     A N39N90 42                            DSPATR(UL)
     A                                      TEXT('received date')
     A                                  7 41'Received time in/out ..'
     A            C1RITM    R        B  7 67REFFLD(MHRITM)
     A                                      EDTWRD(' 0:  -')
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N40N90 42                            DSPATR(HI)
     A N40N90 42                            DSPATR(UL)
     A            C1ROTM    R        B  7 75REFFLD(MHROTM)
     A                                      EDTWRD(' 0:  -')
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N41N90 42                            DSPATR(HI)
     A N41N90 42                            DSPATR(UL)
     A                                  8  2'Origin farm ..........'
     A N90                              8 25'?'
     A            C1ORFSCD  R        B  8 27REFFLD(FSFSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N31N90 42                            DSPATR(HI)
     A N31N90 42                            DSPATR(UL)
     A                                  8 41'Destination farm ......'
     A N90                              8 65'?'
     A            C1DNFSCD  R        B  8 67REFFLD(FSFSCD)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N32N90 42                            DSPATR(HI)
     A N32N90 42                            DSPATR(UL)
     A            C1ORFSNM  R        O  9  3REFFLD(FSFSNM)
     A            C1DNFSNM  R        O  9 49REFFLD(FSFSNM)
     A                                 10  1'Sched'
     A                                      DSPATR(HI)
     A                                 10  8'***** Shipped ******'
     A                                      DSPATR(HI)
     A                                 10 30'***** Arrived ******'
     A                                      DSPATR(HI)
     A                                 10 52'** KOA/DOA **'
     A                                      DSPATR(HI)
     A                                 10 66'** Received **'
     A                                      DSPATR(HI)
     A                                 11  2'Head'
     A                                 11  9'Head'
     A                                 11 16'Pounds'
     A                                 11 25'Avg'
     A                                      COLOR(BLU)
     A                                 11 31'Head'
     A                                 11 38'Pounds'
     A                                 11 47'Avg'
     A                                      COLOR(BLU)
     A                                 11 53'Head'
     A                                 11 58'Pounds'
     A                                 11 67'Head'
     A                                 11 73'Pounds'
     A            C1SCHD    R        B 12  1REFFLD(MDSCHD)
     A                                      EDTCDE(M)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N79N90                            DSPATR(HI)
     A  42N79N90                            DSPATR(UL)
     A            C1SHHD    R        B 12  8REFFLD(MDSHHD)
     A                                      EDTCDE(M)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N36N90                            DSPATR(HI)
     A  42N36N90                            DSPATR(UL)
     A            C1SHLB    R        B 12 15REFFLD(@@LB07)
     A                                      EDTCDE(M)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N37N90                            DSPATR(HI)
     A  42N37N90                            DSPATR(UL)
     A            C1AVSHLB  R        O 12 24REFFLD(HSRREF/@@NU40 *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            C1ARHD    R        B 12 30REFFLD(MDSHHD)
     A                                      EDTCDE(M)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N72N90                            DSPATR(HI)
     A  42N72N90                            DSPATR(UL)
     A            C1ARLB    R        B 12 37REFFLD(@@LB07)
     A                                      EDTCDE(M)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N73N90                            DSPATR(HI)
     A  42N73N90                            DSPATR(UL)
     A            C1AVARLB  R        O 12 46REFFLD(HSRREF/@@NU40 *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  68                                  DSPATR(RI)
     A            C1DOHD    R        O 12 52REFFLD(MDDOHD)
     A                                      EDTCDE(M)
     A            C1DOLB    R        O 12 59REFFLD(MDDOLB)
     A                                      EDTCDE(M)
     A            C1RCHD    R        O 12 66REFFLD(MDSHHD)
     A                                      EDTCDE(M)
     A            C1RCLB    R        O 12 73REFFLD(@@LB07)
     A                                      EDTCDE(M)
     A                                 13  2'----------- ORIGIN -----------'
     A                                      DSPATR(HI)
     A                                 13 34'------------------ DESTINATION ----
     A                                      ------------'
     A                                      DSPATR(HI)
     A                                 14 34'Reject options:'
     A                                      DSPATR(HI)
     A                                 14 50'2=Revise Reasons'
     A                                      DSPATR(HI)
     A N90                             14 67'4=Dlt Reasons'
     A                                      COLOR(BLU)
     A                                 15  4'?'
     A                                 15 10'?'
     A N90                             15 16'?'
     A                                 15 21'Sched'
     A                                 15 28'Ship'
     A                                 15 36'?'
     A                                 15 42'?'
     A N90                             15 48'?'
     A                                 15 54'Qual'
     A                                 15 61'DOA'
     A                                 15 67'KOA'
     A                                 15 72'-Reject-'
     A                                 16  2'Bldg'
     A                                 16  8'Room'
     A                                 16 14'Group'
     A                                 16 22'Head'
     A                                 16 28'Head'
     A                                 16 34'Bldg'
     A                                 16 40'Room'
     A                                 16 46'Group'
     A                                 16 54'Head'
     A                                 16 60'Head'
     A                                 16 66'Head'
     A                                 16 72'Op'
     A                                 16 75'Head'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP186K1
     A*%%TS  SD  20010206  110106  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 10'F4=Prompt'
     A N90                             23 20'F5=Refresh'
     A N90                             23 31'F9=Accept'
     A                                 23 52'F12=Previous'
     A                                 23 65'F13=Fold/Unfold'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP186ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP186EC                   SFLCTL(HP186ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
