      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP611
      * TITLE:       Group Source Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/21/99
      *
      *  FUNCTION:   This program prints all groups that were sources of hogs for
      *              a group.
      *
      *              It will print either a Detail Version (at the group level) or
      *              a Summary Version (at the farm level).
      *
      *              HP411CL runs an open query over the Hog Group file and
      *              then builds a workfile which is used in this program.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/25/01  LeAnne Fedor
      *           Added 'group type' as a selector.
      *
      * 12/17/03  LeAnne Fedor
      *           Added 'from/to closed date range' as a new selector.
      *
      * 11/30/07  LeAnne Ramsey
      *           Added 2 new columns to show the "overrides" that the user
      *           may have already entered for the group. The columns are:
      *             Origin BGF Farm Site
      *             Origin NUR Farm Site
      *           Also, went to 198 character line using the new printer file
      *           instead of the old John Erickson command.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp010    if   e           k disk
      *    Production Type
      *
      *
     Fhsp011    if   e           k disk
      *    Production Phase
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp130    if   e           k disk
      *    Group overrides
      *
      *
     Fhsp315    if   e           k disk
      *    Workfile: Destination hog groups
      *
      *
     Fhsp316    if   e           k disk
      *   Workfile: Farm summary
      *
      *
     Fhsp317    if   e           k disk
      *   Workfile: BGF groups
      *
      *
     Fhsl318a   if   e           k disk
      *   Workfile: Nursery groups
      *
      *
     Fhsl319a   if   e           k disk
      *   Workfile: Grow/Finish groups
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECifICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D hropen          c                   'All groups that are currently open'
     D hronegroup      c                   'N/A--A single group was selected  '
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Standard fields
      *
     D first           s              1    inz('Y')
     D dash            s            159    inz(*all'-')
     d rtime           s              6  0
      *
      * Save fields for control breaks
      *
     D svptcd          s                   like(wdptcd)
     D svppcd          s                   like(wdppcd)
     D svfscd          s                   like(fsfscd)
      *
      *
      * Print fields
      *
     D hffsnm          s                   like(fsfsnm)
      *
     D hrptds          s                   like(ptptds)
     D hrppds          s                   like(ppppds)
     D hrgtcd          s              3
     D hrmgds2         s             48
      *
     D dtghgcd         s                   like(wdhgcd)
     D dtnhgcd         s                   like(wdhgcd)
     D dtbhgcd         s                   like(wdhgcd)
      *
     D dtgfscd         s                   like(wdfscd)
     D dtnfscd         s                   like(wdfscd)
     D dtbfscd         s                   like(wdfscd)
      *
     D dtgblcd         s                   like(wdblcd)
     D dtnblcd         s                   like(wdblcd)
      *
     D dtggncd         s                   like(wgsrgncd)
     D dtngncd         s                   like(wgsrgncd)
     D dtbgncd         s                   like(wgsrgncd)
      *
     D dtobfs          s                   like(goobfs)
     D dtonfs          s                   like(goonfs)
      *
      *---------------------------------------------------------------
      * DATA STRUCTURES
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfsbo                 1      5
     D  ldfymd                 6     13  0
     D  ldtymd                14     21  0
     D  ldppcd                22     26
     D  ldfscd                27     31  0
     D  ldcfcymd              32     39  0
     D  ldctcymd              40     47  0
     D  ldptcd                48     52
     D  ldhgsn                53     59  0
     D  ldofmdy               60     65  0
     D  ldotmdy               66     71  0
      *
     D  ldbods                72    101
     D  ldptds               102    111
     D  ldppds               112    141
     D  ldfsnm               142    166
     D  ldhgcd               167    173
     D  ldrpfl               174    174
     D  ldrpds               175    181
     D  ldmgfl               182    182
     D  ldmgds               183    223
      *
     D  ldgtcd               224    224
     D  ldgtds               225    239
      *
     D  ldcfmdy              240    245  0
     D  ldctmdy              246    251  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process all records in the Destination Group workfile.
      *
     C     *loval        setll     hsp315
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp315                                 90
     C                   if        *in90 = *off                                 If not EOF
      * First time
     C                   if        first = yes
     C                   exsr      $first
     C                   endif
      *
     C                   seton                                        88
      *
      * Control break logic
      *   1) production type
      *   2) production phase
      *   3) farm site
     C                   select
     C                   when      wdptcd <> svptcd
     C                   exsr      $ptbrk
      *
     C                   when      wdppcd <> svppcd
     C                   exsr      $ppbrk
      *
     C                   when      wdfscd <> svfscd
     C                   exsr      $fsbrk
     C                   endsl                                                  if FS BREAK
      *
      * Retrieve any Overrides that the user may have input for this group.
      *
     C     wdhgsn        chain     hsp130                             92
     C                   if        *in92 = *off
     C                   z-add     goobfs        dtobfs
     C                   z-add     goonfs        dtonfs
     C                   endif
      *
      * Detail processing
     C                   if        ldrpfl = 'S'
     C                   exsr      $detailsum
     C                   else
     C                   exsr      $detail
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      *  If no records matched the selection criteria, then print the
      *  standard 'no data' error message.
      *
     C                   if        first = yes
     C                   except    hrhdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *-------------------------------------------------------------------
      * $detail - Detail processing for Detail Version of Report
      *-------------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C     wdhgsn        setll     hsp317
     C     wdhgsn        setll     hsl318a
     C     wdhgsn        setll     hsl319a
      *
     C                   setoff                                       939495
      *
     C                   dou       *in93 = *on and                              Do detail
     C                             *in94 = *on and
     C                             *in95 = *on
      *
     C     wdhgsn        reade     hsp317                                 93
     C                   if        *in93 = *off
     C                   z-add     wbsrfscd      dtbfscd
     C                   movel     wbsrhgcd      dtbhgcd
     C                   movel     wbsrgncd      dtbgncd
     C                   endif
      *
     C     wdhgsn        reade     hsl318a                                94
     C                   if        *in94 = *off
     C                   z-add     wnsrfscd      dtnfscd
     C                   movel     wnsrblcd      dtnblcd
     C                   movel     wnsrhgcd      dtnhgcd
     C                   movel     wnsrgncd      dtngncd
     C                   endif
      *
     C     wdhgsn        reade     hsl319a                                95
     C                   if        *in95 = *off
     C                   z-add     wgsrfscd      dtgfscd
     C                   movel     wgsrblcd      dtgblcd
     C                   movel     wgsrhgcd      dtghgcd
     C                   movel     wgsrgncd      dtggncd
     C                   endif
      *
     C                   if        dtbfscd <> 0 or dtnfscd <> 0 or dtgfscd <> 0 If anything
      *
     C                   if        *inof = *on
     C                   exsr      $headings
     C                   endif
      *
     C                   if        *in88 = *on
     C                   except    blank
     C                   endif
      *
     C                   except    dtline
     C                   setoff                                       88
     C                   endif                                                  If anything
      *
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------
      * Detail processing for Summary Version
      *-------------------------------------------------------------------
      *
     C     $detailsum    begsr
      *
     C     wdhgsn        setll     hsp316
      *
     C                   dou       *in93 = *on                                  Do detail
     C     wdhgsn        reade     hsp316                                 93
     C                   if        *in93 = *off
      *
     C                   if        *inof = *on
     C                   exsr      $headings
     C                   endif
      *
     C                   if        *in88 = *on
     C                   except    blank
     C                   endif
      *
     C                   except    dtline
     C                   setoff                                       88
     C                   endif
      *
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------
      * Print headings
      *-------------------------------------------------------------------
      *
     C     $headings     begsr
      *
     C                   seton                                        88
     C                   except    hrhdr
     C                   except    hfhdr
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------
      * Retrieve farm site name
      *-----------------------------------------------------------------------
      *
     C     $fsnm         begsr
      *
     C     wdfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel     fsfsnm        hffsnm
     C                   endif
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------------
      * Retrieve production type description
      *-------------------------------------------------------------------
      *
     C     $ptds         begsr
      *
     C     wdptcd        chain     hsp010                             92
     C                   if        *in92 = *off
     C                   movel     ptptds        hrptds
     C                   else
     C                   movel(p)  'UNKNOWN'     hrptds
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------
      * Retrieve production phase description
      *-------------------------------------------------------------------
      *
     C     $ppds         begsr
      *
     C     wdppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   movel     ppppds        hrppds
     C                   else
     C                   movel(p)  'UNKNOWN'     hrppds
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------------
      * Break on farm site
      *----------------------------------------------------------------------
      *
     C     $fsbrk        begsr
      *
      * Retrieve name
     C                   exsr      $fsnm
      * Print heading
      *
     C                   if        *inof = *on
     C                   exsr      $headings
     C                   else
     C                   except    hfhdr
     C                   endif
      *
      *
      * Save break fields
     C                   z-add     wdfscd        svfscd
      *
     C                   endsr
      /EJECT
      *------------------------------------------------------------------------
      * Break on Production Phase
      *------------------------------------------------------------------------
      *
     C     $ppbrk        begsr
      *
      * Retrieve new phase description and farm name
      *
     C                   exsr      $ppds
     C                   exsr      $fsnm
      *
      * Always page break
     C                   exsr      $headings
      *
      * Save break field
     C                   movel     wdppcd        svppcd
     C                   z-add     wdfscd        svfscd
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------
      * Break on production type
      *-----------------------------------------------------------------
      *
     C     $ptbrk        begsr
      *
      * Retrieve descriptions
      *
     C                   exsr      $ptds
     C                   exsr      $ppds
     C                   exsr      $fsnm
      *
      * Always page break and print headings
      *
     C                   exsr      $headings
      *
      * Save break fields
     C                   move      wdptcd        svptcd
     C                   moveL     wdppcd        svppcd
     C                   z-add     wdfscd        svfscd
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------
      * First time
      *--------------------------------------------------------------------
      *
     C     $first        begsr
      *
     C                   move      no            first
      *
      * Save break fields
     C                   move      wdptcd        svptcd
     C                   moveL     wdppcd        svppcd
     C                   z-add     wdfscd        svfscd
      *
      * Retrieve descriptions
      *
     C                   exsr      $ptds
     C                   exsr      $ppds
     C                   exsr      $fsnm
      *
      * Print headings
      *
     C                   except    hrhdr
     C                   except    hfhdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Business office
     C                   if        ldfsbo = *blank
     C                   movel(p)  'ALL'         ldfsbo
     C                   endif
      *
      * Logic to set indicators for from/to open date range
      *
     C                   select
     C                   when      ldhgsn <> 0
     C                   seton                                        85
     C                   when      ldofmdy = 0
     C                   seton                                        86
     C                   other
     C                   seton                                        84
     C                   endsl
      *
      * Logic to set indicators for open dates vs closed dates.
      *
     C                   if        ldcfmdy <> 0
     C                   seton                                        83
     C                   endif
      *
      * Production type
     C                   if        ldptcd = *blank
     C                   movel(p)  'ALL'         ldptcd
     C                   endif
      *
      * Production phase
     C                   if        ldppcd = *blank
     C                   movel(p)  'ALL'         ldppcd
     C                   endif
      *
      * Farm site
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   endif
      *
      *
      * Group type
     C                   if        ldgtcd = *blank
     C                   movel(p)  'ALL'         hrgtcd
     C                   else
     C                   movel(p)  ldgtcd        hrgtcd
     C                   endif
      * Hog group
     C                   if        ldhgcd = *blank
     C                   movel(p)  'ALL'         ldhgcd
     C                   endif
      *
      * Detail/Summary
     C                   if        ldrpfl = 'S'
     C                   seton                                        87
     C                   endif
      *
      * If user selected "Mixed BGF Sources only"
      *
     C                   if        ldmgfl = 'B'
     C                   eval      hrmgds2 =
     C                             '(GDU/BOAR Farms excluded from the +
     C                              MIX decision.)'
     C                   endif
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oprint198  E            HRHDR          1 01
     O                       SDPGM               10
     O                                          107 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          106 'GROUP SOURCE REPORT'
     O                                          188 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          2
     O                       LDRPDS             100
     O                                          188 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           21 'BUSINESS OFFICE .....'
     O                       LDFSBO              28
     O                       LDBODS              62
      *
     O          E            HRHDR          1
     O                                           21 'MIXED GROUP OPTION ..'
     O              N85      LDMGFL              24
     O              N85      LDMGDS              73
     O              N85      hrmgds2            124
     O               85      HRONEGROUP          66
      *
      *
     O          E   n83      HRHDR          1
     O                                           21 'FROM/TO OPEN DATES ..'
     O               84      ldofmdy             31 '  /  /  '
     O               84      ldotmdy             41 '  /  /  '
     O               85      HRONEGROUP          66
     O               86      HROPEN              66
      *
      *
     O          E    83      HRHDR          1
     O                                           21 'FROM/TO CLOSED DATES.'
     O                       ldcfmdy             31 '  /  /  '
     O                       ldctmdy             41 '  /  /  '
     O               85      hronegroup          66
      *
     O          E            HRHDR          1
     O                                           21 'PRODUCTION TYPE .....'
     O                       LDPTCD              28
     O                       LDPTDS              42
      *
     O          E            HRHDR          1
     O                                           21 'PRODUCTION PHASE ....'
     O                       LDPPCD              28
     O                       LDPPDS              62
      *
     O          E            HRHDR          1
     O                                           21 'FARM SITE ...........'
     O              N89      LDFSCD        z     29
     O               89                          26 'ALL'
     O                       LDFSNM              57
      *
     O          E            HRHDR          1
     O                                           21 'GROUP TYPE ..........'
     O                       HRGTCD              26
     O                       LDGTDS              47
      *
     O          E            HRHDR          2
     O                                           21 'HOG GROUP ...........'
     O                       LDHGCD              30
      *
     O          E            HRHDR          2
     O                       HRPTDS              15
     O                                           17 '-'
     O                       HRPPDS              48
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
      * Detail Version
      *
     O          E   n87      HRHDR          1
     O                                           25 '------ DESTINATION ------'
      *
     O                                           54 '---- GROW FINISH SOURC'
     O                                           62 'ES -----'
      *
     O                                           94 '------- NURSERY SOURCES '
     O                                          100 '------'
      *
     O                                          132 '----- BGF SOURCES -----'
      *
     O          E   n87      HRHDR          1
     O                                            5 'FARM'
     O                                           17 'HOG'
      *
     O                                           35 'HOG'
     O                                           46 'FARM'
     O                                           62 'GENETIC'
      *
     O                                           73 'HOG'
     O                                           84 'FARM'
     O                                          100 'GENETIC'
      *
     O                                          112 'HOG'
     O                                          123 'FARM'
     O                                          132 'GENETIC'
      *
     O          E   n87      HRHDR          1
     O                                            5 'SITE'
     O                                           11 'BLDG'
     O                                           19 'GROUP'
     O                                           25 'ST'
      *
     O                                           37 'GROUP'
     O                                           46 'SITE'
     O                                           52 'BLDG'
     O                                           59 'LINE'
      *
     O                                           75 'GROUP'
     O                                           84 'SITE'
     O                                           90 'BLDG'
     O                                           97 'LINE'
      *
     O                                          114 'GROUP'
     O                                          123 'SITE'
     O                                          129 'LINE'
      *
      * Summary Version
      *
     O          E    87      HRHDR          1
     O                                           25 '------ DESTINATION ------'
      *
     O                                           52 '--------- GROW FINISH SOU'
     O                                           67 'RCES ----------'
      *
     O                                           95 '----------- NURSERY SOURC'
     O                                          110 'ES ------------'
      *
     O                                          132 '--- BGF SOURCES ---'
      *
     O                                          159 '------ OVERRIDES ------'
      *
     O          E    87      HRHDR          1
     O                                            5 'FARM'
     O                                           17 'HOG'
      *
     O                                           32 'FARM'
     O                                           46 'FARM'
     O                                           60 'FARM'
      *
     O                                           75 'FARM'
     O                                           89 'FARM'
     O                                          103 'FARM'
      *
     O                                          118 'FARM'
     O                                          125 'FARM'
     O                                          132 'FARM'
      *
     O                                          146 'ORIGIN BGF'
     O                                          159 'ORIGIN NUR'
      *
     O          E    87      HRHDR          1
     O                                            5 'SITE'
     O                                           11 'BLDG'
     O                                           19 'GROUP'
     O                                           25 'ST'
      *
     O                                           32 'SITE'
     O                                           38 'BLDG'
     O                                           46 'SITE'
     O                                           52 'BLDG'
     O                                           60 'SITE'
     O                                           66 'BLDG'
      *
     O                                           75 'SITE'
     O                                           81 'BLDG'
     O                                           89 'SITE'
     O                                           95 'BLDG'
     O                                          103 'SITE'
     O                                          109 'BLDG'
      *
     O                                          118 'SITE'
     O                                          125 'SITE'
     O                                          132 'SITE'
      *
     O                                          146 'FARM SITE'
     O                                          159 'FARM SITE'
      *
     O          E            HRHDR          1
     O                       DASH               159
      *
      *------------------------------------------------------------------
      * FARM SITE HEADING
      *------------------------------------------------------------------
      *
     O          E            HFHDR       1  1
     O                       wdfscd        z      5
     O                       hffsnm              32
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E   n87      dtline         1
     O               88      wdblcd              12
     O               88      wdhgcd              21
     O               88      wdgscd              25
      * Grow finish
     O                       dtghgcd        b    39
     O                       dtgfscd       zb    46
     O                       dtgblcd        b    53
     O                       dtggncd        b    60
      * Nursery
     O                       dtnhgcd        b    77
     O                       dtnfscd       zb    84
     O                       dtnblcd        b    91
     O                       dtngncd        b    98
      * BGF
     O                       dtbhgcd        b   116
     O                       dtbfscd       zb   123
     O                       dtbgncd        b   130
      * Overrides
     O                       dtobfs        zb   143
     O                       dtonfs        zb   156
      * Summary Version
      *
     O          E    87      dtline         1
     O               88      wdblcd              12
     O               88      wdhgcd              21
     O               88      wdgscd              25
      * Grow finish
     O                       wfg1fscd      z     32
     O                       wfg1blcd            39
     O                       wfg2fscd      z     46
     O                       wfg2blcd            53
     O                       wfg3fscd      z     60
     O                       wfg3blcd            67
      * Nursery
     O                       wfn1fscd      z     75
     O                       wfn1blcd            82
     O                       wfn2fscd      z     89
     O                       wfn2blcd            96
     O                       wfn3fscd      z    103
     O                       wfn3blcd           110
      * BGF
     O                       wfb1fscd      z    118
     O                       wfb2fscd      z    125
     O                       wfb3fscd      z    132
      * Overrides
     O                       dtobfs        zb   143
     O                       dtonfs        zb   156
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          E            blank          1
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
