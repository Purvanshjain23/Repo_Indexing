     A*%%TS  SD  19950531  131403  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD596 - SELECT GROW FINISH OR NURSERY
     A*                          PRODUCTION PHASE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          04/04/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP596R1
     A*%%TS  SD  19950531  131403  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 10 48)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP596R2
     A*%%TS  SD  19940316  143946  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP596R3
     A*%%TS  SD  19940316  143946  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP596S1                   SFL
     A*%%TS  SD  19940316  143946  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFPPCD    R        O  5  1REFFLD(HSRREF/PPPPCD *LIBL/HSPREF)
     A            DFPPDS    R        O  5  8REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP596C1                   SFLCTL(HP596S1)
     A*%%TS  SD  19950531  131403  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      WINDOW(HP596R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'SELECT GROW FINISH OR NURSERY PROD-
     A                                      UCTION PHASE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  8'Description'
     A                                      DSPATR(HI)
     A          R HP596K1
     A*%%TS  SD  19940316  143946  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      WINDOW(HP596R1)
     A                                  9  1'F12=Previous'
