/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL FOR PRINTING PRE-INVOICE EXCEPTION      */
/*              REPORT.  THIS WILL BE SCHEDULED REGULARLY (CURRENTLY*/
/*              2X PER DAY) AND WILL DEFAULT ALL PRINT SELECTIONS.  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRT PRE-INVOICE EXCEPTION                           */
/*  DATE      - 08/20/96                                            */
/*                                                                  */
/***** MODIFICATION LOG *********************************************/
/*  7/25/97 PLL  ADDED PARM IN PDULPCLP CALL; MISSING SINCE 7/6?    */
/*  JRB DLY029 06/01/07 Call PMD4XFR to set the dfault Company      */
/********************************************************************/

 PDOCUPR:    PGM PARM(&PRTR1 &PRTR2 &PRTREG &PRTEXC)

/* PARMS */
        DCL    &PRTR1    *CHAR  (10)               /* First Printer     */
        DCL    &PRTR2    *CHAR  (10)               /* Second Printer    */
        DCL    &PRTREG   *CHAR  (01)               /*Print Pre-Invc Reg */
        DCL    &PRTEXC   *CHAR  (01)               /*Print Pre-Invc Exc */
        DCL    &BILTYP   *CHAR  (01) VALUE('1')    /* Bill Type (INVC)  */
        DCL    &RFLAG    *CHAR  (01) VALUE(' ')    /* Reprint Flag      */
        DCL    &PGM      *CHAR  (10)   +
                              VALUE('PDULPCLP')    /* CL To Submit      */
        DCL    &OUTQ     *CHAR   (10)              /* Output Queue      */
        DCL    &HOLD     *CHAR   (4) VALUE('*YES') /* Hold Output       */
        DCL    &SAVE     *CHAR   (4) VALUE('*YES') /* Save Output       */
        DCL    &NQSTAT   *CHAR  (01) VALUE('N')    /* If must run night */
        DCL    &NIGHTQ   *CHAR  (10) VALUE('  ')   /* Night Job Queue   */
        DCL    &JOBNME   *CHAR  (10) VALUE(' ')    /* Job name-NOT USED */
        DCL    &COMPNYN  *DEC                      /* Company Number    */
        DCL    &THRDATN  *DEC                      /* Thru Ship Date    */
        DCL    &LOADIDN  *DEC                      /* Load ID           */
        DCL    &ORDRN    *DEC                      /* Order Number      */
        DCL    &COPYN    *DEC                      /* No. of Copies     */
        DCL    &DATA     *CHAR  (10)               /* Data file         */
        DCL    &DATAH    *CHAR  (10)               /* Histort file      */
        DCL    &LSTREG1  *DEC   (04)
        DCL    &LSTREG2  *DEC   (04)
        DCL    &LSTEXC1  *DEC   (04)
        DCL    &LSTEXC2  *DEC   (04)
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3) /* Company +
                          Alpha       */

/* ERROR MESSAGE FIELDS                                                 */
        DCL    &MSG        *CHAR   (512)         /* message             */
        DCL    &MSGID      *CHAR   (7)           /* message id          */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   ENSURE THAT HAVE OMS FILES AVAILABLE                           */
/********************************************************************/
             CALL PGM(CAQXXFR) PARM(' ' &DATA &DATAH)
             IF (&DATA *EQ ' ') GOTO NOFILE

/********************************************************************/
/*   INITIALIZE FIELDS                                              */
/********************************************************************/

/* CALL PGM TO RTV DEFAULT COMPANY NUMBER */
             CALL       PGM(PMD4XFR) PARM('' &COMPA)
        CHGVAR &COMPNYN VALUE(&COMPA)
        CHGVAR &THRDATN VALUE(0)
        CHGVAR &LOADIDN VALUE(0)
        CHGVAR &ORDRN   VALUE(0)
        CHGVAR &COPYN   VALUE(1)
        CHGVAR &OUTQ    VALUE(&PRTR1)

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     CALL PGM(&PGM) PARM(&COMPNYN &LOADIDN +
                          &THRDATN &ORDRN &BILTYP &OUTQ &HOLD &SAVE +
                          &COPYN &NQSTAT &NIGHTQ &RFLAG &PRTREG +
                          &PRTEXC &JOBNME)

            IF (&PRTEXC *EQ 'Y') THEN(DO)
             RTVSPLFAN  SPLF(PDJ7PFR$) SPLNBR(&LSTEXC1)
             DUPSPLFP   SPLF(PDJ7PFR$) OUTQ(&PRTR2)
             RTVSPLFAN  SPLF(PDJ7PFR$) SPLNBR(&LSTEXC2)
             RLSSPLF    FILE(PDJ7PFR$) SPLNBR(&LSTEXC1) +
                          SELECT(*CURRENT &PRTR1)
             RLSSPLF    FILE(PDJ7PFR$) SPLNBR(&LSTEXC2) +
                          SELECT(*CURRENT &PRTR2)
            ENDDO

             IF (&PRTREG *EQ 'Y') THEN(DO)
             RTVSPLFAN  SPLF(PDULPFR$) SPLNBR(&LSTREG1)
             DUPSPLFP   SPLF(PDULPFR$) OUTQ(&PRTR2)
             RTVSPLFAN  SPLF(PDULPFR$) SPLNBR(&LSTREG2)
             RLSSPLF    FILE(PDULPFR$) SPLNBR(&LSTREG1) +
                          SELECT(*CURRENT &PRTR1)
             RLSSPLF    FILE(PDULPFR$) SPLNBR(&LSTREG2) +
                          SELECT(*CURRENT &PRTR2)
             ENDDO

             GOTO       CMDLBL(END)

/*----------------------NO FILE ERROR-------------------------------*/
 NOFILE:     SNDMSG     MSG('PRE-INVOICE EXCEPTION AUTO RUN FAILED +
                          DUE TONO FILE AVAILABLE') TOUSR(*REQUESTER)
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

