      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP681
      * TITLE:       PRINT TRANSFER MOVEMENT BILL OF LADING
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     02/14/96
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A BOL FOR A SCHEDULED
      *              TRANSFER MOVEMENT.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 03/27/02  LeAnne Fedor
      *           'Cell' is now a selection variable for both 'origin' and 'destination'
      *           farms. So, added logic to check 'cell' against the users cell
      *           selections.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP074    UF   E           K DISK
      * TRANSFER MOVEMENT HEADER  (records selected/keyedi n OPNQRY)
      *
      *
     FHSL075D   IF   E           K DISK
      * TRANSFER MOVEMENT DETAIL
      *
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
     D MGID            S              7    DIM(20)                              MSG ID ARRAY
     D MGDT            S             50    DIM(20)                              MSG PARMS
     D MGW             S              1    DIM(50)                              MSG WORK PARMS
      *
      *
      *---------------------------------------------------------------
      * ARRAYS USED FOR 'TOTAL SCHEDULED HEAD' FOR EACH ORIGIN GROUP
      *---------------------------------------------------------------
     D GRP             S              7  0 DIM(50)
     D HED             S              5  0 DIM(50)
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldorcell              69     70  0
     D  lddncell              71     72  0
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
593  C*    *LOVAL        SETLL     HSP074
593  C     *START        SETLL     HSP074
      *
      * READ ALL TRANSFER MOVEMENTS SELECTED BY THE OPNQRY.
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP074                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
     C                   move      yes           procfl
      *
      * Retrieve 'cell' associated with origin farm and compare to user's
      * selected 'origin cell'.
      *
     C                   if        ldorcell <> 0
     C     mhorfs        chain     hsp018                             92
     C                   select
     C                   when      *in92 = *on or ldorcell <> fscell
     C                   move      no            procfl
     C                   endsl
     C                   endif
      *
      * Retrieve 'cell' associated with destination farm and compare to user's
      * selected 'destination cell'.
      *
     C                   if        lddncell <> 0
     C     mhdnfs        chain     hsp018                             92
     C                   select
     C                   when      *in92 = *on or lddncell <> fscell
     C                   move      no            procfl
     C                   endsl
     C                   endif
      *
     C                   if        procfl = yes                                 If process
      *
      * CLEAR ARRAYS AND PRINT FIELDS FOR MOVEMENT
      *
     C                   EXSR      $CLEAR
      *
      * READ ALL DETAIL FOR THE MOVEMENT AND ACCUMULATE
      * SCHEDULED HEAD BY ORIGIN GROUP
      *
     C                   EXSR      $SCHD
      *
     C                   Z-ADD     MHMVSN        HRMVSN
      *
      * FLIP SCHEDULED SHIP DATE FROM CCYYMMDD TO MMDDYY
      *
     C                   Z-ADD     MHSCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HRSMDY
      *
      *
      * RETRIEVE ORIGIN FARM NAME (YOU ONLY PRINT 10 CHARACTERS)
      *
     C                   Z-ADD     MHORFS        HRORFS
     C     MHORFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF FARM HIT
     C                   MOVEL     FSFSNM        HRORFN
     C                   ENDIF                                                  IF FARM HIT
      *
      *
      * RETRIEVE DESTINATION FARM NAME (ONLY PRINT 10 CHARACTERS)
      *
     C                   Z-ADD     MHDNFS        HRDNFS
     C     MHDNFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF FARM HIT
     C                   MOVEL     FSFSNM        HRDNFN
     C                   ENDIF                                                  IF FARM HIT
      *
      * RETRIEVE/PRINT MOVEMENT DETAIL
      *
     C                   EXSR      $DTL
      *
     C                   ADD       1             MHPTCT
     C                   UPDATE    MHREC
      *
     C                   ENDIF                                                  If process
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $SCHD - BUILD THE ARRAYS OF SCHEDULED HEAD PER ORIGIN GROUP
      *---------------------------------------------------------------
      *
     C     $SCHD         BEGSR
      *
     C     MHMVSN        SETLL     HSL075D
      *
     C     *IN91         DOUEQ     *ON                                          DO LOOP
     C     MHMVSN        READE     HSL075D                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   Z-ADD     1             X
     C     MDORSN        LOOKUP    GRP(X)                                 92
     C     *IN92         IFEQ      *ON                                          IF FOUND
     C                   ADD       MDSCHD        HED(X)
     C                   ELSE
     C                   Z-ADD     1             X
     C     *ZERO         LOOKUP    GRP(X)                                 92
     C     *IN92         IFEQ      *ON                                          IF ADD
     C                   Z-ADD     MDORSN        GRP(X)
     C                   Z-ADD     MDSCHD        HED(X)
     C                   ENDIF                                                  IF ADD
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO LOOP
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM CCYYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DTL - RETRIEVE DATA AND PRINT DETAIL LINE FOR BOL
      *---------------------------------------------------------------
      *
     C     $DTL          BEGSR
      *
     C     MHMVSN        SETLL     HSL075D
      *
     C     *IN91         DOUEQ     *ON                                          DO DETAIL
     C     MHMVSN        READE     HSL075D                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE AND PRINT INFO FOR ORIGIN GROUP
      *
     C     MDORSN        IFNE      SVORSN                                       IF DIFF ORIG
     C                   EXSR      $ORIG
     C                   ENDIF                                                  IF DIFF ORIG
      *
      * RETRIEVE AND PRINT DESTINATION INFO FOR THIS
      * ORIGIN GROUP
      *
      * RETRIEVE BUILDING, ROOM AND GROUP CODE OF DESTINATION
      * GROUP
      *
     C     MDDNSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF                                         IF GROUP OK
     C                   MOVEL     HGHGCD        DTDNHG
     C                   MOVEL     HGBLCD        DTDNBL
     C                   MOVEL     HGRMCD        DTDNRM
     C                   ENDIF                                                  IF GROUP OK
      *
     C                   Z-ADD     MDSCHD        DTSCHD
      *
      * THE PREPRINTED FORM ONLY HAS ROOM FOR FIVE
      * DESTINATION GROUPS. SO, IF YOU HAVE MORE THAN FIVE,
      * GO TO THE NEXT FORM TO CONTINUE.
      *
     C     COUNT         IFEQ      5
     C                   EXCEPT    HRHDR
     C                   Z-ADD     0             COUNT
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
     C                   ADD       1             COUNT
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO DETAIL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ORIG - RETRIEVE INFOR AND PRINT HEADER PORTION OF BOL
      *---------------------------------------------------------------
      *
     C     $ORIG         BEGSR
      *
      * RETRIEVE BREED STOCK, SEX, BUILDING, ROOM OF ORIGIN GROUP
      *   NOTES: 1) PRINT FIRST LETTER OF BREED STOCK IN
      *             GENETIC LINE COLUMN
      *
     C     MDORSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF                                         IF GROUP OK
     C                   MOVEL     HGHGCD        HRORHG
     C                   MOVEL     HGBSCD        HRBSCD
     C                   MOVEL     HGSXCD        HRSXCD
     C                   MOVEL     HGBLCD        HRORBL
     C                   MOVEL     HGRMCD        HRORRM
     C                   ENDIF                                                  IF GROUP OK
      *
      * RETRIEVE TOTAL HEAD SCHEDULED OUT OF THIS ORIGIN GROUP
      * FROM ALTERNATING ARRAYS
      *
     C                   Z-ADD     1             X
     C     MDORSN        LOOKUP    GRP(X)                                 92
     C     *IN92         IFEQ      *ON                                          IF FOUND
     C                   Z-ADD     HED(X)        HRSCHD
     C                   ENDIF                                                  IF FOUND
      *
     C                   EXCEPT    HRHDR
      *
     C                   Z-ADD     MDORSN        SVORSN
     C                   Z-ADD     0             COUNT
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLEAR - CLEAR ARRAYS AND PRINT FIELDS FOR EACH MOVEMENT
      *---------------------------------------------------------------
      *
     C     $CLEAR        BEGSR
      *
     C                   Z-ADD     0             PAGE
     C                   Z-ADD     0             COUNT
     C                   Z-ADD     0             SVORSN
      *
      * VALES FOR ARRAYS
     C                   Z-ADD     0             HED
     C                   Z-ADD     0             GRP
     C                   Z-ADD     0             X
      *
     C                   Z-ADD     0             HRMVSN
     C                   Z-ADD     0             HRSMDY
      *
     C                   MOVE      *BLANK        HRSXCD
     C                   MOVE      *BLANK        HRBSCD
      *
     C                   Z-ADD     0             HRSCHD
     C                   MOVE      *BLANK        HRORHG
     C                   Z-ADD     0             HRORFS
     C                   MOVE      *BLANK        HRORFN
     C                   MOVE      *BLANK        HRORBL
     C                   MOVE      *BLANK        HRORRM
      *
     C                   Z-ADD     0             HRDNFS
     C                   MOVE      *BLANK        HRDNFN
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR HEADING
      *
     C     *LIKE         DEFINE    MHMVSN        HRMVSN
     C                   Z-ADD     0             HRSMDY            6 0
      *
     C     *LIKE         DEFINE    HGSXCD        HRSXCD
     C                   MOVE      *BLANK        HRBSCD            1
      *
     C     *LIKE         DEFINE    MDSCHD        HRSCHD
     C     *LIKE         DEFINE    HGHGCD        HRORHG
     C     *LIKE         DEFINE    FSFSCD        HRORFS
     C                   MOVE      *BLANK        HRORFN           10
     C     *LIKE         DEFINE    HGBLCD        HRORBL
     C     *LIKE         DEFINE    HGRMCD        HRORRM
      *
     C     *LIKE         DEFINE    FSFSCD        HRDNFS
     C                   MOVE      *BLANK        HRDNFN           10
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINES FOR DESTINATION GROUPS
      *
     C     *LIKE         DEFINE    HGHGCD        DTDNHG
     C     *LIKE         DEFINE    HGBLCD        DTDNBL
     C     *LIKE         DEFINE    HGRMCD        DTDNRM
     C     *LIKE         DEFINE    MDSCHD        DTSCHD
      *
      * INDEX FIELD FOR ARRAYS FOR ACCUMULATING TOTAL SCHEDULED
      * HEAD PER ORIGIN GROUP ON A MOVEMENT.
      *
     C                   Z-ADD     0             X                 2 0
      *
      * CONTROL FIELDS
      *
     C     *LIKE         DEFINE    MDORSN        SVORSN
     C                   Z-ADD     0             COUNT             2 0
     C                   MOVE      *BLANK        PROCFL            1
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * OUTPUT SPECIFICATIONS
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR             2
     O                       HRMVSN        Z     74
      *
     O          E            HRHDR             8
     O                       PAGE          Z     67
      *
     O          E            HRHDR            11
     O                       HRORFS        Z     22
     O                       HRORFN              35
     O                       HRSMDY        Y     56
      *
     O          E            HRHDR            15
     O                       HRORHG              20
     O                       HRORBL              34
     O                       HRORRM              40
     O                       HRSCHD        Z     44
     O                       HRSXCD              47
     O                       HRBSCD              49
      *
     O          E            HRHDR            31
     O                       HRDNFS        Z     24
     O                       HRDNFN              36
     O                       HRSMDY        Y     60
      *
     O          E            HRHDR          1 33
      *
      *---------------------------------------------------------------
      * DETAIL SECTION OF DESTINATION GROUPS
      *---------------------------------------------------------------
      *
     O          E            DTLINE         2
     O                       DTDNBL         B    13
     O                       DTDNRM         B    19
     O                       DTSCHD        KB    27
     O                       HRSXCD              32
     O                       HRBSCD              38
     O                       DTDNHG         B    60
      *
      /EJECT
