      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Hp3072
      * TITLE:       Government Census: Workfile: Year End Farm Inventory BGF
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     05/27/08
      *
      * FUNCTION:    Creates the BGF workfile.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields deleted from Weekly Production file.
      *             BPPSGLSV - Nbr Gilts Bred w/Passed Date
      *             BPWSLB   - Weekly Ending Lbs Supplement Used
      *             BPP2LT   - Total P2 Litters Farrowed
      *             BPTDGB   - Total Days Gilt Breeding Age
      *
      * 11/07/08  LeAnne Ramsey
      *           Recompile only:
      *           Added 3 new fields to HPPB092-Datamart BGF: Weekly Production
      *            1) Target open gilt pool eligible
      *            2) Target litters farrowed/mated female/year
      *            3) Target pigs weaned/mated female/year
      *
      * 05/28/09  LeAnne Ramsey
      *           Recompile only:
      *           Added new field "Farrowing Crates" to:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 09/01/09  LeAnne Ramsey
      *           Recompile only. Added new field "Report Total Litters Farrowed" to:
      *                 HPPB092-Datamart BGF: Weekly Production
      *
      * 01/14/10  LeAnne Ramsey
      *           Recompile only. Changed text on BPLVHD and BPLVLB in:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * File specifications
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhplb092d  if   e           k disk
      *  Datamart BGF: Weekly Production
      *
      *
     Fhsp3072   uf a e           k disk
      *  Workfile: Year End Farm Inventory (BGF)
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Workfields
      *
     D wkyr            s                   like(bpacyr)
     D wkwedt          s                   like(bp80wedt)
     D wksacd          s                   like(fssacd)
     D wkcnty          s                   like(fscnty)
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldyr                   1      4  0
     D  ldpe                   5      6  0
      *
     D  ldfdt                  9     16  0
     D  ldtdt                 17     24  0
      *
     D  ldgtcd                25     25
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *-------------------------------------------------------------------------------------
      * MAINLINE
      *-------------------------------------------------------------------------------------
      *
      * Read all records for the 'last week-ending date' in the year from the
      * Datamart BGF: Weekly Production.
      *
     C     wkwedt        setll     hplb092d
      *
     C                   dou       *in90 = *on                                  Main do
     C     wkwedt        reade     hplb092d                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Retrieve the County and State of the Farm from the Farm Site Master.
      *
     C     bpfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fscnty        wkcnty
     C                   move      fssacd        wksacd
     C                   else
     C                   move      *blank        wkcnty
     C                   move      *blank        wksacd
     C                   endif
      *
      * Write/update a Workfile record for this State/County/Farm.
      *
     C                   exsr      $wrt3072
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Write/Update a workfile record
      *---------------------------------------------------------------
      *
     C     $wrt3072      begsr
      *
      * Retrieve the Workfile record for this County/State/Farm.
      *
     C     key01         chain     hsp3072                            92
     C                   if        *in92 = *off
     C                   add       bpengshd      w1engshd
     C                   add       bpbohd        w1bohd
     C     w1engshd      add       w1bohd        w1inhd
     C                   update    w1rec
     C                   else
      *
     C                   z-add     bpengshd      w1engshd
     C                   z-add     bpbohd        w1bohd
     C     w1engshd      add       w1bohd        w1inhd
     C                   z-add     bpfscd        w1fscd
     C                   move      wksacd        w1sacd
     C                   move      wkcnty        w1cnty
     C                   write     w1rec
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkcnty
     C                   kfld                    wksacd
     C                   kfld                    bpfscd
      *
      * We need to process records for the "last" week-ending date in the selected year.
      * To come up with this week-ending date, we will position to the next "year" and
      * read prior.
      *
     C     ldyr          add       1             wkyr
     C     10000         mult      wkyr          wkwedt
      *
     C     wkwedt        setll     hplb092d
     C                   readp     hplb092d                               92
     C                   if        *in92 = *off
     C                   z-add     bp80wedt      wkwedt
     C                   endif
      *
     C                   endsr
      /eject
