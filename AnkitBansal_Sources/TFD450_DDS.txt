     A*%%TS  SD  20081218  072538  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD450
     A*  TITLE:         WORK WITH KILL/CUT DATA
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       03/27/07
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF450S1                   SFL
     A*%%TS  SD  20081218  072538  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDWEDT    R        H      REFFLD(@@WEDT)
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O  9  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  9 17REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1WK      R        O  9 25REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF450C1                   SFLCTL(TF450S1)
     A*%%TS  SD  20081218  072538  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'WORK WITH KILL/CUT DATA'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'2=Revise'
     A                                  5  9'Week'
     A                                      DSPATR(HI)
     A                                  6  8'Ending'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  9'Date'
     A                                      DSPATR(HI)
     A                                  7 17'Year'
     A                                      DSPATR(HI)
     A                                  7 24'Week'
     A                                      DSPATR(HI)
     A            C1WEMDY   R        B  8  7REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1YR      R        B  8 17REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1WK      R        B  8 25REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF450K1
     A*%%TS  SD  20070328  093349  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F6=Create'
     A                                 23 34'F7=Listing'
     A                                 23 46'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF450ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF450EC                   SFLCTL(TF450ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
