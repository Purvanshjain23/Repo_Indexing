/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF423CL                                       */
/*  PROGRAM NAME:    BYPRODUCT MIX RECAP REPORT                    */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     02/13/08                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 04/02/09  LEANNE RAMSEY                                         */
/*           ADDED OPEN QUERY ON FILE ROP122-BYPRODUCT DETAIL.     */
/*                                                                 */
/* 05/07/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(110)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFWEDT)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTWEDT)  TYPE(*DEC) LEN(8)

             CHGVAR     VAR(&LDFWEDT)  VALUE(%SST(*LDA 85 8))
             CHGVAR     VAR(&LDTWEDT)  VALUE(%SST(*LDA 99 8))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))


             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*----------------------------------------------------------------*/
/* EXTRACT RECAP RECORDS                                          */
/*----------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 11)) VALUE('YRWEDT *GE ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFWEDT)

             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND YRWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTWEDT)

             OVRDBF     FILE(ROL123A) SHARE(*YES)
             OPNQRYF    FILE((ROL123A)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*----------------------------------------------------------------*/
/* EXTRACT BYPRODUCT DETAIL RECORDS                               */
/*----------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 11)) VALUE('YDWEDT *GE ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFWEDT)

             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND YDWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTWEDT)

/* Note: I put in the 3 selections below just-in-case. At the time we added */
/*       this logic, these were the ONLY records that had CoOwned Amounts.  */
/*       So, we probably would have been OK without these selections.       */

             CHGVAR     VAR(%SST(&QDATA 46 19)) VALUE('*AND YDTOWNPC *NE 0')
             CHGVAR     VAR(%SST(&QDATA 66 19)) VALUE('*AND YDSAFL *EQ "N"')
             CHGVAR     VAR(%SST(&QDATA 86 22)) VALUE('*AND YDITTYCD +
                          *EQ "WP"')

             OVRDBF     FILE(ROL122C) SHARE(*YES)
             OPNQRYF    FILE((ROL122C)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* PRINT RECAP REPORT                                             */

             CALL       PGM(TF623)

             CLOF       OPNID(ROL123A)
             CLOF       OPNID(ROL122C)

             DLTOVR     FILE(*ALL)

             ENDPGM

