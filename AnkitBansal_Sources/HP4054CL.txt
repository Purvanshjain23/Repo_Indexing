/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4077CL                                      */
/*  PROGRAM NAME:    WEEKLY AVERAGE INVENTORY REPORT               */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     08/06/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4054-SPECIFY OPTIONS FOR IDLE FARMS REPORT  */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATAB) TYPE(*CHAR) LEN(48)
             DCL        VAR(&QDATAF) TYPE(*CHAR) LEN(48)
             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(60)

             DCL        VAR(&FCYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&TCYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&CNFSCD) TYPE(*DEC)  LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&FCYMD) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&TCYMD) VALUE(%SST(*LDA 24 8))
             CHGVAR     VAR(&CNFSCD) VALUE(%SST(*LDA 46 5))

/*-----------------------------------------------------------------*/
/* CREATE THE WORKFILES                                            */
/*-----------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3054) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSP3055) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*-------------------------------------------------------------------*/
/* BUILD/RUN QUERY FOR THE FINISHER DATA.                            */
/*-------------------------------------------------------------------*/

/* ALWAYS SELECT ONLY FINISHER RECORDS WITH A WEEK-ENDING DATE IN */
/* THE SELECTED DATE RANGE.                                       */

             CHGVAR     VAR(%SST(&QDATAF 1 12)) VALUE('WB80WEDT *GE')
             CHGVAR     VAR(%SST(&QDATAF 14 8)) VALUE(&FCYMD)
             CHGVAR     VAR(%SST(&QDATAF 23 17)) VALUE('*AND WB80WEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATAF 41 8)) VALUE(&TCYMD)


/*  DATAMART FIN: WEEKLY GROUP DETAIL                            */

             OVRDBF     FILE(HPLF075E) SHARE(*YES)
             OPNQRYF    FILE((HPLF075E)) FORMAT(*FILE) +
                          QRYSLT(&QDATAF) KEYFLD(*FILE) SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* BUILD/RUN QUERY FOR THE BGF DATA.                                 */
/*-------------------------------------------------------------------*/

/* ALWAYS SELECT ONLY BGF RECORDS WITH A WEEK-ENDING DATE IN      */
/* THE SELECTED DATE RANGE.                                       */

             CHGVAR     VAR(%SST(&QDATAB 1 12)) VALUE('BP80WEDT *GE')
             CHGVAR     VAR(%SST(&QDATAB 14 8)) VALUE(&FCYMD)
             CHGVAR     VAR(%SST(&QDATAB 23 17)) VALUE('*AND BP80WEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATAB 41 8)) VALUE(&TCYMD)


/*  DATAMART BGF: WEEKLY PRODUCTION                              */

             OVRDBF     FILE(HPLB092B) SHARE(*YES)
             OPNQRYF    FILE((HPLB092B)) QRYSLT(&QDATAB) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/* BUILD/RUN QUERY FOR THE CONSOLIDATED FARM SELECTION               */
/*-------------------------------------------------------------------*/

/* ALWAYS SELECT ONLY FARMS THAT HAVE A FARM NUMBER..THEY ALL WILL!! */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('FSFSCD *NE 0')

             IF         COND(&CNFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 17)) VALUE('*AND FSCNFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 32 5)) VALUE(&CNFSCD)
             ENDDO

/* FARM SITE                                                     */

             OVRDBF     FILE(HSL018K) SHARE(*YES)
             OPNQRYF    FILE((HSL018K)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*-----------------------------------------------------------------*/
/* BUILD THE WORKFILES                                             */
/*-----------------------------------------------------------------*/

             CALL       PGM(HP3054)

/*-----------------------------------------------------------------*/
/* DELETE FILE OVERRIDES                                           */
/*-----------------------------------------------------------------*/

             CLOF       OPNID(HPLF075E)
             DLTOVR     FILE(HPLF075E)

             CLOF       OPNID(HPLB092B)
             DLTOVR     FILE(HPLB092B)

             CLOF       OPNID(HSL018K)
             DLTOVR     FILE(HSL018K)


/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(115) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE REPORT                                                  */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6054)

             DLTOVR     FILE(*ALL)

             ENDPGM

