      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS Hog Production
      * PROGRAM:     HP3007 - Copy BGF Targests
      * PROGRAMMER:  Sharon Zurn
      * CREATED:     10/16/06
      *
      * FUNCTION:    This program allows users to copy BGF targets from Year-to-Year
      *              and or Table-to-Table.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE       PROGRAMMER
      *
      * 09/26/07  LeAnne Ramsey
      *           Clean up code.
      *
      * 11/06/08  LeAnne Ramsey
      *           Recompile only. Added fields to HSP096.
      *
      * 05/28/09  LeAnne Ramsey
      *           Recompile only. Added 1 new field to HSP096-BGF Yearly Targets:
      *            1) farrowing crates
      *
      * 11/10/10  LeAnne Ramsey (E1151)
      *           Recompile only. Increased some head fields from 5,0 to 6,0 in HSP096.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp096    if   e           k disk
      *   BGF yearly targets (records selected with open query)
      *
      *
     Fhsl096a   uf a e           k disk    rename(byrec:byreca)
      *   BGF yearly targets
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Data areas
      *---------------------------------------------------------------
      *
      * Local data area.
      *
     Dlda             uds                  dtaara(*lda)
     D  ldtcdyr               10     13  0
     D  ldtttbcd              14     18
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Delete all existing target records for the TO side of the copy.
      *
     C                   exsr      $dlt096
      *
      * Process all BGF Yearly Targets records selected by the open query.
      * These records represent the 'copy from' side of the copy.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp096                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   z-add     ldtcdyr       bycdyr
      *
     C                   if        ldtttbcd <> *blank
     C                   move      ldtttbcd      byttbcd
     C                   endif
      *
     C                   write     byreca
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *------------------------------------------------------------------------------------
      * Delete any/all existing target records for TO side of the copy
      *------------------------------------------------------------------------------------
      *
     C     $dlt096       begsr
      *
      * You will delete based on which values the user entered for the TO side:
      *   1) year/table
      *   3) year
     C                   select
     C                   when      ldtttbcd <> *blank
     C     key01         setll     hsl096a
     C                   other
      *
     C     ldtcdyr       setll     hsl096a
     C                   endsl
      *
      *
     C                   dou       *in91 = *on                                  Do loop
      *
     C                   select
     C                   when      ldtttbcd <> *blank
     C     key01         reade     hsl096a                                91
     C                   other
      *
     C     ldtcdyr       reade     hsl096a                                91
     C                   endsl
      *
     C                   if        *in91 = *off
     C                   delete    byreca
     C                   endif
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldtcdyr
     C                   kfld                    ldtttbcd
      *
     C                   endsr
      /eject
