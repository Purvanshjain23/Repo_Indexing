/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER --> HP496CL                                     */
/*  PROGRAM NAME ----> PERIOD CHANGE-IN-INVENTORY REPORT           */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 07/10/03                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> GENERATE THE CHANGE-IN-INVENTORY REPORT.    */
/*                     REPORT VALUES ARE PASSED THRU THE LDA FROM  */
/*                     HP496.                                      */
/*                                                                 */
/*                     THIS REPORT IS ALSO GENERATED IN BATCH AS   */
/*                     PART OF PERIOD END...SO, THE LDA VALUES     */
/*                     MUST NOT CONFLICT WITH THE EOP LDA.         */
/*                                                                 */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(254)

             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDINCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCRCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 9 8))
             CHGVAR     VAR(&LDINCD) VALUE(%SST(*LDA 235 1))
             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 87 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 92 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 97 5))
             CHGVAR     VAR(&LDCRCD) VALUE(%SST(*LDA 104 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 109 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 114 1))

/*------------------------------------------------------------------*/
/*  SET UP OPEN QUERY OVER HOG GROUPS                               */
/*------------------------------------------------------------------*/

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED               */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *NE "VD"')

/*  IF BUSINESS OFFICE                                            */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 17 17)) VALUE('*AND HGFSBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 34 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 39 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                            */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 41 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 58 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 63 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                            */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 65 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 82 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 87 1)) VALUE('"')
             ENDDO

/*  IF GROUP TYPE                                                  */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 89 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 106 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 107 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 109 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 125 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF COMPANY RELATIONSHIP                                      */

             IF         COND(&LDCRCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 131 17)) VALUE('*AND HGCRCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 148 5)) VALUE(&LDCRCD)
             CHGVAR     VAR(%SST(&QDATA 153 1)) VALUE('"')
             ENDDO

/*  ONLY SELECT GROUPS WHERE:                                    */
/*   1) CLOSE DATE IS ZERO OF ON/AFTER BEGINNING PERIOD DATE     */

             CHGVAR     VAR(%SST(&QDATA 205 16)) VALUE('*AND (HGCLDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 222 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 231 14)) VALUE('*OR HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 246 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 254 1)) VALUE(')')


             OVRDBF     FILE(HSP034)  SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) +
                          KEYFLD((HGPPCD) (HGFSCD) (HGHGCD))

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  GENERATE REPORT                                                 */
/*------------------------------------------------------------------*/

             CALL       PGM(HP696) PARM(&LDINCD)

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(*ALL)

             ENDPGM

