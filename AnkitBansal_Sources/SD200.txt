      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Margin Management
      * PROGRAM:     SD200 - Signal Demand: Build Commodity Market Prices
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/23/08
      *
      * NOTE: After the initial load for Signal Demand, we will only send the
      *       "last 10 days" of data. If we need to resend the entire file:
      *       1) reinstate the commented out code in the Initialization Subroutine
      *          so that we get all data from 2003 forward
      *       2) change the FTP CL for file SDP200 to say "TOTAL" instead of "DELTA"
      *       (After the run, you must revert to the "10 day" code.)
      *
      **************************************************************************************
      * MODIFICATIONS:
      **************************************************************************************
      * Date      Programmer
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fomf0cpl1  if   e           k disk
      *   Commodity market price
      *
      *
      *
     Fomf0cpl5  if   e           k disk
      *   Commodity market price
      *
      *
     Fsdp200    o    e           k disk
      *    Signal Demand: Commodity prices
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      *  Standalone field
      *---------------------------------------------------------------
      *
     D wkmkcd          s                   like(f0lqcd)
     D wkpgcd          s                   like(f0lrcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
     D wkdt            s              7  0
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * Mainline
      ***************************************************************************************
      *
      * Read the WCB/WTDAVG records in the Commodity Market Price file to get
      * Live Hog Prices.
      *
     C                   exsr      $hogprices
      *
      * Read the "USD" records in the Commodity Market Price file to get USDA
      * Primal Market prices
      *
     C                   exsr      $usdaprices
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Get Live Hog Prices
      *---------------------------------------------------------------
      *
     C     $hogprices    begsr
      *
      * Set up key fields
     C                   move      'WCB'         wkmkcd
     C                   move      'WTDAVG '     wkpgcd
      *
     C     key01         setll     omf0cpl1
      *
     C                   dou       *in90 = *on                                  Do hog prices
     C     key02         reade     omf0cpl1                               90
     C                   if        *in90 = *off                                 If not EOF
     C                   exsr      $wrt200
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do hog prices
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Get USDA Primal Prices
      *---------------------------------------------------------------
      *
     C     $usdaprices   begsr
      *
      * Set up key field
     C                   move      'USD'         wkmkcd
      *
     C     key03         setll     omf0cpl5
      *
     C                   dou       *in90 = *on                                  Do usda
     C     wkmkcd        reade     omf0cpl5                               90
     C                   if        *in90 = *off                                 If not EOF
     C                   exsr      $wrt200
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do usda
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write Commodity Price reocrds
      *---------------------------------------------------------------
      *
     C     $wrt200       begsr
      *
      * Market code
      * Price group code
     C                   move      f0lqcd        cpmkcd
     C                   move      f0lrcd        cppgcd
      *
      * Price per Pound
     C                   z-add     f0a5pr        cpmppr
      *
      * Flip Synon date to CCYYMMDD
      *
     C     *cymd         test(d)                 f0eqdt                 92
     C                   if        *in92 = *off
     C     *cymd         move      f0eqdt        wkisodate
     C     *iso          move      wkisodate     cpmpdt
     C                   endif
      *
     C                   write     cprec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    wkmkcd
     C                   kfld                    wkpgcd
     C                   kfld                    wkdt
      *
     C     key02         klist
     C                   kfld                    wkmkcd
     C                   kfld                    wkpgcd
      *
      *
     C     key03         klist
     C                   kfld                    wkmkcd
     C                   kfld                    wkdt
      *
      *
      * Find the "date" that is 10 days prior to the System Date.
      * Put it into a Synon format (CYYMMDD)
      *
     C                   move      *date         wkisodate
     C     wkisodate     subdur    10:*d         wkisodate
     C     *cymd         move      wkisodate     wkdt
      *
      *
      * When/if we must send the entire file (instead of just the last 10 days),
      * reinstate the line of code below. (You must also change the FTP CL to say
      * "TOTAL" instead of "DELTA".)
      *
     C****************   z-add     1030101       wkdt
      *
     C                   endsr
      /EJECT
