/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4075CL                                      */
/*  PROGRAM NAME:    MOVEMENT VERIFICATION REPORT                  */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     11/07/00                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4075-SPECIFY OPTIONS FOR MOVEMENT           */
/*                   VERIFICATION REPORT.                          */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/
/*‚06/11/24  Jagdish Mistry (JM-WI628),                            */
/*‚Project 628-RabApp Bypass to Agview                             */
/*‚a. Added parameter to get count for HP370                       */

/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(175)

             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)

             DCL        VAR(&LDORFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDORFSCD) TYPE(*DEC) LEN(5)

             DCL        VAR(&LDDNFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDDNFSCD) TYPE(*DEC) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
/*‚JM-WI628-START                                                        */
             DCL        VAR(&TOTALCNT) TYPE(*DEC) LEN(8 0)
/*‚JM-WI628-END                                                          */

             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 13 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 21 8))

             CHGVAR     VAR(&LDORFSBO) VALUE(%SST(*LDA 29 5))
             CHGVAR     VAR(&LDORPTCD) VALUE(%SST(*LDA 34 5))
             CHGVAR     VAR(&LDORPPCD) VALUE(%SST(*LDA 39 5))
             CHGVAR     VAR(&LDORCELL) VALUE(%SST(*LDA 44 2))
             CHGVAR     VAR(&LDORFSCD) VALUE(%SST(*LDA 46 5))

             CHGVAR     VAR(&LDDNFSBO) VALUE(%SST(*LDA 51 5))
             CHGVAR     VAR(&LDDNPTCD) VALUE(%SST(*LDA 56 5))
             CHGVAR     VAR(&LDDNPPCD) VALUE(%SST(*LDA 61 5))
             CHGVAR     VAR(&LDDNCELL) VALUE(%SST(*LDA 66 2))
             CHGVAR     VAR(&LDDNFSCD) VALUE(%SST(*LDA 68 5))

/*-------------------------------------------------------------------*/
/* CREATE WORKFILES                                                  */
/*-------------------------------------------------------------------*/


             CRTDUPOBJL OBJ(HSP370) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSP376) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSP377) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE MOVEMENT EVENT FILE                         */
/*-------------------------------------------------------------------*/


/*  SELECT ALL SALES AND TRANSFER EVENT RECORDS FOR THE DATE RANGE   */

             IF         COND(&LDFYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MEMEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MEMEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)
             CHGVAR     VAR(%SST(&QDATA 46 40)) VALUE('*AND (MEMTCD +
                          *EQ "T" *OR MEMTCD *EQ "S")')
             ENDDO

             OVRDBF     FILE(HSP058) SHARE(*YES)
             OPNQRYF    FILE((HSP058)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  BUILD THE WORKFILE                                               */
/*-------------------------------------------------------------------*/

/*‚JM-WI628-START                                                        */
/*           CALL PGM(HP370)                                             */
             CALL       PGM(HP370) PARM((&TOTALCNT))
/*‚JM-WI628-END                                                          */

             CLOF       OPNID(HSP058)
             DLTOVR     FILE(HSP058)


/*********************************************************************/
/* ORIGIN DATA                                                       */
/*********************************************************************/

             CHGVAR     VAR(&QDATA) VALUE(' ')


/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('WFORBO *NE +
                          " "')

/*  ORIGIN BUSINESS OFFICE                                           */

             IF         COND(&LDORFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND WFORBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 33 5)) VALUE(&LDORFSBO)
             CHGVAR     VAR(%SST(&QDATA 38 1)) VALUE('"')
             ENDDO

/* ORIGIN PRODUCTION PHASE                                                */

             IF         COND(&LDORPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 40 17)) VALUE('*AND WFORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 57 5)) VALUE(&LDORPPCD)
             CHGVAR     VAR(%SST(&QDATA 62 1)) VALUE('"')
             ENDDO

/* ORIGIN PRODUCTION TYPE                                         */

             IF         COND(&LDORPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA  64 17)) VALUE('*AND WFORPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 81 5)) VALUE(&LDORPTCD)
             CHGVAR     VAR(%SST(&QDATA 86 1)) VALUE('"')
             ENDDO

/* ORIGIN CELL                                                       */

             IF         COND(&LDORCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 88 16)) VALUE('*AND WFORCL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 104 2)) VALUE(&LDORCELL)
             ENDDO


/* ORIGIN FARM SITE                                                  */

             IF         COND(&LDORFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 107 16)) VALUE('*AND WFORFS +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 123 5)) VALUE(&LDORFSCD)
             ENDDO



/*  OPEN QUERY FILE FOR ORIGIN DATA AND COPY TO ORIGIN WORKFILE  */

             OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             CPYFRMQRYF FROMOPNID(HSP370) TOFILE(HSP376) +
                          MBROPT(*ADD) FMTOPT(*NOCHK)
             CLOF       OPNID(HSP370)


/*********************************************************************/
/* DESTINATION DATA                                                  */
/*********************************************************************/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* ONLY INCLUDE TRANSFER MOVEMENTS                                   */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('WFMTCD +
                          *EQ "T"')

/*  DESTINATION BUSINESS OFFICE                                           */

             IF         COND(&LDDNFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND WFDNBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 33 5)) VALUE(&LDDNFSBO)
             CHGVAR     VAR(%SST(&QDATA 38 1)) VALUE('"')
             ENDDO

/*  DESTINATION PRODUCTION PHASE                                          */

             IF         COND(&LDDNPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 40 17)) VALUE('*AND WFDNPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 57 5)) VALUE(&LDDNPPCD)
             CHGVAR     VAR(%SST(&QDATA 62 1)) VALUE('"')
             ENDDO

/*  DESTINATION PRODUCTION TYPE                                   */

             IF         COND(&LDDNPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA  64 17)) VALUE('*AND WFDNPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 81 5)) VALUE(&LDDNPTCD)
             CHGVAR     VAR(%SST(&QDATA 86 1)) VALUE('"')
             ENDDO

/*  DESTINATION CELL                                                 */

             IF         COND(&LDDNCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 88 16)) VALUE('*AND WFDNCL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 104 2)) VALUE(&LDDNCELL)
             ENDDO


/*  DESTINATION FARM SITE                                            */

             IF         COND(&LDDNFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 107 16)) VALUE('*AND WFDNFS +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 123 5)) VALUE(&LDDNFSCD)
             ENDDO



/*  OPEN QUERY FILE FOR ORIGIN DATA AND COPY TO ORIGIN WORKFILE  */

             OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             CPYFRMQRYF FROMOPNID(HSP370) TOFILE(HSP377) +
                          MBROPT(*ADD) FMTOPT(*NOCHK)
             CLOF       OPNID(HSP370)

/*-------------------------------------------------------------------*/
/* SET UP OVERRIDE OVER THE FARM MASTER TO GET IT KEYED APPROPIRATELY*/
/*-------------------------------------------------------------------*/

             OVRDBF     FILE(HSP018) SHARE(*YES)
             OPNQRYF    FILE((HSP018)) KEYFLD((FSCELL) (FSFSCD)) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT OF 15CPI ON 198 COLUMNS OF PRINT  */
/* FORCE IT TO PRINT OUT OF DRAWER 2 ON LANDSCAPE                   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE REPORT                                                  */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6075)

             CLOF       OPNID(HSP018)
             DLTOVR     FILE(*ALL)

             ENDPGM

