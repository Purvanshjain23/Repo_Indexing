      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      HPS---Datamart
      * PROGRAM:     HP218 - Datamart FIN: Update Group Header-Closed/Disposed Groups
      *                     (Nursery/Grow Finish Only)
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/12/01
      *
      * FUNCTION:    Update the group header with closed/disposed group info.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/11/01  LeAnne Fedor
      *           Recompile only. New field 'transferred out head to sow farm' added
      *           to Datamart Group Header file.
      *
      * 07/03/01  LeAnne Fedor
      *           Added calculation for 'down days'.
      *
      * 04/04/02  LeAnne Fedor
      *           Per Jeff Sherboundy, removed all special logic for FHAUL/CHAUL.
      *
      * 06/03/02  LeAnne Fedor
      *           Added History vs Current processing logic.
      *
      * 01/29/03  LeAnne Fedor
      *           Removed all hardcoded special logic for SHAUL.
      *
      * 09/08/03  LeAnne Fedor
      *           Added logic for KOA head.
      *
      * 12/15/03  LeAnne Fedor
      *           Added logic for new Phase 3 Costed Movements fields. At this time, these
      *           fields are only populated for closed/disposed groups.
      *
      *           We retrieve most of the new Phase 3 data using same/different phase logic:
      *           For this, we have added to the file, 22 new fields:
      *                  11 Transferred-In-Same Phase
      *                  11 Transferred-In-Different Phase
      *
      *           The 2 new wean weight head/pounds fields were added--they are not broken
      *           out by phase.
      *
      * 12/15/03  LeAnne Fedor
      *           Separate from the Phase 3 additions, but done at the same time, we added
      *           8 new fields for both head and pounds for:
      *               DOA from cull sales
      *               DOA from feeder pig sales
      *               DOA from market sales
      *               DOA from transfers
      *
      * 12/22/03  Barb Gutierrez
      *           Recalculate the weaned head and the weaned lbs.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart Group Header file.
      *
      * 08/02/06  LeAnne Fedor
      *           Added 2 fields to HPPF034-Datamart Hog Group:
      *               1) Ration Paylean Flag
      *                    If the group consumed ANY of Ration Code 436, the flag is set to YES.
      *                    Otherwise, the flag is NO.
      *               2) Ration 8 Feed Pounds
      *                    This is now used for 'all other' feed pounds
      *           Changed Ration 7 Feed Pounds logic:
      *               For Nursery groups, we set this to zero.
      *               For Grow Finish groups, we populate this with Ration 436 pounds.
      *
      * 08/09/07  LeAnne Ramsey
      *           Added new 'target' logic--which requires the 'adjusted transfer in' head
      *           --which we don't have in our Datamart Group Header file until we get
      *           to this program.
      *
      * 09/10/07  LeAnne Ramsey
      *           Changed field name:
      *                 CAQTR to CAHPSQT
      *
      * 09/21/07  LeAnne Ramsey
      *           Removed/added fields to HPPF034.
      *           Removed 'target' logic added on 08/09/07. Since we now need to do
      *           'targets' for OPEN as well as Disposed/Closed groups, we will move
      *           the target logic to a new program.
      *
      * 02/14/08  LeAnne Ramsey
      *           Removed Calendar file; it was no longer used in this program.
      *
      * 05/15/09  LeAnne Ramsey
      *           Sami Wilson reported that the Deduction/Add-on Amount was doubled.
      *           This has, apparently, been the case since the beginning.
      *           The bug was that there were multiple Deduction/Add-on records with
      *           the same "report code". Our logic should have been retrieving dollars
      *           for each "unique" report code; it was retrieving dollars for each record.
      *           So, I changed the logic for HSP044 and HSP045--which could have the
      *           same problem.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 10/14/16  Barb Gutierrez
      *           Recompile only. Added fields Sold days in phase, Avg Sold days in phase,
      *           Wgt open date, and company number to HPPF034.         E6136
      *
      * 03/25/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl006b   if   e           k disk
      *    Reporting codes
      *
      *
     Fhsp033    if   e           k disk
      *    Closed group summary
      *    (File has been created in QTEMP and now holds both disposed and closed groups)
      *
      *
     Fhsl034q   if   e           k disk    rename(hgrec:hgrecq) prefix(p1)
      *    Hog group
      *
      *
     Fhppf034   uf   e           k disk
      *    Datamart FIN: group header
      *
      *
     Fhsl038e   if   e           k disk
      *    Feed ticket detail
      *
      *
     Fhsl044a   if   e           k disk
      *    Deductions/addons
      *
      *
     Fhsl045a   if   e           k disk
      *    Premium/discount
      *
      *
     Fhsp047    if   e           k disk
      *    Group period
      *
      *
     Fhsj075g   if   e           k disk
      *  Transfer movement detail + Transfer movement header
      *
      *
     Fhsl118c   if   e           k disk
      *    Costed movements
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      * Report Codes
      *
     D fps             c                   const('FPS  ')
     D mrkts           c                   const('MRKTS')
     D cos             c                   const('COS  ')
     D btcar           c                   const('BTCAR')
     D btliv           c                   const('BTLIV')
     D ddead           c                   const('DDEAD')
     D dead            c                   const('DEAD ')
     D trana           c                   const('TRANA')
     D trani           c                   const('TRANI')
     D reji            c                   const('REJI ')
     D tridp           c                   const('TRIDP')
     D trisp           c                   const('TRISP')
     D trano           c                   const('TRANO')
     D rejo            c                   const('REJO ')
     D trodp           c                   const('TRODP')
     D trosp           c                   const('TROSP')
     D fcons           c                   const('FCONS')
     D inadj           c                   const('INADJ')
     D culls           c                   const('CULLS')
     D wgain           c                   const('WGAIN')
     D cyiel           c                   const('CYIEL')
     D bfin            c                   const('BFIN ')
     D lean            c                   const('LEAN ')
     D leye            c                   const('LEYE ')
     D pdays           c                   const('PDAYS')
     D fcal            c                   const('FCAL ')
      *
      * Report Categories
      *
     D opcst           c                   const('OPCST')
     D otcst           c                   const('OTCST')
     D sales           c                   const('SALES')
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D svrpcd          s                   like(rprpcd)
      *
      *
      * Workfields
      *
     D kyrpcd          s                   like(rprpcd)
      *
     D wklb            s                   like(cgcglb)
     D wkhd            s                   like(cgcghd)
     D wk$lb           s             15 10
     D wkavg           s              7  4
      *
     D wkdays          s              5  0
     D wkcymdiso       s               D   datfmt(*iso)
     D wkccyymmdd      s              8  0
     D wknulldt        s                   like(hgdsdt) inz(D'0001-01-01')
     D wkwkbegdt       s                   like(hgcldt)
      *
      *
      * Parm fields
      *
     D xxcurbegdt      s              8  0
     D xxcurhist       s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *-----------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Read each record in the Datamart Group Header file.  If the group status
      * is closed or disposed, start processing the record.
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hppf034                                90
     C                   if        *in90 = *off and
     C                             (hggscd = 'CL' or hggscd = 'DS')
      *
      * Continue processing this group when this is:
      * 1) a History run or
      * 2) the group is Disposed or
      * 3) the group is closed and the closed date is on/after the 'current'
      *    beginning date from the data area
      *
     C                   if        xxcurhist = 'H' or                           If continue
     C                             hggscd = 'DS' or
     C                             hgcldt >= wkwkbegdt
      *
      * Retrieve the following information for the hog group.
      *    KOA
      *    Movements
      *    Performance
      *    Sales
      *    Costs (including cull sales as a cost)
      *    Other costs
      *    Down days
      *    Feed pounds
      *    Transfer-In data from the Costed Movements file
      *    Wean weight
      *
     C                   exsr      $koa
     C                   exsr      $move
     C                   exsr      $perf
     C                   exsr      $sale
     C                   exsr      $cost
     C                   exsr      $otcst
     C                   exsr      $downdays
     C                   exsr      $feed
     C                   exsr      $trani
     C                   exsr      $wean
      *
      * Update the group record.
      *
     C                   update    hgrec
      *
     C                   endif                                                  If continue
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *--------------------------------------------------------------
      * EOF Processing
      *--------------------------------------------------------------
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * KOA-Killed on Arrival
      *----------------------------------------------------------------
      *
      * In September 2003, Alice wanted KOA fields added/populated in the Datamart file.
      * The KOA fields are companion fields to the existing DOA fields.
      * Since we don't have KOA broken out from DOA in the Closed Group/Disposed group file,
      * we have to go back to the Transfer Movement detail to get the KOA head (we don't have
      * KOA pounds available in the HPS database.) So, AMB wanted the KOA retrieval logic
      * cloned from HP220-Datamart FIN: Build Weekly Group Detail.
      *
     C     $koa          begsr
      *
      * Read all transfer movement detail records where the group is the origin
      * group.
      *
     C     hghgsn        setll     hsj075g
      *
     C                   dou       *in91 = *on                                  Do tran KOA loop
     C     hghgsn        reade     hsj075g                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             mdkoahd <> 0
     C                   add       mdkoahd       hgkoahd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do tran KOA loop
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Movement information
      *----------------------------------------------------------------
      *
     C     $move         begsr
      *
      *-------------------------------
      * Transferred in
      *-------------------------------
      *
     C                   movel     trani         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgtihd
     C                   z-add     cgcglb        hgtilb
     C                   z-add     cgcgam        hgtiam
     C                   endif
      *
      *-------------------------------
      * Reject in
      *-------------------------------
      *
     C                   movel     reji          kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgrihd
     C                   z-add     cgcglb        hgrilb
     C                   endif
      *
      *-------------------------------
      * Transferred in-same phase
      *-------------------------------
      *
     C                   movel     trisp         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgstihd
     C                   z-add     cgcglb        hgstilb
     C                   z-add     cgcgam        hgstiam
     C                   endif
      *
      *-------------------------------
      * Transferred in-different phase
      *-------------------------------
      *
     C                   movel     tridp         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgdtihd
     C                   z-add     cgcglb        hgdtilb
     C                   z-add     cgcgam        hgdtiam
     C                   endif
      *
      *-------------------------------
      * Adjusted Transferred in
      *-------------------------------
      *
     C                   movel     trana         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgajtihd
     C                   z-add     cgcglb        hgajtilb
     C                   endif
      *
      *---------------------------------
      * Transferred out
      *----------------------------------
      *
     C                   z-add     0             wk$lb
      *
     C                   movel     trano         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgtohd
     C                   z-add     cgcglb        hgtolb
     C                   z-add     cgcgam        hgtoam
     C                   z-add     cgdohd        hgtdoahd
     C                   z-add     cgdolb        hgtdoalb
     C                   if        hgtolb <> 0
     C     hgtoam        div       hgtolb        wk$lb
     C                   endif
     C                   endif
      *
      *
      *-------------------------------
      * Reject out
      *-------------------------------
      *
     C                   movel     rejo          kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgrohd
     C                   z-add     cgcglb        hgrolb
     C                   endif
      *
      *---------------------------------
      * Transferred out-same phase
      *----------------------------------
      *
     C                   movel     trosp         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgstohd
     C                   z-add     cgcglb        hgstolb
     C                   endif
      *
      *----------------------------------
      * Transferred out-different phase
      *----------------------------------
      *
     C                   movel     trodp         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgdtohd
     C                   z-add     cgcglb        hgdtolb
     C                   endif
      *
      * Determine how much of the total transfer out dollars should go
      * to each of the out-same phase and out-different phase records.
      *
     C                   select
     C                   when      hgstolb <> 0 and
     C                             hgdtolb <> 0
     C     wk$lb         mult(h)   hgstolb       hgstoam
     C     hgtoam        sub       hgstoam       hgdtoam
      *
     C                   when      hgstolb <> 0
     C                   z-add     hgtoam        hgstoam
      *
     C                   when      hgdtolb <> 0
     C                   z-add     hgtoam        hgdtoam
     C                   endsl
      *
      *--------------------------------------------------------------
      * Daily dead
      *--------------------------------------------------------------
      *
     C                   movel     ddead         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgddhd
     C                   z-add     cgcglb        hgddlb
     C                   endif
      *
      *------------------------------------------------------
      * Get the dead for use in calculating DOA values
      *------------------------------------------------------
      *
     C                   movel     dead          kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        wkhd
     C                   z-add     cgcglb        wklb
     C                   else
     C                   z-add     0             wkhd
     C                   z-add     0             wklb
     C                   endif
      *
      *------------------------------------------------------
      * Calculate DOA values as DEAD values less DDEAD values
      *------------------------------------------------------
      *
     C     wkhd          sub       hgddhd        hgdoahd
     C     wklb          sub       hgddlb        hgdoalb
      *
      *----------------------------------------------------------
      * Get the adjustments and subtract from the mortality head
      *----------------------------------------------------------
      *
     C                   movel     inadj         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcghd        hgiahd
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Performance Statistics
      *----------------------------------------------------------------
      *
     C     $perf         begsr
      *
      *----------------------------------------------------
      * Retrieve total cost to use as phase cost
      *----------------------------------------------------
      *
     C                   movel     cos           kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcgam        hgp$am
     C                   endif
      *
      * Deduct the cost of transfers in from the phase cost
      *
     C                   sub       hgtiam        hgp$am
      *
      *-------------------------
      * Weight gain
      *-------------------------
      *
     C                   movel     wgain         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcglb        hgwglb
     C                   endif
      *
      *-------------------------
      * Retrieve pig days
      *-------------------------
      *
     C                   movel     pdays         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcgqt        hgpigday
     C                   endif
      *
      *-------------------------------------------------------------------------------
      * The remaining code in this subroutine is only executed for Grow Finish Groups
      *-------------------------------------------------------------------------------
      *
     C                   if        hgppcd = 'GF   '                             If grow/finish
      *
      *--------------------------
      * Retrieve backfat inches
      *---------------------------
      *
     C                   movel     bfin          kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcgqt        hgbfin
     C                   z-add     cgcrlb        hgcrlb
     C                   endif
      *
      *------------------------
      * Retrieve carcass yield
      *------------------------
      *
     C                   movel     cyiel         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcglb        hgcylb
     C                   z-add     cgcrlb        hgcycb
     C                   endif
      *
      *------------------------
      * Retrieve lean percent
      *------------------------
      *
     C                   movel     lean          kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcgqt        hglean
     C                   z-add     cgcrlb        hglncb
     C                   endif
      *
      *--------------------------
      * Retrieve loin eye depth
      *---------------------------
      *
     C                   movel     leye          kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcgqt        hgleye
     C                   endif
      *
     C                   endif                                                  If grow/finish
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Retrieve sale info
      *----------------------------------------------------------------
      *
     C     $sale         begsr
      *
      *-----------------------------------------------------
      * Retrieve butcher hog, live and feeder pig sales
      *-----------------------------------------------------
      *
      * Market sales paid carcass
      *
     C                   move      btcar         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     cgcghd        hgcarhd
     C                   z-add     cgcglb        hgcarlb
     C                   z-add     cgcgam        hgcaram
     C                   endif                                                  If hit
      *
      * Market sales paid live
      *
     C                   move      btliv         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     cgcghd        hglivhd
     C                   z-add     cgcglb        hglivlb
     C                   z-add     cgcgam        hglivam
     C                   endif                                                  If hit
      *
      * Market sales
      *
     C                   move      mrkts         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     cgdohd        hgmdoahd
     C                   z-add     cgdolb        hgmdoalb
     C                   endif                                                  If hit
      *
      * Feeder pig sales
      *
     C                   move      fps           kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     cgcghd        hgfpshd
     C                   z-add     cgcglb        hgfpslb
     C                   z-add     cgcgam        hgfpsam
     C                   z-add     cgdohd        hgfdoahd
     C                   z-add     cgdolb        hgfdoalb
     C                   endif                                                  If hit
      *
      *
      *
      *-------------------------------
      * Retrieve calories
      *-------------------------------
      *
     C                   movel     fcal          kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcgqt        hgfcal
     C                   endif
      *
      *-------------------------------
      * Retrieve feed consumed
      *-------------------------------
      *
     C                   movel     fcons         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   z-add     cgcglb        hgfclb
     C                   z-add     cgcgam        hgfcam
     C                   endif
      *
      *
      *-----------------------------------------------------------
      * Retrieve premium/discount dollars
      *-----------------------------------------------------------
      *
     C                   move      *blank        svrpcd
     C     *loval        setll     hsl045a
      *
     C                   dou       *in91 = *on                                  Do premiums
     C                   read      hsl045a                                91
     C                   if        *in91 = *off and pdrpcd <> svrpcd            If not EOF
     C                   movel     pdrpcd        svrpcd
     C                   movel     pdrpcd        kyrpcd
      *
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   add       cgcgam        hgpdam
     C                   endif
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do premiums
      *
      *---------------------------------------------------
      * Retrieve deduction/add-on dollars
      *---------------------------------------------------
      *
     C                   move      *blank        svrpcd
     C     *loval        setll     hsl044a
      *
     C                   dou       *in91 = *on                                  Do discounts
     C                   read      hsl044a                                91
     C                   if        *in91 = *off and darpcd <> svrpcd            If not EOF
     C                   movel     darpcd        svrpcd
     C                   movel     darpcd        kyrpcd
      *
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off
     C                   add       cgcgam        hgdaam
     C                   endif
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do discounts
      *
      *
      * Calculate net sales dollars =
      *     sales + deduction addons + premium discounts
      *
     C                   eval      hgn$am = hgcaram + hglivam + hgfpsam
     C                                    + hgdaam + hgpdam
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Cost information
      *----------------------------------------------------------------
      *
     C     $cost         begsr
      *
      * Add transfer in dollars to total cost
      *
     C                   add       hgtiam        hgtcam
      *
      * Add feed dollars into total cost
      *
     C                   add       hgfcam        hgtcam
      *
      *-----------------------------------------------------------------
      * Retrieve operating costs
      *-----------------------------------------------------------------
      *
     C     opcst         setll     hsl006b
     C                   setoff                                       91
      *
     C                   dou       *in91 = *on                                  Do oper cost
     C     opcst         reade     hsl006b                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   movel     rprpcd        kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off                                 If hit
     C                   add       cgcgam        hgocam
     C                   add       cgcgam        hgtcam
     C                   endif                                                  If hit
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do oper cost
      *
      *---------------------------------------------------------------------
      * Retrieve cull sales
      *----------------------------------------------------------------------
      *
     C                   movel     culls         kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off                                 If culls
     C                   z-add     cgcghd        hgcuhd
     C                   z-add     cgcglb        hgculb
     C                   z-add     cgcgam        hgcuam
     C                   z-add     cgdohd        hgcdoahd
     C                   z-add     cgdolb        hgcdoalb
      *
      * Subtract the cull sales dollars from the costs
      *
     C                   sub       cgcgam        hgtcam
     C                   sub       cgcgam        hgocam
     C                   endif                                                  If culls
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Retrieve Other Costs
      *----------------------------------------------------------------
      *
      * Do not include the total cost record in this processing
      *
     C     $otcst        begsr
      *
     C     otcst         setll     hsl006b
     C                   setoff                                       91
      *
     C                   dou       *in91 = *on                                  Do other
     C     otcst         reade     hsl006b                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             rprpcd <> cos
      *
     C                   movel     rprpcd        kyrpcd
     C     key01         chain     hsp033                             92
     C                   if        *in92 = *off                                 If hit
     C                   add       cgcgam        hgtcam
     C                   add       cgcgam        hgocam
     C                   endif                                                  If hit
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do other
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Down days
      *----------------------------------------------------------------
      *
     C     $downdays     begsr
      *
     C                   z-add     0             wkdays
      *
      * Convert the open date of the group from date format to CCYYMMDD.
      *
     C                   if        hgdsdt <> wknulldt                           If disposed
     C                   movel     hgopdt        wkccyymmdd
      *
      * Position yourself in the HPS Hog Group file at the group you are
      * processing.  Then, read the next record for that farm/building/room.
      * If you found an open group in the building/room, determine the
      * difference in the disposed date of the group you are processing and the
      * open date of the open group that you just found.
      *
     C     key02         chain     hsl034q                            92        If processed
     C                   if        *in92 = *off
     C     key03         reade     hsl034q                                92
     C                   if        *in92 = *off                                 If found group
      *
     C     *iso          test(d)                 p1hgopdt               92
     C                   if        *in92 = *off                                 If OK date
     C     *iso          movel     p1hgopdt      wkcymdiso
     C     wkcymdiso     subdur    hgdsdt        wkdays:*days
     C                   endif                                                  If OK date
      *
     C                   endif                                                  If found group
     C                   endif                                                  If processed
      *
     C                   endif                                                  If disposed
      *
      * Decrease the calculated difference by 1 day.
      *
     C     wkdays        sub       1             wkdays
      *
      * If down days is greater than 0, populate the field in the file.
      *
     C                   if        wkdays > 0
     C                   z-add     wkdays        hgdwnday
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Feed pounds
      *----------------------------------------------------------------
      *
      * We do a lot of hard-coding here.  We are accumulating feed pounds for 7
      * hard-coded rations; pounds for all other rations are lumped into an eighth
      * field. The '7 rations' are different between Nursery and Grow Finish.
      *
     C     $feed         begsr
      *
      * Initialize the "Ration Paylean Flag" to NO.
      *
     C                   move      no            hgpayfl
      *
      * For Nursery groups, set the Ration 7 field to zero. (The users do not
      * want ration 7 populated for Nursery groups.)
      *
     C                   if        hgppcd = 'NUR  '
     C                   z-add     0             hg7fdlb
     C                   endif
      *
     C     hghgsn        setll     hsl038e
      *
     C                   dou       *in93 = *on                                  Do feed
     C     hghgsn        reade     hsl038e                                93
     C                   if        *in93 = *off                                 If not EOF
      *
      * If the group consumed ANY of ration code 436, set the Paylean Flag to
      * YES; otherwise, the flag is No.
      *
     C                   if        fdrncd = '436  '
     C                   move      yes           hgpayfl
     C                   endif
      *
      * Nursery groups
     C                   select
     C                   when      hgppcd = 'NUR  '
      *
     C                   select
     C                   when      fdrncd = '420  '
     C                   add       fdfdlb        hg1fdlb
     C                   when      fdrncd = '421  '
     C                   add       fdfdlb        hg2fdlb
     C                   when      fdrncd = '422  '
     C                   add       fdfdlb        hg3fdlb
     C                   when      fdrncd = '423  '
     C                   add       fdfdlb        hg4fdlb
     C                   when      fdrncd = '424  '
     C                   add       fdfdlb        hg5fdlb
     C                   when      fdrncd = '431  '
     C                   add       fdfdlb        hg6fdlb
     C                   other
     C                   add       fdfdlb        hg8fdlb
     C                   endsl
      *
      * Grow finish groups
     C                   when      hgppcd = 'GF   '
     C                   select
     C                   when      fdrncd = '424  '
     C                   add       fdfdlb        hg1fdlb
     C                   when      fdrncd = '431  '
     C                   add       fdfdlb        hg2fdlb
     C                   when      fdrncd = '432  '
     C                   add       fdfdlb        hg3fdlb
     C                   when      fdrncd = '433  '
     C                   add       fdfdlb        hg4fdlb
     C                   when      fdrncd = '434  '
     C                   add       fdfdlb        hg5fdlb
     C                   when      fdrncd = '435  '
     C                   add       fdfdlb        hg6fdlb
     C                   when      fdrncd = '436  '
     C                   add       fdfdlb        hg7fdlb
     C                   other
     C                   add       fdfdlb        hg8fdlb
     C                   endsl
      *
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do feed
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Transfer in data from the Costed Movements file
      *----------------------------------------------------------------
      *
     C     $trani        begsr
      *
     C     hghgsn        setll     hsl118c
      *
     C                   dou       *in91 = *on                                  Do costed move
     C     hghgsn        reade     hsl118c                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      cmorppcd = cmdnppcd
     C                   add       cmorpday      hgispday
     C                   add       cmfdlb        hgisfdlb
     C                   add       cmiahd        hgisiahd
     C                   add       cmcshd        hgiscuhd
     C                   add       cmcslb        hgisculb
     C                   add       cmddhd        hgisddhd
     C                   add       cmddlb        hgisddlb
     C                   add       cmdohd        hgisdohd
     C                   add       cmdolb        hgisdolb
     C                   add       cmkohd        hgiskohd
     C                   add       cmkolb        hgiskolb
     C                   other
      *
     C                   add       cmorpday      hgidpday
     C                   add       cmfdlb        hgidfdlb
     C                   add       cmiahd        hgidiahd
     C                   add       cmcshd        hgidcuhd
     C                   add       cmcslb        hgidculb
     C                   add       cmddhd        hgidddhd
     C                   add       cmddlb        hgidddlb
     C                   add       cmdohd        hgiddohd
     C                   add       cmdolb        hgiddolb
     C                   add       cmkohd        hgidkohd
     C                   add       cmkolb        hgidkolb
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do costed move
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Wean weight
      *----------------------------------------------------------------
      *
     C     $wean         begsr
      *
     C     hghgsn        chain     hsp047                             92
     C                   if        *in92 = *off
     C     gptdwwhd      sub       hgisiahd      hgwwhd
     C                   sub       hgidiahd      hgwwhd
     C                   add       hgiscuhd      hgwwhd
     C                   add       hgidcuhd      hgwwhd
     C                   add       hgisddhd      hgwwhd
     C                   add       hgidddhd      hgwwhd
     C                   add       hgisdohd      hgwwhd
     C                   add       hgiddohd      hgwwhd
     C                   add       hgiskohd      hgwwhd
     C                   add       hgidkohd      hgwwhd
      *
      * calculate the average wean lbs
      *
     C     gptdwwlb      div       gptdwwhd      wkavg                          Original avg wn wgt
     C     hgwwhd        mult      wkavg         hgwwlb                         New total wean wgt
      *
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxcurhist
     C                   parm                    xxcurbegdt
      *
      * If this is a 'Current' run, you will ultimately need the incoming date in ISO format
      * date comparison purposes. So, just get it ready now!
      *
     C     *iso          movel     xxcurbegdt    wkwkbegdt
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    hghgsn
     C                   kfld                    kyrpcd
      *
     C     key02         klist
     C                   kfld                    hgfscd
     C                   kfld                    hgblcd
     C                   kfld                    hgrmcd
     C                   kfld                    wkccyymmdd
     C                   kfld                    hghgsn
      *
     C     key03         klist
     C                   kfld                    hgfscd
     C                   kfld                    hgblcd
     C                   kfld                    hgrmcd
      *
     C                   endsr
      /EJECT
