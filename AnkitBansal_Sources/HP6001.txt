      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6001
      * TITLE:       Listing of BGF Farrowing and Preg Checked Positive Data
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/19/00
      *
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP7001-View BGF Pregnancy
      *            and Farrowing Data.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 10/02/01  LeAnne Fedor
      *           Added additional report selectors.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp093    if   e           k disk
      *    BGF farrowing and pregnancy check events (records selected by openquery)
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D procfl          s              1    inz('Y')
      *
      *
      * Save fields
      *
     D svfscd          s                   like(fsfscd)
      *
      *
      * Print fields
      *
     D h1fsnm          s                   like(fsfsnm)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldevyr                 1      4  0
     D  ldevwk                 5      6  0
     D  ldevfl                 7      7
     D  ldfscd                 8     12  0
     D  ldcell                13     14  0
     D  ldsvyr                15     18  0
     D  ldsvwk                19     20  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     *loval        setll     hsp093
     C                   except    h1hdr
      *
      * Read all records in the file that were selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp093                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      yes           procfl
      *
      * If the user selected a specific cell, compare the cell of the farm
      * to the user's selected cell.
      *
     C                   if        ldcell <> 0                                  If 1 cell
     C     befscd        chain     hsp018                             92
     C                   if        *in92 = *on or fscell <> ldcell
     C                   move      no            procfl
     C                   endif
     C                   endif                                                  If 1 cell
      *
     C                   if        procfl = yes                                 If continue
      *
     C                   if        first = yes
     C                   z-add     befscd        svfscd
     C                   move      no            first
     C                   endif
      *
      * Print 1) report headings or 2) a blank line between farm sites if
      * you are printing all data
      *
     C                   select
     C                   when      *inof = *on
     C                   except    h1hdr
      *
     C                   when      befscd <> svfscd
     C                   except    blank
     C                   z-add     befscd        svfscd
     C                   endsl
      *
      *
      * Print detail line
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If continue
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      *
      * Event year/week
     C                   if        ldevyr = 0 and ldevwk = 0
     C                   seton                                        87
     C                   endif
      *
      *
      * Farrowing/preg checked positive
      *
     C                   if        ldevfl = *blank
     C                   seton                                        85
     C                   endif
      *
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        86
     C                   endif
      *
      * Farm site
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   else
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel(p)  fsfsnm        h1fsnm
     C                   else
     C                   movel(p)  'UNKNOWN'     h1fsnm
     C                   endif
     C                   endif
      *
      *
      * Service year/week
     C                   if        ldsvyr = 0 and ldsvwk = 0
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           47 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           36 'LISTING OF BGF FARROWING'
     O                                           60 'AND PREG CHECK POSITIVE'
     O                                           65 'DATA'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                           32 'Event year/week:'
     O              N87      ldevyr        z     38
     O              N87      ldevwk              42
     O               87                          37 'ALL'
      *
      *
     O          e            h1hdr          1
     O                                           23 'Farrowing/preg checked '
     O                                           32 'positive:'
     O              N85      ldevfl              35
     O               85                          37 'ALL'
      *
     O          e            h1hdr          1
     O                                           32 'Cell:'
     O              N86      ldcell        z     36
     O               86                          37 'ALL'
      *
     O          e            h1hdr          1
     O                                           32 'Farm site:'
     O              N89      ldfscd        z     38
     O              N89      fsfsnm              65
     O               89                          37 'ALL'
      *
      *
     O          e            h1hdr          2
     O                                           32 'Service year/week:'
     O              N88      ldsvyr        z     38
     O              N88      ldsvwk              42
     O               88                          37 'ALL'
      *
      *-------------------------------------------------------------
      * Columne headings
      *-------------------------------------------------------------
     O          e            h1hdr          1
     O                                            5 'FARM'
     O                                           20 '-- EVENT ---'
     O                                           33 'FARROWED/ '
     O                                           47 '--SERVICED--'
     O                                           60 'GILT'
     O                                           70 'GROUP'
     O          e            h1hdr          1
     O                                            5 'SITE'
     O                                           12 'YEAR'
     O                                           20 'WEEK'
     O                                           32 'PREG POS'
     O                                           40 'YEAR'
     O                                           47 'WEEK'
     O                                           60 'HEAD'
     O                                           70 'HEAD'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       befscd        z      5
     O                       beevyr        z     12
     O                       beevwk        z     20
     O                       beevfl              28
     O                       besvyr        z     40
     O                       besvwk        z     46
     O                       beglevhd      k     61
     O                       beevhd        k     71
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
