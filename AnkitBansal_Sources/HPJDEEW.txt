     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPJDEE
      * TITLE:       RETRIEVE ACCOUNT BALANCE FOR HPS EXPENSE INTERFACE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     12/30/97
      *
      * FUNCTION:  THIS PROGRAM IS CALLED FROM THE HPS INTERFACE PROGRAM
      *            TO RETRIEVE THE ACCOUNT BALANCES AND WRITE RECORDS
      *            TO THE HPS RECEIVING FILE.
      *
      *            This is the JDE World version of HPJDEE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/10/01  LEANNE FEDOR
      *           CHANGED LOGIC USED TO EXTRACT ONLY NURSERY AND GROW FINISH
      *           DATA.
      *
      * 04/08/04  BARB GUTIERREZ
      *           ADDED LOGIC TO ONLY INCLUDE LEDGER CODES FROM JDE OF
      *           "AA" (ACTUAL AMOUNTS).  DON'T WANT TO INCLUDE
      *           "AU" (ACTUAL UNITS).
      *
      * 04/19/05  LeAnne Fedor
      *           (Converted this program to RPGLE.)
      *           We will now exclude records for account "6150" for farms
      *           that have been set up with a Feed Delivery Zone of '999  '.
      *
      * 10/21/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference' to HSP018.
      *
      * 03/01/16  Barb Gutierrez  (E5290)
      *           Changed key to HSL018I to use company in addition to cost center.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FF0902CB   IF   E           K DISK
      *    JDE ACCOUNT BALANCES FILE
      *
      *
     FF0006     IF   E           K DISK
      *    COST CENTER MASTER
      *
      *
     FHSL018I   IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP230    O    E           K DISK
      *    HPS RECEIVING FILE
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *  SETUP COMPANY NUMBER TO BE RIGHT JUSTIFIED ZERO FILLED.
      *  IE. 350 = 00350
      *
     D                 DS
     D  WKCO                   1      5
     D  WKZERO                 1      2    INZ('00')
     D  WKCOCD                 3      5
      *
      *
      * EXTRACT FISCAL YEAR FROM CENTURY YEAR FIELD
      *
     D                 DS
     D  WKCCYY                 1      4  0
     D  WKCC                   1      2  0
     D  WKYY                   3      4  0
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C     SDPGM         IFEQ      *BLANK
     C                   ENDIF
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C     KEY01         SETLL     F0902CB
      *
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C     KEY01         READE     F0902CB                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
     C     GBLT          ANDEQ     'AA'                                         LEDGER TYPE
     C                   CLEAR                   ERREC
      *
     C                   MOVE      NO            PROCFL
      *
      * DETERMINE IF THIS IS 'NURSERY' OR 'GROW FINISH' COST CENTER.
      *
     C                   EXSR      $NURGF
      *
      * If you're still good to process and this is account 6150,
      * check the Farm's Feed Zone.
      *
     C                   if        procfl = yes and
     C                             gbobj = '6150  '
     C                   exsr      $zone
     C                   endif
      *
     C     PROCFL        IFEQ      YES                                          IF NUR/GF
      *
      * ALWAYS DIVIDE THE JDE AMOUNT BY 100. THE DOLLAR
      * AMOUNT IS HELD IN A 15,0 FIELD--BUT IT REALLY
      * HAS TWO DECIMAL PLACES!!!!
      *
     C                   SELECT
     C     XXPER         WHENEQ    1
     C     GBAN01        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    2
     C     GBAN02        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    3
     C     GBAN03        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    4
     C     GBAN04        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    5
     C     GBAN05        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    6
     C     GBAN06        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    7
     C     GBAN07        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    8
     C     GBAN08        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    9
     C     GBAN09        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    10
     C     GBAN10        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    11
     C     GBAN11        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    12
     C     GBAN12        DIV       100           ERAM
      *
     C     XXPER         WHENEQ    13
     C     GBAN13        DIV       100           ERAM
     C                   ENDSL
      *
      *
      * WRITE A RECEIVING FILE RECORD IF AMOUNT IS NOT ZERO
      *
     C     ERAM          IFNE      0
     C                   MOVEL     XXCOCD        ERCOCD
     C                   Z-ADD     XXPER         ERACPE
     C                   Z-ADD     XXCCYY        ERACYR
     C                   MOVEL     XXEXCD        EREXCD
     C                   MOVEL     GBAID         ERAID
     C                   MOVEL     GBMCU         ERCJD
     C                   MOVEL     GBOBJ         EROBJ
     C                   MOVEL     GBSUB         ERSUB
     C                   WRITE     ERREC
     C                   ENDIF
      *
     C                   ENDIF                                                  IF NUR/GF
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * CHECK THAT THIS IS A NURSERY OR GROW FINISH COST CENTER
      * AND THAT THE "IDLE FACILITY" CODE IS = 'AC '
      *---------------------------------------------------------------
      *
     C     $NURGF        BEGSR
      *
     C     GBMCU         CHAIN     F0006                              92
     C     *IN92         IFEQ      *OFF                                         IF OK
     C     MCRP12        ANDEQ     'AC '
      *
     C     MCRP03        IFEQ      'NUR'                                        NUR
     C     MCRP03        OREQ      'G/F'                                        G/F
     C                   MOVE      YES           PROCFL
     C                   ENDIF
      *
     C                   ENDIF                                                  IF OK
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Check the Farm's Feed Delivery Zone
      *---------------------------------------------------------------
      *
      * Using the JDE "Business Unit", retrieve the Farm Site using the logical file
      * that is keyed by "Cost Center". If the Feed Delivery Zone for this retrieved
      * farm is "999  ", we will NOT extract this JDE Record.
      *
     C     $zone         begsr
      *
     C     key02         chain     hsl018i                            92
     C                   if        *in92 = *off and
     C                             fsdzcd = '999  '
     C                   move      no            procfl
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C     WKCOCD        PARM                    XXCOCD            3
     C     WKCCYY        PARM                    XXCCYY            4 0
     C                   PARM                    XXPER             2 0
     C                   PARM                    XXEXCD            5
     C                   PARM                    XXOBJ             6
      *
      * KEY LISTS
      *
     C     KEY01         KLIST
     C                   KFLD                    WKCO
     C                   KFLD                    WKYY
     C                   KFLD                    XXOBJ
      *
     C     KEY02         KLIST
     C                   KFLD                    WKCOCD
     C                   KFLD                    GBMCU
      *
      *
     C                   MOVE      *BLANK        PROCFL            1
      *
     C                   ENDSR
      /EJECT
