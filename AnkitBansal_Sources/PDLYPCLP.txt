/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR PICK SLIPS         */
/*             CO#, ORDER, REQ. SHIP DATE, AND WAREHOUSE            */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT/RE-PRINT PICK SLIPS                           */
/*  DATE      -  4/20/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FRMORDN    - FROM ORDER                                */
/*          &REQDATN    - REQUESTED SHIP DATE                       */
/*          &WHSE       - WAREHOUSE                                 */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT OUTPUT QUEUE                        */
/********************************************************************/

 PDLYPCLP:   PGM        PARM(&COMPNYN &FRMORDN &REQDATN &WHSE &DESC +
                          &OUTQ &HOLD &SAVE &COPYN &NIGHTQ)

/* PARMS                                                                      */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &FRMORDN    *DEC                  /* ORDER NUMBER   (Passed)   */
        DCL    &REQDATN    *DEC                  /* REQ SHIP DATE  (Passed)   */
        DCL    &WHSE       *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &DESC       *CHAR   (25)          /* DESCRIPTION               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &PGMO       *CHAR    10           /*  PRINTER FILE  */
        DCL    &B          *CHAR    30           /*  QRY-SEL CO.#  */
        DCL    &CC         *CHAR    30           /*  QRY-SEL ORDER NO.  */
        DCL    &DDD        *CHAR    30           /*  QRY-SEL REQ. SHIP DATE  */
        DCL    &E          *CHAR   245           /*  QRY COMPLETE SEL. */
        DCL    &COMMAND    *CHAR   (512)

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &FRORDR     *DEC    (7 0)         /* From ORDER                */
        DCL    &REQDAT     *DEC    (7 0)         /* REQ. SHIP DATE            */

/* OPENQRYF FIELDS                                                            */
        DCL    &COMPA      *CHAR    3            /* ACK COMPLETE              */
        DCL    &FRMORDA    *CHAR    7            /* FROM ORDER                */
        DCL    &REQDATA    *CHAR    7            /* REQ SHIP DATE  (Passed)   */

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/

             CHGVAR     VAR(&PGMO) VALUE(PDLZPFR$)
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&FRORDR) VALUE(&FRMORDN)
             CHGVAR     VAR(&FRMORDA) VALUE(&FRORDR)
             CHGVAR     VAR(&REQDAT) VALUE(&REQDATN)
             CHGVAR     VAR(&REQDATA) VALUE(&REQDAT)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED TO SELECT ACKS.               */
/*********************************************************************/

/*  IF CO. NUMBER SELECTED  */
             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&B) VALUE('BEAIC3 *EQ ' *CAT &COMPA)
             ENDDO

/*  IF ORDER IS ENTERED */

             IF         COND(&FRMORDA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&CC) VALUE('*AND BEC4NB *EQ ' *CAT &FRMORDA)
             ENDDO

/*  IF REQUESTED SHIP DATE IS SELECTED */

             IF         COND(&REQDATA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&DDD) VALUE('*AND BEA5DT *EQ ' *CAT +
                          &REQDATA)
             ENDDO

             CHGVAR     VAR(&E) VALUE(&B *BCAT &CC *BCAT &DDD)

             OVRDBF     FILE(OPBFCPLQ) SHARE(*YES)
             OPNQRYF    FILE((OPBFCPLQ)) QRYSLT(&E) KEYFLD((BEAIC3) +
                          (BEC4NB)) SEQONLY(*YES 1)

/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)


/*********************************************************************/
/*   CALL PRINT PROGRAM -
/*********************************************************************/

             CALL       PGM(PDLZPFR) PARM(' ' &COMPANY &WHSE &DESC)

 END:        ENDPGM
