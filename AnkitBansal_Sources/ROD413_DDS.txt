     A*%%TS  SD  20080103  160659  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD413
     A*  TITLE:         SPECIFY MEAT COST REPORT
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       01/22/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO413R1
     A*%%TS  SD  20080103  160659  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 32'SPECIFY REPORT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Company ...............'
     A            D1CONO    R        B  4 28REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  4 40'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 59'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 66'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Date ..................'
     A            D1MDY     R        B  5 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O  5 44REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  5 59REFFLD(@@YR)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  5 67REFFLD(@@WK)
     A                                      EDTCDE(Z)
     A                                  7  2'Report ................'
     A                                  7 26'?'
     A            D1RPFL    R        B  7 28REFFLD(ROREF/@@FLAG *LIBL/ROPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A            D1RPDS    R        O  7 30REFFLD(ROREF/@@ITDS *LIBL/ROPREF)
     A                                  8  2'Detail or summary .....'
     A                                  8 26'?'
     A            D1DSFL    R        B  8 28REFFLD(ROREF/@@FLAG *LIBL/ROPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A            D1DSDS         7A  O  8 30
     A                                  8 40'(applicable to Meat Cost Report on-
     A                                      ly)'
     A                                      COLOR(BLU)
     A                                 10  2'Primal group code .....'
     A                                 10 26'?'
     A            D1PGCD    R        B 10 28REFFLD(ROREF/@@PGCD *LIBL/ROPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A            D1PGDS    R        O 10 38REFFLD(ROREF/@@ITDS *LIBL/ROPREF)
     A                                 10 70'(Blank=All)'
     A                                 11  2'Production line code ..'
     A            D1PLCD    R        B 11 28REFFLD(ROREF/@@PLCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                 11 70'(Blank=All)'
     A                                 12  2'Item code .............'
     A                                 12 26'?'
     A            D1ITCD    R        B 12 28REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A            D1ITDS    R        O 12 38REFFLD(ROREF/@@ITDS *LIBL/ROPREF)
     A                                 12 70'(Blank=All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO413K1
     A*%%TS  SD  20060928  082122  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO413ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R RO413EC                   SFLCTL(RO413ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
