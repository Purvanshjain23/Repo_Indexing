      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS
      * PROGRAM:     HP271 - Validate Farm Site for Synon
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/16/03
      *
      * FUNCTION:    This program validates incoming Farm Site for SYNON
      *
      *
      *************************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/03/03  LeAnne Fedor
      *           Per Purva Droge, only BGF and GF phase farms are valid.
      *           So, a Nursery farm is not valid here.
      *           Also, added Transportation Office as an output parm.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/22/13  LeAnne Ramsey (E2831)
      *           We will now pass the MTech References back out
      *           to the calling Synon program.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS:
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsp046    if   e           k disk
      *  Truckers
      *
      *
     Fhsp107    if   e           k disk
      *  Drivers
      *
      *
     Fhsp108    if   e           k disk
      *  Load crews
      *
      *
     Fhsp109    if   e           k disk
      *  Transportation office
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parm fields
      *
     D xxreturn        s              7
     D xxfscd          s                   like(fsfscd)
     D xxfsnm          s                   like(fsfsnm)
     D xxfsref         s                   like(fsmtechref)
     D xxsacd          s                   like(fssacd)
     D xxtocd          s                   like(fstocd)
     D xxlccd          s                   like(tolccd)
     D xxlcref         s                   like(lcmtechref)
     D xxticd          s                   like(toticd)
     D xxtiref         s                   like(timtechref)
     D xxdicd          s                   like(todicd)
     D xxdiref         s                   like(dimtechref)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      *
     C     xxfscd        chain     hsp018                             92
     C                   if        *in92 = *on or                               If not found
     C                             fsppcd = 'NUR  '
     C                   eval      xxreturn = 'ERROR'
     C                   else
     C                   movel     fsfsnm        xxfsnm
     C                   movel     fssacd        xxsacd
     C                   movel     fstocd        xxtocd
     C                   movel     fsmtechref    xxfsref
      *
      * Retrieve default Trucker/Driver/Load Crew for the Transportation Office
      *
     C     fstocd        chain     hsp109                             92
     C                   if        *in92 = *off
     C                   move      toticd        xxticd
     C                   move      todicd        xxdicd
     C                   move      tolccd        xxlccd
     C                   endif
      *
      * Retrieve Trucker MTech Reference
      *
     C     xxticd        chain     hsp046                             92
     C                   if        *in92 = *off
     C                   move      timtechref    xxtiref
     C                   endif
      *
      * Retrieve Driver  MTech Reference
      *
     C     xxdicd        chain     hsp107                             92
     C                   if        *in92 = *off
     C                   move      dimtechref    xxdiref
     C                   endif
      *
      * Retrieve Load Crew MTech Reference
      *
     C     xxlccd        chain     hsp108                             92
     C                   if        *in92 = *off
     C                   move      lcmtechref    xxlcref
     C                   endif
     C                   endif                                                  If not found
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxreturn
     C                   parm                    xxfscd
     C                   parm                    xxfsnm
     C                   parm                    xxsacd
     C                   parm                    xxtocd
     C                   parm                    xxticd
     C                   parm                    xxlccd
     C                   parm                    xxdicd
     C                   parm                    xxfsref
     C                   parm                    xxtiref
     C                   parm                    xxlcref
     C                   parm                    xxdiref
      *
     C                   move      *blank        xxfsref
     C                   move      *blank        xxtiref
     C                   move      *blank        xxlcref
     C                   move      *blank        xxdiref
      *
     C                   endsr
      /eject
