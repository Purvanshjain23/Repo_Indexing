     A*%%TS  SD  20050928  161032  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD431
     A*  TITLE:         SPECIFY CLAIM EXPENSE DISTRIBUTION REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       09/28/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF431R1
     A*%%TS  SD  20050928  161032  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 15'SPECIFY OPTIONS FOR CLAIM EXPENSE -
     A                                      DISTRIBUTION REPORT'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'From date ..'
     A            D1FMDY    R     Y  B  4 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                  4 31'Week-ending:'
     A            D1FWEMDY  R     Y  O  4 44REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  5  2'To date ....'
     A            D1TMDY    R     Y  B  5 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                  5 31'Week-ending:'
     A            D1TWEMDY  R     Y  O  5 44REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF431K1
     A*%%TS  SD  20050324  162631  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F17=Print Options'
     A                                 23 41'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF431ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF431EC                   SFLCTL(TF431ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
