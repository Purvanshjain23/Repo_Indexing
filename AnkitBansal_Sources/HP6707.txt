      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6707
      * TITLE:       Listing of Farm/Template Links
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/07/05
      *
      *
      *            This listing is submitted from HP7700-View All Farm/Template Links.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/09/05  LeAnne Fedor
      *           Moved everything to the right 10 characters---so, the users can
      *           punch holes and put this in a binder.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp101    if   e           k disk
      *    Type of farm
      *
      *
     Fhsp185    if   e           k disk
      *    Budget template header
      *
      *
     Fhsl187a   if   e           k disk
      *    Budget template/farm links (records selected in open query)
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
     D upper           c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     D lower           c                   'abcdefghijklmnopqrstuvwxyz'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            110    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D procfl          s              1
     D first           s              1    inz('Y')
     D svfscd          s                   like(fsfscd)
      *
      *
      * Workfields
      *
     D string          s              2  0
     D wkdesc          s                   like(thbtds)
     D wkbtds          s                   like(thbtds)
      *
      *
      * Print fields
      *
     D h1btds          s                   like(thbtds)
     D h1ppds          s                   like(ppppds)
     D h1tfds          s                   like(tftfds)
     D h1fsnm          s                   like(fsfsnm)
      *
     D l1btcd          s                   like(thbtcd)
     D l1btds          s                   like(thbtds)
     D l1fscd          s                   like(fsfscd)
     D l1fsnm          s                   like(fsfsnm)
     D l1ppcd          s                   like(fsppcd)
     D l1cell          s                   like(fscell)
     D l1tfcd          s                   like(fstfcd)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfscd                 1      5  0
     D  ldbtcd                 6     11
     D  ldbtds                12     36
     D  ldcell                37     38  0
     D  ldtfcd                39     43
     D  ldppcd                44     48
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     *loval        setll     hsl187a
     C                   except    h1hdr
      *
      * Process all records in the file (some selections were made with
      * open query).
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl187a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      yes           procfl
      *
      * Retrieve farm info so that you can make more user selections.
      *
     C     tlfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
      *
      * Retrieve Budget Template description
      *
     C     tlbtcd        chain     hsp185                             92
     C                   if        *in92 = *on                                  If no hit
     C                   move      *blank        l1btds
     C                   else
      *
     C                   move      thbtds        l1btds
      *
      * Prepare to scan on Budget Template Description
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      ldbtds = %trim(ldbtds)
     C     ' '           checkr    ldbtds        string
      *
     C                   if        ldbtds <> *blank
     C     lower:upper   xlate     ldbtds        wkdesc
     C     lower:upper   xlate     thbtds        wkbtds
     C     wkdesc:string scan      wkbtds                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
     C                   endif                                                  If no hit
      *
      * Budget template
     C                   if        ldbtcd <> *blank and
     C                             ldbtcd <> tlbtcd
     C                   move      no            procfl
     C                   endif
      *
      * Cell
     C                   if        ldcell <> 0 and
     C                             ldcell <> fscell
     C                   move      no            procfl
     C                   endif
      *
      * Type of farm
     C                   if        ldtfcd > *blank and
     C                             ldtfcd <> fstfcd
     C                   move      no            procfl
     C                   endif
      *
      * Production phase
     C                   if        ldppcd > *blank and
     C                             ldppcd <> fsppcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        procfl = yes                                 If process
     C                   z-add     tlfscd        l1fscd
     C                   move      tlbtcd        l1btcd
     C                   move      fsppcd        l1ppcd
     C                   z-add     fscell        l1cell
     C                   move      fsfsnm        l1fsnm
     C                   move      fstfcd        l1tfcd
      * Print
     C                   exsr      $print
      *
     C                   endif                                                  If process
     C                   endif                                                  If farm hit
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------------------------------------------------------------------
      * EOF processing
      *-----------------------------------------------------------------------------
      *
      * If no records met the user's selections, print the standard 'no data...'
      * message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print
      *---------------------------------------------------------------
      *
     C     $print        begsr
      *
      * Print a blank line between farms
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     tlfscd        svfscd
      *
     C                   when      tlfscd <> svfscd
     C                   z-add     tlfscd        svfscd
     C                   except    blank
     C                   endsl
      *
      * Print headings if necessary
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
      * Print detail line
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *  None
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Farm site
     C                   select
     C                   when      ldfscd = 0
     C                   seton                                        85
     C                   other
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   eval      h1fsnm = fsfsnm
     C                   else
     C                   eval      h1fsnm = 'Unknown'
     C                   endif
     C                   endsl
      *
      *
      * Budget template code
      *
     C                   select
     C                   when      ldbtcd = *blank
     C                   seton                                        80
     C                   other
     C     ldbtcd        chain     hsp185                             92
     C                   if        *in92 = *off
     C                   eval      h1btds = thbtds
     C                   else
     C                   eval      h1btds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Budget template description
      *
     C                   if        ldbtds = *blank
     C                   seton                                        87
     C                   endif
      *
      *
      * Production phase
      *
     C                   select
     C                   when      ldppcd = *blank
     C                   seton                                        83
     C                   other
     C     ldppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   eval      h1ppds = ppppds
     C                   else
     C                   eval      h1ppds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Type of farm
     C                   select
     C                   when      ldtfcd = *blank
     C                   seton                                        84
     C                   other
     C     ldtfcd        chain     hsp101                             92
     C                   if        *in92 = *off
     C                   eval      h1tfds = tftfds
     C                   else
     C                   eval      h1tfds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        86
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               20
     O                                           75 'HOG PRODUCTION SYSTEM'
     O                                          110 'DATE'
     O                       udate         y    120
      *
     O          e            h1hdr          1
     O                       sdusr               20
     O                                           75 'LISTING OF FARM/TEMPLATE'
     O                                           81 'LINKS'
     O                                          110 'TIME'
     O                       rtime              120 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          110 'PAGE'
     O                       page          z    120
      *
      *
     O          e            h1hdr          1
     O                                           34 'Farm site code .........'
     O               85                          39 'All'
     O              n85      ldfscd        z     41
     O              n85      h1fsnm              68
      *
      *
     O          e            h1hdr          1
     O                                           34 'Budget template code ...'
     O               80                          39 'All'
     O              n80      ldbtcd              41
     O              n80      h1btds              68
      *
      *
     O          e            h1hdr          1
     O                                           34 'Description ............'
     O               87                          39 'All'
     O              n87      ldbtds              68
      *
      *
     O          e            h1hdr       0  1
     O                                           34 'Cell ...................'
     O               86                          39 'All'
     O              n86      ldcell        z     38
      *
      *
     O          e            h1hdr       0  1
     O                                           34 'Type of farm code ......'
     O               84                          39 'All'
     O              n84      ldtfcd              40
     O              n84      h1tfds              68
      *
      *
     O          e            h1hdr       0  2
     O                                           34 'Production phase code ..'
     O               83                          39 'All'
     O              n83      ldppcd              40
     O              n83      h1ppds              73
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           24 'Budget'
     O                                           79 '------------------------ '
     O                                           84 'Farm '
     O                                          110 'Site ---------------------'
      *
      *
     O          e            h1hdr       0  1
     O                                           15 'Farm'
     O                                           25 'Template'
     O                                           68 'Type of'
     O                                           81 'Production'
      *
      *
     O          e            h1hdr       0  1
     O                                           15 'Site'
     O                                           23 'Code'
     O                                           38 'Description'
     O                                           58 'Cell'
     O                                           66 'Farm'
     O                                           79 'Phase'
     O                                           89 'Name'
      *
     O          e            h1hdr       0  2
     O                       dash               120
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
      *
     O          e            l1dtl          1
     O                       l1fscd        zb    15
     O                       l1btcd         b    24
     O                       l1btds         b    52
     O                       l1cell        zb    57
     O                       l1tfcd         b    67
     O                       l1ppcd         b    79
     O                       l1fsnm         b   110
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           29 'NO DATA MEETS YOUR'
     O                                           48 'SELECTION CRITERIA'
