      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP242 - FOS Download: Build Farms/Bins/Rooms/Groups file
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/29/07
      *
      * FUNCTION:    This function builds a file of Farms/Rooms/Bins/Groups.
      *
      *              John Zimmerman/Brad Ulrick requested this file for use with
      *              both the FOS downloaded data and the MSW downloaded data.
      *
      *              So, the group "rules" for this FOS file are not the SAME as the
      *              "rules" for the other 2 FOS download files.
      *
      *              Here, we want:
      *              1) BGF, Nursery and Grow Finish groups
      *              2) group status of:
      *                    created, opened, disposed and closed in the last 12 weeks
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 05/20/11  LeAnne Ramsey (E1557)
      *           Recompile only. Added Yard Dead to HSJ085E.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsj034w   if   e           k disk
      *  Hog Group + Rooms/Bin sets + Bin Set Bins
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsp020    if   e           k disk
      *  Building rooms
      *
      *
     Fhsj085e   if   e           k disk
      *  Sale movement detail + Sales movement header
      *
      *
     Fhsp242    o    e             disk
      *    Farms/bins/rooms/groups
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               D   datfmt(*iso)
     D wkcldt          s                   like(hgcldt)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Read each record from the join logical.
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsj034w                                90
     C                   if        *in90 = *off and
     C                             (hgcldt = 0 or hgcldt > wkcldt) and
     C                             hggscd <> 'VD'
      *
      * Retrieve Farm info
      *
     C     hgfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        w1cell
     C                   z-add     fspod         w1pod
     C                   move      fsaist        w1aist
     C                   endif
      *
      * Do not process BGF groups on "Inactive" farms.
      *
     C                   if        hgppcd = 'BGF  ' and                         If inactive BGF
     C                             w1aist = 'I'
     C                   else
      *
      * Populate fields with data from the Join file record
      *
     C                   z-add     hghgsn        w1hgsn
     C                   move      hghgcd        w1hgcd
     C                   move      hggscd        w1gscd
     C                   move      hgppcd        w1ppcd
     C                   z-add     hgcrdt        w1crdt
     C                   z-add     hgopdt        w1opdt
     C                   z-add     hgdsdt        w1dsdt
     C                   z-add     hgcldt        w1cldt
      *
     C                   z-add     hgfscd        w1fscd
     C                   move      hgblcd        w1blcd
     C                   move      hgrmcd        w1rmcd
      *
     C                   move      bnbicd        w1bicd
     C                   move      bnbncd        w1bncd
      *
      * Retrieve Room status
      *
     C     key01         chain     hsp020                             92
     C                   if        *in92 = *off
     C                   move      brrmst        w1rmst
     C                   endif
      *
      * Retrieve the date of the first Market Sale for this group.
      *
     C                   if        hgppcd <> 'BGF  '                            If not BGF
     C                   exsr      $mrkts
     C                   endif                                                  If not BGF
      *
      * Write record
     C                   write     w1rec
     C                   clear                   w1rec
     C                   endif                                                  If inactive BGF
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *---------------
      * EOF Processing
      *---------------
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Find the first Market Sale for this group
      *----------------------------------------------------------------
      *
     C     $mrkts        begsr
      *
     C     hghgsn        setll     hsj085e
      *
     C                   dou       *in93 = *on                                  Do sales
     C     hghgsn        reade     hsj085e                                93
     C                   if        *in93 = *off and shstcd = 'MRKTS'            If market
      *
     C                   if        w1rcdt = 0 or
     C                             w1rcdt > shrcdt
     C                   z-add     shrcdt        w1rcdt
     C                   endif
     C
     C                   endif                                                  If market
     C                   enddo                                                  Do sales
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    hgfscd
     C                   kfld                    hgblcd
     C                   kfld                    hgrmcd
      *
      * We want to limit "closed" groups to those groups that closed in the last
      * 12 weeks.
      *
     C     *mdy          movel     udate         wkcymdiso
     C     wkcymdiso     subdur    84:*days      wkcymdiso
     C                   movel     wkcymdiso     wkcldt
      *
     C                   endsr
      /EJECT
