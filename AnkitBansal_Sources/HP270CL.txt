/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP270CL                                       */
/*  PROGRAM NAME:    ERM: DOWNLOAD WEEKLY HOG INVENTORY            */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     11/05/01                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE JOB SCHEDULER   */
/*                   ON BIGBYTE.                                   */
/*                                                                 */
/*  THE ERM SYSTEM (ENVIRONMENTAL RESOURCE MAINTENANCE) IS AN      */
/*  ACCESS DATABASE SYSTEM.  THEY WANT THE WEEKLY AVERAGE HEAD     */
/*  (ON A FARM) FROM HPS FOR THEIR DATABASE.                       */
/*                                                                 */
/*  SO, WE BUILD A WEEKLY RECORD FOR THEM TO ACCESS. WE POPULATE   */
/*  THE RECORD USING WEEKLY DATA ALREADY EXTRACTED FOR DATAMART.   */
/*  IN DATAMART, A 'WEEK' ENDS ON SATURDAY. SO, THE DATA WE PASS   */
/*  TO ERM IS FOR A WEEK ENDING ON SATURDAY.                       */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATAB) TYPE(*CHAR) LEN(60)
             DCL        VAR(&QDATAF) TYPE(*CHAR) LEN(60)

             DCL        VAR(&CDYR) TYPE(*DEC)  LEN(4)
             DCL        VAR(&CDWK) TYPE(*DEC)  LEN(2)
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(4)

/*-----------------------------------------------------------------*/
/* CLEAR THE DOWNLOAD FILE                                         */
/*-----------------------------------------------------------------*/

             CLRPFM     FILE(*LIBL/HSP270) /* Weekly Hog Inventory */

/*-------------------------------------------------------------------*/
/* I COULDN'T GET AN OPEN QUERY TO WORK OVER THE 'DATE FORMAT' FIELDS*/
/* SO, I DO AN INITIAL CALL TO THE DOWNLOAD PROGRAM JUST TO GET THE  */
/* CALENDAR YEAR/WEEK TO USE AS VARIABLES IN MY OPEN QUERIES!        */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&MODE) VALUE('I')
             CALL       PGM(HP270) PARM(&MODE &CDYR &CDWK)
             CHGVAR     VAR(&MODE) VALUE(' ')

/*-------------------------------------------------------------------*/
/* BUILD/RUN QUERY FOR THE FINISHER DATA.                            */
/*-------------------------------------------------------------------*/

/* ALWAYS SELECT ONLY FINISHER RECORDS ON/AFTER THE RETRIEVED     */
/* CALENDAR YEAR/WEEK                                             */

             CHGVAR     VAR(%SST(&QDATAF 1 11)) VALUE('(WBCDYR *EQ')
             CHGVAR     VAR(%SST(&QDATAF 13 4)) VALUE(&CDYR)
             CHGVAR     VAR(%SST(&QDATAF 18 15)) VALUE('*AND WBCDWK *GE')
             CHGVAR     VAR(%SST(&QDATAF 34 2)) VALUE(&CDWK)
             CHGVAR     VAR(%SST(&QDATAF 36 5)) VALUE(') *OR')
             CHGVAR     VAR(%SST(&QDATAF 42 11)) VALUE('(WBCDYR *GT')
             CHGVAR     VAR(%SST(&QDATAF 54 4)) VALUE(&CDYR)
             CHGVAR     VAR(%SST(&QDATAF 58 1)) VALUE(')')


/*  DATAMART FIN: WEEKLY GROUP DETAIL                            */

             OVRDBF     FILE(HPLF075A) SHARE(*YES)
             OPNQRYF    FILE((HPLF075A)) FORMAT(*FILE) +
                          QRYSLT(&QDATAF) KEYFLD(*FILE) SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* BUILD/RUN QUERY FOR THE BGF DATA.                                 */
/*-------------------------------------------------------------------*/

/* ALWAYS SELECT ONLY BGF WEEKLY RECORDS ON/AFTER THE RETRIEVED     */
/* CALENDAR YEAR/WEEK                                               */

             CHGVAR     VAR(%SST(&QDATAB 1 11)) VALUE('(BPCDYR *EQ')
             CHGVAR     VAR(%SST(&QDATAB 13 4)) VALUE(&CDYR)
             CHGVAR     VAR(%SST(&QDATAB 18 15)) VALUE('*AND BPCDWK *GE')
             CHGVAR     VAR(%SST(&QDATAB 34 2)) VALUE(&CDWK)
             CHGVAR     VAR(%SST(&QDATAB 36 5)) VALUE(') *OR')
             CHGVAR     VAR(%SST(&QDATAB 42 11)) VALUE('(BPCDYR *GT')
             CHGVAR     VAR(%SST(&QDATAB 54 4)) VALUE(&CDYR)
             CHGVAR     VAR(%SST(&QDATAB 58 1)) VALUE(')')


/*  DATAMART BGF: WEEKLY PRODUCTION                              */

             OVRDBF     FILE(HPLB092B) SHARE(*YES)
             OPNQRYF    FILE((HPLB092B)) QRYSLT(&QDATAB) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*-----------------------------------------------------------------*/
/* BUILD THE WEEKLY HOG INVENTORY FILE                             */
/*-----------------------------------------------------------------*/

/* NOTE: THE PARM VALUES ARE NOT RELEVANT ON THIS CALL SINCE WE    */
/* HAVE ALREADY SELECTED OUR DATA WITH THE OPEN QUERIES            */

             CALL       PGM(HP270) PARM(&MODE &CDYR &CDWK)

/*-----------------------------------------------------------------*/
/* DELETE FILE OVERRIDES                                           */
/*-----------------------------------------------------------------*/

             CLOF       OPNID(HPLF075A)
             DLTOVR     FILE(HPLF075A)

             CLOF       OPNID(HPLB092B)
             DLTOVR     FILE(HPLB092B)

             ENDPGM

