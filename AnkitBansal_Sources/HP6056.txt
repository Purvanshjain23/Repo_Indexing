      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6056
      * TITLE:       Daily/Weekly Mortality Variance Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/09/03
      *
      * FUNCTION:  Batch program to read workfile and print report.
      *            This listing is submitted from HP4056-Specify Options etc.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/28/03  LeAnne Fedor
      *           Added additional user selections in the headings. Also, although this
      *           program reads the same workfile, that workfile is now generated from:
      *                    HSP103-Reported Daily Activity
      *                    HSP068-Killed/Dead Hogs
      *           instead of the Datamart files it originally used:
      *                    HPPF103-Datamart FIN: Reported Daily Activity
      *                    HPPF075-Datamart FIN: Weekly Group Detail
      *
      * 04/21/10  LeAnne Ramsey
      *           Per Sami Wilson, remove the last 3 columns from the Report:
      *             Total Weekly Vs Projected
      *              Daily   Weekly  Variance
      *              Head    Head     Head
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp3056   if   e           k disk
      *    Workfile: Daily/Weekly Mortality Variance
      *
      *
     Fqprint    o    f  117        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            117    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svppcd          s                   like(w1ppcd)
     d count           s              5  0
      *
      *
      * Print fields
      *
     D h2ppds          s                   like(ppppds)
     D h1ppcd          s              5
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcdyr                 1      4  0
     D  ldcdwk                 5      6  0
     D  ldfcymd                7     14  0
     D  ldtcymd               15     22  0
     D  ldwemdy               23     28  0
      *
     D  ldppcd                41     45
     D  ldcell                46     47  0
      *
     D  ldppds               115    144
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read/print all records in the workfile.
      *
     C     *loval        setll     hsp3056
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3056                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Print headings
     C                   select
     C                   when      first = yes or w1ppcd <> svppcd
     C                   exsr      $ppds
     C                   move      no            first
      *
     C                   when      *inof = *on
     C                   exsr      $h1hdr
      *
     C                   when      count = 3
     C                   except    blank
     C                   z-add     0             count
     C                   endsl
      *
      * Print detail
     C                   except    l1dtl
     C                   add       1             count
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        prtfl = no
     C                   except    h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Production Phase description for heading
      *---------------------------------------------------------------
      *
     C     $ppds         begsr
      *
     C     w1ppcd        chain     hsp011                             92
     C                   if        *in92 = *off                                 If found
     C                   move      ppppds        h2ppds
     C                   else
     C                   movel     'Unknown'     h2ppds
     C                   endif                                                  If found
      *
     C                   move      w1ppcd        svppcd
      *
     C                   if        *inof = *on or first = yes
     C                   exsr      $h1hdr
     C                   else
     C                   except    h2hdr
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Set up user's selections for Report Heading
      *
     C                   if        ldppcd = *blank
     C                   movel     'All'         h1ppcd
     C                   else
     C                   movel     ldppcd        h1ppcd
     C                   endif
      *
     C                   if        ldcell = 0
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           65 'HOG PRODUCTION SYSTEM'
     O                                          107 'DATE'
     O                       udate         y    117
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           58 'DAILY/WEEKLY MORTALITY'
     O                                           74 ' VARIANCE REPORT'
     O                                          107 'TIME'
     O                       rtime              117 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          107 'PAGE'
     O                       page          z    117
      *
      *
     O          e            h1hdr          1
     O                                           17 'Saturday date:'
     O                       ldwemdy             27 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           17 'Calendar year:'
     O                       ldcdyr        z     23
      *
      *
     O          e            h1hdr          1
     O                                           17 'Calendar week:'
     O                       ldcdwk        z     21
      *
      *
     O          e            h1hdr          1
     O                                           17 'Production phase:'
     O                       h1ppcd              24
     O                       ldppds              56
      *
     O          e            h1hdr          2
     O                                           17 'Cell:'
     O              n88      ldcell        z     21
     O               88                          22 'All'
      *
      *
     O          e            h1hdr          1
     O                                            5 'FARM'
     O                                           31 '--- NATURALLY DEAD ----'
     O                                           59 '----- EUTHANIZED ------'
     O                                           88 '----- TOTAL MORTALITY ----'
      *
     O          e            h1hdr          1
     O                                            5 'SITE'
      *
     O                                           13 'DAILY'
     O                                           21 'WEEKLY'
     O                                           31 'VARIANCE'
      *
     O                                           41 'DAILY'
     O                                           49 'WEEKLY'
     O                                           59 'VARIANCE'
      *
     O                                           69 'DAILY'
     O                                           77 'WEEKLY'
     O                                           87 'VARIANCE'
      *
     O          e            h1hdr          1
      *
     O                                           13 'HEAD'
     O                                           21 'HEAD'
     O                                           31 'HEAD'
      *
     O                                           41 'HEAD'
     O                                           49 'HEAD'
     O                                           58 'HEAD'
      *
     O                                           69 'HEAD'
     O                                           76 'HEAD'
     O                                           86 'HEAD'
      *
     O          e            h1hdr          0
     O                       dash               117
      *
      *-------------------------------------------------------------
      * Production Phase header
      *-------------------------------------------------------------
     O          e            h2hdr       2  1
     O                                           17 'Production Phase:'
     O                       h2ppds              49
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       w1fscd        z      5
     O                       w1ddhd        k     14
     O                       w1wdhd        k     22
     O                       w1vdhd        k     31
      *
     O                       w1dshd        k     42
     O                       w1wshd        k     50
     O                       w1vshd        k     59
      *
     O                       w1dmhd        k     70
     O                       w1wmhd        k     78
     O                       w1vmhd        k     87
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
