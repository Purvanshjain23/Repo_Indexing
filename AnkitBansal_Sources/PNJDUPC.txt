/********************************************************************/
/*                                                                  */
/* PNJDUPC   - SBM PRT JCO PLT INV-LD CL                            */
/* SYNOPSIS  - PROMPT JOB CONTROL FOR PRINTER OVERRIDE INFORMATION  */
/*             TO PRINT ALL ORDERS FOR A SINGLE LOAD ON ONE         */
/*             GATE INVOICE FOR THE JAPANESE INVOICES AND THEN      */
/*             TO  EMAIL THEM IF REQUESTED                          */
/*                                                                  */
/* SYSTEM    - ORDER MANAGEMENT SYSTEM                              */
/* DATE      - 05/22/2009                                           */
/* PROGRAMMER- LARA BUSER                                           */
/*                                                                  */
/* PARAMETERS:                                                      */
/*   &RETURN     - RETURN CODE                                      */
/*   &APPLIC     - APPLICATION                                      */
/*   &COMPNYN    - COMPANY NUMBER                                   */
/*   &ORDERN     - ORDER (BLANKS=ALL FOR SHIP TO CUSTOMER)          */
/*   &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT SCREEN)    */
/*   &OUTQ       - OUTPUT QUEUE                                     */
/*   &HOLD       - HOLD PRINT                                       */
/*   &SAVE       - SAVE PRINT                                       */
/*   &NQSTAT     - NIGHT QUEUE STATUS                               */
/*   &NIGHTQ     - NIGHT OUTPUT QUEUE                               */
/*   &COPYN      - NUMBER OF COPIES                                 */
/*   &EMAILADDR  - EMAIL ADDRESS                                    */
/*   &EMAILS     - EMAIL SELECTION                                  */
/*   &CUSTPO     - CUSTOMER PO#                                     */
/*   &CUSTNAME   - CUSTOMER NAME                                    */
/********************************************************************/
/*  MODIFICATIONS:                                                  */
/* ---------------------------------------------------------------- */
/*                                                                  */
/********************************************************************/
 PNJDUPC:    PGM        PARM(&RETURN &APPLIC &DESC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &COMPNYN &COPYN &LOADID +
                          &EMAILADDR &EMAILS &CUSTNAME &CUSTPO)

/* PARMS                                                            */
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&APPLIC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DESC) TYPE(*CHAR) LEN(25)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&NQSTAT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COMPNYN) TYPE(*DEC)
             DCL        VAR(&COPYN) TYPE(*DEC)
             DCL        VAR(&LOADID) TYPE(*DEC)
             DCL        VAR(&EMAILS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&EMAILADDR) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&CUSTNAME) TYPE(*CHAR) LEN(30)
             DCL        VAR(&CUSTPO) TYPE(*CHAR) LEN(15)
             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&LOAD) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&ORDER) TYPE(*DEC) LEN(7 0)

/* ERROR MESSAGE FIELDS                                              */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&COMP) VALUE(&COMPNYN)
             CHGVAR     VAR(&LOAD) VALUE(&LOADID)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/* PROMPT FOR PRINTER OVERRIDE INFORMATION                          */
/* ONLY PROMPT THE FIRST TIME THROUGH                               */
/********************************************************************/
             IF         COND(&OUTQ *EQ '          ') THEN(DO)
 PRINTER:    CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             ENDDO

/********************************************************************/
/* PROMPT FOR EMAIL ADDRESS - ONLY PROMPT THE FIRST TIME THROUGH    */
/* ERROR IF EMAIL ADDRESS IS LEFT BLANK                             */
/********************************************************************/
             IF         COND(&EMAILS *EQ 'E') THEN(DO)
             IF         COND(&EMAILADDR *EQ '                 ') +
                          THEN(DO)
             CALL       PGM(PMYMPVR) PARM('' &COMP &ORDER &CUSTNAME +
                          &CUSTPO &EMAILADDR)
                IF         COND(&EMAILADDR *EQ '                 ') +
                             THEN(DO)
                CHGVAR     VAR(&RETURN) VALUE('USR3670')
                GOTO       CMDLBL(END)
                ENDDO
             ENDDO
             ENDDO

/*********************************************************************/
/* SUBMIT THE CL THAT CALLS THE PRINT PROGRAM                        */
/* PRINTS ALL ORDERS FOR ONE LOAD ON A SINGLE GATE PRICED INVOICE    */
/*********************************************************************/
             SBMJOB     CMD(CALL PGM(PNJCUPC) PARM(&RETURN &APPLIC +
                          &DESC &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COMPNYN &COPYN &LOADID &EMAILS +
                          &EMAILADDR)) JOB(JCOINVCLD)

/* TEST ***  CALL       PGM(PNJCUPC) PARM(&RETURN &APPLIC &DESC +
                          &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COMPNYN &COPYN &LOADID &EMAILS &EMAILADDR) +
*** TEST *****/
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
/* USER QUIT REQUESTED                                              */
/*------------------------------------------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U9999')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             CHGJOB     LOG(4 20 *SECLVL) LOGCLPGM(*YES)

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
