     A*%%TS  SD  20140306  140230  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD403
     A* DESCRIPTION:  MP2: WORK WITH UNPOSTED RECEIPTS
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         03/20/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP403S1                   SFL
     A*%%TS  SD  20140306  135633  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPOSN    R        H      REFFLD(@@POSN)
     A            D1OPT     R        B  9  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1JDVNNO  R        O  9  5REFFLD(@@JDVNNO)
     A                                      EDTCDE(Z)
     A            D1PONO    R        O  9 14REFFLD(@@PONO)
     A            D1RCNO    R        O  9 23REFFLD(@@RCNO)
     A                                      EDTCDE(Z)
     A            D1SERNO   R        O  9 33REFFLD(@@SERNO)
     A                                      EDTCDE(Z)
     A            D1TTCD    R        O  9 43REFFLD(@@TTCD)
     A            D1MCU     R        O  9 46REFFLD(@@MCU)
     A            D1OBJ     R        O  9 59REFFLD(@@OBJ)
     A            D1SUB     R        O  9 66REFFLD(MPREC/@@SUB *LIBL/MP2REF)
     A            D1ACONO   R        O  9 75REFFLD(@@ACONO)
     A                                      EDTCDE(Z)
     A            D1ERFL    R        O  9 79REFFLD(@@ERFL)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP403C1                   SFLCTL(MP403S1)
     A*%%TS  SD  20140306  140230  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 30'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 27'WORK WITH UNPOSTED RECEIPTS'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  6  8'JDE'
     A                                      DSPATR(HI)
     A                                  6 14'Purchase'
     A                                      DSPATR(HI)
     A                                  6 25'Receipt'
     A                                      DSPATR(HI)
     A                                  6 36'Serial'
     A                                      DSPATR(HI)
     A                                  6 43'Tr'
     A                                      DSPATR(HI)
     A                                  6 58'Account'
     A                                      DSPATR(HI)
     A                                  6 75'Acc'
     A                                      DSPATR(HI)
     A                                  6 79'Er'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  7'Vendor'
     A                                      DSPATR(HI)
     A                                  7 14'Order'
     A                                      DSPATR(HI)
     A                                  7 26'Number'
     A                                      DSPATR(HI)
     A                                  7 36'Number'
     A                                      DSPATR(HI)
     A                                  7 43'Cd'
     A                                      DSPATR(HI)
     A                                  7 47'Cost Center'
     A                                      DSPATR(HI)
     A                                  7 59'Object'
     A                                      DSPATR(HI)
     A                                  7 68'Subsid'
     A                                      DSPATR(HI)
     A                                  7 75'Cmp'
     A                                      DSPATR(HI)
     A                                  7 79'Fl'
     A                                      DSPATR(HI)
     A            C1JDVNNO  R        B  8  5REFFLD(PHREC/PHJDVNNO *LIBL/MPP102)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            C1PONO    R        B  8 14REFFLD(PHREC/PHPONO *LIBL/MPP102)
     A                                      DSPATR(HI)
     A            C1RCNO    R        B  8 23REFFLD(@@RCNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            C1TTCD    R        B  8 43REFFLD(@@TTCD)
     A                                      DSPATR(HI)
     A            C1ACONO   R        B  8 75REFFLD(@@ACONO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            C1ERFL    R        B  8 79REFFLD(@@ERFL)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP403K1
     A*%%TS  SD  20011024  165925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP403ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R MP403EC                   SFLCTL(MP403ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
