     A*%%TS  SD  19940408  150401  ISMPALM     REL-V2R2M0  5738-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD560 - SELECT PREMIUM/DISCOUNT FLAG
     A*  RPG PROGRAM:   HP560  - SELECT PREMIUM/DISCOUNT FLAG
     A*  CREATED BY:    ALICE BROWNFIELD
     A*  DATE:          05/31/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP560R1
     A*%%TS  SD  19940408  144919  ISMPALM     REL-V2R2M0  5738-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 8 29)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP560R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP560R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP560S1                   SFL
     A*%%TS  SD  19940408  150401  ISMPALM     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFPDST    R        O  4  1REFFLD(@@FLAG)
     A            DFPDDS    R        O  4  5REFFLD(@@DS20)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP560C1                   SFLCTL(HP560S1)
     A*%%TS  SD  19940408  144919  ISMPALM     REL-V2R2M0  5738-PW1
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0002)
     A                                      WINDOW(HP560R1)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  5'SELECT PREM/DISC FLAG'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A          R HP560K1
     A*%%TS  SD  19940408  144919  ISMPALM     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP560R1)
     A                                  7  1'F12=Previous'
