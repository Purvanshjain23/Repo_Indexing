/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM FOR SALES PRICE EDIT                   */
/*      CO#, CUST#, SALES ROUTE, SCHED SHIP DATE, ACTUAL SHIP DATE  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT SALES PRICE EDIT                              */
/*  DATE      -  9/16/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT OUTPUT QUEUE                        */
/*          &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*          &CUSTN      - CUSTOMER NUMBER (PASSED)                  */
/*          &SCHDATN    - SCHED SHIP DT  (PASSED)                   */
/*          &ACTDATN    - ACTUAL SHIP DT (PASSED)                   */
/*          &SLSRTE     - SALES ROUTE     (PASSED)                  */
/********************************************************************/

 PDPDCLP:    PGM        PARM(&COMPNYN &CUSTN &SCHDATN &ACTDATN +
                          &SLSRTE &OUTQ &HOLD &SAVE &COPYN &NIGHTQ)

/* PARMS                                                                      */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &CUSTN      *DEC                  /* CUSTOMER NO.   (Passed)   */
        DCL    &SCHDATN    *DEC                  /* SCHEDULED DATE (Passed)   */
        DCL    &ACTDATN    *DEC                  /* ACTUAL SHIP DT (Passed)   */
        DCL    &SLSRTE     *CHAR   (3)           /* SALES ROUTE               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &PGMO       *CHAR    10           /*  PRINTER FILE  */
        DCL    &B          *CHAR    30           /*  QRY-SEL CO.#  */
        DCL    &D          *CHAR    30           /*  QRY-CUSTOMER NO.   */
        DCL    &CCC        *CHAR    30           /*  QRY-SEL SALES ROUTE*/
        DCL    &DDD        *CHAR    30           /*  QRY-SEL SCHD SHIP DATE  */
        DCL    &EEE        *CHAR    30           /*  QRY-SEL ACT SHP DTDATE  */
        DCL    &E          *CHAR   150           /*  QRY COMPLETE SEL. */
        DCL    &COMMAND    *CHAR   (512)

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &CUSTOMER   *DEC    (7 0)         /* CUSTOMER Number           */
        DCL    &SCHDAT     *DEC    (7 0)         /* SCHEDULED DATE            */
        DCL    &ACTDAT     *DEC    (7 0)         /* ACTUAL SHIP DATE          */

/* OPENQRYF FIELDS                                                            */
        DCL    &COMPA      *CHAR    3            /* COMPANY                   */
        DCL    &CUSTA      *CHAR    7            /* CUSTOMER                  */
        DCL    &SCHDATA    *CHAR    7            /* SCHED SHIP DT  (Passed)   */
        DCL    &ACTDATA    *CHAR    7            /* ACTUAL SHIP DT (Passed)   */

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/

             CHGVAR     VAR(&PGMO) VALUE(PDPDPFR$)
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&CUSTOMER) VALUE(&CUSTN)
             CHGVAR     VAR(&CUSTA) VALUE(&CUSTOMER)
             CHGVAR     VAR(&SCHDAT) VALUE(&SCHDATN)
             CHGVAR     VAR(&SCHDATA) VALUE(&SCHDAT)
             CHGVAR     VAR(&ACTDAT) VALUE(&ACTDATN)
             CHGVAR     VAR(&ACTDATA) VALUE(&ACTDAT)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/* ELIMINATE DECIMAL DATA ERROR                    */
             IF         COND(&COMPA *EQ '   ') THEN(DO)
             CHGVAR     VAR(&COMPANY) VALUE(0)
             ENDDO
             IF         COND(&CUSTA *EQ '       ') THEN(DO)
             CHGVAR     VAR(&CUSTOMER) VALUE(0)
             ENDDO
             IF         COND(&SCHDATA *EQ '   ') THEN(DO)
             CHGVAR     VAR(&SCHDAT) VALUE(0)
             ENDDO
             IF         COND(&ACTDATA *EQ '   ') THEN(DO)
             CHGVAR     VAR(&ACTDAT) VALUE(0)
             ENDDO
/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED                               */
/*********************************************************************/

/*  IF CO. NUMBER SELECTED  */
             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&B) VALUE('BEAIC3 *EQ ' *CAT &COMPA)
             ENDDO

/*  IF CUSTOMER NUMBER SELECTED  */
             IF         COND(&CUSTA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND BEBKC7 *EQ ' *CAT &CUSTA)
             ENDDO

/*  IF SCHEDULED SHIP DATE IS SELECTED */

             IF         COND(&SCHDATA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&DDD) VALUE('*AND BEAODT *EQ ' *CAT &SCHDATA)
             ENDDO

/*  IF ACTUAL SHIPPED DT SELECTED*/

             IF         COND(&ACTDATA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&EEE) VALUE('*AND BEGNDT *EQ ' *CAT &ACTDATA)
             ENDDO

/*  IF SALES ROUTE ID ENTERED */

             IF         COND(&SLSRTE *NE '   ') THEN(DO)
             CHGVAR     VAR(&CCC) VALUE('*AND BER4CD *EQ "' *CAT &SLSRTE +
                        *CAT '"')
             ENDDO


/* BUILD QUERY STRING                                        */

             CHGVAR     VAR(&E) VALUE(&B *BCAT &D *BCAT &DDD *BCAT +
                          &EEE *BCAT &CCC)


             OVRDBF     FILE(OPBFCPL1) SHARE(*YES)
             OPNQRYF    FILE((OPBFCPL1)) QRYSLT(&E) KEYFLD(*FILE) +
                          SEQONLY(*YES 1)

/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/*********************************************************************/
/*   CALL PRINT PROGRAM -
/*********************************************************************/

             CALL       PGM(POBDPFR) PARM(' ' &COMPANY +
                          &CUSTOMER &ACTDAT &SCHDAT &SLSRTE)

 END:        ENDPGM
