      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP4771-Update Company Control Record for cost cube update
      * PROGRAMMER:  Barb Gutierrez
      * CREATED:     05/10/16
      *
      * Function:    For the company parm passed in, retrieve the last HPS
      *              closed beg/end dates, last closed year and period, and
      *              the cost cube build flag to be used in HPBLDCD$.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp0071   uf   e           k disk
      *    Company control
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
     D                 DS
     D WKYEAR                         4  0
     D  WKCC                          2  0 overlay(WKYEAR)
     D  WKYY                          2  0 overlay(WKYEAR:3)
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJeCT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read the company control file for the company passed in.
      *
     c     ppcocd        chain     ccrec                              50
     c     *in50         ifeq      *off
      *
     c                   select
      *
      * If the flag being passed in is blank, pass back out the
      * last hps closed begin and end dates along with the cost cube
      * update flag.  If the cost cube update flag is an 'F', the cost cube
      * will be rebuilt.
      *
     c                   when      ppc$fl = ' '
     C                   move      cclbpdt       pplbpdt
     C                   move      cclepdt       pplepdt
     C                   move      ccccly        wkyear
     C                   move      wkcc          ppcclc
     C                   move      wkyy          ppccly
     C                   move      cccclp        ppcclp
     C                   move      ccc$fl        ppc$fl
      *
      * If the cost cube flag being passed in is an 'R', the cubes were just
      * rebuilt and we need to blank out the flag in the control file for this
      * company so we don't build the cost cubes again until the next period close.
      *
     c                   when      ppc$fl = 'R'
     C                   move      ' '           ccc$fl
     C                   update    ccrec
     c                   endsl
     c                   endif
      *
     c                   seton                                        LR
      /EJeCT
      ****************************************************************
      * Initialization subroutine
      ****************************************************************
      *
     C     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    ppcocd            3            company
     c                   parm                    ppc$fl            1            cost cube bld flag
     c                   parm                    pplbpdt           8            hps lst clsed beg dt
     c                   parm                    pplepdt           8            hps lst clsed end dt
     c                   parm                    ppcclc            2            hps lst clsed cntury
     c                   parm                    ppccly            2            hps lst clsed year
     c                   parm                    ppcclp            2            hps lst clsed period
      *
     C                   endsr
      /eject
