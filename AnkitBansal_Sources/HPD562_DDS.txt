     A*%%TS  SD  19950816  091122  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD562 - SELECT MOVEMENT REPORT
     A*  CREATED BY:    SHARON ZURN
     A*  DATE:          06/09/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP562R1
     A*%%TS  SD  19950815  080132  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 37)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP562R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP562R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP562S1                   SFL
     A*%%TS  SD  19950815  080132  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFMRCD    R        O  5  1REFFLD(HSRREF/MRMRCD *LIBL/HSPREF)
     A            DFMRDS    R        O  5  7REFFLD(MRMRDS)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP562C1                   SFLCTL(HP562S1)
     A*%%TS  SD  19950816  091122  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP562R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  7'SELECT MOVEMENT REPORT'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter.'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  7'Description'
     A                                      DSPATR(HI)
     A          R HP562K1
     A                                      TEXT('command line 1')
     A                                      OVERLAY
     A                                      WINDOW(HP562R1)
     A                                 15  1'F12=Previous'
