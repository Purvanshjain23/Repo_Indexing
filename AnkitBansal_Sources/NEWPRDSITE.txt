      *****************  RPG PROGRAM HEADING  ************************
      *
      * PROGRAMMER:  Brad Baden
      * CREATED:      8/23/2019
      *
      * FUNCTION: This program reads a copy of the Producer Site file
      *           that was created from a spreadsheet.  The file is a
      *           copy of the Producer Site file for Triumpg Foods.
      *           The first column of the file has a new producer code
      *           in the first column.  Create new Producer Site code
      *           records with the new Producer Code and keep the existing
      *           values in the rest of the fields.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * xx/xx/xxxx xxxxxxxxxxxxxxxxxxx Exxxx
      *            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      *            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
     fplasrepn  if   e             disk    prefix(n_)
      *  Spreadsheet of Producer Sites
      *
      *
     fplasrel0  uf a e           k disk
      *   Producer Site Mileage - Update index
      *
      *
     fplasrel1  if   e           k disk
      *   Producer Site Mileage - Retrieval index
      *
      *
     fplatrel0  uf a e           k disk
      *   Producer Site - Update index
      *
      *
     fplatrel1  if   e           k disk
      *   Producer Site - Retrieval index
      *
      *
      /eject
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     d active          c                   'A'
      *
      *---------------------------------------------------------------
      *  Stand alone fields
      *---------------------------------------------------------------
      *
     d jobdate         s                   like(asaadt)
     d wkasabcd        s                   like(asabcd)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     d pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     c                   if        sdpgm = *blank
     c                   endif
      /eject
      **********************************************************************************************
      * Mainline
      **********************************************************************************************
      *
     c*    *start        setll     plasrepn
     c*                  if        %found(plasrepn)                             if %found(plasrepn)
     c                   dou       %eof(plasrepn)                               dou %eof(plasrepn)
     c                   read      plasrepn
     c                   if        not %eof(plasrepn)                           if not %eof(plasrepn
      *
      ** If new producer field is not blank, continue processing
     c                   if        n_asabcdn <> *blanks                         if n_asabcdn<>asabcd
      *
      ** Write a Producer Site record
     c                   exsr      $wrtsite
      *
      ** Write Producer Site Mileage records
     c                   exsr      $wrtmiles
      *
     c                   endif                                                  fi n_asabcdn<>asabcd
     c                   endif                                                  fi not %eof(plasrepn
     c                   enddo                                                  uod %eof(plasrepn)
     c*                  endif                                                  fi %found(plasrepn)
      *
     c                   eval      *inlr = *on
      /eject
      ****************************************************************
      * $wrtsite - Load and write new producer site record           *
      ****************************************************************
      *
     c     $wrtsite      begsr
      *
      * Check if a record with the new Producer Code exists
     c                   eval      wkasabcd = n_asabcd
     c     key01         chain     platrel1
     c                   if        not %found(platrel1)                         if not% found(platre
      *
      * Retrieve values from current record
     c                   eval      wkasabcd = asabcd
     c     key01         chain     platrel1
     c                   if        %found(platrel1)                             if %found(platrel1)
      *
      ** Load new producer code and audit stamp values
     c                   eval      atabcd = n_asabcdn                           new producer
      *
     c                   eval      ataavn = *blanks                             User changed
     c                   eval      ataadt = *zero                               Date changed
     c                   eval      atamtm = *zero                               Time changed
     c                   eval      atajst = active                              Record status
     c                   eval      atahvn = sdjob                               Job name
     c                   eval      atagvn = sdpgm                               Program name
     c                   eval      atabvn = sdusr                               User added
     c                   eval      atabdt = jobdate                             Date added
     c                   eval      atabtm = sdstm                               Time added
      *
     c                   write     @atrell
      *
     c                   endif                                                  fi %found(platrel1)
     c                   endif                                                  fi not% found(platre
      *
     c                   endsr
      /eject
      ****************************************************************
      * $wrtmiles - Load and write new producer mileage record       *
      ****************************************************************
      *
     c     $wrtmiles     begsr
      *
      * Check if a record with the new Producer Code exists
     c                   eval      wkasabcd = n_asabcd
     c     key02         chain     plasrel1
     c                   if        not %found(plasrel1)                         if not% found(plasre
      *
      ** load new producer code and audit stamp values
     c                   eval      wkasabcd = asabcd
     c     key02         chain     plasrel1
     c                   if        %found(plasrel1)                             if %found(plasrel1)
      *
     c                   eval      asabcd = n_asabcdn                           new producer
      *
     c                   eval      asaavn = *blanks                             User changed
     c                   eval      asaadt = *zero                               Date changed
     c                   eval      asamtm = *zero                               Time changed
     c                   eval      asajst = active                              Record status
     c                   eval      asahvn = sdjob                               Job name
     c                   eval      asagvn = sdpgm                               Program name
     c                   eval      asabvn = sdusr                               User added
     c                   eval      asabdt = jobdate                             Date added
     c                   eval      asabtm = sdstm                               Time added
      *
     c                   write     @asrek8
      *
     c                   endif                                                  fi %found(plasrel1)
     c                   endif                                                  fi not% found(plasre
      *
     c                   endsr
      /eject
      ****************************************************************
      * *INZSR - Initialization                                      *
      ****************************************************************
      *
     c     *inzsr        begsr
      *
      ** Key list for Producer Site
     c     key01         klist
     c                   kfld                    wkasabcd                       Producer code
     c                   kfld                    asnycd                         Site ID
      *
      ** Key list for Producer Site Mileage
     c     key02         klist
     c                   kfld                    wkasabcd                       Producer code
     c                   kfld                    asnycd                         Site ID
     c                   kfld                    aslka1                         Comapny number
      *
      ** Convert job date to cyymmdd (Synon date)
     C                   eval      jobdate = %dec(%char(%date( +                Job date
     C                                sdsdt:*mdy):*cymd0):7:0)
      *
     c                   endsr
