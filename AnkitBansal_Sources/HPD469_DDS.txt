     A*%%TS  SD  20010508  143351  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD469
     A*  TITLE:         SPECIFY INVENTORY REPORT OPTIONS
     A*  PROGRAMMER:    MARY PALMER
     A*  DATE:          06/15/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP469R1
     A*%%TS  SD  20010508  143351  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(38)
     A                                      SETOF(42)
     A                                      SETOF(47)
     A                                      SETOF(49)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'SPECIFY INVENTORY REPORT OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .....'
     A                                  4 24'?'
     A            DFFSBO    R        B  4 26REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 34REFFLD(BOBODS)
     A                                  4 65'(Blank for all)'
     A                                  5  2'Cut-off date ........'
     A            DFMDY     R        B  5 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'Production type .....'
     A                                  6 24'?'
     A            DFPTCD    R        B  6 26REFFLD(PTPTCD)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  6 34REFFLD(@@DS10)
     A                                  6 65'(Blank for all)'
     A                                  7  2'Production phase ....'
     A                                  7 24'?'
     A            DFPPCD    R        B  7 26REFFLD(PPPPCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  7 34REFFLD(PPPPDS)
     A                                  7 65'(Blank for all)'
     A                                  8  2'Group type ..........'
     A                                  8 24'?'
     A            DFGTCD    R        B  8 26REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFGTDS    R        O  8 34REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                  8 65'(Blank for all)'
     A                                  9  2'Cell ................'
     A            DFCELL    R        B  9 26REFFLD(FSCELL)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  9 65'(Blank for all)'
     A                                 10  2'Farm site ...........'
     A                                 10 24'?'
     A            DFFSCD    R        B 10 26REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 10 34REFFLD(FSFSNM)
     A                                 10 65'(Blank for all)'
     A                                 11  2'Hog group ...........'
     A                                 11 24'?'
     A            DFHGCD    R        B 11 26REFFLD(HGHGCD)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 11 65'(Blank for all)'
     A                                 13  2'Report ..............'
     A                                 13 24'?'
     A            DFRPFL    R        B 13 26REFFLD(@@FLAG)
     A N49                                  DSPATR(HI)
     A  49
     AO 59                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFRPDS        40A  O 13 34
     A                                 14  2'Page break on farm ..'
     A                                 14 24'?'
     A            DFPBFL    R        B 14 26REFFLD(@@FLAG)
     A N47                                  DSPATR(HI)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 15  2'Print entry line ....'
     A                                 15 24'?'
     A            DFENFL    R        B 15 26REFFLD(@@FLAG)
     A N42                                  DSPATR(HI)
     A  42
     AO 52                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP469K1
     A*%%TS  SD  20000207  162634  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44                             23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP469ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP469EC                   SFLCTL(HP469ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
