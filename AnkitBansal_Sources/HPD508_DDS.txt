     A*%%TS  SD  20010705  081058  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD508 - SELECT MANAGER
     A*  RPG PROGRAM:   HP508  - SELECT MANATER
     A*  CREATED BY:    ROSE CENTONZE
     A*  DATE:          03/17/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP508R1
     A*%%TS  SD  20010705  081058  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 18 35)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP508R2
     A*%%TS  SD  19940316  134645  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP508R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP508S1                   SFL
     A*%%TS  SD  19940317  141653  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFMGCD    R        O  6  1REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A            DFMGNM    R        O  6  8REFFLD(HSRREF/@@MGNM *LIBL/HSPREF)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP508C1                   SFLCTL(HP508S1)
     A*%%TS  SD  20010705  081058  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A                                      WINDOW(HP508R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  9'SELECT MANAGER'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter.'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  8'Name'
     A                                      DSPATR(HI)
     A            C1MGCD    R        B  5  1REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1MGNM    R        B  5  8REFFLD(HSRREF/@@MGNM *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      CHGINPDFT(LC UL FE)
     A          R HP508K1
     A*%%TS  SD  19940317  141653  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('command line 1')
     A                                      OVERLAY
     A                                      WINDOW(HP508R1)
     A                                 17  1'F12=Previous'
