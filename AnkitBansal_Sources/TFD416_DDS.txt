     A*%%TS  SD  20080902  082652  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD416
     A*  TITLE:         SPECIFY MARGIN ADJUSTMENT REPORTS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       09/26/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF416R1
     A*%%TS  SD  20080902  082652  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SPECIFY OPTIONS FOR MARGIN ADJUSTM-
     A                                      ENT REPORTS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 27'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 48'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 54'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 60'Period'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Date .....'
     A            D1MDY     R        B  5 15REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O  5 31REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  5 48REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  5 55REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A  36                                  DSPATR(RI)
     A            D1PE      R        O  5 62REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A  37                                  DSPATR(RI)
     A                                  7  2'Reports .........'
     A                                  7 20'?'
     A            D1RPT     R        B  7 22REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1RPTDS       20A  O  7 27
     A                                  8  2'Week or Period ..'
     A                                  8 20'?'
     A            D1WPFL    R        B  8 22REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A            D1WPDS        15A  O  8 27
     A                                 11  2'TF class group code .......'
     A                                 11 30'?'
     A            D1TFCGCD  R        B 11 32REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A N32                                  DSPATR(HI)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1TFCGDS  R        O 11 38REFFLD(TFREF/@@TFCGDS *LIBL/TFPREF)
     A                                 11 70'(Blank=All)'
     A                                 12  2'TF classification code ....'
     A                                 12 30'?'
     A            D1TFCLCD  R        B 12 32REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1TFCLDS  R        O 12 38REFFLD(TFREF/@@TFCLDS *LIBL/TFPREF)
     A                                 12 70'(Blank=All)'
     A                                 13  2'Item structure type code ..'
     A                                 13 30'?'
     A            D1ISTYCD  R        B 13 32REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1ISTYDS  R        O 13 38REFFLD(TFREF/@@ISTYDS *LIBL/TFPREF)
     A                                 13 70'(Blank=All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF416K1
     A*%%TS  SD  20040928  155440  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF416ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF416EC                   SFLCTL(TF416ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
