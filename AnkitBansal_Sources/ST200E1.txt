     H DEBUG(*YES)
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
      *
      * System:      Transportation - Copy frm ST200
      * Program:     ST200 - Process Flat file of PowerPro data into a fixed format file
      * Programmer:  Barb Gutierrez    / Rose Centonze
      * Created:     01/08/2010        /  03/27/2020
      *
      * Purpose:     This program will read the flat file that was uploaded from power pro and write
      *              write the records into multiple fields in stp201.  STP201 will be used to
      *              write batches of journal entries into JDE.
      *
      *****************
      * Modifications:
      *  3/27/2020 p16169 Rose Centonze
      *                   Modified to use E1 programs & files:
      *                   x0901  --> e1I0901
      *                   F0901  --> e10901
      ***********************************************************************************
      *
      *
     Fstp200    if   e             disk
      * Workfile: Power Pro uploaded flat file
     F*F0901LB   IF   E           K DISK                                  p16169
     Fe10901LB  IF   E           K DISK
     F*                                    RENAME(I0901:I0901LB)          p16169
     F* Jde account master
      *
     Fstp201    o    e             disk
      * Workfile: Power Pro data formated from uploaded flat file
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * For call to JDE account routine
      *
     d xxsym           s              1
     d xxomod          s              1
     d xximod          s              1
     d xxerrm          s              4
      *
     d wkacct          s             29
     d wkaid           s              8
     d xxani           s             29
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *--- File Data Structures ---
      *
     Dstrecd           ds
     D  x1acct                 1     28                                         Account number
     D  x1date                29     36                                         posting date
     D  x1srce                37     38                                         source type
     D  x1damt                39     50                                         debit amt
     D  x1camt                51     62                                         credit amount
     D  x1idno                63     70                                         ID number
     D  x1desc                71    100                                         description
      *
     d i0901ds         ds           500
     d xxco                                like(gmco)
     d xxaid                               like(gmaid)
     d xxmcu                               like(gmmcu)
     d xxobj                               like(gmobj)
     d xxsub                               like(gmsub)
     d xxans                               like(gmans)
     d xxdl01                              like(gmdl01)
     d xxlda                               like(gmlda)
     d xxbpc                               like(gmbpc)
     d xxpec                               like(gmpec)
     d xxbill                              like(gmbill)
     d xxcrcd                              like(gmcrcd)
     d xxum                                like(gmum)
     d xxr001                              like(gmr001)
     d xxr002                              like(gmr002)
     d xxr003                              like(gmr003)
     d xxr004                              like(gmr004)
     d xxr005                              like(gmr005)
     d xxr006                              like(gmr006)
     d xxr007                              like(gmr007)
     d xxr008                              like(gmr008)
     d xxr009                              like(gmr009)
     d xxr010                              like(gmr010)
     d xxr011                              like(gmr011)
     d xxr012                              like(gmr012)
     d xxr013                              like(gmr013)
     d xxr014                              like(gmr014)
     d xxr015                              like(gmr015)
     d xxr016                              like(gmr016)
     d xxr017                              like(gmr017)
     d xxr018                              like(gmr018)
     d xxr019                              like(gmr019)
     d xxr020                              like(gmr020)
     d xxr021                              like(gmr021)
     d xxr022                              like(gmr022)
     d xxr023                              like(gmr023)
     d xxobja                              like(gmobja)
     d xxsuba                              like(gmsuba)
     d xxwcmp                              like(gmwcmp)
     d xxcct                               like(gmcct)
     d xxerc                               like(gmerc)
     d xxhtc                               like(gmhtc)
     d xxqlda                              like(gmqlda)
     d xxccc                               like(gmccc)
     d xxfmod                              like(gmfmod)
     d xxuser                              like(gmuser)
     d xxpid                               like(gmpid)
     d xxjobn                              like(gmjobn)
     d xxupmj                              like(gmupmj)
     d xxupmt                              like(gmupmt)
      *
      /eject
      ****************************************************************************
      * Mainline
      ****************************************************************************
      *
      * Read all workfile records in flat file and write to fixed format file.
      *
b1   C                   dou       *in90 = *on                                  Main do
     C                   read      strec                                  90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   movel     stfield       strecd
     C                   exsr      $move
     C                   write     strp201
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *------
      * EOF
      *------
     C                   seton                                        lr
      *
      *-----------------------------------------------------------------------------------
      * Move fields
      *-----------------------------------------------------------------------------------
      *
     C     $move         begsr
      *
      *  Parse out the account number into separate fields.  Account number in stp200 is
      *  28 alpha long.
      *
     C*
     C                   move      *blanks       wkacct
     C                   movel     x1acct        wkacct
     C*
      * Call pgm to parse out account number that was passed in.
     C*
     C*                  CALL      'X0901'
     C                   CALL      'E1I0901'
     C                   PARM                    xxsym
     C                   PARM      '9'           xxomod
     C                   PARM      '2'           xximod
     C                   PARM      wkacct        xxani
     C     stmcu         PARM                    xxmcu
     C     stobj         PARM                    xxobj
     C     stsub         PARM                    xxsub
     C                   PARM                    xxerrm
     C                   PARM                    i0901ds
      *
     C                   movel     x1date        stdate                         posting date
     C                   movel     x1srce        stsrcty                        source type
     C                   move      x1damt        stdamt                         debit amount
     C                   move      x1camt        stcamt                         credit amount
     C                   move      x1idno        stidno                         id number
     C                   movel     x1desc        stdesc                         description
     C*
      * Call pgm to retrieve the account id associated with the cost center/object/sub
     C*
     C*                  CALL      'X0901'
     C                   CALL      'E1I0901'
     C                   PARM                    xxsym
     C                   PARM      '1'           xxomod
     C                   PARM      '9'           xximod
     C     wkaid         PARM      *blank        xxani
     C                   PARM      stmcu         xxmcu
     C                   PARM      stobj         xxobj
     C                   PARM      stsub         xxsub
     C                   PARM                    xxerrm
     C                   PARM                    i0901ds
      *
     C                   if        xxerrm = *blank
     C                   move      wkaid         staid                          Account ID
     C                   else
     C                   move      *blank        staid                          Account ID
     C                   endif
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Initialization subroutine
      *------------------------------------------------------------------------------------
     C     *inzsr        begsr
      *
     C                   endsr
      /eject
