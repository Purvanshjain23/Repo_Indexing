      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6022
      * TITLE:       Listing--BGF Weekly Wean Projections (Multiple)
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/08/04
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4021-Work With BGF Weekly Wean
      *            Projections.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/19/12  LeAnne Ramsey (E1909)
      *           When Sami W. requested DWL/EMail capability for this report,
      *           I had to go to a Workfile. So, I changed this print program
      *           to read the workfile instead of HSL086a.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp3022   if   e           k disk
      *    Workfile: BGF Weekly Wean Projections
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            198
     d rtime           s              6  0
      *
      * Save fields
      *
     D svfscd          s                   like(fsfscd)
      *
      *
      * Print fields
      *
     D h1fsnm          s                   like(fsfsnm)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldfscd                 2      6  0
     D  ldfsnm                 7     31
      *
     D  ldcell                40     41  0
      *
     D  ldfrpcdyr             42     45  0
     D  ldfrpcdwk             46     47  0
     D  ldfrpwedt             48     55  0
      *
     D  ldtrpcdyr             56     59  0
     D  ldtrpcdwk             60     61  0
     D  ldtrpwedt             62     69  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time.
      *
     C     *loval        setll     hsp3022
     C                   except    h1hdr
      *
      * Read/print all records from the workfile.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3022                                90
     C                   if        *in90 = *off                                 If not EOF
     C                   exsr      $print
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------
      * EOF processing
      *----------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print a detail line
      *---------------------------------------------------------------
      *
     C     $print        begsr
      *
     C                   if        w1fscd <> svfscd
     C                   z-add     w1fscd        svfscd
     C                   except    blank
     C                   seton                                        88
     C                   endif
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   seton                                        88
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
     C                   setoff                                       88
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      *  Setup the user selections in the headings.
      *
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        86
     C                   endif
      *
      * Farm site
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   else
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel(p)  fsfsnm        h1fsnm
     C                   else
     C                   movel(p)  'UNKNOWN'     h1fsnm
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                          107 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          102 'LISTING OF BGF WEEKLY '
     O                                          118 'WEAN PROJECTIONS'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           25 'From report year/week .:'
     O                       ldfrpcdyr     z     31
     O                       ldfrpcdwk     z     34
      *
      *
     O          e            h1hdr          1
     O                                           25 'To report year/week ...:'
     O                       ldtrpcdyr     z     31
     O                       ldtrpcdwk     z     34
      *
      *
     O          e            h1hdr          1
     O                                           25 'Cell ..................:'
     O              N86      ldcell        z     29
     O               86                          30 'All'
      *
     O          e            h1hdr          2
     O                                           25 'Farm site .............:'
     O              N89      ldfscd        z     31
     O              N89      fsfsnm              57
     O               89                          30 'All'
      *
     O          e            h1hdr          1
     O                                           35 '------ Report -------'
     O                                           59 '---- 1 Week Out -----'
     O                                          100 '- 1 Week Out -'
     O                                          132 'Total'
      *
     O                                          179 '---- 2 Weeks Out ----'
     O                                          189 'Total'
      *
      *
     O          e            h1hdr          1
     O                                            5 'Farm'
     O                                           22 'Calendar'
     O                                           35 'Week-Ending'
      *
     O                                           46 'Calendar'
     O                                           59 'Week-Ending'
      *
     O                                           85 '------------------------'
     O                                          100 'Piglets Weaned'
     O                                          123 '----------------------'
      *
     O                                          132 'Piglets'
     O                                          140 'Weaned'
      *
     O                                          166 'Calendar'
     O                                          179 'Week-Ending'
      *
     O                                          189 'Piglets'
     O                                          197 'Weaned'
      *
     O                                          148 'Sow'
     O                                          156 'Boar'
      *
     O          e            h1hdr          1
     O                                            5 'Site'
     O                                           12 'Cell'
      *
     O                                           18 'Year'
     O                                           22 'Wk'
     O                                           32 'Date'
      *
     O                                           42 'Year'
     O                                           46 'Wk'
     O                                           56 'Date'
      *
     O                                           67 'Sunday'
     O                                           75 'Monday'
     O                                           84 'Tuesday'
     O                                           95 'Wednesday'
     O                                          105 'Thursday'
     O                                          113 'Friday'
     O                                          123 'Saturday'
      *
     O                                          132 'Weaned'
     O                                          140 'Sows'
      *
     O                                          162 'Year'
     O                                          166 'Wk'
     O                                          176 'Date'
      *
     O                                          189 'Weaned'
     O                                          197 'Sows'
      *
     O                                          148 'Culls'
     O                                          156 'Culls'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O               88      w1fscd        z      5
     O               88      w1cell        z     11
      *
     O                       w1rpcdyr      z     18
     O                       w1rpcdwk      z     22
     O                       w1rpwemdy           33 '  /  /  '
      *
     O                       w1pwcdyr1     z     42
     O                       w1pwcdwk1     z     46
     O                       w1pwwemdy1          57 '  /  /  '
      * Week 1
     O                       w1supwhd      k     67
     O                       w1mopwhd      k     75
     O                       w1tupwhd      k     84
     O                       w1wepwhd      k     95
     O                       w1thpwhd      k    105
     O                       w1frpwhd      k    111
     O                       w1sapwhd      k    122
     O                       w1topwhd1     k    133
     O                       w1pwsohd1     k    141
      *
     O                       w1socuhd1     k    148
     O                       w1bocuhd1     k    157
      * Week 2
     O                       w1pwcdyr2     z    162
     O                       w1pwcdwk2     z    166
     O                       w1pwwemdy2         177 '  /  /  '
      *
     O                       w1topwhd2     k    190
     O                       w1pwsohd2     k    198
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
