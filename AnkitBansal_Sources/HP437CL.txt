/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP437CL                                       */
/*  PROGRAM NAME:    SPECIFY FEED RECEIVING DATA TO MOVE TO        */
/*                   EDITING FILES                                 */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/28/97                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP437.                                        */
/*                   USER OPTIONS ARE PASSED THROUGH THE LDA.      */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*  06/19/97  LEANNE FEDOR                                         */
/*            CHANGED OUT QUEUE LOGIC.                             */
/*                                                                 */
/*  10/28/09  LEANNE RAMSEY                                        */
/*            IN CONJUNCTION WITH DAVID WEAVER'S REWRITE OF THE PC */
/*            FEED SYSTEMS, WE NO LONGER NEED/HAVE THE STAGING     */
/*            FILE.                                                */
/*                                                                 */
/*  05/20/10  LEANNE RAMSEY                                        */
/*            REINSTATED THE TEMP REMOVAL OF THE ALCOBJ CODE.      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(50)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF COND(&OUTQ = '          ') THEN(CHGVAR VAR(&OUTQ) +
                          VALUE(*JOB))

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/* THE USERS MUST NOT UPLOAD MORE DATA FROM THE P.C. WHILE THIS     */
/* FUNCTION IS RUNNING. SO, ALLOCATE THE UPLOAD RECEIVING FILE      */
/*------------------------------------------------------------------*/

             ALCOBJ     OBJ((*LIBL/HSP235 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** Your move of feed receiving data to +
                        the editing files could not run because another +
                        upload of feed data from the P.C. to the AS/400 +
                        was in progress. Wait until that upload has completed; +
                        then resubmit your move. *****') TOUSR(&USER)
             GOTO       EXIT
                        ENDDO

/*------------------------------------------------------------------*/
/* CALL PROGRAMS TO PRINT/MOVE/WRITE/DELETE UPLOAD DATA             */
/*------------------------------------------------------------------*/

             CALL       PGM(HP636) /* Listing of Receiving */

             CALL       PGM(HP237) /* Move Receiving to Editing */

             CALL       PGM(HP235) /* Write History/Delete Receiving */

/*------------------------------------------------------------------*/
/*  CALL BATCH EDIT/LISTING OF DATA IN EDIT FILES                   */
/*------------------------------------------------------------------*/

/* OVERRIDE THE PRINT FILE AGAIN TO SHARE THE DATA PATH SO THAT     */
/* ERROR MESSAGES WILL PRINT CORRECTLY.                             */
/* THE PAGE BREAKS MAY BE WEIRD SINCE WE HAVE TO 'COUNT' LINES      */
/* IN THE BATCH EDIT PROGRAM TO PRINT ERROR MESSAGES.               */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD) SHARE(*YES)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP238)

/*------------------------------------------------------------------*/
/* DELETE PRINTER FILE OVERRIDES AND DEALLOCATE THE RECEIVING FILE  */
/*------------------------------------------------------------------*/

             DLTOVR QPRINT

             DLCOBJ     OBJ((*LIBL/HSP235 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
EXIT:
/*------------------------------------------------------------------*/
/*  RESET THE 'MOVEMENT OF RECEIVING DATA IN PROGRESS' FLAG IN DATA */
/*  AREA DAGROS BACK TO BLANK.                                      */
/*------------------------------------------------------------------*/
             CHGDTAARA  DTAARA(DAGROS (1 1)) VALUE(' ')

             ENDPGM

