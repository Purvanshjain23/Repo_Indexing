/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES COMPANY, LOAD, DATE, AND       */
/*              WAREHOUSES.                                         */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT PRELIMINARY PICK                              */
/*  DATE      - 02/28/96                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &CLP        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &LOADN      - LOAD ID        (PASSED)                   */
/*     B    &DATEN      - SCHED DATE     (PASSED)                   */
/*     B    &FLAG       - SUMMARY OR DETAIL FLAG (PASSED)           */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &SOFLAG     - SELECT/OMIT    (PASSED)                   */
/*     B    &AUTOP      - AUTO PRINT     (PASSED)                   */
/*     B    &WHSE1      - WAREHOUSE 1    (PASSED)                   */
/*     B    &WHSE2      - WAREHOUSE 2    (PASSED)                   */
/*     B    &WHSE3      - WAREHOUSE 3    (PASSED)                   */
/*     B    &WHSE4      - WAREHOUSE 4    (PASSED)                   */
/*     B    &WHSE5      - WAREHOUSE 5    (PASSED)                   */
/*     B    &WHSE6      - WAREHOUSE 6    (PASSED)                   */
/*     B    &WHSE7      - WAREHOUSE 7    (PASSED)                   */
/*     B    &WHSE8      - WAREHOUSE 8    (PASSED)                   */
/*     B    &WHSE9      - WAREHOUSE 9    (PASSED)                   */
/*     B    &WHSE10     - WAREHOUSE 10   (PASSED)                   */
/*     B    &R1LOAD     - LOAD ID    1   (PASSED)                   */
/*     B    &R2SEQ      - SCHED SEQ  1   (PASSED)                   */
/*     B    &R3DEP      - DEADLINE D 1   (PASSED)                   */
/*     B    &LDPLTP     - LD PLN TYP 2   (PASSED)                   */
/*                                                                  */
/********************************************************************/
/*** CHANGES                                                      ***/
/*                                                                  */
/*   HARD CODE THE JOBQ TO THE PRIORITY JOBQ. THE JOBQ IS CALLED    */
/*   PRIORITY.  PER JUDY SCHAUB     MEA 09/05/96                    */
/*                                                                  */
/*   CHANGE SO THIS PRINTS INTERACTIVELY PER CALL C2591.            */
/*                                  PLL 04/16/97                    */
/*                                                                  */
/* 108074 ERIC L 4 PARMS ADDED                                      */
/********************************************************************/


 PDI7UPR:    PGM        PARM(&RETURN &APPLIC &CLP &FLAG &DESC &OUTQ +
                          &HOLD &SAVE &NQSTAT &NIGHTQ &COMPNYN +
                          &DATEN &LOADN &COPYN &WHSE1 &WHSE2 &WHSE3 +
                          &WHSE4 &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 +
                          &WHSE10 &SOFLAG &AUTOP &R1LOAD &R2SEQ +
                          &R3DEP &LDPLTP)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &CLP        *CHAR   (10)          /* CL to Submit              */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &FLAG       *CHAR   (1)           /* SUMMARY/DETAIL RPT t Q    */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &LOADN      *DEC                  /* LOAD ID        (Passed)   */
        DCL    &DATEN      *DEC                  /* Thru Date      (Passed)   */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &SOFLAG     *CHAR   (1)           /* SOFLAG                    */
        DCL    &AUTOP      *CHAR   (1)           /* AUTO PRINT                */
        DCL    &WHSE1      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE2      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE3      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE4      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE5      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE6      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE7      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE8      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE9      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE10     *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &R1LOAD     *CHAR   (1)           /* LOAD ID         */
        DCL    &R2SEQ      *CHAR   (1)           /* SCHED SEQ       */
        DCL    &R3DEP      *CHAR   (1)           /* DEADLINE D      */
        DCL    &LDPLTP     *CHAR   (2)           /* LD PLN TYP      */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     IF         COND(&NIGHTQ *EQ '*JOBD') THEN(DO)
             SBMJOB     CMD(CALL PGM(&CLP) PARM(&COMPNYN &LOADN +
                          &DATEN &FLAG &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ &WHSE1 &WHSE2 &WHSE3 &WHSE4 +
                          &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 +
                          &WHSE10 &R1LOAD &R2SEQ &R3DEP +
                          &LDPLTP &SOFLAG &AUTOP)) JOB(&CLP) +
                          JOBQ(PRIORITY)
             ENDDO
             IF         COND(&NIGHTQ *NE '*JOBD') THEN(DO)
             SBMJOB     CMD(CALL PGM(&CLP) PARM(&COMPNYN &LOADN +
                          &DATEN &FLAG &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ &WHSE1 &WHSE2 &WHSE3 &WHSE4 +
                          &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 +
                          &WHSE10 &R1LOAD &R2SEQ &R3DEP +
                          &LDPLTP &SOFLAG &AUTOP)) JOB(&CLP) +
                          JOBQ(&NIGHTQ)
             ENDDO
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
