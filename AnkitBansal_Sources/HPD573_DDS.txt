     A*%%TS  SD  20050321  131800  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD573 - SELECT FARM COST EFFECTIVE DATE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          06/28/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP573R1
     A*%%TS  SD  19941107  083624  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 41)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP573R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP573R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP573S1                   SFL
     A*%%TS  SD  20050321  131800  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFMDY     R        O  6  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            DFFSCD    R        O  6 13REFFLD(FCFSCD)
     A                                      EDTCDE(M)
     A            DFHDRT    R        O  6 21REFFLD(FCHDRT)
     A                                      EDTCDE(K)
     A            DFLBRT         5Y 4O  6 33EDTCDE(K)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP573C1                   SFLCTL(HP573S1)
     A*%%TS  SD  20050321  131800  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP573R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'SELECT FARM COST EFFECTIVE DATE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  3 24'Cost'
     A                                      DSPATR(HI)
     A                                  3 34'Cost'
     A                                      DSPATR(HI)
     A                                  4  1'Date'
     A                                      DSPATR(HI)
     A                                  4 13'Farm'
     A                                      DSPATR(HI)
     A                                  4 22'Per Head'
     A                                      DSPATR(HI)
     A                                  4 32'Per Pound'
     A                                      DSPATR(HI)
     A            C1MDY     R        B  5  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  5 13REFFLD(FSFSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A          R HP573K1
     A*%%TS  SD  19940506  091739  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP573R1)
     A                                      OVERLAY
     A                                 15  1'F12=Previous'
