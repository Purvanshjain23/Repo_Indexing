      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP259 - Retrieve Group with Most Pigs on Sales Movement
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/26/02
      *
      * FUNCTION:    This program was written for the SYNON programmers.
      *              This program is an almost clone of HP258.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp016    if   e           k disk
      *    Manager
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp084    if   e           k disk
      *    Sales detail
      *
      *
     Fhsp085    if   e           k disk
      *    Sales detail
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkhd            s                   like(sgshhd)
     D svhd            s                   like(sgshhd)
     D wkhgsn          s                   like(sghgsn)
      *
      *
      * Parms
      *
     D xxreturn        s              7
     D xxmvsn          s                   like(shmvsn)
     D xxhgcd          s                   like(hghgcd)
     D xxfscd          s                   like(fsfscd)
     D xxfsnm          s                   like(fsfsnm)
     D xxsacd          s                   like(fssacd)
     D xxcmgcd         s                   like(fscmgcd)
     D xxmgnm          s                   like(mgmgnm)
     D xxsxcd          s                   like(hgsxcd)
     D xxgncd          s                   like(hggncd)
     D xxcnty          s                   like(fscnty)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *-----------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Retrieve the Sales Movement header record to get the status. Then,
      * process all the detail records to find the group with the most pigs.
      *
      *
     C     xxmvsn        chain     hsp084                             92
     C                   if        *in92 = *off                                 If found
     C                   move      *blank        xxreturn
     C                   exsr      $hgsn
     C                   else
      *
     C                   eval      xxreturn = 'HPSM1'
     C                   move      *blank        xxhgcd
     C                   endif                                                  If found
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Find the group with the most pigs
      *----------------------------------------------------------------
      *
     C     $hgsn         begsr
      *
     C     xxmvsn        setll     hsp085
      *
     C                   dou       *in91 = *on                                  Do count
     C     xxmvsn        reade     hsp085                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      shmscd = 'PP' or
     C                             shmscd = 'FP' or
     C                             shmscd = 'NC'
     C     sglvhd        add       sgdohd        wkhd
      *
     C                   when      shmscd = 'SH'
     C                   z-add     sgshhd        wkhd
      *
     C                   when      shmscd = 'SC'
     C                   z-add     sgschd        wkhd
     C                   endsl
      *
     C                   if        wkhd > svhd
     C                   z-add     sghgsn        wkhgsn
     C                   z-add     wkhd          svhd
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do count
      *
      * Retrieve hog group stuff to put into output parms
      *
     C     wkhgsn        chain     hsl034d                            92
     C                   if        *in92 = *off                                 If group hit
     C                   move      hghgcd        xxhgcd
     C                   move      hgsxcd        xxsxcd
     C                   move      hggncd        xxgncd
      *
      * Retrieve Farm info for this group for output parms
      *
     C     hgfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
     C                   z-add     fsfscd        xxfscd
     C                   move      fsfsnm        xxfsnm
     C                   move      fssacd        xxsacd
     C                   move      fscmgcd       xxcmgcd
     C                   move      fscnty        xxcnty
      *
      * Retrieve Cell Manager name for this farm for output parm
      *
     C     fscmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If manager hit
     C                   move      mgmgnm        xxmgnm
     C                   endif                                                  If manager hit
     C                   endif                                                  If farm hit
     C                   endif                                                  If group hit
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxreturn
     C                   parm                    xxmvsn
     C                   parm                    xxhgcd
     C                   parm                    xxfscd
     C                   parm                    xxfsnm
     C                   parm                    xxsacd
     C                   parm                    xxcmgcd
     C                   parm                    xxmgnm
     C                   parm                    xxsxcd
     C                   parm                    xxgncd
     C                   parm                    xxcnty
      *
     C                   endsr
      /EJECT
