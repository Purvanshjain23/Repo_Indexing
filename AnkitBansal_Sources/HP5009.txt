      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:   HOG PRODUCTION
      * PROGRAM:  HP5009
      * TITLE:    SELECT MIXED GROUP OPTION
      * AUTHOR:   LEANNE FEDOR
      * CREATED:  01/10/00
      *
      * FUNCTION: THIS IS A SELECT FUNCTION WINDOW DISPLAY OF
      *           THE VALID MIXED GROUP OPTIONS FOR REPORT
      *
      *         IT DOES NOT HAVE SELECTORS OR POSITIONERS.
      *         IT LOADS THE DATA FROM COMPILE-TIME ARRAYS.
      *         IT LOADS THE WINDOW SUBFILE A PAGE AT A TIME.
      *         IT DOES NOT USE THE STANDARD MESSAGE HANDLING PROGRAM.
      *         A RECORD IS SELECTED BY POSITIONING THE CURSOR
      *         ON THE LINE AND PRESSING ENTER.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD5009   CF   E             WORKSTN
     F                                     SFILE(HP5009S1:HDRRN1)
      *
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      * DEFINE STATUS CODES AND DESCRIPTIONS IN COMPILE-TIME ARRAYS
      *
     D CDE             S              1    DIM(4) CTDATA PERRCD(4)
     D DSC             S             41    DIM(4) CTDATA PERRCD(1)
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO
      /EJECT
      *---------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *  CLEAR/FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
     C                   WRITE     HP5009K1
     C                   EXFMT     HP5009C1
      *
      * RESET ERROR FLAG
     C                   Z-ADD     0             ERROR
      *
      * GET USER'S ENTRY AND RESET STANDARD RESPONSE
      *
     C                   SELECT
     C     *IN12         WHENEQ    *ON                                          F12=PREVIOUS
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C                   EXSR      $LOAD1
     C                   MOVEL     SCRN1         ROUTNE
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      *
     C     NORECS        IFEQ      YES                                          IF EMPTY SFL
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDIF                                                  IF EMPTY SFL
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * IF THERE ARE NO ERRORS, READ AND PROCESS ANY RECORD SELECTED
      * BY THE USER. IF THE USER SELECTED A RECORD, THEN EXIT THE
      * PROGRAM.
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
     C                   EXSR      $PROC1
      *
     C     XXMGFL        IFNE      *BLANK
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
      * ELSE THERE WERE ERRORS, SO SET TO REDISPLAY SCREEN
      *
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLR1 - SUBROUTINE TO CLEAR THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
     C                   CLEAR                   HP5009S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP5009C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * CLEAR THE SUBFILE
      * RELOAD THE SUBFILE FROM STATUS ARRAYS "CDE" AND "DSC"
      *
     C                   EXSR      $CLR1
     C                   EXSR      $LOAD1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1 - SUBROUTINE TO LOAD THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
      * FILL THE SUBFILE WITH THE THREE COMPILE TIME ARRAY ENTRIES
      *
     C                   Z-ADD     1             X                 1 0
      *
     C                   DO        4                                            FILL LOOP
     C                   MOVEL     CDE(X)        DFMGFL
     C                   MOVEL     DSC(X)        DFMGDS
     C                   ADD       1             HDRRN1
     C                   WRITE     HP5009S1
     C                   SETON                                        81
     C                   ADD       1             X
      *
      * FOR POSITIONING, PLACE THE SUBFILE RELATIVE RECORD NUMBER INTO
      * THE SFLRCDNBR FIELD (HDPAG1)
      *
     C                   Z-ADD     HDRRN1        HDPAG1
      *
      * *IN82 IS ASSOCIATED WITH THE SFLEND KEYWORD. SET IT ON TO
      * PREVENT THE '+' SIGN FROM APPEARING ON THE LAST SUBFILE RECORD.
      *
     C                   SETON                                        82
     C                   ENDDO                                                  DO FILL LOOP
      *
      * IF NO RECORDS WERE WRITTEN TO THE SUBFILE, THEN ISSUE
      * THE STANDARD MESSAGE 'NO DATA TO DISPLAY'.
      *
     C     HDRRN1        IFEQ      0
     C                   Z-ADD     1             ERROR
     C                   MOVE      YES           NORECS
     C                   ELSE
     C                   MOVE      NO            NORECS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO READ SUBFILE RECORDS AND PROCESS
      *          USER SELECTIONS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
     C                   Z-ADD     0             ERROR
      *
      * IF CURSOR LOCATION RETURNED IS THE RRN OF A RCD IN THE SUBFILE,
      * CHAIN TO THE SUBFILE FOR THAT RECORD AND LOAD THE DATA INTO THE
      * PARMS TO RETURN TO THE CALLING PROGRAM.
      *
     C                   SETOFF                                       82
     C     HDRRN         IFNE      0
     C                   Z-ADD     HDRRN         HDRRN1
     C     HDRRN1        CHAIN     HP5009S1                           82
     C                   MOVEL     DFMGFL        XXMGFL
     C                   MOVEL     DFMGDS        XXMGDS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *     NONE
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    XXMGFL
     C                   PARM                    XXMGDS
      *
     C     *LIKE         DEFINE    DFMGFL        XXMGFL
     C     *LIKE         DEFINE    DFMGDS        XXMGDS
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   MOVEL     NO            NORECS            1            NO RECORDS
     C                   Z-ADD     0             ERROR             3 0          ERRORS
      *
      *  DO ONE TIME ONLY:  MAKE SURE CALLING PROGRAM SCREEN IS NOT
      *  CLEARED AND WRITE THE WINDOW DEFINITION RCDFMT
      *
     C                   WRITE     HP5009R2                                     DUMMY FMT
     C                   WRITE     HP5009R1                                     REF WINDOW
      *
     C                   ENDSR
      /EJECT
     ******************************************************************
      * ARRAYS FOR LOADING OF DATA TO SUBFILE
     ******************************************************************
**  VALID MIXED GROUP OPTIONS
IOSB
**  DESCRIPTIONS
Include mixed groups on report
Omit mixed groups from report
Select mixed groups only
Only select groups with mixed BGF sources
