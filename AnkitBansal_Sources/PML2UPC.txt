/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PML2UPC                                       */
/*  PROGRAM NAME:    PURGE RECORDS USING SQL TO DELETE             */
/*  PROGRAMMER:      LARA BUSER                                    */
/*  CREATE DATE:     08/14/2007                                    */
/*                                                                 */
/*  FUNCTION:        THIS CL WILL USE SQL TO DELETE RECORDS        */
/*                   THAT ARE OLDER THAN THE PURGE DATE            */
/*                   FROM THE PRODUCTION FILE                      */
/*                                                                 */
/*  NOTE:            THE PURGE DATE FORMAT MUST MATCH THE          */
/*                   FILE DATE FORMAT. THIS FORMAT WILL BE         */
/*                   STORED IN THE PURGE FILE FILE                 */
/*                                                                 */
/*  PARAMETERS:                                                    */
/*    &PURGEDATE  7.0  PURGE THROUGHD DATE                         */
/*    &PRGLIB    10    GROUP CODE LIBRARY                          */
/*    &FILENAME  10    FILE TO PURGE                               */
/*    &FIELDNAME  6    DATE FIELD NAME IN THE FILE TO USE          */
/*    &DATEFMT   10    DATE FORMAT                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  09/16/2009 LJB S00941  ADD PARMS SO PROGRAM CAN BE CALLED      */
/*    FROM PMLAUPC GENERIC CALL FOR PURGE PROGRAM                  */
/*******************************************************************/
/*******     PGM        PARM(&RTNCODE &PURGEDATE &PRGLIB &FILENAME +
                         &FIELDNAME &DATEFMT)            ***********/
             PGM        PARM(&RTNCODE &PURGEDATE &PCGCODE &PCGLIB +
                          &PCAPPLIC &FILENAME &PCFLGL &PCFTAPE +
                          &PCFIELD &DATEFMT)

             DCL        VAR(&RTNCODE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PURGEDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&PCGCODE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PCGLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCAPPLIC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FILENAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFLGL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFTAPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFIELD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DATEFMT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRGDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PURGEDATEA) TYPE(*CHAR)  LEN(8)
             DCL        VAR(&SEL)  TYPE(*CHAR) LEN(100) /* SELECT */

/*-----------------------------------------------------------------*/
/* INITIALIZE                                                      */
/*-----------------------------------------------------------------*/
             CHGVAR     VAR(&PRGDATE) VALUE(&PURGEDATE)
             CHGVAR     VAR(&PURGEDATEA) VALUE(&PRGDATE)

/* MATCH THE PURGE DATE TO THE FILE DATE FORMAT IF NOT *CYMD       */
             IF         COND(&DATEFMT *NE '       ') THEN(DO)
             CVTDAT     DATE(&PRGDATE) TOVAR(&PURGEDATEA) +
                          FROMFMT(*CYMD) TOFMT(&DATEFMT) TOSEP(*NONE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DELETEPRD))
             ENDDO
/*-----------------------------------------------------------------*/
/* MAINLINE                                                        */
/*-----------------------------------------------------------------*/
/* Delete from PRODUCTION */
             CHGVAR     VAR(&SEL) VALUE('from ' *CAT &PCGLIB *TCAT +
                          '/' *TCAT &FILENAME *CAT ' where ' *CAT +
                          &PCFIELD *BCAT '<=' *BCAT &PURGEDATEA)

             DELETE     SQL(&SEL) OPTIMIZE(*MINWAIT)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DELETEPRD))

/* Successful Completion */
             GOTO       CMDLBL(ENDPGM)
/*-----------------------------------------------------------------*/
/*                     ERROR MESSAGES                              */
/*-----------------------------------------------------------------*/
 DELETEPRD:  CHGVAR     VAR(&RTNCODE) VALUE('SQLERR')

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:     ENDPGM
