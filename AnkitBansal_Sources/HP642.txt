      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP642
      * TITLE:       FACILITY UTILIZATION REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     07/16/96
      *
      *  FUNCTION:   THIS PROGRAM PRINTS THE FACILITY UTILIZATION REPORT
      *              WHICH SHOWS FARM SITE UTILIZATION FOR A VARIABLE
      *              NUMBER OF WEEKS (THE USER SELECTS THE NUMBER OF WEEKS
      *              UP TO A MAXIMUM OF 26.)
      *
      *              THE SELECTIONS ARE ENTERED THROUGH HP442 AND AN
      *              OPNQRY IS USED IN HP442CL TO SELECT RECORDS.
      *              THREE WORKFILES ARE BUILT IN HP342.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 10/10/96  ADDED LOGIC TO SUPPORT 2 ADDITIONAL USER SELECTIONS:
      *              1) CELL
      *              2) NUMBER OF WEEKS TO INCLUDE ON THE REPORT
      *           ALSO, ADDED AN ADDITIONAL COLUMN OF 'AVERAGE'.
      *
      * 11/04/96  LEANNE FEDOR
      *           AS REQUESTED BY JAY KAYSER, ADDED A NEW TOTAL FIELD
      *           FOR 'FARM CAPACITY HEAD' AND CHANGED LOGIC USED
      *           TO CALCULATE 'AVERAGE' COLUMN VALUE.
      *
      * 11/12/96  LEANNE FEDOR
      *           AS REQUESTED BY JAY KAYSER, CHANGED ACCUMULATION AND
      *           CALCULATION LOGIC FOR TOTAL LINE TO BE CONSISTANT
      *           WITH DETAIL LINE LOGIC WHICH WAS CHANGED ON 11/4/96.
      *
      * 09/27/01  LeAnne Fedor
      *           Added 'group type' as a selector.
      *
      * 07/23/02  LeAnne Fedor
      *           In preparation for allowing groups on a single farm to have different
      *           production phases, we have made NO CHANGES to this report. We don't know what
      *           to do since the 'capacity' is calculated based on the farm's target square
      *           feet and target pigs per square feet. Since 'nursery' and 'grow finish' pigs
      *           per square feet would be so different, we didn't have any idea what to do with
      *           this report. And, since Alice doesn't think anyone is actually using it, we
      *           have made no changes at this time!
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *   PRODUCTION TYPE            (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *   PRODUCTION PHASE           (KEY: PPPPCD)
      *
     FHSP026    IF   E           K DISK
      *   MONTH                      (KEY: MMMMCD)
      *
     FHSP025    IF   E           K DISK
      *    GROUP TYPE
      *
     FHSP341    IF   E           K DISK
      *  WORKFILE OF WEEKS           (KEY: WWWEEK)
      *
     FHSP342    IF   E           K DISK
      *  WORKFILE OF PIG DAYS        (KEY: WPFSCD WPWEEK)
      *
     FHSP343    IF   E           K DISK
      *  FARM WORKFILE               (KEY: WFPTCD WFPPCD WFFSCD)
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *---------------------------------------------------------------
      * ARRAYS FOR PRINTING
      *---------------------------------------------------------------
      *
      * VALUES FOR HEADING LINES
      *
     D WEK             S              2  0 DIM(26)                              WEEK NUMBER
     D DAY             S              2  0 DIM(26)                              SATURDAY DAY DATE
     D MON             S              3    DIM(26)                              MONTH ABBREV
      *
      * VALUES FOR FARM DETAIL LINE
      *
     D PER             S              3  0 DIM(26)                              PERCENT FULL
      *
      * VALUES FOR PRODUCTION TYPE/PHASE TOTAL LINE
      *
     D CAP             S             15  0 DIM(26)                              CAPACITY DAYS
     D PIG             S             15  0 DIM(26)                              PIG DAYS
     D FUL             S              3  0 DIM(26)                              PERCENT FULL
      *
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDTYMD                 6     13  0
     D  LDFYMD                14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDFSCD                32     36  0
      *
     D  LDFMDY                37     42  0
     D  LDTMDY                43     48  0
      *
     D  LDCELL                49     50  0
     D  LDWK                  51     52  0
      *
     D  LDGTCD                53     53
     D  LDGTDS                54     68
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412  0
     D  LDOUTQ               413    422
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *
     D  DUMMYD                 1      1
      *
      *---------------------------------------------------------------
      * NAMED CONSTANTS
      *---------------------------------------------------------------
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * PROCESS EACH RECORD IN THE FARM WORKFILE
      *
     C     *LOVAL        SETLL     HSP343
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO LOOP
     C                   READ      HSP343                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   MOVEL     WFPTCD        SVPTCD
     C                   MOVEL     WFPPCD        SVPPCD
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HDR
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      *--------------------------------------------------------------
      * PERFORM CONTROL BREAK
      *--------------------------------------------------------------
      *
      * IF BREAK IN PRODUCTION TYPE OF PRODUCTION PHASE,
      *    PRINT TOTAL LINE FOR TYPE/PHASE
      *    RETRIEVE DESCRIPTION OF NEW TYPE
      *    RETRIEVE DESCRIPTION OF NEW PHASE
      *    PAGE BREAK AND PRINT REPORT HEADINGS
      *
     C     WFPTCD        IFNE      SVPTCD                                       IF BREAK
     C     WFPPCD        ORNE      SVPPCD
      *
      * IF THE USER HAS NOT LIMITED THE REPORT TO A SINGLE
      * FARM, CALCULATE PERCENT FULL FOR EACH WEEK AND PRINT
      * THE TOTAL LINE FOR PRODUCTION TYPE/PHASE
      *
     C     LDFSCD        IFEQ      0
     C                   EXSR      $TPTOT
     C                   ENDIF
      *
     C     WFPTCD        IFNE      SVPTCD                                       IF NEW TYPE
     C                   MOVEL     WFPTCD        SVPTCD
     C                   EXSR      $PTDS
     C                   ENDIF                                                  IF NEW TYPE
      *
     C     WFPPCD        IFNE      SVPPCD                                       IF NEW PHASE
     C                   MOVEL     WFPPCD        SVPPCD
     C                   EXSR      $PPDS
     C                   ENDIF                                                  IF NEW PHASE
      *
     C                   EXSR      $HDR
     C                   ENDIF                                                  IF BREAK
      *
      *----------------------------------------------------
      * DETAIL PROCESSING
      *----------------------------------------------------
      *
      * ACCUMULATE FARM CAPACITY HEAD FOR TOTAL LINE
      *
     C                   ADD       WFCPHD        TPCPHD
      *
      * READ ALL WEEK RECORDS FOR FARM.
      *  1) LOAD THE FARM WEEKLY PERCENT ARRAY
      *  2) LOAD THE ARRAYS FOR CALCULATING/PRINTING TOTALS
      *     FOR PRODUCTION TYPE/PHASE
      *
     C                   EXSR      $PER
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO LOOP
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      * IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA,
      *         PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      * OTHERWISE
      *         PRINT LAST TOTAL LINE
      *
      *
     C     FIRST         IFEQ      YES                                          IF NO DATA
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * IF THE USER SELECTED ALL FARMS FOR THE REPORT, THEN
      * PRINT THE TOTAL LINE FOR THE LAST PRODUCTION PHASE/TYPE
      *
     C     LDFSCD        IFEQ      0                                            IF ALL FARMS
     C                   EXSR      $TPTOT
     C                   ENDIF                                                  IF ALL FARMS
      *
     C                   ENDIF                                                  IF NO DATA
      *
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $PER  - LOAD ARRAYS WITH WEEKS OF CAPACITY VALUES FOR FARM
      *----------------------------------------------------------------
      *
     C     $PER          BEGSR
      *
     C                   Z-ADD     0             PER
     C                   Z-ADD     0             X
     C                   MOVEL     NO            HEADFL
      *
     C     WFFSCD        SETLL     HSP342
      *
     C                   DO        LDWK          X                              DO WEEKS
     C     WFFSCD        READE     HSP342                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     WPPDAY        IFNE      0
     C                   MOVEL     YES           HEADFL
     C                   ENDIF
      *
     C                   Z-ADD     WPFLPR        PER(X)
      *
      * IF 1) THE USERS HAVE SET THIS FARM UP WITH A CAPACITY, AND
      *    2) THERE ARE OR HAVE BEEN PIGS ON THE FARM FOR THIS WEEK
      *       OR A PREVIOUS WEEK
      * ACCUMULATE BOTH THE PIG DAY CAPACITY AND THE ACTUAL
      * PIG DAYS INTO THE ARRAYS FOR THE PRODUCTION TYPE/PHASE
      * TOTALS.
     C     WFCPPD        IFNE      0                                            IF CAPACITY
     C     HEADFL        ANDEQ     YES
     C                   ADD       WFCPPD        CAP(X)
     C                   ADD       WPPDAY        PIG(X)
     C                   ENDIF                                                  IF CAPACITY
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO WEEKS
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HDR
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PTDS - RETRIEVE PRODUCTION TYPE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PTDS         BEGSR
      *
     C     SVPTCD        CHAIN     HSP010                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     PTPTDS        HPPTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HPPTDS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     SVPPCD        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     PPPPDS        HPPPDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HPPPDS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $HDR  - PRINT REPORT HEADINGS
      *----------------------------------------------------------------
      *
     C     $HDR          BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TPTOT - CALCULATE WEEKLY FULL PRECENT AND PRINT TOTAL LINE FOR
      *          PRODUCTION TYPE/PHASE.
      *----------------------------------------------------------------
      *
      *
     C     $TPTOT        BEGSR
      *
     C                   Z-ADD     0             WKFLD2
     C                   Z-ADD     0             X
      *
     C                   DO        LDWK          X                              DO WEEKS
     C     CAP(X)        IFNE      0
     C     PIG(X)        DIV       CAP(X)        WKFLD1
     C     100           MULT(H)   WKFLD1        FUL(X)
     C                   ADD       FUL(X)        WKFLD2
     C                   ENDIF
     C                   ENDDO                                                  DO WEEKS
      *
      *
      * CALCULATE AVERAGE FULL PERCENT FOR TOTAL LINE
      *
     C     LDWK          IFNE      0
     C     WKFLD2        DIV(H)    LDWK          TPAVPR
     C                   ENDIF
      *
     C                   EXCEPT    TPTOT
      *
     C                   Z-ADD     0             CAP
     C                   Z-ADD     0             PIG
     C                   Z-ADD     0             FUL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $RTVHR - RETRIEVE MONTH ABBREVIATION, SUNDAY DATE, WEEK NUMBER
      *----------------------------------------------------------------
      *
      * RETRIEVE INFORMATION AND LOAD THREE ARRAYS THAT WILL BE PRINTED
      * FOR COLUMN HEADINGS.
      *
     C     $RTVHR        BEGSR
      *
      * PROCESS WEEK WORKFILE
      *
     C     *LOVAL        SETLL     HSP341
      *
     C                   DO        LDWK          X                              DO WEEKS
     C                   READ      HSP341                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * PUT WEEK NUMBER IN ARRAY
      *
     C                   Z-ADD     WWWEEK        WEK(X)
      *
      * EXTRACT THE DAY DATE OF THE ENDING DATE FOR THE WEEK
      * AND PUT IN ARRAY. (IE: EXTRACT '07' FROM 1/7/96)
      *
     C                   Z-ADD     WWEYMD        WKCYMD
     C                   Z-ADD     WKDAY         DAY(X)
      *
      * RETRIEVE THE NAME OF THE MONTH ASSOCIATED WITH THE
      * ENDING DATE FROM THE I.S. CONTROLLED 'MONTH' FILE.
      * MOVE THE FIRST 3 CHARACTERS OF THE NAME TO THE ARRAY.
      * (IE: EXTRACT '01' FROM 1/7/96 AND RETRIEVE 'JANUARY' AND
      * MOVE 'JAN' TO ARRAY)
      *
     C     WKMON         CHAIN     HSP026                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     MMMMNM        MON(X)
     C                   ENDIF
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO WEEKS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE INDEX FOR ARRAYS
      *
     C                   Z-ADD     0             X                 3 0
      *
      * SAVE FIELDS FOR LEVEL BREAK COMPARISONS
      *
     C     *LIKE         DEFINE    WFPTCD        SVPTCD
     C     *LIKE         DEFINE    WFPPCD        SVPPCD
      *
      * WORKFIELDS
     C                   Z-ADD     0             WKFLD1           15 5
     C                   Z-ADD     0             WKFLD2           15 0
      *
      * PRINT FIELD FOR REPORT HEADINGS
      *
     C     *LIKE         DEFINE    PPPPDS        HPPPDS
     C     *LIKE         DEFINE    PTPTDS        HPPTDS
      *
      *
      * PRINT FIELD FOR PRODUCTION TYPE/PHASE TOTAL LINE
      *
     C                   Z-ADD     0             TPCPHD            9 0
     C     *LIKE         DEFINE    WFAVPR        TPAVPR
      *
      *  SETUP THE USER SELECTIONS IN THE REPORT HEADINGS.
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDFSBO
     C                   ENDIF
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDPPCD
     C                   ENDIF
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDPTCD
     C                   ENDIF
      *
      *
     C                   if        ldgtcd = *blank
     C                   seton                                        82
     C                   endif
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
     C     LDCELL        IFEQ      0
     C                   SETON                                        88
     C                   ENDIF
      *
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           HEADFL            1
      *
      * RETRIEVE THE WEEK/MONTH INFO FOR THE REPORT COLUMN HEADINGS
      *
     C                   EXSR      $RTVHR
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADINGS
      *-------------------------------------------------------------
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          112 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          108 'FACILITY UTILIZATION'
     O                                         +  1 'REPORT'
     O                                          188 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          113 'PERCENT OF FARM CAPACITY'
     O                                          188 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           25 'BUSINESS OFFICE:'
     O                       LDFSBO              32
     O                       LDBODS            +  5
      *
     O          E            HRHDR          1
     O                                           25 'SATURDAY CUTOFF DATE:'
     O                       LDTMDY            +  2 '  /  /  '
      *
     O          E            HRHDR          1
     O                                           25 'NUMBER OF WEEKS:'
     O                       LDWK          Z   +  2
      *
     O          E            HRHDR          1
     O                                           25 'PRODUCTION TYPE:'
     O                       LDPTCD              32
     O                       LDPTDS            +  5
      *
     O          E            HRHDR          1
     O                                           25 'PRODUCTION PHASE:'
     O                       LDPPCD              32
     O                       LDPPDS            +  5
      *
      *
     O          e            hrhdr          1
     O                                           25 'GROUP TYPE:'
     O              n82      ldgtcd              28
     O               82                          30 'ALL'
     O                       ldgtds              52
      *
      *
     O          E            HRHDR          1
     O                                           25 'CELL:'
     O              N88      LDCELL        Z     32
     O               88                          30 'ALL'
      *
     O          E            HRHDR          2
     O                                           25 'FARM SITE:'
     O              N89      LDFSCD        Z     32
     O              N89      LDFSNM              62
     O               89                          30 'ALL'
      *
      *-------------------------------------------------------------
      * COLUMN HEADINGS
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                           44 'FARM'
     O                                           60 'MONTH:'
     O                       MON(1)              72
     O                       MON(2)              77
     O                       MON(3)              82
     O                       MON(4)              87
     O                       MON(5)              92
     O                       MON(6)              97
     O                       MON(7)             102
     O                       MON(8)             107
     O                       MON(9)             112
     O                       MON(10)            117
     O                       MON(11)            122
     O                       MON(12)            127
     O                       MON(13)            132
     O                       MON(14)            137
     O                       MON(15)            142
     O                       MON(16)            147
     O                       MON(17)            152
     O                       MON(18)            157
     O                       MON(19)            162
     O                       MON(20)            167
     O                       MON(21)            172
     O                       MON(22)            177
     O                       MON(23)            182
     O                       MON(24)            187
     O                       MON(25)            192
     O                       MON(26)            197
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           44 'CAPACITY'
     O                                           60 'WEEK END DATE:'
     O                       DAY(1)        Z     72
     O                       DAY(2)        Z     77
     O                       DAY(3)        Z     82
     O                       DAY(4)        Z     87
     O                       DAY(5)        Z     92
     O                       DAY(6)        Z     97
     O                       DAY(7)        Z    102
     O                       DAY(8)        Z    107
     O                       DAY(9)        Z    112
     O                       DAY(10)       Z    117
     O                       DAY(11)       Z    122
     O                       DAY(12)       Z    127
     O                       DAY(13)       Z    132
     O                       DAY(14)       Z    137
     O                       DAY(15)       Z    142
     O                       DAY(16)       Z    147
     O                       DAY(17)       Z    152
     O                       DAY(18)       Z    157
     O                       DAY(19)       Z    162
     O                       DAY(20)       Z    167
     O                       DAY(21)       Z    172
     O                       DAY(22)       Z    177
     O                       DAY(23)       Z    182
     O                       DAY(24)       Z    187
     O                       DAY(25)       Z    192
     O                       DAY(26)       Z    197
      *
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           12 'NAME'
     O                                           44 'HEAD'
     O                                           60 'WEEK NUMBER:'
     O                                           65 'AVG'
     O                       WEK(1)        Z     72
     O                       WEK(2)        Z     77
     O                       WEK(3)        Z     82
     O                       WEK(4)        Z     87
     O                       WEK(5)        Z     92
     O                       WEK(6)        Z     97
     O                       WEK(7)        Z    102
     O                       WEK(8)        Z    107
     O                       WEK(9)        Z    112
     O                       WEK(10)       Z    117
     O                       WEK(11)       Z    122
     O                       WEK(12)       Z    127
     O                       WEK(13)       Z    132
     O                       WEK(14)       Z    137
     O                       WEK(15)       Z    142
     O                       WEK(16)       Z    147
     O                       WEK(17)       Z    152
     O                       WEK(18)       Z    157
     O                       WEK(19)       Z    162
     O                       WEK(20)       Z    167
     O                       WEK(21)       Z    172
     O                       WEK(22)       Z    177
     O                       WEK(23)       Z    182
     O                       WEK(24)       Z    187
     O                       WEK(25)       Z    192
     O                       WEK(26)       Z    197
      *
      *
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HPHDR          2
     O                                           73 '*****'
     O                                           95 'PRODUCTION TYPE/PHASE'
     O                                          101 '*****'
     O                       HPPTDS            +  3
     O                                         +  1 '/'
     O                       HPPPDS            +  2
      *
      *-------------------------------------------------------------
      * DETAIL FARM
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       WFFSCD        Z      5
     O                       WFFSNM              33
     O                       WFCPHD        K     45
     O                       WFAVPR        Z     65
     O                       PER(1)        Z     72
     O                       PER(2)        Z     77
     O                       PER(3)        Z     82
     O                       PER(4)        Z     87
     O                       PER(5)        Z     92
     O                       PER(6)        Z     97
     O                       PER(7)        Z    102
     O                       PER(8)        Z    107
     O                       PER(9)        Z    112
     O                       PER(10)       Z    117
     O                       PER(11)       Z    122
     O                       PER(12)       Z    127
     O                       PER(13)       Z    132
     O                       PER(14)       Z    137
     O                       PER(15)       Z    142
     O                       PER(16)       Z    147
     O                       PER(17)       Z    152
     O                       PER(18)       Z    157
     O                       PER(19)       Z    162
     O                       PER(20)       Z    167
     O                       PER(21)       Z    172
     O                       PER(22)       Z    177
     O                       PER(23)       Z    182
     O                       PER(24)       Z    187
     O                       PER(25)       Z    192
     O                       PER(26)       Z    197
      *
      *-------------------------------------------------------------
      * TOTAL LINE FOR PRODUCTION TYPE/PHASE
      *-------------------------------------------------------------
      *
     O          E            TPTOT       1
      *
     O                                           19 'TOTAL:'
     O                       SVPTCD              26
     O                                           27 '/'
     O                       SVPPCD              32
      *
     O                       TPCPHD        KB    45
     O                       TPAVPR        ZB    65
     O                       FUL(1)        Z     72
     O                       FUL(2)        Z     77
     O                       FUL(3)        Z     82
     O                       FUL(4)        Z     87
     O                       FUL(5)        Z     92
     O                       FUL(6)        Z     97
     O                       FUL(7)        Z    102
     O                       FUL(8)        Z    107
     O                       FUL(9)        Z    112
     O                       FUL(10)       Z    117
     O                       FUL(11)       Z    122
     O                       FUL(12)       Z    127
     O                       FUL(13)       Z    132
     O                       FUL(14)       Z    137
     O                       FUL(15)       Z    142
     O                       FUL(16)       Z    147
     O                       FUL(17)       Z    152
     O                       FUL(18)       Z    157
     O                       FUL(19)       Z    162
     O                       FUL(20)       Z    167
     O                       FUL(21)       Z    172
     O                       FUL(22)       Z    177
     O                       FUL(23)       Z    182
     O                       FUL(24)       Z    187
     O                       FUL(25)       Z    192
     O                       FUL(26)       Z    197
      *
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      2
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
