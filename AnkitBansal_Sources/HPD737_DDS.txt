     A*%%TS  SD  19980302  104516  ISLRAMS     REL-V3R7M0  5716-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD737
     A*  TITLE:         VIEW CARCASS DATA AND PREMIUMS/DISCOUNTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/22/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*****************************************************************
     A* SUBFILE RECORD
     A*****************************************************************
     A          R HP737S1                   SFL
     A*%%TS  SD  19950615  152711  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('display prem/disc subfile')
     A                                      SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFPDCD    R        O 19 16REFFLD(HSRREF/PDPDCD *LIBL/HSPREF)
     A            DFPDDS    R        O 19 30REFFLD(HSRREF/PDPDDS *LIBL/HSPREF)
     A            DFPDAM    R        O 19 56REFFLD(HSRREF/CJCJAM *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            DFPCWT    R        O 19 68REFFLD(HSRREF/CJCJAM *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*****************************************************************
     A* CONTROL RECORD FORMAT
     A*****************************************************************
     A          R HP737C1                   SFLCTL(HP737S1)
     A*%%TS  SD  19980302  104516  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0003)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 17'VIEW CARCASS DATA AND PREMIUM/DISC-
     A                                      OUNTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Check number .........:'
     A            C1CHNO    R        O  4 27REFFLD(HSRREF/CHCHNO *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  5  2'Customer A/R number ..:'
     A            C1CVNO    R        O  5 27REFFLD(HSRREF/CHCVNO *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            C1CNAM    R        O  5 37REFFLD(@@DS30)
     A                                  6  2'Movement number ......:'
     A            C1MVSN    R        O  6 27REFFLD(HSRREF/CDMVSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  7  2'Hog group ............:'
     A            C1HGCD    R        O  7 27REFFLD(@@HGCD)
     A                                  8  2'Tattoo ...............:'
     A            C1TANO    R        O  8 28REFFLD(HSRREF/CDTANO *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                  9  2'Gross amount .........:'
     A            C1GRAM    R        O  9 27REFFLD(HSRREF/CDGRAM *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                  9 60'Head'
     A                                  9 69'Pounds'
     A                                 10 43'Live .......:'
     A            C1LVHD    R        O 10 58REFFLD(HSRREF/CDLVHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1LVLB    R        O 10 69REFFLD(HSRREF/CDLVLB *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 11 43'Condemned ..:'
     A            C1CDHD    R        O 11 58REFFLD(HSRREF/CDCDHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1CDLB    R        O 11 69REFFLD(HSRREF/CDCDLB *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 12 43'Yard Dead ..:'
     A            C1YDHD    R        O 12 58REFFLD(HSRREF/CDYDHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1YDLB    R        O 12 69REFFLD(HSRREF/CDYDLB *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 13  2'Carcass pounds .......:'
     A            C1CRLB    R        O 13 28REFFLD(HSRREF/CDCRLB *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 14  2'Lean percent .........:'
     A            C1LNPR    R        O 14 28REFFLD(HSRREF/CDLNPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 15  2'Average backfat ......:'
     A            C1BFAM    R        O 15 28REFFLD(HSRREF/CDBFAM *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 15 55'------- TOTALS -------'
     A                                 16  2'Average loin eye .....:'
     A            C1LEAM    R        O 16 28REFFLD(HSRREF/CDLEAM *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1PDAM    R        O 16 56REFFLD(CJCJAM)
     A                                      EDTCDE(K)
     A            C1PCWT    R        O 16 68REFFLD(CJCJAM)
     A                                      EDTCDE(K)
     A                                 17 59'Dollar'
     A                                      DSPATR(HI)
     A                                 17 70'Dollars'
     A                                      DSPATR(HI)
     A                                 18 10'Premium/Discount'
     A                                      DSPATR(HI)
     A                                 18 30'Description'
     A                                      DSPATR(HI)
     A                                 18 59'Amount'
     A                                      DSPATR(HI)
     A                                 18 70'Per CWT'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP737K1
     A*%%TS  SD  19950622  100308  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP737ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP737EC                   SFLCTL(HP737ES)
     A*%%TS  SD  19940602  132831  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
