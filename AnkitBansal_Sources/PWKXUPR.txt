     z* CRTBNDRPG
     z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
      *
      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production Evaluation
      * PROGRAM:     PWKXUPR
      * TITLE:       Create Editing File from Received Records
      * PROGRAMMER:  Brad Baden
      * CREATED:      9/3/2020
      *
      * FUNCTION:    Read the Tattoo Numbers in the workfile and create
      *              records in the Tattoo Nbr Created by PLT (PKATREP)
      *
      ****************************************************************
      * MODIFICATIONS
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     fpwavcpp   if   e             disk
      * VWF Tattoo# Crtd by Plant - Physical file
      *
     fpkatrel1  if   e           k disk
      * Tattoo Nbr Created by Plt - Retrieval index
      *
     fpkatrel4  if   e           k disk
      * Tattoo Nbr Created by Plt - Co/GrpSeq/Grp
      *
     fpkatrep   o  a e             disk
      * Tattoo Nbr Created by Plt - Physical file
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     d yes             c                   'Y'
     d no              c                   'N'
     d active          c                   'A'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d seq#            s              5  0
     d group#          s              5  0
     d jobdate         s              7  0
     d jobtime         s              6  0
     d returncd        s              7
     d warehouse       s              3
     d userco#         s              3  0
     d application     s              6
     d salesperson     s              3
     d multicompany    s              1
     d dfltprinter     s             10
     d username        s             30
     d systemvaluea    s             40
     d accessdenied    s              1
     d companyvalue    s             10
     d allowcopy       s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *---------------------------------------------------------------
      * Data areas
      *---------------------------------------------------------------
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     c                   if        sdpgm = *blank
     c                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      ** read all workfile records until end-of-file
      *
     c                   dou       %eof(pwavcpp)                                dou %eof(pwavcpp)
     c                   read      pwavcpp
     c                   if        not %eof(pwavcpp)                            if not %eof(pwavcpp)
      *
      ** check if Tattoo Number already exists in PKATREL1
      *
     c     key01         setll     pkatrel1
     c                   if        not %equal(pkatrel1)                         if not %equal(pkatre
      *
     c                   exsr      $wrtrcd
      *
     c                   endif                                                  fi not %equal(pkatre
     c                   endif                                                  fi not %eof(pwavcpp)
     c                   enddo                                                  uod %eof(pwavcpp)
      *
      ** normal exit processing
      *
     c                   exsr      $exit
      /EJECT
      *---------------------------------------------------------------
      * $wrtrcd - write a record to PKATREP
      *---------------------------------------------------------------
      *
     c     $wrtrcd       begsr
      *
      ** Load values for PKATREP
      *
     c                   eval      atuva1 = userco#                             Company number
     c                   eval      atcvnb = avcvnb                              Tattoo number
     c                   eval      atb3nz = group#                              Tattoo number group
     c                   eval      atb4nz = seq#                                Tattoo group seq#
     c                   eval      atl6ss = *blank                              Tattoo usage status
     c                   eval      ataavn = *blanks                             Change user
     c                   eval      ataadt = *zero                               Change date
     c                   eval      atamtm = *zero                               Change time
     c                   eval      atajst = active                              Record status
     c                   eval      atahvn = sdjob                               Job nametatus
     c                   eval      atagvn = sdpgm                               Program name
     c                   eval      atabvn = sdusr                               Create user
     c                   eval      atabdt = jobdate                             Create date
     c                   eval      atabtm = jobtime                             Create time
      *
      * write record
      *
     c                   write     @atreab
      *
      * increment sequence number
      *
     c                   eval      seq# += 1
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * $nxtgrp - retrieve next available group number
      *----------------------------------------------------------------
      *
     c     $nxtgrp       begsr
      *
      * Default group# to 1
     c                   eval      group# = 1
      *
     c                   eval      seq# = 1
      *
      ** Read using key Co# and Seq# for READE
      *
     c     key02         setll     pkatrel4
      *
     c                   dou       %eof(pkatrel4)
     c     key02         reade     pkatrel4
     c                   if        %found(pkatrel4)
     c                   eval      group# = atb3nz + 1
     c                   endif
     c                   enddo
      *
     c                   endsr
      /eject
      *----------------------------------------------------------------
      * $exit - exit program
      *----------------------------------------------------------------
      *
     c     $exit         begsr
      *
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     c     *inzsr        begsr
      *
      * Key for PKATREL1
      *
     c     key01         klist
     c                   kfld                    userco#
     c                   kfld                    avcvnb
      *
      * Key for PKATREL4
      *
     c     key02         klist
     c                   kfld                    userco#
     c                   kfld                    seq#
      *
      * Run OMS security program to determine user's Company Number
      *
     c                   call      'PDGXXFR'
     c                   parm      *blanks       returncd
     c                   parm                    warehouse
     c                   parm                    userco#
     c                   parm      'HPE   '      application
     c                   parm                    salesperson
     c                   parm                    multicompany
     c                   parm                    dfltprinter
     c                   parm                    username
     c                   parm                    systemvaluea
     c                   parm                    accessdenied
      *
      * Check if user's company is setup to copy records
      *
     c                   call      'POMTXFR'
     c                   parm      *blanks       returncd
     c                   parm                    userco#
     c                   parm      'TATTASSGN '  companyvalue
     c                   parm                    allowcopy
      *
      ** exit program if company is not set up for this process
      *
     c                   if        allowcopy = no
     c                   exsr      $exit
     c                   endif
      *
      * Load default values
      *
     c                   eval      seq# = 1
     c                   eval      jobtime = %dec(%time())
     c                   eval      jobdate = %dec(%date():*cymd)
      *
      * Retrieve the next available Group number
      *
     c                   exsr      $nxtgrp
      *
     C                   endsr
      /EJECT
