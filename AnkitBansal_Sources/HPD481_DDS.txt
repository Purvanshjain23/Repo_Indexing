     A*%%TS  SD  20020326  161808  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD481
     A*  TITLE:         SPECIFY FORM TO PRINT-SCHEDULED TRANSFER ONLY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/07/96
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CA12(12 'PREVIOUS')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP481R1
     A*%%TS  SD  20020326  161808  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(50)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2  8'SPECIFY BILL OF LADINGS TO PRINT---
     A                                      SCHEDULED TRANSFERS ONLY'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Origin business office ........'
     A                                  4 34'?'
     A            DFORBO    R        B  4 36REFFLD(@@BOCD)
     A N30                                  DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFOROD    R        O  4 46REFFLD(BOBODS)
     A                                  5  2'From scheduled date ...........'
     A            DFFMDY    R     Y  B  5 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To scheduled date .............'
     A            DFTMDY    R     Y  B  6 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  8  2'Movement number ...............'
     A                                  8 34'?'
     A            DFMVSN    R        B  8 36REFFLD(@@MVSN)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 10  2'Origin production type ........'
     A                                 10 34'?'
     A            DFORPT    R        B 10 36REFFLD(@@PTCD)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFORTD    R        O 10 46REFFLD(PTPTDS)
     A                                 11  2'Origin production phase .......'
     A                                 11 34'?'
     A            DFORPP    R        B 11 36REFFLD(@@PPCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFORPD    R        O 11 46REFFLD(PPPPDS)
     A                                 12  2'Origin cell ...................'
     A            DFORCELL  R        B 12 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13  2'Origin farm site ..............'
     A                                 13 34'?'
     A            DFORFS    R        B 13 36REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFORFN    R        O 13 46REFFLD(FSFSNM)
     A                                 15  2'Destination business office ...'
     A                                 15 34'?'
     A            DFDNBO    R        B 15 36REFFLD(@@BOCD)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDNOD    R        O 15 46REFFLD(BOBODS)
     A                                 16  2'Destination production type ...'
     A                                 16 34'?'
     A            DFDNPT    R        B 16 36REFFLD(@@PTCD)
     A N37                                  DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDNTD    R        O 16 46REFFLD(PTPTDS)
     A                                 17  2'Destination production phase ..'
     A                                 17 34'?'
     A            DFDNPP    R        B 17 36REFFLD(@@PPCD)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDNPD    R        O 17 46REFFLD(PPPPDS)
     A                                 18  2'Destination cell ..............'
     A            DFDNCELL  R        B 18 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 19  2'Destination farm site .........'
     A                                 19 34'?'
     A            DFDNFS    R        B 19 36REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDNFN    R        O 19 46REFFLD(FSFSNM)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP481K1
     A*%%TS  SD  19960213  100746  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44                             23 22'F8=Submit'
     A N99                             23 33'F12-Previous'
     A N99                             23 47'F17=Print Options'
     A N99                             23 65'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP481ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP481EC                   SFLCTL(HP481ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
