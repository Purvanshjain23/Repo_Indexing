      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP651
      * TITLE:       LISTING OF SELECTED FARM SITE DETAIL
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     4/06/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE DETAIL FOR A FARM SITE
      *           SENT THROUGH A PARM.
      *
      *           THIS PROGRAM IS CALLED FROM HP450 - WORK WITH
      *           FARM SITES.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file
      *           to (production manager/cell manager/multisite manager).
      *
      * 07/10/01  LeAnne Fedor
      *           Removed reference to Bin Set file; we have deleted the file.
      *
      * 09/04/01  LeAnne Fedor
      *           Removed 'hog capacity' from 'building' section. Added new 'square feet'
      *           values to general farm section.
      *
      * 01/23/02  LeAnne Fedor
      *           Added new 'room' fields to listing: total square feet, number of water
      *           nipples, total inches of feeder space.
      *
      * 07/01/02  LeAnne Fedor
      *           Added new 'county' and 'type of farm' fields to listing. Changed layout.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 04/06/20  Danny Nguyen - P405 - Farm Number Increase
      *           Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *           Changed array 'BN' from 5A to 6A.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE
      *
      *
     FHSP019    IF   E           K DISK
      *    FARM BUILDINGS
      *
      *
     FHSP020    IF   E           K DISK
      *    BUILDINGS ROOMS
      *
     FHSP023    IF   E           K DISK
      *    INSTALLED EQUIPMENT
      *
      *
     FHSP025    IF   E           K DISK
      *    GROUP TYPE
      *
      *
     FHSP012    IF   E           K DISK
      *    COMPANY RELATIONSHIP
      *
      *
     FHSP013    IF   E           K DISK
      *    FEED MILL DELIVERY ZONES
      *
      *
     FHSP016    IF   E           K DISK
      *    MANAGER CODE
      *
      *
     FHSP036    IF   E           K DISK
      *    BIN
      *
      *
     FHSP101    IF   E           K DISK
      *    TYPE OF FARM
      *
      *
     Fhsp113    if   e           k disk
      *    Rooms/Bin Sets
      *
      *
     FQPRINT    O    F  100        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      * ARRAY FOR PRINTING BIN CODES FOR A BIN SET
      *
P405 D*BN              S              5    DIM(12)                              BIN CODES
P405 D BN              S              6    DIM(12)                              BIN CODES
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO, DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSCD                 7     11  0
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE FARM SITE MASTER FILE FOR THE PARM FARMSITE
      * SETON *INOF TO PRINT HEADINGS FIRST TIME THROUGH
      *
     C                   SETON                                        OF
      *
     C     LDFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF FARM
      *
      * RETRIEVE DESCRIPTIONS AND NAMES FOR FARM SITE FIELDS
      *
     C     FSFSBO        CHAIN     HSP009                             91
     C                   MOVEL     BOBODS        WKFSDS
     C     FSPTCD        CHAIN     HSP010                             91
     C     FSPPCD        CHAIN     HSP011                             91
     C     FSCRCD        CHAIN     HSP012                             91
     C     FSGTCD        CHAIN     HSP025                             91
     C     FSTFCD        CHAIN     HSP101                             91
      *
     C     FSPMGCD       CHAIN     HSP016                             91
     C                   if        *in91 = *off
     C                   move      mgmgnm        hfpmgnm
     C                   endif
      *
     C     FSCMGCD       CHAIN     HSP016                             91
     C                   if        *in91 = *off
     C                   move      mgmgnm        hfcmgnm
     C                   endif
      *
     C     FSMMGCD       CHAIN     HSP016                             91
     C                   if        *in91 = *off
     C                   move      mgmgnm        hfmmgnm
     C                   endif
      *
     C     FSFMBO        CHAIN     HSP009                             91
     C                   MOVEL     BOBODS        WKFMDS
     C     K3P013        CHAIN     HSP013                             91
      *
      * RETRIEVE J.D. EDWARDS NAME
      *
     C     FSFAJD        IFNE      0
     C                   Z-ADD     FSFAJD        INAJD
     C                   MOVEL(P)  'F'           INAT1
     C                   EXSR      $JDEV
     C                   MOVEL     OUALPH        WKFJNM
     C                   ENDIF
      *
      * RETRIEVE PRODUCER JDE NUMBER NAME FIELD
      *
     C     FSPAJD        IFNE      0
     C                   Z-ADD     FSPAJD        INAJD
     C                   MOVEL(P)  'P'           INAT1
     C                   EXSR      $JDEV
     C                   MOVEL     OUALPH        WKPJNM
     C                   ENDIF
      *
      * RETRIEVE COST CENTER JDE DESCRIPTION FIELD
      *
     C     FSCJD         IFNE      *BLANK
     C                   MOVEL     FSCJD         INCJD
     C                   EXSR      $JDEC
     C                   MOVEL     OUCCDS        WKCCDS
     C                   ENDIF
      *
      * CALCULATE 'HOG CAPACITY' AFTER CALLING THE GENERIC PROGRAM TO
      * CALCULATE TOTAL SQUARE FEET FROM ACTIVE ROOMS ON THE FARM.
      *
     C                   call      'HP8003'
     C                   parm                    fsfscd
     C                   parm      0             hffssqft
      *
     C                   IF        FSHDSQFT <> 0
     C     HFFSSQFT      DIV(H)    FSHDSQFT      HFCPHD
     C                   ENDIF
      *
      * PRINT FARM SITE HEADER LINE
      *
     C                   EXCEPT    HFHDR
     C                   MOVEL     YES           PRTFLG
      *
      *----------------------------------------------------------------
      * PRINT BINS CONFIGURATION
      *----------------------------------------------------------------
      *
     C                   EXCEPT    DTBSET
      *
      * RETRIEVE BINS FOR THE FARM AND LOAD INTO ARRAY FOR PRINTING.
      * PRINT AND CLEAR ARRAY WHEN 12 BINS HAVE BEEN LOADED.
      *
     C     FSFSCD        SETLL     HSP036
     C                   MOVE      YES           FIRSTBIN          1
      *
     C     *IN98         DOWEQ     *OFF                                         DO BINS
     C     FSFSCD        READE     HSP036                                 98
     C     *IN98         IFEQ      *OFF                                         IF BIN
      *
     C                   IF        FIRSTBIN = YES
     C                   MOVE      BNBICD        SVBICD
     C                   MOVE      NO            FIRSTBIN
     C                   ENDIF
      *
     C                   IF        SVBICD <> BNBICD
     C                   EXSR      $PRINTBIN
     C                   MOVE      BNBICD        SVBICD
     C                   ENDIF
      *
     C                   ADD       1             X                 2 0
     C     X             IFEQ      13
     C                   EXSR      $PRINTBIN
     C                   Z-ADD     1             X
     C                   ENDIF
     C                   MOVEL     BNBNCD        BN(X)
     C                   ENDIF                                                  IF BIN
     C                   ENDDO                                                  DO BINS
      *
      * Print out last bin line
      *
     C                   IF        FIRSTBIN = NO
     C                   EXSR      $PRINTBIN
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * PRINT FARM SITE BUILDINGS, ROOMS, AND EQUIPMENT
      *----------------------------------------------------------------
      *
      * SETON NEW HEADING'S INDICATORS AND PRINT FIRST TIME
      *
     C                   SETON                                        1011
     C                   EXCEPT
     C                   SETOFF                                       10
      *
      * RETRIEVE FARM SITE BUILDINGS
      *
     C     FSFSCD        SETLL     HSP019
     C     *IN94         DOWEQ     *OFF                                         DO BUILDING
     C     FSFSCD        READE     HSP019                                 94
     C     *IN94         IFEQ      *OFF                                         IF BUILDINGS
     C                   EXCEPT    DTBLDG
      *
      * RETRIEVE FARM SITE BUILDING'S ROOMS
      *
     C                   SETOFF                                       95
     C     K1P020        SETLL     HSP020
      *
     C     *IN95         DOWEQ     *OFF                                         DO ROOMS
     C     K1P020        READE     HSP020                                 95
     C     *IN95         IFEQ      *OFF                                         IF ROOM
      *
      * Retrieve the Bin Sets assigned to this room.
      *
     C                   exsr      $rtv113
      *
     C                   EXCEPT    DTROOM
      *
      * RETRIEVE FARM SITE BUILDING'S ROOM'S EQUIPMENT
      *
     C                   SETOFF                                       96
     C     K2P023        SETLL     HSP023
      *
     C     *IN96         DOWEQ     '0'                                          DO EQUIP
     C     K2P023        READE     HSP023                                 96
     C     *IN96         IFEQ      *OFF                                         IF EQUIP
     C                   EXCEPT    DTEQUP
     C                   ENDIF                                                  IF EQUIP
     C                   ENDDO                                                  DO EQUIP
      *
     C                   ENDIF                                                  IF ROOM
     C                   ENDDO                                                  DO ROOMS
      *
     C                   ENDIF                                                  IF BUILDING
     C                   ENDDO                                                  DO BUILDINGS
      *
     C                   ENDIF                                                  IF FARM
      *
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------------------------------
      * Retrieve Bin Sets for the Room
      *----------------------------------------------------------------------------------------
      *
      * Although the database relationship between Rooms and Bin Sets is 1-to-many and
      * the Hog Group System code will handle the 1-to-many relationship, we are
      * arbitrarily limiting the user to the entry of 2 Bin Sets per Room.
      *
      * Therefore, retrieve 2 Room/Bin Set records for the Room.
      *
     C     $rtv113       begsr
      *
     C                   move      *blank        dtbicd1
     C                   move      *blank        dtbicd2
      *
     C     key04         chain     hsp113                             92
     C                   if        *in92 = *off                                 If hit
     C                   move      rbbicd        dtbicd1
      *
     C     key04         reade     hsp113                                 92
     C                   if        *in92 = *off                                 If another rec
     C                   move      rbbicd        dtbicd2
     C                   endif                                                  If another rec
     C                   endif                                                  If hit
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * Print bin line
      *----------------------------------------------------------------
      *
     C     $PRINTBIN     BEGSR
      *
     C                   EXCEPT    DTBINS
     C                   MOVE      *BLANK        BN
     C                   Z-ADD     0             X
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEC - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         COST CENTERS.
      *----------------------------------------------------------------
      *
     C     $JDEC         BEGSR
      *
     C                   MOVEL     NO            JDEFL
      *
     C                   CALL      'HPJDEC'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INCJD                          COST CENTER
     C                   PARM                    BOCOCD                         COMPANY
     C                   PARM      *BLANK        OUCCDS                         C.C. DESCR
     C                   PARM                    JDEFL                          ERROR FLAG
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LIST
      *
     C     K1P020        KLIST
     C                   KFLD                    FSFSCD
     C                   KFLD                    FBBLCD
      *
      *
     C     K2P023        KLIST
     C                   KFLD                    FSFSCD
     C                   KFLD                    FBBLCD
     C                   KFLD                    BRRMCD
      *
     C     K3P013        KLIST
     C                   KFLD                    FSFMBO
     C                   KFLD                    FSDZCD
      *
     C     key04         klist
     C                   kfld                    brfscd
     C                   kfld                    brblcd
     C                   kfld                    brrmcd
      *
      *
     C                   MOVE      *BLANK        WKFJNM           30
     C     *LIKE         DEFINE    BNBICD        SVBICD
     C     *LIKE         DEFINE    BOBODS        WKCCDS
     C     *LIKE         DEFINE    BOBODS        WKPJNM
     C     *LIKE         DEFINE    BOBODS        WKFSDS
     C     *LIKE         DEFINE    BOBODS        WKFMDS
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    FSFAJD        INAJD                          JDE ADDR IN
     C     *LIKE         DEFINE    BOBODS        OUCCDS                         COST CNTR DESC
     C     *LIKE         DEFINE    FSCJD         INCJD                          COST CENTER SC
      *
      *
      * Print fields
      *
     C                   z-add     0             HFFSSQFT          7 0
     C     *LIKE         DEFINE    MGMGNM        HFPMGNM
     C     *LIKE         DEFINE    MGMGNM        HFCMGNM
     C     *LIKE         DEFINE    MGMGNM        HFMMGNM
     C                   z-add     0             hfcphd            5 0
     C     *LIKE         DEFINE    RBBICD        DTBICD1
     C     *LIKE         DEFINE    RBBICD        DTBICD2
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH            100
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    H    OF                     1 01
     O                       SDPGM               10
     O                                           58 'HOG PRODUCTION SYSTEM'
     O                                           90 'DATE'
     O                       UDATE         Y    100
      *
     O          H    OF                     1
     O                       SDUSR               10
     O                                           60 'FARM SITE DETAIL LISTING'
     O                                           90 'TIME'
     O                       RTIME              100 '  :  :  '
      *
     O          H    OF                     1
     O                                           90 'PAGE'
     O                       PAGE          Z    100
      *
      * OVERFLOW HEADINGS FOR BUILDING CONFIGURATIONS
      *
     O          E    OF 11               1  1
     O                                           11 '(CONTINUED)'
     O          E    OF 11               1  1
     O                                           10 'FARM SITE '
     O                                           27 '.................'
     O                       FSFSCD        M     35
     O                       FSFSNM              68
      *
      * OF AND NEW HEADINGS FOR BUILDING CONFIGURATIONS
      *
     O          E    10                  2
     O         OR    OF 11
     O                                           54 'TOTAL'
     O                                           65 'NUMBER'
     O                                           75 'TOTAL'
      *
     O          E    10                  1
     O         OR    OF 11
     O                                           54 'SQUARE'
     O                                           65 'OF'
     O                                           75 'INCHES'
      *
      *
     O          E    10                  1
     O         OR    OF 11
     O                                           54 'FEET'
     O                                           65 'WATER'
     O                                           75 'FEEDER'
      *
     O          E    10                  1
     O         OR    OF 11
     O                                            4 'BLDG'
     O                                           27 'BUILDING DESCRIPTION'
     O                                           33 'ROOM'
     O                                           41 'STATUS'
      *
     O                                           54 'IN ROOM'
     O                                           65 'NIPPLES'
     O                                           75 'SPACE'
      *
     O                                           93 '-- BIN SETS --'
     O                                          100 'EQUIP'
     O          E    10                  1  1
     O         OR    OF 11
     O                       DASH               100
      *
      *-------------------------------------------------------------
      * FARM SITE HEADER FIELDS AND DESCRIPTIONS
      *-------------------------------------------------------------
      *
     O          EF           HFHDR       1
     O                                           16 'BUSINESS OFFICE '
     O                                           27 '...........'
     O                       FSFSBO              34
     O                       WKFSDS              73
      *
     O          E            HFHDR       1
     O                                           10 'FARM SITE '
     O                                           27 '.................'
     O                       FSFSCD        M     35
     O                       FSFSNM              68
      *
     O          E            hfhdr       1
     O                                           16 'COUNTY .........'
     O                                           27 '...........'
     O                       fscnty              54
      *
     O          E            hfhdr       1
     O                                           16 'STATE ..........'
     O                                           27 '...........'
     O                       fssacd              32
      *
      *
     O          E            HFHDR       1
     O                                           16 'PRODUCTION TYPE '
     O                                           27 '...........'
     O                       FSPTCD              34
     O                       PTPTDS              53
      *
     O          E            HFHDR       1
     O                                           17 'PRODUCTION PHASE '
     O                                           27 '..........'
     O                       FSPPCD              34
     O                       PPPPDS              73
      *
     O          E            HFHDR       1
     O                                           17 'GROUP TYPE ......'
     O                                           27 '..........'
     O                       FSGTCD              30
     O                       GTGTDS              58
      *
     O          E            HFHDR       1
     O                                           17 'TYPE OF FARM ....'
     O                                           27 '..........'
     O                       FSTFCD              34
     O                       TFTFDS              68
      *
     O          E            HFHDR       1
     O                                           21 'CELL ................'
     O                                           27 '......'
     O                       FSCELL        M     32
      *
      *
     O          E            HFHDR       1
     O                                           21 'POD .................'
     O                                           27 '......'
     O                       FSPOD         M     32
      *
      *
     O          E            HFHDR       1
     O                                           21 'CONSOLIDATED FARM ...'
     O                                           27 '......'
     O                       FSCNFSCD      M     32
      *
     O          E            HFHDR       1
     O                                           16 'G/L COST CENTER '
     O                                           27 '...........'
     O                       FSCJD               41
     O                       WKCCDS              73
      *
     O          E            HFHDR       1
     O                                           21 'COMPANY RELATIONSHIP '
     O                                           27 '......'
     O                       FSCRCD              34
     O                       CRCRDS              53
      *
     O          E            HFHDR       1
     O                                           18 'FEED MILL BUSINESS'
     O                                           27 'OFFICE .'
     O                       FSFMBO              34
     O                       WKFMDS              73
      *
     O          E            HFHDR       1
     O                                           19 'FEED DELIVERY ZONE '
     O                                           27 '........'
     O                       FSDZCD              34
     O                       DZDZDS              63
      *
     O          E            HFHDR       1
     O                                           21 'FARM SITE JDE NUMBER '
     O                                           27 '......'
     O                       FSFAJD        Z     37
     O                       WKFJNM              73
      *
     O          E            HFHDR       1
     O                                           20 'PRODUCER JDE NUMBER '
     O                                           27 '.......'
     O                       FSPAJD        Z     37
     O                       WKPJNM              73
      *
     O          E            HFHDR       1
     O                                           21 'SQUARE FEET FOR FARM '
     O                                           27 '......'
     O                       HFFSSQFT      k     42
      *
      *
     O          E            HFHDR       1
     O                                           21 'TARGET SQUARE FEET PE'
     O                                           27 'R PIG.'
     O                       FSHDSQFT      k     42
      *
     O          E            HFHDR       1
     O                                           21 'HOG CAPACITY ........'
     O                                           27 '......'
     O                       HFCPHD        kB    42
      *
     O          E            HFHDR       1
     O                                           12 'PRODUCTION M'
     O                                           27 'ANAGER ........'
     O                       FSPMGCD             34
     O                       HFPMGNM             68
      *
     O          E            HFHDR       1
     O                                           12 'CELL MANAGER'
     O                                           27 ' ..............'
     O                       FSCMGCD             34
     O                       hfcmgnm             68
      *
     O          E            HFHDR       1
     O                                           12 'MULTISITE MA'
     O                                           27 'NGER ..........'
     O                       FSMMGCD             34
     O                       hfmmgnm             68
      *
      *
      *-------------------------------------------------------------
      * FARM SITE BIN SET AND BIN CONFIGURATION
      *-------------------------------------------------------------
      *
     O          EF           DTBSET      2
     O                                            7 'BIN SET'
     O                                           13 'BINS'
     O          E            DTBSET      1
     O                       DASH               100
     O                                            8 ' '
     O          E            DTBINS      1
     O                       SVBICD         B     6
P405 O**                     BN(1)               13
P405 O                       BN(1)               14
P405 O**                     BN(2)               19
P405 O                       BN(2)               21
P405 O**                     BN(3)               25
P405 O                       BN(3)               28
P405 O**                     BN(4)               31
P405 O                       BN(4)               35
P405 O**                     BN(5)               37
P405 O                       BN(5)               42
P405 O**                     BN(6)               43
P405 O                       BN(6)               49
P405 O**                     BN(7)               49
P405 O                       BN(7)               56
P405 O**                     BN(8)               55
P405 O                       BN(8)               63
P405 O**                     BN(9)               61
P405 O                       BN(9)               70
P405 O**                     BN(10)              67
P405 O                       BN(10)              77
P405 O**                     BN(11)              73
P405 O                       BN(11)              84
P405 O**                     BN(12)              79
P405 O                       BN(12)              91
      *-------------------------------------------------------------
      * FARM SITE BUILDINGS, ROOMS, AND EQUIPMENT
      *-------------------------------------------------------------
     O          EF           DTBLDG      1
     O                       FBBLCD               5
     O                       FBBLDS              27
      *
     O          EF           DTROOM      1
     O                       BRRMCD              34
     O                       BRRMST              38
     O                       BRRMSQFT      k     55
     O                       BRWANINO      k     66
     O                       BRFDSPIN      k     76
     O                       DTBICD1        b    86
     O                       DTBICD2        b    93
      *
     O          EF           DTEQUP      1
     O                       IEETCD             100
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          EF           NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
