      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6718
      * TITLE:       Pick List
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/17/04
      *
      *            This program is submitted from HP4718-Work With Requests.
      *
      *            Originally written to be used as a Pick List, in July 2004, it
      *            was opened up to allow printing of all orders and became, in
      *            effect, a Work Order Listing--as well as a Pick List.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      * 02/08/05  LeAnne Fedor
      *           Changed to sort by Stock Location.
      *
      * 02/15/05  LeAnne Fedor
      *           Changed layout:
      *               Moved Requested Quantity to be the last column.
      *               Shortened Item Number from 25 to 6 to make room for new fields.
      *               Added a 'Complete' column with a blank for them to initial.
      *               Added a 'Back Ordered' column with a blank for them to write in.
      *
      * 03/07/05  LeAnne Fedor
      *           Users had us pad the description with dots (ie: text......)
      *
      * 03/21/05  LeAnne Fedor
      *           Users had us change Column headings over the "fill-in-the-blank" columns.
      *
      * 03/23/05  LeAnne Fedor
      *           We had to change the position of the Request Number in the LDA to
      *           accommodate the new Work With function that E-Mails the On Hold Report
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
      *
     Fhsj181e   if   e           k disk
      *    Item Master + Request detail
      *    (Open query used to select specific Request)
      *    (Logical file omits Request Detail records with status of D and E)
      *
      *
     Fhsp194    if   e           k disk
      *    Request status codes
      *
      *
     Fhsp192    if   e           k disk
      *    Request header
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               d   datfmt(*iso)
      *
      *
      * Workfields
      *
     D nonblank        s              2  0
     D wkdots          s             50    inz(*all'.')
      *
      *
      *
      *
      * Print fields
      *
     D h1fscd          s                   like(fsfscd)
     D h1fsnm          s                   like(fsfsnm)
     D h1wo            s                   like(qhwo)
     D h1com           s                   like(qhcom)
     D h1rqsds         s                   like(rqrqsds)
     D h1mdy           s              6  0
      *
     D l1itcd          s              6
     D l1itds          s                   like(imitds)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldrqsn               142    148  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Print headings.
      *
     C                   except    h1hdr
      *
      * Read all records in the Item Master/Request Detail join file.
      *    (Open query used to select the specific Request)
      *    (Logical file omits Request Detail records with status of D and E)
      *
     C     *loval        setll     hsj181e
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsj181e                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * If line is 'on hold' set an indicator to print "HOLD'.
      *
     C                   if        qdrqscd = 'H'
     C                   seton                                        97
     C                   else
     C                   setoff                                       97
     C                   endif
      *
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   movel(p)  qditcd        l1itcd
      *
     C                   move      wkdots        l1itds
     C     ' '           checkr    imitds        nonblank
     C                   eval      %subst(l1itds:1:nonblank) =
     C                             %subst(imitds:1:nonblank)
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------
      * EOF processing
      *---------------------
      *
      * If no records met the user's selections, print the standard 'no data...'
      * message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------------------
      * Initialization subroutine
      *-------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Retrieve Request Header
      *
     C     ldrqsn        chain     hsp192                             92
     C                   if        *in92 = *off                                 If header
     C                   z-add     qhfscd        h1fscd
     C                   move      qhwo          h1wo
     C                   move      qhcom         h1com
      *
      * Flip the date from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 qhrqdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      qhrqdt        wkcymdiso
     C     *mdy          move      wkcymdiso     h1mdy
     C                   endif                                                  If OK date
      *
      *
      * Retrieve Request Status description
      *
     C     qhrqscd       chain     hsp194                             92
     C                   if        *in92 = *off
     C                   move      rqrqsds       h1rqsds
     C                   endif
      *
      * Retrieve Farm Site name
      *
     C     qhfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fsfsnm        h1fsnm
     C                   endif
      *
     C                   endif                                                  If header
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           81 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           75 'PICK LIST'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          1
     O                                           18 'Work order number:'
     O                       h1wo                30
      *
      *
     O          e            h1hdr          1
     O                                           18 'Request status ..:'
     O                       h1rqsds             29
      *
      *
     O          e            h1hdr          1
     O                                           18 'Request date ....:'
     O                       h1mdy               28 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           18 'Farm site .......:'
     O                       h1fscd        z     25
     O                       h1fsnm              55
      *
      *
     O          e            h1hdr          1
     O                                           18 'Comment .........:'
     O                       h1com               70
      *
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                            5 'Stock'
     O                                           21 'Item'
     O                                           90 'Approved'
     O                                          100 'Quantity'
     O                                          113 'Quantity'
     O                                          125 'Completed'
      *
     O          e            h1hdr       0  1
     O                                            8 'Location'
     O                                           23 'Number'
     O                                           36 'Description'
     O                                           90 'Quantity'
     O                                          100 'Pulled'
     O                                          113 'Backordered'
     O                                          125 'Backorders'
      *
     O          e            h1hdr       0  2
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       imstkloc            15
     O                       l1itcd         b    23
     O                       l1itds              75
     O                       qdapqt        k     91
     O                                          100 '________'
     O                                          113 '___________'
     O                                          125 '__________'
     O               97                         132 '**Hold'
      *
      *
     O          e            l1dtl          1
     O                       qdcom               26
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
