      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3054-Build Workfiles: Average Weekly Inventory Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     08/06/02
      *
      * Function:    The function builds two workfiles:
      *              1) Farm workfile--1 record per farm
      *              2) Consolidated farm workfile--1 record per consolidated farm (This
      *                 file is, basically, a 'total' file of the associated farm records.)
      *
      *              This program is called from HP4054-Specify Options for Average Weekly
      *              Inventory Report.
      *
      *              This report is required by the same users of the ERM system who
      *              currently have us extracting weekly average inventory from the Datamart
      *              files.  To make sure they compare apples-to-apples if/when they compare
      *              the downloaded data to this report, we will derive our report values using
      *              the same Datamart files/logic.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/30/04  LeAnne Fedor
      *           Recompile only.
      *
      * 10/27/04  LeAnne Fedor
      *           Recompile only. New field "3-week average dead" added to file
      *           Datamart FIN: Weekly Group Detail.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart BGF: Weekly Production file.
      *
      * 02/07/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *                    Added: Transferred In Open Gilts
      *                  Removed: Total Lactation Pounds
      *
      * 07/05/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *           Added 1 new field: Average Daily Beginning Piglet Inventory
      *
      * 08/23/05  LeAnne Fedor
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Man hours
      *
      * 03/23/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *
      * 04/24/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *                 Cognos Report Grouping
      *                 Weaning Cognos Report Grouping
      *                 Report PCP Cognos Report Grouping
      *                 Report Farrowed Cognos Report Grouping
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Total Gilt Weight @ 28 Weeks
      *
      * 09/19/07  LeAnne Ramsey
      *           Recompile only. New "target" fields added to the Datamart FIN: Weekly
      *           Group Detail file.
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields deleted from Weekly Production file.
      *             BPPSGLSV - Nbr Gilts Bred w/Passed Date
      *             BPWSLB   - Weekly Ending Lbs Supplement Used
      *             BPP2LT   - Total P2 Litters Farrowed
      *             BPTDGB   - Total Days Gilt Breeding Age
      *
      * 11/07/08  LeAnne Ramsey
      *           Recompile only:
      *           Added 3 new fields to HPPB092-Datamart BGF: Weekly Production
      *            1) Target open gilt pool eligible
      *            2) Target litters farrowed/mated female/year
      *            3) Target pigs weaned/mated female/year
      *
      * 05/28/09  LeAnne Ramsey
      *           Recompile only:
      *           Added new field "Farrowing Crates" to:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 09/01/09  LeAnne Ramsey
      *           Recompile only. Added new field "Report Total Litters Farrowed" to:
      *                 HPPB092-Datamart BGF: Weekly Production
      *
      * 01/14/10  LeAnne Ramsey
      *           Recompile only. Changed text on BPLVHD and BPLVLB in:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 05/24/10  LeAnne Ramsey
      *           Recompile only. Added 2 fields to HPPF075-Weekly Group Detail:
      *              WBWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      *              WBPAJTIHD -Placement Adjusted Transfer In Head
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl018k   if   e           k disk
      *    Farm site
      *    (records selected in open query)
      *
      *
     Fhplf075e  if   e           k disk
      *    DataMart FIN: Weekly Group Detail
      *    (records selected in open query)
      *
      *
     Fhplb092b  if   e           k disk
      *    DataMart BGF: Weekly Production
      *    (records selected by open query)
      *
      *
     Fhsp3054   o    e           k disk
      *    Workfile: Average Weekly Inventory-Detail
      *
      *
     Fhsp3055   o    e           k disk
      *    Workfile: Average Weekly Inventory-Summary
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D firstdtl        s              1    inz('Y')
     D firstsum        s              1    inz('Y')
     D nbrofweeks      s              3  0
     D svfcymd         s                   like(bp80wedt)
     D svtcymd         s                   like(bp80wedt)
     D svcnfscd        s                   like(fscnfscd)
     D svfmdy          s              6  0
     D svtmdy          s              6  0
     D sv80wedt        s                   like(bp80wedt)
      *
      *
      * Workfields
      *
     D wkfld           s             15  0
     D wkhd            s             15  0
     D wktotinhd       s             15  0
     D wktotavhd       s              9  0
     D wktotfcymd      s              8  0
     D wktottcymd      s              8  0
      *
      *
      * Date manipulation fields
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Data areas
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfmdy                 1      6  0
     D  ldtmdy                 8     13  0
     D  ldfcymd               15     22  0
     D  ldtcymd               24     31  0
      *
     D  lddsfl                33     33
     D  lddsds                35     44
     D  ldcnfscd              46     50  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each farm in the farm site master.
      *
     C     *loval        setll     hsl018k
      *
     C                   dou       *in90 = *on                                  Do farms
     C                   read      hsl018k                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control break
     C                   select
     C                   when      firstsum = yes
     C                   z-add     fscnfscd      svcnfscd
     C                   move      no            firstsum
     C                   when      fscnfscd <> svcnfscd
     C                   exsr      $wrt3055
     C                   endsl
      *
      * Detail proccessing
     C                   z-add     0             wkhd
     C                   z-add     0             nbrofweeks
     C                   move      yes           firstdtl
      *
      * Process the farm as: a) BGF or b) finisher
      *
     C                   select
     C                   when      fsppcd = 'BGF  '
     C                   exsr      $bgf
     C                   other
     C                   exsr      $fin
     C                   endsl
      *
      * Write out the workfile record for the farm site
      *
     C                   if        wkhd <> 0
     C                   exsr      $wrt3054
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do farms
      *
      *
      * EOF processing
      *  1) Write out the workfile record for the last consolidated farm site
      *
     C                   exsr      $wrt3055
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Process Datamart Weekly Production Records for BGF farms
      *---------------------------------------------------------------
      *
     C     $bgf          begsr
      *
     C     fsfscd        setll     hplb092b
      *
     C                   dou       *in91 = *on                                  Do BGF loop
     C     fsfscd        reade     hplb092b                               91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Keep track of the unique 'number' of weeks that you process.
      * Since we expect only 1 group on BGF farms, a record count would
      * probably equal the 'week' count. But, to be safe, we will only
      * increment weeks when the week-ending date changes.
      *
     C                   select
     C                   when      firstdtl = yes
     C                   z-add     bp80wedt      svfcymd
     C                   z-add     bp80wedt      sv80wedt
     C                   add       1             nbrofweeks
     C                   move      no            firstdtl
      *
     C                   when      bp80wedt <> sv80wedt
     C                   add       1             nbrofweeks
     C                   z-add     bp80wedt      sv80wedt
     C                   endsl
      *
      * Save the week-ending date...just so you have the 'last' week-ending date to use
      * later for the 'to' date in the farm workfile record.
      *
     C                   z-add     bp80wedt      svtcymd
      *
      * Accumulate:
      *   1) gilt and sow ending inventory
      *   2) boar inventory
      *
     C                   add       bpengshd      wkhd
     C                   add       bpbohd        wkhd
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do BGF loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process Weekly Group Detail Records for Finishers
      *---------------------------------------------------------------
      *
     C     $fin          begsr
      *
     C     fsfscd        setll     hplf075e
      *
     C                   dou       *in91 = *on                                  Do finishers
     C     fsfscd        reade     hplf075e                               91
     C                   if        *in91 = *off                                 If not EOF
      *
      *
      * Keep track of the unique 'number' of weeks that you process.
      * Since finisher farms have many groups (each with a weekly record), we
      * cannot just 'count' records to arrive at 'weeks'. So, only
      * increment weeks when the week-ending date changes.
      *
     C                   select
     C                   when      firstdtl = yes
     C                   z-add     wb80wedt      svfcymd
     C                   z-add     wb80wedt      sv80wedt
     C                   add       1             nbrofweeks
     C                   move      no            firstdtl
      *
     C                   when      wb80wedt <> sv80wedt
     C                   add       1             nbrofweeks
     C                   z-add     wb80wedt      sv80wedt
     C                   endsl
      *
      *
      * Save the week-ending date...just so you have the 'last' week-ending date to use
      * later for the 'to' date in the farm workfile record.
      *
     C                   z-add     wb80wedt      svtcymd
      *
      * Accumulate:
      *   1) inventory head (You derive inventory head by dividing pig days by 7 days)
      *
     C     wbpigday      div(h)    7             wkfld
     C                   add       wkfld         wkhd
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do finishers
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write workfile records for farm  (ie: detail workfile)
      *---------------------------------------------------------------
      *
      * Note: This is NOT the 'consolidated' farm (ie: summary workflie record)
      *
     C     $wrt3054      begsr
      *
     C                   z-add     fsfscd        wdfscd
     C                   move      fsfsnm        wdfsnm
     C                   z-add     fscnfscd      wdcnfscd
     C                   move      fsppcd        wdppcd
      *
     C                   z-add     wkhd          wdinhd
     C                   z-add     nbrofweeks    wdwkno
      *
     C                   if        nbrofweeks <> 0
     C     wdinhd        div(h)    nbrofweeks    wdavhd
     C                   endif
      *
     C                   z-add     svfcymd       wdfcymd
     C                   z-add     svtcymd       wdtcymd
      *
      * Flip the dates from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 wdfcymd                92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wdfcymd       wkisodate
     C     *mdy          move      wkisodate     wdfmdy
     C                   endif                                                  If OK date
      *
     C     *iso          test(d)                 wdtcymd                92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wdtcymd       wkisodate
     C     *mdy          move      wkisodate     wdtmdy
     C                   endif                                                  If OK date
      *
      * Accumulate values for writing the 'consolidated' farm record later.
      *
     C                   add       wdinhd        wktotinhd
     C                   add       wdavhd        wktotavhd
      *
     C                   if        wdfcymd < wktotfcymd or wktotfcymd = 0
     C                   z-add     wdfcymd       wktotfcymd
     C                   z-add     wdfmdy        svfmdy
     C                   endif
      *
     C                   if        wdtcymd > wktottcymd
     C                   z-add     wdtcymd       wktottcymd
     C                   z-add     wdtmdy        svtmdy
     C                   endif
      *
      * Write detail record
      *
     C                   write     wdrec
     C                   clear                   wdrec
      *
     C                   z-add     0             nbrofweeks
     C                   z-add     0             wkhd
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write workfile record for consolidated farm
      *---------------------------------------------------------------
      *
     C     $wrt3055      begsr
      *
     C                   z-add     svcnfscd      wscnfscd
     C                   z-add     svfmdy        wsfmdy
     C                   z-add     svtmdy        wstmdy
     C                   z-add     wktotinhd     wsinhd
     C                   z-add     wktotavhd     wsavhd
      *
     C                   if        wktotinhd <> 0
     C                   write     wsrec
     C                   endif
      *
     C                   clear                   wsrec
      *
     C                   z-add     0             wktotinhd
     C                   z-add     0             wktotavhd
     C                   z-add     fscnfscd      svcnfscd
     C                   z-add     0             wktotfcymd
     C                   z-add     0             wktottcymd
     C                   z-add     0             svfmdy
     C                   z-add     0             svtmdy
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C                   endsr
      /eject
