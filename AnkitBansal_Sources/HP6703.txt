      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6703
      * TITLE:       Listing of Budget Items
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/11/04
      *
      *            This listing is submitted from HP4701-Work With Budget Items.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp180    if   e           k disk
      *    Item types
      *
      *
     Fhsl181c   if   e           k disk
      *    Items  (records selected in open query)
      *
      *
     Fqprint    o    f  123        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
     D upper           c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     D lower           c                   'abcdefghijklmnopqrstuvwxyz'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            123    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D procfl          s              1
      *
      *
      * Workfields
      *
     D wkrollbgit      s                   like(ldbgit)
     D wkdesc          s                   like(ldbgit)
     D wkbgit          s                   like(ldbgit)
     D nbrchar         s              2  0
      *
     D wkname          s                   like(l1desc)
     D wkbgitds        s                   like(l1desc)
     D long            s              2  0
      *
      *
      * Print fields
      *
     D h1ittyds        s                   like(tmittyds)
      *
     D l1desc          s                   like(imitds)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldreport               1      1
     D  ldbgit                 2     26
     D  lddesc                27     76
     D  ldittycd              77     82
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     *loval        setll     hsl181c
     C                   except    h1hdr
      *
      * Read all records in the file. (Some user selections were made
      * with an open query.)
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl181c                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             wkrollbgit <> imbgit
      *
     C                   move      yes           procfl
     C                   move      imbgit        wkrollbgit
      *
      * Populate Budget Item Description with something
      *
     C                   if        imitcd = imbgit
     C                   eval      l1desc = imitds
     C                   else
     C                   eval      l1desc = imbgit
     C                   endif
      *
      * Make a user selection on Budget Item.
      *
     C                   if        ldbgit <> *blank
     C     lower:upper   xlate     ldbgit        wkdesc
     C     lower:upper   xlate     imbgit        wkbgit
     C     wkdesc:nbrcharscan      wkbgit                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      *
      * Make a user selection on Budget Item Description.
      *
     C                   if        lddesc <> *blank
     C     lower:upper   xlate     lddesc        wkname
     C     lower:upper   xlate     l1desc        wkbgitds
     C     wkname:long   scan      wkbgitds                               92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      *
      * Print detail line
      *
     C                   if        procfl = yes                                 If process
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    l1dtl
     C                   movel     yes           prtfl
     C                   endif                                                  If process
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------------------------------------------------------------------
      * EOF processing
      *-----------------------------------------------------------------------------
      *
      * If no records met the user's selections, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------------------
      * Initialization subroutine
      *-------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *  None
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Item type code
      *
     C                   select
     C                   when      ldittycd = *blank
     C                   seton                                        81
     C                   other
     C     ldittycd      chain     hsp180                             92
     C                   if        *in92 = *off
     C                   eval      h1ittyds = tmittyds
     C                   else
     C                   eval      h1ittyds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Prepare to scan on Budget Item
      *  1) find first non-blank position from right
      *
     C                   if        ldbgit = *blank
     C                   seton                                        82
     C                   else
     C     ' '           checkr    ldbgit        nbrchar
     C                   endif
      *
      *
      * Prepare to scan on Budget Item Description
      *  1) find first non-blank position from right
      *
     C                   if        lddesc = *blank
     C                   seton                                        83
     C                   else
     C     ' '           checkr    lddesc        long
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           70 'HOG PRODUCTION SYSTEM'
     O                                          113 'DATE'
     O                       udate         y    123
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           71 'LISTING OF BUDGET ITEMS'
     O                                          113 'TIME'
     O                       rtime              123 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          113 'PAGE'
     O                       page          z    123
      *
      *
     O          e            h1hdr          1
     O                                           21 'Budget item contains:'
     O               82                          26 'All'
     O              n82      ldbgit              48
      *
      *
     O          e            h1hdr          1
     O                                           21 'Description contains:'
     O               83                          26 'All'
     O              n83      lddesc              73
      *
      *
     O          e            h1hdr       0  2
     O                                           21 'Item type code ......'
     O               81                          26 'All'
     O              n81      ldittycd            29
     O              n81      h1ittyds            61
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           68 'Item'
      *
      *
     O          e            h1hdr       0  1
     O                                           68 'Type'
     O                                           55 'Critical'
      *
     O          e            h1hdr       0  1
     O                                           11 'Budget Item'
     O                                           30 'UOM'
     O                                           40 'UOW'
     O                                           53 'Flag'
     O                                           68 'Code'
     O                                           96 'Budget Item Description'
      *
     O          e            h1hdr       0  2
     O                       dash               123
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
      *
     O          e            l1dtl          1
     O                       imbgit              25
     O                       imuom               35
     O                       imuow               42
     O                       imcritfl            52
     O                       imittycd            70
     O                       l1desc             123
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
