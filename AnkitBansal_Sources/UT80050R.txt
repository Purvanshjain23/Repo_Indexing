     F*
     F******************************************************************
     F*  REMARKS
     F*
     F*  CREATION - UNKNOWN.  COPIED FROM ARTICLE BY MEL BECKMAN IN
     F*                       THE NOVEMBER 1989 ISSUE OF NEWS 3X/400
     F*             COPIED WITH MODIFICATIONS BY GREG SOENDKER ON
     F*             12/01/89.
     F*
     F*  PROGRAM FUNCTION
     F*       THIS PROGRAM USES THE FOLLOWING PARAMETERS AND IBM'S
     F*       PROGRAM "QCLCNVDT" TO TEST FOR A VALID DATE AND REFORMAT
     F*       THE DATE IF REQUESTED.
     F*
     F*  PARAMETERS
     F*       PFMR8  - 9(8) - DATE TO BE CONVERTED
     F*       PTO8   - 9(8) - PASS CONVERTED DATE BACK
     F*       PFRFMT - X(4) - FROM DATE FORMAT
     F*                          MDY,DMY,YMD.CYMD,JUL
     F*       PTOFMT - X(4) - TO DATE FORMAT
     F*                          MDY,DMY,YMD.CYMD,JUL
     F*       PRTRN  - X    - RETURN CODE
     F*                          ' ' - VALID DATE
     F*                          '1' - DATE CANNOT BE ZERO
     F*                          '2' - INVALID DATE
     F*                          '3' - FROM FORMAT IS INVALID
     F*                          '4' - TO FORMAT IS INVALID
     F*
     F*  CALLED PROGRAMS
     F*      CLCNVDT - COPY OF QCLCNVDT
     F*                IBM'S COMMAND PROCESSOR FOR THE CL COMMAND CVTDAT
     F*
     F*  CHANGE LOG
     F********************************************************************
     F*  DATE      CHANGED BY           DESCRIPTION OF CHANGE
     F*
     F*
     F*
     F*
     F*
     F********************************************************************
      /EJECT
     E*
     E* LOOKUP TABLE OF DATA FORMAT SPECIAL VALUES FOR DERIVING QCLCNVDT CODES
     E*
     E                    TABFMT  1   5  4   TABFMN  1 0
      /EJECT
     I*
     I* DATA STRUCTURES FOR PASSING DATES WITH BINARY LENGTH PREFIXES
     I*
     IFRDATA      DS
     I                                    B   1   20FRDATL
     I                                        3  10 FRDATV
     ITODATA      DS
     I                                    B   1   20TODATL
     I                                        3  10 TODATV
     I*
     I* DATA STRUCTURE FOR CONVERTING NUMERIC FORMAT CODE TO BINARY
     I*
     I            DS
     I                                    B   1   20FRFMTV
     I                                    B   3   40TOFMTV
      /EJECT
     C           *ENTRY    PLIST
     C                     PARM           PFRM8   80
     C                     PARM           PTO8    80
     C                     PARM           PFRFMT  4
     C                     PARM           PTOFMT  4
     C                     PARM           PRTRN   1
     C*
     C* PERFORM INITIALIZATION
     C*
     C           INITED    IFNE 'Y'
     C                     EXSR INIT
     C                     END
     C* IF FORMAT IS CYMD THEN TEST FOR VALID CENTURY
     C           PFRFMT    IFEQ 'CYMD'
     C           PFRM8     IFGT 20110000
     C           PFRM8     ORLT 19110000
     C                     MOVE '2'       PRTRN
     C                     GOTO END
     C                     END
     C                     END
     C* FROM DATE CANNOT BE ZERO
     C           PFRM8     IFNE *ZEROS
     C                     MOVE *BLANKS   FRDATV
     C           PFRFMT    IFNE 'JUL'
     C                     Z-ADDPFRM8     FRM6    60
     C                     MOVELFRM6      FRDATV
     C                     Z-ADD6         FRDATL
     C                     ELSE
     C                     Z-ADDPFRM8     FRM5    50
     C                     MOVELFRM5      FRDATV
     C                     Z-ADD5         FRDATL
     C                     END
     C                     MOVE *BLANKS   TODATV
     C                     MOVE *BLANKS   PRTRN
     C*
     C* TRANSLATE FMT SPECIAL VALUES TO BINARY
     C*
     C           PFRFMT    LOKUPTABFMT    TABFMN         11
     C           *IN11     IFEQ '0'
     C                     MOVE '3'       PRTRN
     C                     ELSE
     C                     Z-ADDTABFMN    FRFMTV
     C           PTOFMT    LOKUPTABFMT    TABFMN         11
     C           *IN11     IFEQ '0'
     C                     MOVE '4'       PRTRN
     C                     ELSE
     C                     Z-ADDTABFMN    TOFMTV
     C                     END
     C                     END
     C*
     C*  IF PRTRN IS BLANK CALL IBM'S "QCLCNVDT" TO CONVERT DATE
     C*
     C           PRTRN     IFEQ ' '
     C                     MOVE '0'       *IN11
     C                     CALL 'CLCNVDT'              11
     C                     PARM           FRDATA
     C                     PARM           TODATA
     C                     PARM           FRFMTV
     C                     PARM           TOFMTV
     C                     PARM           TOSEPV
     C           *IN11     IFEQ '1'
     C                     MOVE '2'       PRTRN
     C                     ELSE
     C*
     C                     MOVE *ZEROS    PTO8
     C           PTOFMT    IFNE 'JUL'
     C                     MOVELTODATV    TO6     6
     C                     MOVE TO6       PTO8
     C                     ELSE
     C                     MOVELTODATV    TO5     5
     C                     MOVE TO5       PTO8
     C                     END
     C*  IF TO FORMAT IS CYMD ADD CENTURY TO DATE
     C           PTOFMT    IFEQ 'CYMD'
     C           PTO8      IFLT 110000
     C                     ADD  20000000  PTO8
     C                     ELSE
     C                     ADD  19000000  PTO8
     C                     END
     C                     END
     C                     END
     C                     END
     C                     ELSE
     C                     MOVE '1'       PRTRN
     C                     END
     C           END       TAG
     C                     RETRN
      /EJECT
     C********************************************************************
     C*  INITIALIZATION ROUTINE
     C********************************************************************
     C           INIT      BEGSR
     C                     MOVE 'Y'       INITED  1
     C                     Z-ADD8         TODATL
     C*  SET SEPARATOR TO *NONE, HEX VALUE OF '38'
     C                     MOVE ' '       TOSEPV  1
     C                     BITOF'01234567'TOSEPV
     C                     BITON'234'     TOSEPV
     C* IF SYSTEM YEAR IS 9 SEND MESSAGE TO OPERATOR THAT
     C* DATE PROGRAM NEEDS TO BE CHANGED.
     C*          UYEAR     IFEQ 9
     C*                    CALL 'UT80055J'
     C*                    END
     C                     ENDSR
** TABLE OF FORMAT SPECIAL VALUES AND EQUIVALENT NUMERIC CODES
MDY 2
DMY 3
YMD 4
CYMD4
JUL 5
