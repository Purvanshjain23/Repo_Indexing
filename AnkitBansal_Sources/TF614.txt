      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF614 - Margin: Missing Cost/Yield Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     09/25/06
      *
      * Function:    This report is generated from:
      *               1) TF Margin Adjustment Close      (No Byproducts)
      *               2) RO Meat Cost Weekly Calculation (ByProducts and No ByProducts)
      *               3) On Demand                       (ByProducts or No ByProducts)
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/18/06  LeAnne Ramsey
      *           Recompile only. The keys were changed on TFL010F.
      *
      * 11/13/06  LeAnne Ramsey
      *           Changed LDA positions.
      *
      * 02/14/07  LeAnne Ramsey
      *           Recompile only. New field "STD Other Cost/LB" was added to
      *           Weekly Product Revenue Detail file.
      *
      * 04/04/07  LeAnne Ramsey
      *           Added an edit on Packaging Cost.
      *
      * 04/16/07  LeAnne Ramsey
      *           We switched the following flag fields:
      *             1) Exclude from Mix Flag    is now Mix Flag
      *             2) Exclude from Volume Flag is now Volume Flag
      *           Before this change Y=Yes meant EXCLUDE the product from processing
      *           After this change  Y=Yes means INCLUDE the product in processing
      *
      * 05/21/07  LeAnne Ramsey
      *           Recompile only. "Co-owner" fields were added to file TFP010.
      *
      * 06/13/07  LeAnne Ramsey
      *           We added logic to run this report for:
      *             1) Byproducts or
      *             2) No Byproducts
      *           We will now exclude from all processing all products that are
      *           TF Classification Code = CVZ on the 'No Byproducts' report.
      *
      * 08/14/07  LeAnne Ramsey
      *           Recompile only. "Item Type Code" was added file TFP010.
      *           TFP010 will now include Work-in-Process items as well
      *           as Finished Goods items. We will include both WP and FG in this
      *           report.
      *
      * 10/26/07  Alice Brownfield
      *           Changed to NOT check PKG cost for "WP" items.
      *
      * 03/18/08  LeAnne Ramsey
      *           Recompile only. Synon file POAXCPP was recompiled.
      *
      * 06/12/08  LeAnne Ramsey
      *           We now have multiple records for a Week/Item in the Product Exception
      *           file. So, the "chain" logic had to be replaced with "loop" logic to
      *           process all records for Week/Item.
      *
      * 09/11/08  LeAnne Ramsey
      *           Recompile only. Export/Domestic Flag was added to TFP010-
      *           Weekly Product Revenue Detail file.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we change the LDA position of "report" from
      *           200 to 113.
      *
      * 02/03/09  LeAnne Ramsey
      *           We are including WPs on the new Weekly Yield Reports. Damon Ginther needed
      *           them on this Report; but they are not in the TFP010-Weekly Product Revenue
      *           file. The ones that Damon needs are in the Inventory Stock Closing file.
      *           So, we went to Workfile logic to bring all the Items together.
      *
      * 06/17/09  LeAnne Ramsey
      *           We are adding a new section at the end of the report for Consumed Component
      *           Items with Base Price of Zero.
      *
      * 02/17/10  LeAnne Ramsey
      *           Recompile only. Two new fields were added to the Resource Optimization Header:
      *                Cap&Blade Allocation % for Special Trim
      *                Cap&Blade Allocation % for Regular Trim
      *
      * 03/08/12  LeAnne Ramsey (E1984)
      *           Recompile only.
      *           Added 6 new Skirt Meat Allocation Flag fields to ROP100.
      *
      * 11/14/17  Danny Nguyen  (R12011A-Weekly Product Revenue)
      *           Recompile only. Added 25 STF fields to TFP010 file.
      *
      * 05/17/22  Danny Nguyen  (DO2484 - WI 479 STF Variance Reporting)
      *           Recompile only. Added 2 STF fields to TFP010 file.
      *           PRXYPC   - STF STD YIELD %
      *           PRXPMPPC - STF PUMP %
      *
      * W105621 RMC 10/10/2022 INCREASE FIELD SIZES TO HANDLE PRICES > 999.99
      *         CHANGE @@ANPRPR TO SIZE 10.6
      *           CHANGES PRANPRPR IN TFP010
      *
      /eject
      *********************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fombyrel1  if   e           k disk
      *  Item default
      *
      *
     Frol100a   if   e           k disk
      *   Resource optimization header
      *
      *
     Frop102    if   e           k disk
      *  Component item meat cost (records selected/keyed in open query)
      *
      *
     frop103    if   e           k disk
      *  Item meat cost
      *
      *
     Ftfl010a   if   e           k disk
      *   Weekly product revenue detail
      *
      *
     Ftfp314    if   e           k disk
      *  Workfile: Missing Cost/Yield Report
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
     D dash            s            167    inz(*all'-')
      *
      *
      * Control fields
      *
     D firstmiss       s              1    inz('Y')
     D firstzero       s              1    inz('Y')
     D svprcd          s                   like(w1prcd)
     D svciitcd        s                   like(cmciitcd)
      *
      *
      * Print fields
      *
     D h1rptds         s             13
     D h1title         s             50
      *
     D l2ciitds        s                   like(byavna)
     D l2itds          s                   like(byavna)
      *
     D l3msg           s             50
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwesyn               37     43  0
     D  ldwemdy               44     49  0
      *
     D  ldrpt                113    113
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * SECITON 1: Print Missing Costs/Yields
      * Note: If no Weekly Revenue Close has been run for the week, there is no point
      *       in executing this subroutine. So, check first for a Weekly Revenue record.
      *
     C                   seton                                        88
     C     ldwedt        chain     tfl010a                            92
     C                   if        *in92 = *off
     C                   exsr      $missing
     C                   else
      *
     C                   eval      l3msg = 'No Revenue Close has been run +
     C                                       yet for this Week.'
     C                   exsr      $l3dtl
     C                   endif
     C                   setoff                                       88
      *
      *
      * SECTION 2: Print the 'Consumed Component Items having a Base Price of Zero'.
      * Note: If no Meat Costing has been run for the week, there is no point in
      *       executing this subroutine. So, check first for a Weekly Meat Cost record.
      *
     C                   seton                                        89
     C     ldwedt        chain     rol100a                            92
     C                   if        *in92 = *off
     C                   exsr      $zero
     C                   else
      *
     C                   eval      l3msg = 'Meat Costing has not yet been +
     C                                       run for this Week.'
     C                   exsr      $l3dtl
     C                   endif
      *
      * EOF processing
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print Missing Costs/Yields Section
      *---------------------------------------------------------------
      *
      * Process all the Workfile records
      *
     C     $missing      begsr
      *
     C                   dou       *in91 = *on                                  Do missing
     C                   read      tfp314                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   if        firstmiss = yes
     C                   exsr      $h1hdr
     C                   move      no            firstmiss
     C                   endif
      *
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do missing
      *
      * If no errors
     C                   if        firstmiss = yes
     C                   eval      l3msg = 'No missing costs/yields.'
     C                   exsr      $l3dtl
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------
      * Print the Consumed Component Items Having a Base Price of Zero Section
      *-------------------------------------------------------------------------
      *
      * Process the Component Item Meat Cost records selected by the Open Query
      * for this WeekEnding Date:
      *   1) BOM Type is Consumed
      *   2) Item is NOT 99000-Cut Loss
      *   3) Component Item Base Price is zero
      *
     C     $zero         begsr
      *
     C                   dou       *in91 = *on                                  Do zero
     C                   read      rop102                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * We have stored the TF Class Group Code for each Item in the
      * Item Meat Cost record for the Week. So, retrieve that record and
      * continue if it corresponds to the version (ByProduct vs NonByProduct)
      * being generated.
      *
     C     key01         chain     rop103                             92
     C                   if        *in92 = *off and                             If process
     C                             ((ldrpt =  'B' and imtfcgcd =  'BP') or
     C                              (ldrpt <> 'B' and imtfcgcd <> 'BP'))
      *
     C                   if        firstzero = yes
     C                   exsr      $h1hdr
     C                   move      no            firstzero
     C                   endif
      *
      * Retrieve Component Item Description
      *
     C     cmciitcd      chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l2ciitds = byavna
     C                   else
     C                   eval      l2ciitds = 'Unknown'
     C                   endif
      *
      * Retrieve Item Description
      *
     C     cmitcd        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l2itds = byavna
     C                   else
     C                   eval      l2itds = 'Unknown'
     C                   endif
      *
     C                   exsr      $l2dtl
      *
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do zero
      *
      *
      * If no errors
     C                   if        firstzero = yes
     C                   eval      l3msg = 'There are no base prices of zero.'
     C                   exsr      $l3dtl
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
     C                   seton                                        87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print detail line for Missing Costs/Yield Section
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   if        w1prcd <> svprcd
     C                   z-add     w1prcd        svprcd
     C                   seton                                        87
     C                   endif
      *
     C                   except    l1dtl
     C                   setoff                                       87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print detail line for Zero Base Price Section
      *---------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   if        cmciitcd <> svciitcd
     C                   z-add     cmciitcd      svciitcd
     C                   seton                                        87
     C                   endif
      *
     C                   except    l2dtl
     C                   setoff                                       87
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Print No Data/No Error messages
      *-------------------------------------------------------------------------------------
      *
     C     $l3dtl        begsr
      *
     C                   exsr      $h1hdr
      *
     C                   except    l3dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    cmcono
     C                   kfld                    cmwedt
     C                   kfld                    cmitcd
      *
      * Set up heading stuff
      *
     C                   time                    rtime
      *
     C                   select
     C                   when      ldrpt = 'B'
     C                   eval      h1rptds = '  Byproducts '
      *
     C                   when      ldrpt = 'N'
     C                   eval      h1rptds = 'No Byproducts'
     C                   endsl
      *
     C                   eval      h1title = 'Consumed Component Items with a +
     C                                        Base Price of Zero'
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           90 'Missing Cost/Yield Report'
     O                                          157 'DATE'
     O                       udate         y    167
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1rptds             84
     O                                          157 'TIME'
     O                       rtime              167 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          157 'PAGE'
     O                       page          z    167
      *
      *
     O          e            h1hdr          1
     O                                           17 'Year/Week:'
     O                       ldyr          z     23
     O                       ldwk          z     27
      *
     O          e            h1hdr          2
     O                                           17 'Week-ending date:'
     O                       ldwemdy             27 '  /  /  '
     O               89      h1title            105
      *
      *--------------------------------------------------------------
      * Column headings for Missing Costs/Yield Section
      *--------------------------------------------------------------
      *
     O          e    88      h1hdr          1
     O                                            2 'TF'
     O                                           15 'Item'
     O                                           89 'Aggregate'
     O                                          104 'Aggregate'
      *
     O          e    88      h1hdr          1
     O                                            5 'Class'
     O                                           17 'Structure'
     O                                           24 'Item'
     O                                           35 'Product'
     O                                           89 'Sold'
     O                                          104 'Produced'
      *
     O          e    88      h1hdr          1
     O                                            4 'Code'
     O                                           17 'Type Code'
     O                                           24 'Type'
     O                                           35 'Code'
     O                                           50 'Description'
     O                                           89 'Pounds'
     O                                          104 'Pounds'
     O                                          114 'Message'
      *
      *--------------------------------------------------------------
      * Column headings for Base Price of Zero Section
      *--------------------------------------------------------------
      *
     O          e    89      h1hdr          1
     O                                           16 '--------------- '
     O                                           39 'Consumed Component Item'
     O                                           54 ' --------------'
      *
     O          e    89      h1hdr          1
     O                                            7 'Item'
     O                                           47 'BOM'
     O                                           54 'Item'
     O                                           62 'Item'
     O                                          103 'Item'
     O                                          114 'Consumed'
      *
     O          e    89      h1hdr          1
     O                                            7 'Code'
     O                                           27 'Item Description'
     O                                           47 'Type'
     O                                           54 'Type'
     O                                           62 'Code'
     O                                           82 'Item Description'
     O                                          103 'Type'
     O                                          114 'Percent'
      *
     O          e            h1hdr          0
     O                       dash               167
      *
      *-------------------------------------------------------------
      * Detail line for missing costs/yields section
      *-------------------------------------------------------------
      *
     O          e    87      l1dtl       1
      *
     O          e            l1dtl       1
     O               87      w1tfclcd             3
     O               87      w1istycd      z     14
     O               87      w1itycd             24
     O               87      w1prcd        z     35
     O               87      w1prds              69
     O               87      w1asllb       k     90
     O               87      w1apulb       k    105
     O                       w1msg              167
      *
      *-------------------------------------------------------------
      * Detail line for Zero Base Price Section
      *-------------------------------------------------------------
      *
      *
     O          e    87      l2dtl       1
      *
     O          e            l2dtl       1
     O               87      cmciitcd      z      7
     O               87      l2ciitds       b    41
     O               87      cmbomty             46
     O               87      cmciittycd          53
     O                       cmitcd        z     62
     O                       l2itds         b    96
     O                       cmittycd           102
     O                       cmcspc        k    115
      *
      *-------------------------------------------------------------
      * Message at the end of the listing
      *-------------------------------------------------------------
      *
     O          e            l3dtl       2
     O                       l3msg               50
      /eject
