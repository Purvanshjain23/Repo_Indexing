      *
      * ENVIRONMENT: PORK
      * SYSTEM:      ACCOUNT RECEIVABLE SYSTEM
      * PROGRAM:     AR310
      * TITLE:       FOREIGN CORPORATE TAX SAVINGS
      *              WORKFILES: YEARLY STATEMENT OF ACCOUNT REPORTS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/24/97
      *
      *  FUNCTION:   THIS PROGRAM BUILDS TWO WORKFILES THAT WILL BE
      *              USED TO PRINT THE YEARLY STATEMENT OF ACCOUNT REPORTS.
      *              THESE ARE USED IN DETERMINING THE TAX SAVINGS
      *              THAT CAN BE REALIZED ON EXPORT SALES.
      *
      *                  ARP310 - CUSTOMER DETAIL
      *                  ARP311 - CUSTOMER SUMMARY
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/23/98  BRUCE LAWSON - Database change to OMHSTPLP, OMHSTPLA
      *                          Recompile only
      *
      * 02/23/99  LEANNE FEDOR
      *           RECOMPILE ONLY. DATABASE CHANGES TO:
      *                OPBFCPP--PD ORDER HEADER
      *                OPBGWKP--PD ORDER DETAIL
      *                OMHSTPP--OM HISTORY
      *******************************************************************
      /EJECT
      *******************************************************************
      * FILE SPECIFICTIONS
      *******************************************************************
      *
     FARP310  O   E           K        DISK
      *  WORKFILE: CUSTOMER DETAIL
      *
     FARP311  UF  E           K        DISK                      A
      *  WORKFILE: CUSTOMER SUMMARY
      *
      *
     FCABBREL0IF  E           K        DISK
      *  Customer
      *
      *
     FOMHSTPLAIF  E           K        DISK
      *  Order Management History
      *  (RECORDS SELECTED IN CLP WITH OPEN QUERY)
      *
     FOMHSTPLPIF  E           K        DISK
      *  Order Management History
      *  (RECORDS SELECTED IN CLP WITH OPEN QUERY)
      *
      *******************************************************************
      * EXTENSION SPECIFICATIONS
      *******************************************************************
      *
      *******************************************************************
      * INPUT SPECS
      *******************************************************************
      *
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     I              'Y'                   C         YES
     I              'N'                   C         NO
      *
      *
      *----------------------------------------------------------------
      * LDA
      *----------------------------------------------------------------
      *
     I           UDS
     I                                        1   40LDCCYY
     I                                        5  110LD0YMD
     I                                       12  190LDCYMD
     I                                       20  250LDMDY
      *
      *---------------------------------------------------------------
      *  DATA STRUCTURES
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     IPGMDS     ESDSUTPGFR
      *
      *
      *---------------------------------------------------------------
      * FORMATS FOR FLIPPING DATES
      *---------------------------------------------------------------
      *
      * DATE IN CCYYMMDD FORMAT
      *
     I            DS
     I                                        1   80WKCYMD
     I                                        1   20WKCEN
     I                                        3   40WKYER
     I                                        5   60WKMON
     I                                        7   80WKDAY
      *
      *
      * DATE IN CYYMMDD FORMAT USED IN THE OMS SYSTEM
      *
     I            DS
     I                                        1   70WKOMDT
     I                                        1   10OMSC
     I                                        2   30OMSYY
     I                                        4   50OMSMM
     I                                        6   70OMSDD
      *
      /EJECT
      *******************************************************************
      * MAINLINE PROCESSING
      *******************************************************************
      *
      *
      * PROCESS EACH RECORD IN THE OMS HISTORY FILE. PLEASE
      * NOTE THAT AN OPEN QUERY HAS ALREADY BEEN RUN TO SELECT
      * OUT ONLY THOSE RECORDS WHERE:
      *      1) ACCOUNTING YEAR EQUALS SELECTED YEAR
      *      2) EXPORT STATUS EQUALS YES
      *      3) INVOICE SUFFIX EQUALS 'IN'
      *
      *
     C           *IN90     DOUEQ*ON                        MAIN DO LOOP
     C                     READ OMHSTPLA                 90
     C           *IN90     IFEQ *OFF                       IF NOT EOF
      *
     C           FIRST     IFEQ YES
     C                     Z-ADDF4ANC7    SVANC7
     C                     MOVELNO        FIRST
     C                     ENDIF
      *
      * BREAK PROCESSING ON A/R CUSTOMER NUMBER
      *
     C           SVANC7    IFNE F4ANC7
     C                     EXSR $CUST
     C                     ENDIF
      *
      * DETAIL PROCESSING OF EXPORT SALES
      *
     C                     ADD  F4KDVA    WKTOTE
     C                     Z-ADDF4KDVA    WFKDVA
      *
     C                     Z-ADDF4AIC3    WFAIC3
     C                     Z-ADDF4ANC7    WFANC7
     C                     Z-ADDF4CONB    WFCONB
     C                     MOVELF4U2ST    WFU2ST
     C                     MOVELF4G7NA    WFG7NA
      *
      * FLIP ACTUAL SHIP DATE FROM CYMD TO MMDDYY
      *
     C                     EXSR $TOMDY
      *
      * WRITE A RECORD TO THE WORKFILE USED TO PRINT THE
      * INDIVIDUAL CUSTOMER REPORT.
      *
     C                     WRITEWFREC
     C                     CLEARWFREC
      *
     C                     ENDIF                           IF NOT EOF
     C                     ENDDO                           MAIN DO LOOP
      *
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *
      * IF YOU PROCESSED ANY DATA:
      *    A) DO LAST RECORD PROCESSING FOR EXPORT SALES
      *    B) DO DOMESTIC SALE PROCESS FOR EACH CUSTOMER
      *       IN THE SUMMARY FILE.
      *
      *
     C           FIRST     IFEQ NO                         IF DATA
     C                     EXSR $CUST
     C                     EXSR $SUM
     C                     ENDIF                           IF DATA
      *
      *
     C                     SETON                     LR
      /EJECT
      *------------------------------------------------------------------
      * $ADDCC - ADD CENTURY TO A SYNON DATE
      *------------------------------------------------------------------
      *
     C           $ADDCC    BEGSR
      *
      * SYNON HOLDS DATES AS A 7,0 NUMERIC FIELD WHERE THE
      * LEADING DIGIT IS 0 OR 1.
      *             0 = 19TH CENTURY
      *             1 = 20TH CENTURY
      *
      * I WANT TO USE THE STANDARD DATE ROUTINE WHICH USES AN 8,0
      * FORMAT. SO, TURN THE 7,0 DATE FIELD INTO AN 8,0 DATE.
      *
     C                     Z-ADD0         WKCYMD
      *
     C                     SELEC
     C           OMSC      WHEQ 0
     C                     Z-ADD19        WKCEN
     C           OMSC      WHEQ 1
     C                     Z-ADD20        WKCEN
     C                     ENDSL
      *
     C                     Z-ADDOMSYY     WKYER
     C                     Z-ADDOMSMM     WKMON
     C                     Z-ADDOMSDD     WKDAY
      *
     C                     ENDSR
      /EJECT
      *-----------------------------------------------------------------
      * $CUST - CUSTOMER BREAK
      *-----------------------------------------------------------------
      *
     C           $CUST     BEGSR
      *
      * CLEAR CUSTOMER SUMMARY RECORD
      *
     C                     CLEARWSREC
      *
     C                     MOVE *BLANK    WKADD1
     C                     MOVE *BLANK    WKADD2
     C                     MOVE *BLANK    WKADD3
     C                     MOVE *BLANK    WKADD4
     C                     MOVE *BLANK    WKCITY
     C                     MOVE *BLANK    WKZIP
      *
      * RETRIEVE OUTSTANDING BALANCE FOR A/R CUSTOMER---THIS BALANCE
      * INCLUDES BOTH EXPORT AND DOMESTIC SALES.
      *
     C                     CALL 'ARCUSBAL'
     C                     PARM 0         XXAIC3
     C                     PARM LD0YMD    XXDT
     C                     PARM SVANC7    XXANC7
     C           WSBAL     PARM 0         XXBAL
      *
      * SET UP WORKFILE RECORD FIELDS FOR A/R CUSTOMER NUMBER
      * AND TOTAL EXPORT SALES DOLLARS
      *
     C                     Z-ADDSVANC7    WSANC7
     C                     Z-ADDWKTOTE    WSTOTE
      *
      * RETRIEVE CUSTOMER INFO FOR SAVED A/R CUSTOMER
      * AND SET UP A/R CUSTOMER NAME AND ADDRESS
      *
     C           SVANC7    CHAINCABBREL0             92
     C           *IN92     IFEQ *OFF                       IF HIT
      *
      * NAME
     C                     MOVELBBAYTX    WSAQTX
      * ADDRESS 1
     C                     MOVELBBACNA    WKADD1    P
      * ADDRESS 2
     C                     MOVELBBADNA    WKADD2    P
      * ADDRESS 3
     C                     MOVELBBAENA    WKADD3    P
      * CITY
     C                     MOVELBBAJNA    WSAJNA
      * STATE
     C                     MOVELBBBGCD    WSBGCD
      * ZIP
     C                     MOVELBBBTTX    WSBTTX
      * COUNTRY
     C                     MOVELBBKXCD    WSKXCD
      *
      *
      * CONCATENATE CITY/STATE
      *
     C                     EXSR $CITY
      *
      *
      * CONCATENATE CITY/STATE AND ZIP CODE
      * (CHECK ZIP CODE FOR A PERIOD BEFORE CONCATENATING)
      *
     C                     MOVELBBBTTX    WKFLD
     C                     SELEC
     C           WKFLD     WHEQ '.'
     C                     MOVELWKCITY    WKZIP
     C                     OTHER
     C           WKCITY    CAT  BBBTTX:1  WKZIP
     C                     ENDSL
      *
      * CONCATENATE CITY/STATE/ZIP AND COUNTRY
      *
     C           WKZIP     CAT  BBKXCD:1  WKADD4
     C                     ENDIF                           IF HIT
      *
      * FINALIZE THE FOUR ADDRESS LINES
      *
     C                     EXSR $ADDR
      *
      * WRITE THE CUSTOMER SUMMARY WORKFILE RECORD
      *
     C                     WRITEWSREC
      *
     C                     Z-ADDF4ANC7    SVANC7
     C                     Z-ADD0         WKTOTE
      *
     C                     ENDSR
      /EJECT
      *-----------------------------------------------------------------
      * $CITY - CONCATENATE CITY/STATE TOGETHER
      *-----------------------------------------------------------------
      *
     C           $CITY     BEGSR
      *
     C                     MOVE *BLANK    WKCITY
      * NOTES:
      *   1) FOREIGN CUSTOMERS DO NOT HAVE STATE; THE STATE
      *      FIELD WILL HOLD A '.'  SO, DO NOT CONCATENATE
      *      WHEN STATE IS '.')
      *   2) SOMETIMES CITY IS ALSO A '.'  SO, DO NOT CONCATENATE
      *      WHEN CITY IS '.')
      *
     C                     MOVELBBAJNA    WSAJNA
     C                     MOVELBBBGCD    WSBGCD
      *
      * CHECK FOR STATE
      *
     C           BBBGCD    IFEQ '. '
     C           BBBGCD    OREQ ' .'
     C           BBBGCD    OREQ *BLANK
     C                     MOVELNO        STFL
     C                     ELSE
     C                     MOVELYES       STFL
     C                     ENDIF
      *
      * CHECK FOR CITY
      *
     C                     MOVELBBAJNA    WKFLD
     C           BBAJNA    IFEQ *BLANK
     C           WKFLD     OREQ '.'
     C                     MOVELNO        CITYFL
     C                     ELSE
     C                     MOVELYES       CITYFL
     C                     ENDIF
      *
      *
     C                     SELEC
      *
      * NO CITY, NO STATE
      *
     C           CITYFL    WHEQ NO
     C           STFL      ANDEQNO
      *
      *
      * CITY AND STATE
      *
     C           CITYFL    WHEQ YES
     C           STFL      ANDEQYES
     C           BBAJNA    CAT  ',':0     WKCITY    P
     C                     CAT  BBBGCD:1  WKCITY
      *
      *
      * CITY WITHOUT STATE
      *
     C           CITYFL    WHEQ YES
     C           STFL      ANDEQNO
     C                     MOVELBBAJNA    WKCITY
      *
      *
      * STATE WITHOUT CITY
      *
     C           CITYFL    WHEQ NO
     C           STFL      ANDEQYES
     C                     MOVELBBBGCD    WKCITY
     C                     ENDSL
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $ADDR - FINALIZE 4 LINES OF ADDRESS
      *----------------------------------------------------------------
      *
     C           $ADDR     BEGSR
      *
      *
     C           WKADD1    IFNE *BLANK
     C                     MOVELWKADD1    WSADD1    P
     C                     ENDIF
      *
      *
     C           WKADD2    IFNE *BLANK
     C                     SELEC
     C           WSADD1    WHEQ *BLANK
     C                     MOVELWKADD2    WSADD1    P
     C                     OTHER
     C                     MOVELWKADD2    WSADD2    P
     C                     ENDSL
     C                     ENDIF
      *
      *
     C           WKADD3    IFNE *BLANK
     C                     SELEC
     C           WSADD1    WHEQ *BLANK
     C                     MOVELWKADD3    WSADD1    P
     C           WSADD2    WHEQ *BLANK
     C                     MOVELWKADD3    WSADD2    P
     C                     OTHER
     C                     MOVELWKADD3    WSADD3    P
     C                     ENDSL
     C                     ENDIF
      *
     C           WKADD4    IFNE *BLANK
     C                     SELEC
     C           WSADD1    WHEQ *BLANK
     C                     MOVELWKADD4    WSADD1    P
     C           WSADD2    WHEQ *BLANK
     C                     MOVELWKADD4    WSADD2    P
     C           WSADD3    WHEQ *BLANK
     C                     MOVELWKADD4    WSADD3    P
     C                     OTHER
     C                     MOVELWKADD4    WSADD4    P
     C                     ENDSL
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SUM - PROCESS CUSTOMERS IN SUMMARY FILE
      *----------------------------------------------------------------
      *
      * THIS SUBROUTINE WILL READ EACH CUSTOMER IN THE SUMMARY
      * FILE (NOTE: ONLY CUSTOMERS WITH EXPORT SALES WILL BE IN THE
      * SUMMARY FILE).  FOR EACH CUSTOMER, THE LOGIC WILL
      * RETRIEVE ALL THE DOMESTIC SALES FOR THE CUSTOMER AND WRITE
      * DETAIL WORKFILE RECORDS.  THEN, THE SUMMARY FILE
      * WILL BE UPDATED WITH THE TOTAL DOMESTIC SALES DOLLARS AND
      * THE TOTAL SALES DOLLARS (IE: DOMESTIC + EXPORT)
      *
      *
     C           $SUM      BEGSR
      *
     C           *LOVAL    SETLLARP311
      *
     C           *IN90     DOUEQ*ON                        DO SUMMARY
     C                     READ ARP311                   90
     C           *IN90     IFEQ *OFF                       IF NOT EOF
      *
      * WRITE DETAIL RECORDS FOR THE DOMESTIC SALES
      *
     C                     EXSR $DSALE
      *
      * UPDATE THE CUSTOMER SUMMARY FILE
      *
     C                     Z-ADDWKTOTD    WSTOTD
     C           WSTOTE    ADD  WSTOTD    WSTOT
     C                     UPDATWSREC
      *
     C                     ENDIF                           IF NOT EOF
     C                     ENDDO                           DO SUMMARY
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DSALE - RETRIEVE THE DOMESTIC SALES FOR THIS CUSTOMER
      *----------------------------------------------------------------
      *
     C           $DSALE    BEGSR
      *
     C                     Z-ADD0         WKTOTD
      *
     C           WSANC7    SETLLOMHSTPLP
      *
     C           *IN91     DOUEQ*ON                        DO DOMESTIC
     C           WSANC7    READEOMHSTPLP                 91
     C           *IN91     IFEQ *OFF                       IF NOT EOF
      *
      *
      * DETAIL PROCESSING OF DOMESTIC SALES
      *
     C                     ADD  F4KDVA    WKTOTD
     C                     Z-ADDF4KDVA    WFKDVA
      *
     C                     Z-ADDF4AIC3    WFAIC3
     C                     Z-ADDF4ANC7    WFANC7
     C                     Z-ADDF4CONB    WFCONB
     C                     MOVE *BLANK    WFU2ST
     C                     MOVELF4G7NA    WFG7NA
      *
      *
      * FLIP ACTUAL SHIP DATE FROM CYMD TO MMDDYY
      *
     C                     EXSR $TOMDY
      *
      * WRITE A RECORD TO THE WORKFILE USED TO PRINT THE
      * INDIVIDUAL CUSTOMER REPORT.
      *
     C                     WRITEWFREC
     C                     CLEARWFREC
      *
     C                     ENDIF                           IF NOT EOF
     C                     ENDDO                           DO DOMESTIC
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOMDY - FLIP ACTUAL SHIP DATE TO MMDDYY FORMAT
      *----------------------------------------------------------------
      *
     C           $TOMDY    BEGSR
      *
      * FLIP ACTUAL SHIP DATE FROM CYMD TO MMDDYY
      *   1) ADD CENTURY VALUES INSTEAD OF THE 0/1 INDICATOR
      *      USED IN SYNON, THEN
      *   2) USE STANDARD DATE ROUTINE TO FLIP TO MMDDYY
      *
     C                     Z-ADDF4GNDT    WFGNDT
     C                     Z-ADDF4GNDT    WKOMDT
     C                     EXSR $ADDCC
     C                     Z-ADDWKCYMD    PFRM8
     C                     Z-ADD0         PTO8
     C                     MOVEL'CYMD'    PFRFMT
     C                     MOVEL'MDY '    PTOFMT
     C                     MOVE *BLANK    PCODE
     C                     Z-ADD0         PDAYS
     C                     EXSR $DATE
     C           PRTRN     IFEQ *BLANK
     C                     Z-ADDPTO8      WFMDY
     C                     ELSE
     C                     Z-ADD0         WFMDY
     C                     ENDIF
      *
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C           $DATE     BEGSR
      *
     C                     MOVE *BLANK    PRTRN
     C                     CALL 'UT80060R'
     C                     PARM           PFRM8   80
     C                     PARM           PTO8    80
     C                     PARM           PFRFMT  4
     C                     PARM           PTOFMT  4
     C                     PARM           PDAYS   40
     C                     PARM           PCODE   1
     C                     PARM           PRTRN   1
      *
     C                     ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
      * PARMS
      *
      *
      * KEY LISTS
      *
      *
      * FIELDS TO CONTROL PROCESSING
      *
     C                     MOVELYES       FIRST   1
     C                     MOVE *BLANK    CITYFL  1
     C                     MOVE *BLANK    STFL    1
      *
      * SAVE FIELDS
      *
     C           *LIKE     DEFN F4ANC7    SVANC7
      *
      *
      * PARM FIELDS
      *
     C                     Z-ADD0         XXBAL  112
     C                     Z-ADD0         XXDT    70
     C           *LIKE     DEFN F4AIC3    XXAIC3
     C           *LIKE     DEFN F4ANC7    XXANC7
      *
      * WORKFIELDS
      *
     C                     Z-ADD0         WKTOTE 112
     C                     Z-ADD0         WKTOTD 112
     C                     MOVE *BLANK    WKFLD   1
      *
     C                     MOVE *BLANK    WKCITY 24
     C                     MOVE *BLANK    WKZIP  36
     C                     MOVE *BLANK    WKADD1 45
     C                     MOVE *BLANK    WKADD2 45
     C                     MOVE *BLANK    WKADD3 45
     C                     MOVE *BLANK    WKADD4 45
      *
     C                     ENDSR
      /EJECT
