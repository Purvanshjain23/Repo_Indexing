      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6011
      * TITLE:       Listing of Weekly Sire Line Doses
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/09/01
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4011-Work With Weekly Sire
      *            Line Doses.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl081a   if   e           k disk
      *    Weekly Sire Line Doses  (records selected by open query)
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D procfl          s              1    inz('Y')
     D contfl          s              1    inz('Y')
      *
      * Save fields
      *
     D svfscd          s                   like(fsfscd)
     D svfscd2         s                   like(fsfscd)
     D svcdyr2         s                   like(swcdyr)
     D svcdwk2         s                   like(swcdwk)
      *
      * Print fields
      *
     D h1fsnm          s                   like(fsfsnm)
     D h1yesno         s              3
     D l1mdywedt       s              6  0
     D l1cell          s                   like(fscell)
      *
      *
      * Work fields for date manipulation
      *
     D wkmdy           s               D   datfmt(*mdy)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldynfl                 1      1
     D  ldfscd                 2      6  0
     D  ldcdyr                 7     10  0
     D  ldcdwk                11     12  0
     D  ldcell                13     14  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
     C     *loval        setll     hsl081a
     C                   except    h1hdr
      *
      * Read all records in the file that were selected by the
      * open query.
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl081a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Default the 'continue' flag to YES.
      *
     C                   move      yes           contfl
      *
      * If the user selected a single cell and this farm is in that cell,
      * then continue.
      *
     C                   if        ldcell <> 0                                  If 1 cell
     C     swfscd        chain     hsp018                             92
     C                   if        *in92 = *on or fscell <> ldcell
     C                   move      no            contfl
     C                   endif
     C                   endif                                                  If 1 cell
      *
     C                   if        contfl = yes                                 If continue
      *
     C                   if        first = yes
     C                   z-add     swfscd        svfscd2
     C                   z-add     swcdyr        svcdyr2
     C                   z-add     swcdwk        svcdwk2
     C                   move      no            first
     C                   endif
      *
     C                   select
     C                   when      ldynfl = no
     C                   move      yes           procfl
     C                   when      ldynfl = yes and swfscd <> svfscd2
     C                   z-add     swfscd        svfscd2
     C                   z-add     swcdyr        svcdyr2
     C                   z-add     swcdwk        svcdwk2
     C                   move      yes           procfl
     C                   when      ldynfl = yes and swcdyr = svcdyr2 and
     C                             swcdwk = svcdwk2
     C                   move      yes           procfl
     C                   other
     C                   move      no            procfl
     C                   endsl
      *
      * If you need to process this record.
      *
     C                   if        procfl = yes                                 If process
      *
      *
      * Print 1) report headings or 2) a blank line between farm sites if
      * you are printing all data...not just the 'most recent' entries.
      *
     C                   select
     C                   when      *inof = *on
     C                   except    h1hdr
      *
     C                   when      swfscd <> svfscd and
     C                             ldynfl = no
     C                   except    blank
     C                   z-add     swfscd        svfscd
     C                   endsl
      *
      * Flip effective date from CCYYMMDD to MMDDYY
      *
      *
     C     *iso          test(d)                 swwedt                 92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             l1mdywedt
     C                   else
     C     *iso          move      swwedt        wkmdy
     C                   movel     wkmdy         l1mdywedt
     C                   endif                                                  If bad date
      *
      * Retrieve cell for farm
      *
     C     swfscd        chain     hsp018                             92
     C                   if        *in92 = *on
     C                   z-add     0             l1cell
     C                   else
     C                   z-add     fscell        l1cell
     C                   endif
      *
      * Print detail line
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If continue
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      *
      *  Setup the user selections in the headings.
      *
      * Print most recent entries only
      *
     C                   if        ldynfl = yes
     C                   move      'Yes'         h1yesno
     C                   else
     C                   move      ' No'         h1yesno
     C                   endif
      *
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        86
     C                   endif
      *
      * Farm site
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   else
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel(p)  fsfsnm        h1fsnm
     C                   else
     C                   movel(p)  'UNKNOWN'     h1fsnm
     C                   endif
     C                   endif
      *
      * Calendar year
     C                   if        ldcdyr = 0
     C                   seton                                        88
     C                   endif
      *
      * Calendar week
     C                   if        ldcdwk = 0
     C                   seton                                        87
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           47 'LISTING OF WEEKLY SIRE'
     O                                           58 'LINE DOSES'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                           27 'Show most recent week only'
     O                                           28 ':'
     O                       h1yesno             34
      *
      *
     O          e            h1hdr          1
     O                                           27 'Cell .....................'
     O                                           28 ':'
     O              N86      ldcell        z     34
     O               86                          34 'All'
      *
     O          e            h1hdr          1
     O                                           27 'Farm site ................'
     O                                           28 ':'
     O              N89      ldfscd        z     34
     O              N89      fsfsnm              61
     O               89                          34 'All'
      *
      *
     O          e            h1hdr          1
     O                                           27 'Calendar year ............'
     O                                           28 ':'
     O              N88      ldcdyr        z     34
     O               88                          34 'All'
      *
      *
     O          e            h1hdr          2
     O                                           27 'Calendar week ............'
     O                                           28 ':'
     O              N87      ldcdwk        z     34
     O               87                          34 'All'
      *
      *
     O          e            h1hdr          1
     O                                            5 'FARM'
     O                                           24 '-CALENDAR-'
     O                                           37 'SIRE LINE'
     O                                           46 'SIRE'
     O                                           59 'NUMBER'
     O                                           76 'WEEK-ENDING'
     O          e            h1hdr          1
     O                                            5 'SITE'
     O                                           12 'CELL'
     O                                           18 'YEAR'
     O                                           24 'WEEK'
     O                                           36 'SOURCE'
     O                                           46 'LINE'
     O                                           59 'OF DOSES'
     O                                           73 'DATE'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       swfscd        z      5
     O                       l1cell        z     11
     O                       swcdyr        z     18
     O                       swcdwk        z     23
     O                       swsscd              36
     O                       swsicd              47
     O                       swdono        k     60
     O                       l1mdywedt           75 '  /  /  '
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
