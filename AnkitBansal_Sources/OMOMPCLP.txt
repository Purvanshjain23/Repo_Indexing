/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT OPERATING MARGIN REPORTS                       */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - SHEILA SCHERTZ                                      */
/*  DATE      - 06/08/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                             */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - FORMS TYPE                                */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/********************************************************************/

 OMOMPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &OUTQ +
                        &HOLD &SAVE &COPIESL &NIGHTQ)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)           /*  FROM DATE     */
        DCL    &FDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &TDATE     *DEC    (7 0)           /*  THROUGH DATE  */
        DCL    &TDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &OUTQ      *CHAR   10              /*  OUTPUT QUEUE  */
        DCL    &NIGHTQ    *CHAR   10              /*  NIGHT JOB QUEUE*/
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO DATE ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL     &PGMO     *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY FRM DATE   */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   70              /* QRY ALL        */
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/********************************************************************/
/* Retrieve Data Library name and Data History library name         */
/********************************************************************/

             CALL       PGM(CAQXXFR) PARM(' ' &DATA &DATAH)

/*********************************************************************/
/*   CREATE WORK FILES IN QTEMP                                      */
/*********************************************************************/

             RMVLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)

             CLRPFM     FILE(OMHSCPP)
             CRTDUPOBJ  OBJ(OMHSCPP) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPL0) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPL1) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPLA) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPL8) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPL9) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR REPORT                    */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4AWDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND F4AWDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             CHGVAR     VAR(&D) VALUE(&A *BCAT &B *BCAT &C)

             OVRDBF     FILE(OMHSTPL1) SHARE(*YES)
             OPNQRYF    FILE(OMHSTPL1) +
                        QRYSLT(&D) +
                        KEYFLD(*FILE)

/********************************************************************/
/* BUILD SALES & ACCRUAL WORK FILE                                  */
/********************************************************************/

             CALL       PGM(OMNRXFR) PARM('')

             CLOF       OPNID(OMHSTPL1)
             DLTOVR     FILE(OMHSTPL1)

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             CALL      PGM(&PGM) PARM('' &FDATE &TDATE)

/*   DELETE WORK FILES IN QTEMP.  */

             CPYF       FROMFILE(QTEMP/OMHSCPP) +
                          TOFILE(SLSFLIB/OMHSCPP) MBROPT(*REPLACE) +
                          CRTFILE(*YES)
             DLTF       FILE(QTEMP/OMHSCPL0)
             DLTF       FILE(QTEMP/OMHSCPL1)
             DLTF       FILE(QTEMP/OMHSCPLA)
             DLTF       FILE(QTEMP/OMHSCPL8)
             DLTF       FILE(QTEMP/OMHSCPL9)
             DLTF       FILE(QTEMP/OMHSCPP)

 END:        ENDPGM
