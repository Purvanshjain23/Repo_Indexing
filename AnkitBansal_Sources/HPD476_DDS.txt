     A*%%TS  SD  20090624  143752  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD476
     A*  TITLE:         SPECIFY ACTUAL MOVEMENT LOG OPTIONS
     A*  PROGRAMMER:    SHARON ZURN
     A*  DATE:          06/08/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP476R1
     A*%%TS  SD  20090624  143752  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(42)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'SPECIFY ACTUAL MOVEMENT LOG OPTION-
     A                                      S'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ...'
     A                                  4 22'?'
     A            DFFSBO    R        B  4 24REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 34REFFLD(BOBODS)
     A                                  5  2'Report ............'
     A                                  5 22'?'
     A            DFMRCD    R        B  5 24REFFLD(MRMRCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 55                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFMRDS    R        O  5 34REFFLD(MRMRDS)
     A                                  7  2'Instead of printing the report, em-
     A                                      ail the data as a spreadsheet to:'
     A                                      DSPATR(HI)
     A                                  8  2'E-mail address ....'
     A            DFEMAIL   R        B  8 24REFFLD(TFREF/@@EMAIL *LIBL/TFPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A                                 11  2'Enter a From/To Date Range or an U-
     A                                      pdate Date--Do Not Enter Both:'
     A                                      DSPATR(HI)
     A                                 12  2'From date .........'
     A            DFFMDY    R     Y  B 12 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13  2'To date ...........'
     A            DFTMDY    R     Y  B 13 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 14  4'--OR--'
     A                                      DSPATR(HI)
     A                                 15  2'Update date .......'
     A            DFUMDY    R     Y  B 15 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 17  2'Production phase ..'
     A                                 17 22'?'
     A            DFPPCD    R        B 17 24REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 53                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O 17 34REFFLD(PPPPDS)
     A                                 17 65'(Blank for all) '
     A                                 18  2'Cell ..............'
     A            DFCELL    R        B 18 24REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 18 65'(Blank for all) '
     A                                 19  2'Farm site .........'
     A                                 19 22'?'
     A            DFFSCD    R        B 19 24REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 54                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 19 34REFFLD(FSFSNM)
     A                                 19 65'(Blank for all) '
     A                                 20  2'Hog group .........'
     A                                 20 22'?'
     A            DFHGCD    R        B 20 24REFFLD(HGHGCD)
     A N37                                  DSPATR(HI)
     A  37
     AO 52                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 20 65'(Blank for all)'
     A                                 21  2'Customer number ...'
     A                                 21 22'?'
     A            DFCVNO    R        B 21 24REFFLD(CDCVNO)
     A N42                                  DSPATR(HI)
     A  42
     AO 59                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFCNAM        30A  O 21 34
     A                                 21 65'(Blank for all)'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP476K1
     A*%%TS  SD  19950224  140604  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A N99                             23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP476ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP476EC                   SFLCTL(HP476ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
