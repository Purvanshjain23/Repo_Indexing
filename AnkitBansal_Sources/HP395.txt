      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP395
      * TITLE:       BUILD WORKFILE--INVENTORY RECAP BY GROUP
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     03/17/95
      *
      *  FUNCTION:   THIS PROGRAM BUILDS A WORKFILE FOR USE IN THE
      *              INVENTORY RECAP BY GROUP REPORT.  THE WORKFILE IS
      *              REQUIRED SINCE 'COST CENTER' IS ONE OF THE SORT
      *              FIELDS IN THE REPORT AND 'COST CENTER' IS NOT
      *              CONTAINED IN THE HOG GROUP FILE--WHICH WAS
      *              ORIGINALLY THE PRIMARY FILE FOR THE REPORT.
      *
      *              SO, THIS PROGRAM SIMPLY READS THE SELECTED
      *              RECORDS FROM THE HOG GROUP FILE, RETRIEVES THE
      *              COST CENTER FOR EACH GROUP, AND WRITES A WORKFILE
      *              RECORD.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/03/01  LeAnne Fedor
      *           Added 'group type' to workfile.
      *
      * 10/30/02  LeAnne Fedor
      *           Added several fields to workfile:
      *               weeks in phase
      *               days of phase
      *               stage of phase
      *
      * 02/11/03  LeAnne Fedor
      *           Changed all the 'phase' fields added last October to be 'growth' fields.
      *
      * 04/23/03  LeAnne Fedor
      *           Added 'version' parm on call to generic 'stage of growth' program.
      *
      * 11/04/03  LeAnne Fedor
      *           We had a discrepancy on the 'stage of growth' version between the 1) EOP run
      *           and 2) on-demand run for the same period. Basically, groups were plopping
      *           into different 'stage' categories. This occurred when a group was in a
      *           CREATE status on the EOP run and an OPEN status at the time of the on-demand
      *           run. Our 'elapsed' days logic was giving us 'negative' elapsed days. So,
      *           I added code to set all negative 'elapsed' days to zero.
      *
      * 11/14/03  Alice
      *           Changed the calc for Stage of Growth. Do not
      *           increment the week for partial weeks.
      *
      *
      * 11/03/04  LeAnne Fedor
      *           The users changed the calc for 'Average Days of Growth In'.
      *           We now use a hardcoded value of '16' days instead of '14' days in
      *           the calc.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP
      *    (RECORDS SELECTED/KEYED IN OPNQRY)
      *
      *
     FHSP395    O    E           K DISK
      *    WORKFILE FOR GROUP
      *
      /EJECT
      ****************************************************************
      * Definition specs
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
      *
      *
      * Workfields
      *
     D wkadgi          s                   like(hgadoi)
      *
      * Workfields for dates
      *
     D wkdays          s              5  0
     D wkisoopdt       s               D   datfmt(*iso)
     D wkisoepdt       s               D   datfmt(*iso)
      *
      * Parms
      *
     D xxwk            s                   like(wggrwwk)
     D xxversion       s              4
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldbpdt                 1     08  0
     D  ldepdt                 9     16  0
     D  ldper                 17     18  0
     D  ldeffl                19     19
     D  ldccyy                20     23  0
     D  ldbmdy                60     66  0
     D  ldemdy                70     76  0
     D  lderfl                80     80
     D  ldcell               102    103  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * Read each group selected by the open query. If you are generating the
      * report from the menu option (ie: on-demand) instead of from the EOP
      * function, you will need to make additional checks for some of the user's
      * selections.
      *
593  C*    *loval        SETLL     HSP034
593  C     *start        SETLL     HSP034
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP034                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
     C                   move      yes           procfl
      *
      * If this is the on-demand report, make additional comparisions
      * to user's selections.
      *
     C                   if        ldeffl = 'D' and ldcell <> 0
     C                   exsr      $select
     C                   endif
      *
      * If the group will be included on the report, continue processing:
      *
     C                   if        procfl = yes                                 If process
      *
     C                   Z-ADD     HGHGSN        WGHGSN
     C                   MOVEL     HGHGCD        WGHGCD
     C                   MOVEL     HGPPCD        WGPPCD
     C                   Z-ADD     HGFSCD        WGFSCD
     C                   MOVEL     HGGTCD        WGGTCD
     C                   MOVEL     HGRMCD        WGRMCD
     C                   MOVEL     HGBLCD        WGBLCD
     C                   Z-ADD     HGOPDT        WGOPDT
      *
      * Calculate the days in the phase for a group
      *
     C                   exsr      $days
      *
      * RETRIEVE FARM SITE VALUES.
      *
     C     HGFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     FSCJD         WGCJD
     C                   MOVEL     FSFSNM        WGFSNM
     C                   ELSE
     C                   MOVE      *BLANK        WGCJD
     C                   MOVE      *BLANK        WGFSNM
     C                   ENDIF
      *
     C                   WRITE     WGREC
      *
     C                   endif                                                  If process
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *------------------
      * EOF PROCESSING
      *------------------
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Make addtional checks on user's selections for the on-demand version
      *--------------------------------------------------------------------------------------
      *
     C     $select       begsr
      *
      * Check if group is on a farm with user's selected:
      *  1) cell
      *
     C     hgfscd        chain     hsp018                             92
     C                   select
     C                   when      *in92 = *on
     C                   move      no            procfl
     C                   other
      *
     C                   if        ldcell <> 0 and fscell <> ldcell
     C                   move      no            procfl
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Calculate the days in the phase for a group
      *---------------------------------------------------------------
      *
     C     $days         begsr
      *
      * Find the elapsed days between the open date of the group and the
      * period end date. (Test for a valid open date first.)
      * If elapsed days is negative, reset the field to zero. (We get negative days when
      * a group's open date is AFTER the period end date.)
      *
     C                   z-add     0             wkdays
      *
     C     *iso          test(d)                 hgopdt                 92
     C                   if        *in92 = *off                                 If valid open
     C                   movel     hgopdt        wkisoopdt
     C                   movel     ldepdt        wkisoepdt
     C     wkisoepdt     subdur    wkisoopdt     wkdays:*d
      *
     C                   if        wkdays < 0
     C                   z-add     0             wkdays
     C                   endif
      *
     C                   endif                                                  If valid open
      *
      *
      *
      * Populate 'average days of growth in' as follows:
      *   If 'average days old in' is less than 20 days,
      *      Set the value to zero
      *   Otherwise
      *      Subtract 16 days from 'average days old in'
      *   (Note: We use 16 days because the users think it is the average number of days
      *          a piglet lives in the BGF farm before it is weaned.)
      *
     C                   select
     C                   when      hgadoi < 20
     C                   z-add     0             wkadgi
     C                   other
     C     hgadoi        sub       16            wkadgi
     C                   endsl
      *
      *
      * Add 'elapsed days' and 'avg days of growth in' together
      * to get the 'total days of growth' for this group.
      *
     C     wkdays        add       wkadgi        wgtotday
      *
      *
      * Convert the 'total days of growth' to weeks/days of growth for printing.
      *
     C     wgtotday      div       7             wggrwwk
     C                   mvr                     wggrwday
      *
      * Call generic program to calculate 'stage of growth' the 'accounting dept' way!
      * If you have a partial week from the calc above, increment the week value
      * that is passed into the generic program.
      *
     C******/////        if        wggrwday > 0
     C**/  wggrwwk       add       1             xxwk
     C****////           else
     C                   z-add     wggrwwk       xxwk
     C****///            endif
      *
     C                   call      'HP8012'
     C                   parm                    xxwk
     C                   parm      *blank        wgalphstg
     C                   parm      0             wgnumstg
     C                   parm      'ACCT'        xxversion
      *
     C                   endsr
      /EJECT
