     A*%%TS  SD  20090707  093035  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD142
     A*  TITLE:         CREATE HOG GROUPS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/21/99
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*  63  THE HOG GROUP HAS BEEN DELETED OUT FROM UNDER THE USER SINCE
     A*      SELECTED. PROTECT FIELDS FROM ENTRY AND LIMIT FUNCTION KEYS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N63                                  CF04(04 'PROMPT')
     A N63                                  CF05(05 'REFRESH')
     A N63                                  CF09(09 'ACCEPT')
     A                                      CF12(12 'PREVIOUS')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP142S1                   SFL
     A*%%TS  SD  20090707  093035  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1BLCD    R        B 14  2REFFLD(@@BLCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N63                               DSPATR(HI)
     A N31N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1RMCD    R        B 14  9REFFLD(@@RMCD)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N63                               DSPATR(HI)
     A N32N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1HGCD    R        B 14 16REFFLD(HSRREF/@@HGCD)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N63                               DSPATR(HI)
     A N30N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1PTCD    R        B 14 25REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N63                               DSPATR(HI)
     A N33N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1PPCD    R        B 14 32REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N63                               DSPATR(HI)
     A N34N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1GTCD    R        B 14 40REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35N63                               DSPATR(HI)
     A N35N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1SDCD    R        B 14 45REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39N63                               DSPATR(HI)
     A N39N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1ADOI    R        B 14 50REFFLD(HGREC/HGADOI *LIBL/HSP034)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N63                               DSPATR(HI)
     A N38N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1DFTADOI R        O 14 58REFFLD(HSRREF/HGADOI *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1BSCD    R        B 14 64REFFLD(HSRREF/@@BSCD *LIBL/HSPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N63                               DSPATR(HI)
     A N36N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1SXCD    R        B 14 72REFFLD(HSRREF/@@SXCD *LIBL/HSPREF)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N63                               DSPATR(HI)
     A N37N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A            D1CFFL    R        B 14 77REFFLD(HSRREF/@@CFFL *LIBL/HSPREF)
     A  44
     AO 46                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A N44N63                               DSPATR(HI)
     A N44N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT 1
     A*----------------------------------------------------------------
     A*
     A          R HP142C1                   SFLCTL(HP142S1)
     A*%%TS  SD  20090707  093035  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0007)
     A N63                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(44)
     A                                      SETOF(46)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRR1     R        H      REFFLD(@@RRN)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 30'CREATE HOG GROUPS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  3'Business office:'
     A            C1FSBO    R        O  4 20REFFLD(HGFSBO)
     A                                  4 33'Group type:'
     A            C1GTCD    R        O  4 45REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A                                  4 53'Average days old IN:'
     A            C1ADOI    R        O  4 74REFFLD(HGREC/HGADOI *LIBL/HSP034)
     A                                      EDTCDE(K)
     A                                  5  9'Farm site:'
     A            C1FSCD    R        O  5 20REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A                                  5 32'Breed stock:'
     A            C1BSCD    R        O  5 45REFFLD(HSRREF/@@BSCD *LIBL/HSPREF)
     A                                  5 57'Continuous flow:'
     A            C1CFFL    R        O  5 74REFFLD(HSRREF/@@CFFL *LIBL/HSPREF)
     A                                  6 10'Building:'
     A            C1BLCD    R        O  6 20REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A                                  6 40'Sex:'
     A            C1SXCD    R        O  6 45REFFLD(HSRREF/@@SXCD *LIBL/HSPREF)
     A                                  7 14'Room:'
     A            C1RMCD    R        O  7 20REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  87                              7 33'Feed group:'
     A  87        C1FGCD    R        O  7 45REFFLD(HSRREF/@@FGCD *LIBL/HSPREF)
     A                                  8  2'Production phase:'
     A            C1PPCD    R        O  8 20REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                  8 31'Date created:'
     A            C1CRDTMDY R        O  8 45REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  9  3'Production type:'
     A            C1PTCD    R        O  9 20REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                 10 50'Avg'
     A                                 10 56'Default'
     A                                 11  4'?'
     A                                 11 11'?'
     A                                 11 27'?'
     A                                 11 34'?'
     A                                 11 40'?'
     A                                 11 45'?'
     A                                 11 49'Days'
     A                                 11 58'Days'
     A                                 11 66'?'
     A                                 11 72'?'
     A                                 11 77'?'
     A                                 12 16'Hog'
     A                                 12 25'-Production-'
     A                                 12 39'Grp'
     A                                 12 44'Stk'
     A                                 12 50'Old'
     A                                 12 58'Old'
     A                                 12 64'Breed'
     A                                 12 76'Cont'
     A                                 13  2'Bldg'
     A                                 13  9'Room'
     A                                 13 16'Group'
     A                                 13 25'Type'
     A                                 13 32'Phase'
     A                                 13 39'Typ'
     A                                 13 44'Den'
     A                                 13 51'In'
     A                                 13 59'In'
     A                                 13 64'Stock'
     A                                 13 71'Sex'
     A                                 13 76'Flow'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP142K1
     A*%%TS  SD  19990920  142131  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N63                             23 11'F4=Prompt'
     A N63                             23 22'F5=Refresh'
     A N63                             23 34'F9=Accept'
     A N63                             23 45'F12=Previous'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP142ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP142EC                   SFLCTL(HP142ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
