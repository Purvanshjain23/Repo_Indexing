     A*%%TS  SD  20110106  134143  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD412
     A*  TITLE:         WORK WITH CLAIM ADJUSTMENTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/04/05
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF07(07 'LISTING')
     A                                      CF10(10 'CUSTOMER CLAIMS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF412S1                   SFL
     A*%%TS  SD  20110106  134143  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDWEDT    R        H      REFFLD(@@WEDT)
     A            HDMMLNNO  R        H      REFFLD(@@MMLNNO)
     A            HDCLLNNO  R        H      REFFLD(@@CLLNNO)
     A            D1OPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O 10  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1MMNO    R        O 10 15REFFLD(TFREF/@@MMNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1DSCD    R        O 10 25REFFLD(TFREF/@@DSCD *LIBL/TFPREF)
     A            D1YNFL    R        O 10 29REFFLD(TFREF/@@DSCD *LIBL/TFPREF)
     A            D1MMTY    R        O 10 32REFFLD(TFREF/@@MMTY *LIBL/TFPREF)
     A            D1CONO    R        O 10 36REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1CLNO    R        O 10 41REFFLD(TFREF/@@CLNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1CLORNO  R        O 10 50REFFLD(TFREF/@@CLORNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1PRCD    R        O 10 59REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1RPFL    R        O 10 68REFFLD(TFREF/@@RPFL *LIBL/TFPREF)
     A            D1RPDEPT  R        O 10 71REFFLD(TFREF/@@RPDEPT *LIBL/TFPREF)
     A            D1RSCD    R        O 10 76REFFLD(TFREF/@@RSCD *LIBL/TFPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF412C1                   SFLCTL(TF412S1)
     A*%%TS  SD  20110106  134143  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 23'WORK WITH CLAIM ADJUSTMENTS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'5=View'
     A                                  5 25'?'
     A                                      DSPATR(HI)
     A                                  5 29'?'
     A                                      DSPATR(HI)
     A                                  5 62'?'
     A                                      DSPATR(HI)
     A                                  6  8'Week'
     A                                      DSPATR(HI)
     A                                  6 16'Cr/Db'
     A                                      DSPATR(HI)
     A                                  6 51'Claim'
     A                                      DSPATR(HI)
     A                                  7  7'Ending'
     A                                      DSPATR(HI)
     A                                  7 17'Memo'
     A                                      DSPATR(HI)
     A                                  7 24'Dt'
     A                                      DSPATR(HI)
     A                                  7 27'Afil'
     A                                      DSPATR(HI)
     A                                  7 32'Mm'
     A                                      DSPATR(HI)
     A                                  7 36'Comp'
     A                                      DSPATR(HI)
     A                                  7 43'Claim'
     A                                      DSPATR(HI)
     A                                  7 51'Order'
     A                                      DSPATR(HI)
     A                                  7 59'Product'
     A                                      DSPATR(HI)
     A                                  7 68'Respnb'
     A                                      DSPATR(HI)
     A                                  7 76'Rsn'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  8'Date'
     A                                      DSPATR(HI)
     A                                  8 16'Number'
     A                                      DSPATR(HI)
     A                                  8 24'Sr'
     A                                      DSPATR(HI)
     A                                  8 27'Sale'
     A                                      DSPATR(HI)
     A                                  8 32'Ty'
     A                                      DSPATR(HI)
     A                                  8 36'Nbr'
     A                                      DSPATR(HI)
     A                                  8 42'Number'
     A                                      DSPATR(HI)
     A                                  8 51'Number'
     A                                      DSPATR(HI)
     A                                  8 61'Code'
     A                                      DSPATR(HI)
     A                                  8 68'Fl Dep'
     A                                      DSPATR(HI)
     A                                  8 76'Code'
     A                                      DSPATR(HI)
     A            C1WEMDY   R        B  9  6REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1MMNO    R        B  9 15REFFLD(TFREF/@@MMNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1DSCD    R        B  9 25REFFLD(TFREF/@@DSCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1YNFL    R        B  9 29REFFLD(TFREF/@@DSCD *LIBL/TFPREF)
     A  53                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1MMTY    R        B  9 32REFFLD(TFREF/@@MMTY *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            C1CONO    R        B  9 36REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CLNO    R        B  9 41REFFLD(TFREF/@@CLNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CLORNO  R        B  9 50REFFLD(TFREF/@@CLORNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1PRCD    R        B  9 59REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1RPFL    R        B  9 68REFFLD(TFREF/@@RPFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            C1RPDEPT  R        B  9 71REFFLD(TFREF/@@RPDEPT *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            C1RSCD    R        B  9 76REFFLD(TFREF/@@RSCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF412K1
     A*%%TS  SD  20060816  092007  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F7=Listing'
     A                                 23  2'F10=Work with Customer Claims (Dat-
     A                                      a Source=C claims only)'
     A                                 23 61'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF412ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF412EC                   SFLCTL(TF412ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
