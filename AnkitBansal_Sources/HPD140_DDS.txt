     A*%%TS  SD  20090706  153514  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD140
     A*  TITLE:         REVISE HOG GROUP
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/06/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN REOPEN MODE
     A*  INDICATOR 63 IS ON WHEN IN DISPOSE MODE
     A*  INDICATOR 64 IS ON WHEN IN A GROUP HAS A STATUS OF DS=DISPOSED
     A*  INDICATOR 65 IS ON WHEN THE GROUP IS NURSERY WEAN-TO-FINISH
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A N61N62N63
     A N64                                  CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP140R1
     A*%%TS  SD  20090706  153514  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(44)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 29'REVISE HOG GROUP'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .....:'
     A            DFFSBO    R        O  4 27REFFLD(HGFSBO)
     A            DFBODS    R        O  4 36REFFLD(BOBODS)
     A N61                              5  2'Hog group ............'
     A N61        DFHGCD    R        B  5 27REFFLD(HGHGCD)
     A  55
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N62 25
     A N63                                  DSPATR(HI)
     A N31N62 25
     A N63                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A N61                              5 36'Hog group number:'
     A N61        DFHGSN    R        O  5 54REFFLD(HGHGSN)
     A                                      EDTCDE(M)
     A N61                              6  2'Group status code ...:'
     A N61        DFGSCD    R        O  6 27REFFLD(HGGSCD)
     A                                  7  2'Farm site ............'
     A N42N45                           7 25'?'
     A            DFFSCD    R        B  7 27REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N62N42
     A  25N63N45                            DSPATR(HI)
     A N32N62N42
     A  25N63N45                            DSPATR(UL)
     A  42
     AO 45
     AO 62                                  DSPATR(PR)
     A            DFFSNM    R        O  7 36REFFLD(FSFSNM)
     A                                  8  2'Building .............'
     A N42N45                           8 25'?'
     A            DFBLCD    R        B  8 27REFFLD(HGBLCD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N62N42
     A  25N63N45                            DSPATR(HI)
     A N33N62N42
     A  25N63N45                            DSPATR(UL)
     A  42
     AO 45
     AO 62                                  DSPATR(PR)
     A            DFBLDS    R        O  8 36REFFLD(FBBLDS)
     A                                  9  2'Room .................'
     A N42N45                           9 25'?'
     A            DFRMCD    R        B  9 27REFFLD(HGRMCD)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N62N42
     A  25N63N45                            DSPATR(HI)
     A N34N62N42
     A  25N63N45                            DSPATR(UL)
     A  42
     AO 45
     AO 62                                  DSPATR(PR)
     A*
     A N61                             10  2'Production type ......'
     A N45N61                          10 25'?'
     A N61        DFPTCD    R        B 10 27REFFLD(PTREC/PTPTCD *LIBL/HSP010)
     A  41
     AO 51                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A N41N45                               DSPATR(HI)
     A N41N45                               DSPATR(UL)
     A  45                                  DSPATR(PR)
     A*
     A N61        DFPTDS    R        O 10 36REFFLD(PTREC/PTPTDS *LIBL/HSP010)
     A N61                             11  2'Production phase .....'
     A N61N45                          11 25'?'
     A N61        DFPPCD    R        B 11 27REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A N48N45                               DSPATR(HI)
     A N48N45                               DSPATR(UL)
     A  45                                  DSPATR(PR)
     A*
     A N61        DFPPDS    R        O 11 36REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A N61                             12  2'Group type ...........'
     A N61N45                          12 25'?'
     A N61        DFGTCD    R        B 12 27REFFLD(@@GTCD)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N62N45
     A  25                                  DSPATR(HI)
     A N30N62N45
     A  25                                  DSPATR(UL)
     A  62
     AO 45                                  DSPATR(PR)
     A N61        DFGTDS    R        O 12 36REFFLD(GTGTDS)
     A                                 13  2'Stocking density .....'
     A*
     A  65                             13 25'?'
     A            DFSDCD    R        B 13 27REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A N65                                  DSPATR(PR)
     A  65 40
     AO 65 49                               DSPATR(PC)
     A  65 40                               DSPATR(RI)
     A  65N40                               DSPATR(HI)
     A  65N40                               DSPATR(UL)
     A            DFSDDS    R        O 13 36REFFLD(HSRREF/@@SDDS *LIBL/HSPREF)
     A*
     A                                 14  2'Stayed/moved code ...:'
     A            DFSMCD    R        O 14 27REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A            DFSMDS    R        O 14 36REFFLD(HSRREF/@@SMDS *LIBL/HSPREF)
     A                                 15  2'Breed stock ..........'
     A                                 15 25'?'
     A            DFBSCD    R        B 15 27REFFLD(HGBSCD)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N62 25                            DSPATR(HI)
     A N36N62 25                            DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            DFBSDS    R        O 15 36REFFLD(BSBSDS)
     A                                 16  2'Sex ..................'
     A                                 16 25'?'
     A            DFSXCD         1A  B 16 27
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39N62 25                            DSPATR(HI)
     A N39N62 25                            DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            DFSXDS    R        O 16 36REFFLD(SXSXDS)
     A                                 17  2'Feed group ...........'
     A                                 17 25'?'
     A            DFFGCD    R        B 17 27REFFLD(HSRREF/@@FGCD *LIBL/HSPREF)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A N47N62 25                            DSPATR(HI)
     A N47N62 25                            DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            DFFGDS    R        O 17 36REFFLD(HSRREF/@@FGDS *LIBL/HSPREF)
     A                                 17 51'Average days old IN..'
     A*
     A            DFADOI    R        B 17 73REFFLD(HGADOI HSPREF)
     A                                      EDTCDE(K)
     A N66                                  DSPATR(PR)
     A  66 35                               DSPATR(PC)
     A  66 35                               DSPATR(RI)
     A  66N35                               DSPATR(HI)
     A  66N35                               DSPATR(UL)
     A                                 18  2'Continuous flow ......'
     A                                 18 25'?'
     A            DFCFFL    R        B 18 27REFFLD(HSRREF/@@FGCD *LIBL/HSPREF)
     A  44
     AO 46                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A N44N62 25                            DSPATR(HI)
     A N44N62 25                            DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A N61                             18 51'Average days old OUT:'
     A N61        DFADOO    R        O 18 73REFFLD(HGADOO)
     A                                      EDTCDE(K)
     A                                 19  2'Date created .........'
     A            DFCRDT    R        B 19 27REFFLD(@@DT06)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTWRD('  /  /  -')
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N62 25                            DSPATR(HI)
     A N37N62 25                            DSPATR(UL)
     A  62                                  DSPATR(PR)
     A N61                             19 51'Origin BGF farm ....:'
     A N61        DFOBFS    R        O 19 73REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A                                 20  2'Date opened ..........'
     A            DFOPDT    R        O 20 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N61                             20 51'Origin nursery farm.:'
     A N61        DFONFS    R        O 20 73REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A                                 21  2'Date disposed ........'
     A            DFDSDT    R        B 21 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N62 63                            DSPATR(HI)
     A N38N62 63                            DSPATR(UL)
     A  60
     AO 61
     AO 62                                  DSPATR(PR)
     A N61                             21 51'Genetic line .......:'
     A N61        DFGNCD    R        O 21 73REFFLD(HGGNCD)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP140K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N61N62N63
     AAN64                             23 34'F11=Delete/Void'
     A                                 23 51'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP140ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP140EC                   SFLCTL(HP140ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
