      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP250 - INTERACTIVE EDIT OF HOG GROUP IN
      *                      HOG PROCUREMENT AND EVALUATION SYSTEM
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     09/11/95
      *
      * FUNCTION: THIS PROGRAM INTERFACES THE HOG PROCUREMENT AND
      *           EVALUATION SYSTEM TO THE HOG PRODUCTION SYSTEM.
      *
      *           ITS PURPOSE IS TO EDIT THE HOG GROUP CODE ENTERED
      *           IN THE HOG PROCUREMENT AND EVALUATION SYSTEM. THUS,
      *           THIS PROGRAM IS CALLED BY A SYNON PROGRAM IN HPES.
      *
      *           A RETURN CODE IS USED TO INDICATE TO HPES THAT THERE
      *           IS AN ERROR ON THE GROUP.
      *
      *           THE SYNON PROGRAM CAN ONLY HANDLE ONE ERROR MESSAGE
      *           ON EACH CALL. SO, THIS PROGRAM LOGIC ONLY ISSUES THE
      *           FIRST ERROR MESSAGE.
      *
      *           CURRENTY, THE SAME MESSAGE IS SENT FOR EACH ERROR.
      *           IN THE FUTURE, UNIQUE ERROR MESSAGES MAY BE REQUIRED
      *           FOR EACH ERROR CONDITION.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 06/05/96  LEANNE FEDOR
      *           REMOVED EDIT FOR 'GROW FINISH' GROUPS ONLY; NOW
      *           BGF, NURSERY AND GROW FINISH GROUPS ARE VALID.
      *           ALSO, REMOVED EDIT FOR 'CLOSED/VOIDED'. WE WILL
      *           ALLOW 'CLOSED/VOIDED' TO BE ENTERED IN HPES; BUT
      *           THE BATCH INTERFACE EDIT WILL STILL NOT ALLOW
      *           GROUPS THAT ARE CLOSED/VOIDED.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP034    IF   E           K DISK
      *  HOG GROUP                   (KEY: HGHGCD)
      *
     FHSL034D   IF   E           K DISK
     F                                     RENAME(HGREC:HGRECD)
      *    HOG GROUP                 (KEY: HGHGSN)
      *
     FHSP085    IF   E           K DISK
      *  SALES MOVEMENT DETAIL       (KEY: SGMVSN SGHGSN)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * DATA STRUCTURE FOR BREAKING THE 10-ALPHA HPES FIELD INTO
      * THE 7-ALPHA HOG GROUP CODE REQUIRED BY HPS.
      *---------------------------------------------------------------
      *
     D                 DS
     D  WK10                   1     10
     D  WKHGCD                 1      7
     D  WK3                    8     10
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * IF THE HPES PROGRAM HAS SENT A MOVEMENT NUMBER INTO THIS
      * PROGRAM BUT NO HOG GROUP CODE, RETRIEVE THE FIRST HOG
      * GROUP ON THE MOVEMENT--THEN PROCEED WITH GROUP EDITS.
      *
     C     XXHGCD        IFEQ      *BLANK                                       IF DEFAULT
     C     WKMVSN        CHAIN     HSP085                             92
     C     *IN92         IFEQ      *ON                                          IF SALES DTL
     C                   MOVEL     'HPSG1  '     RETURN
     C                   ELSE
     C     SGHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF                                         IF GRP FOUND
     C                   MOVEL(P)  HGHGCD        XXHGCD
     C                   ENDIF                                                  IF GRP FOUND
     C                   ENDIF                                                  IF SALES DTL
     C                   ENDIF                                                  IF DEFAULT
      *
      * PERFORM THE FOLLOWING EDITS FOR THE HOG GROUP:
      *    HOG GROUP CODE MUST BE ALPHA IN FIRST 7 POSITIONS OF FIELD
      *    HOG GROUP MUST EXIST IN HOG GROUP FILE
      *
     C                   MOVEL     XXHGCD        WK10
      *
     C     WK3           IFNE      *BLANK                                       IF TOO MANY
     C                   MOVEL     'HPSG1  '     RETURN
     C                   ELSE
      *
      * USING ALPHA HOG GROUP CODE, RETRIEVE HOG GROUP RECORD
      *
     C     WKHGCD        CHAIN     HSP034                             92
     C     *IN92         IFEQ      *ON
     C                   MOVEL     'HPSG1  '     RETURN
     C                   ELSE
      *
      * THE GROUP IS VALID, SO SET UP OUTPUT PARMS.
      *
     C                   MOVE      HGHGSN        XXHGSN
     C                   MOVEL     HGBSCD        XXBSCD
     C                   MOVEL     HGSXCD        XXSXCD
     C                   ENDIF                                                  IF NO HIT
     C                   ENDIF                                                  IF TOO MANY
      *
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    RETURN                         OUTPUT
     C                   PARM                    WKMVSN                         INPUT
     C                   PARM                    XXHGCD           10            IN/OUT ALPHA
     C                   PARM                    XXHGSN           10            OUTPUT NBR
     C                   PARM                    XXBSCD                         OUTPUT
     C                   PARM                    XXSXCD                         OUTPUT
      *
     C                   MOVE      *BLANK        RETURN            7
     C     *LIKE         DEFINE    SGMVSN        WKMVSN
     C     *LIKE         DEFINE    HGBSCD        XXBSCD
     C                   MOVE      *BLANK        XXBSCD
     C     *LIKE         DEFINE    HGSXCD        XXSXCD
     C                   MOVE      *BLANK        XXSXCD
     C                   MOVE      *BLANK        XXHGSN
      *
     C                   ENDSR
      /EJECT
