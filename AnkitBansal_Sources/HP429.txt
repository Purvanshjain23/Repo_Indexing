      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP429
      * TITLE:       WORK WITH SCHEDULED TRANSFERS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/06/95
      *
      * FUNCTION: THIS IS A WORK WITH THAT DISPLAYS THE TRANSFER
      *           MOVEMENTS WITH A SCHEDULED STATUS.
      *
      *           IT HAS F4-PROMPT LOGIC.
      *           IT LOADS THE SUBFILE A PAGE AT A TIME.
      *           IT USES THE STANDARD MESSAGE HANDLING PROGRAM.
      *           IT HAS STANDARD SUBFILE OPTION EDITING.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 02/13/96  LEANNE FEDOR
      *           ADDED NEW FUNCTION KEY F17-PRINT BOL.
      *           NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO TRANSFER MOVEMENT HEADER FILE.
      *
      * 08/20/96  LEANNE FEDOR
      *           CHANGED LOGIC FOR ALL DATES USED AS SELECTORS OR
      *           POSITIONERS. IN ORDER TO RETRIEVE THE VALUE FOR
      *           'CENTURY' THAT WILL BE USED IN POSITIONING OR
      *           SELECTING RECORDS, WE WILL NOW CALL THE DATE
      *           UTILITY PROGRAM. IF THE DATE KEYED BY THE USER IS
      *           INVALID, THE DATE RETURNED BY THE UTILITY PROGRAM
      *           WILL BE ZERO. WE WILL NOT ISSUE AN ERROR MESSAGE.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 10/21/99  LeAnne Fedor
      *           We added a new option 4=delete to the Work With. When the user
      *           takes this option, the movement will be deleted without displaying
      *           any other screens.  So, we have to check for the user's authority
      *           to delete movements in this program.
      *
      * 11/09/99  LeAnne Fedor
      *           The change of 10/21/99 overlaid 5=view in the control record.
      *           Fixed display file and recompiled.
      *
      * 11/23/99  LeAnne Fedor
      *           Taking F6-create now calls the new create function which creates a
      *           separate movement for each subfile line. If the user needs to create
      *           a movement with 2 detail lines, he will create it through this
      *           new function with 1 detail line and then take 2=revise to add the
      *           second detail line.
      *
      * 06/26/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 12/08/04  LeAnne Fedor
      *           Since September the users have been using new BOL programs called from
      *           the menu. They are now ready to replace the old BOL programs called
      *           from this Work With with the new versions. So, we have replaced the
      *           call to the old HP481 with a call to the new HP493.
      *
      * 12/13/04  LeAnne Fedor
      *           Per AMB, we want to call the new HP493 with a reprint value of NO. Thus,
      *           thru this Work With, you will only be allowed to print BOLs that have
      *           never been printed before. If you wish to reprint a movement, you
      *           will have to do it thru the Reprint Menu Option.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 02/22/16  Rose Centonze   E5190
      *           Added Business office restrictor
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD429    CF   E             WORKSTN SFILE(HP429S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSJ074F   IF   E           K DISK    INFDS(DBFEED)
      *  TRANSFER HEADER + DETAIL
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *STANDARD SUBFILE SELECTOR CONVERSION
      *---------------------------------------------------------------
     D                 DS
     D  DFOPT                  1      2
     D  WKOPT1                 1      1
     D  WKOPT2                 2      2
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *  FILL THE SUBFILE
      *
     C     TERMFL        IFEQ      NO
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
     C                   ENDIF
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * IF THE USER FAILED THE INITIAL SECURITY CHECKS, SETON 99
      * TO PROTECT INPUT FIELDS AND INVALIDATE FUNCTION KEYS.
      *
     C     TERMFL        IFEQ      YES
     C                   SETON                                        99
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP429K1
     C                   WRITE     HP429EC
     C                   EXFMT     HP429C1
      *
      * SAVE THE VALUE OF THE LOWEST SUBFILE RRN ON THE SCREEN FOR
      * LATER REPOSITIONING THE SUBFILE
      *
     C                   Z-ADD     IDFLOW        WKFLOW
      *
      * CLEAR THE MESSAGES FROM THE MESSAGE DATA STRUCTURES AND PROGRAM
      * MESSAGE QUEUE.
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND SET THE NEXT ROUTINE
      *
     C                   SELECT
      *
      *  USER WANTS OUT OR FAILED THE INITIAL SECURITY CHECKS
      *
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C     TERMFL        OREQ      YES
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          F4-PROMPT
     C                   EXSR      $F4S1
      *
     C     FLD           IFEQ      'C1FSBO'                                     E5190
     C     C1FSBO        ANDNE     SVFSBO
     C                   MOVEL(P)  'HP429'       WKFNCD
     C                   EXSR      $CHKBO
     C                   ENDIF
      *
     C     ERROR         IFEQ      0
     C                   MOVEL     c1FSBO        svFSBO
     C                   MOVEL     c1BODS        svBODS
     C     SVMVSN        IFNE      C1MVSN
     C     SVSMDY        ORNE      C1SMDY
     C     SVORHG        ORNE      C1ORHG
     C     SVORFS        ORNE      C1ORFS
     C     SVORBL        ORNE      C1ORBL
     C     SVORPP        ORNE      C1ORPP
     C     SVDNHG        ORNE      C1DNHG
     C     SVDNFS        ORNE      C1DNFS
     C     SVDNBL        ORNE      C1DNBL
     C                   EXSR      $CHKSV
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C                   MOVEL     YES           RESET
     C                   MOVEL     SVFSBO        C1FSBO                          E5190
     C                   MOVEL     SVBODS        C1BODS                          E5190
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN06         WHENEQ    *ON                                          F6-CREATE
     C     C1FSBO        IFNE      SVFSBO                                       E5190
     C                   MOVEL(P)  'HP429'       WKFNCD
     C                   EXSR      $CHKBO
     C                   ENDIF                                                  E5190
      *
      *  CHECK USER AUTHORITY TO THE FUNCTION
      *
     C     ERROR         IFEQ      0
     C                   MOVEL(P)  'HP169'       WKFNCD
     C                   EXSR      $SEC
     C                   MOVEL     C1FSBO        SVFSBO                            e5190
     C                   MOVEL     C1BODS        SVBODS                            "
      *
     C     ERROR         IFEQ      0
     C                   CALL      'HP169'
     C                   ENDIF
     C                   ENDIF
      *
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN17         WHENEQ    *ON                                          F17-BOL
     C     C1FSBO        IFNE      SVFSBO
     C                   MOVEL(P)  'HP429'       WKFNCD
     C                   EXSR      $CHKBO
     C                   ENDIF
     C                   IF        ERROR = 0
     C                   MOVEL     C1FSBO        SVFSBO                           E5190
     C                   MOVEL     C1BODS        SVBODS                           E5190
     C                   MOVEL(P)  'HP493'       WKFNCD
     C                   EXSR      $SEC
     C     ERROR         IFEQ      0
     C                   CALL      'HP493'
     C                   parm      'N'           xxreprint
     C                   ENDIF
     C                   ENDIF
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN20         WHENEQ    *ON                                          F20-WRKSPLF
     C                   CALL      'QEZOUTPT'                                       API
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
      *
      * REPOSITION THE SUBFILE IF THE USER HAS CHANGED ANY OF THE
      * POSITIONERS OR SELECTORS IN THE CONTROL RECORD.
      *
     C     SVFSBO        WHENNE    C1FSBO                                       IF RESTRICT  E5190
     C     SVMVSN        orne      C1MVSN                                       WH POSITION
     C     SVSMDY        ORNE      C1SMDY
     C     SVORHG        ORNE      C1ORHG
     C     SVORFS        ORNE      C1ORFS
     C     SVORBL        ORNE      C1ORBL
     C     SVORPP        ORNE      C1ORPP
     C     SVDNHG        ORNE      C1DNHG
     C     SVDNFS        ORNE      C1DNFS
     C     SVDNBL        ORNE      C1DNBL
      *
     C     SVFSBO        IFNE      C1FSBO                                       E5190
     C                   MOVE      *BLANK        C1BODS                            "
     C                   MOVEL(P)  'HP429'       WKFNCD                            "
     C                   EXSR      $CHKBO                                          "
     C                   ENDIF                                                     "
      *
     C     ERROR         IFEQ      0                                               "
     C                   MOVEL     C1FSBO        SVFSBO                           E5190
     C                   MOVEL     C1BODS        SVBODS                           E5190
     C                   EXSR      $CHKSV
     c                   else
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                     "
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C                   EXSR      $LOAD1
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      *
     C     NORECS        IFEQ      YES                                          IF EMPTY SFL
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDIF                                                  IF EMPTY SFL
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SAVE THE CONTROL RECORD POSITIONERS/SELECTORS
      *----------------------------------------------------------------
      *
     C     $CHKSV        BEGSR
      *
     C                   MOVEL     NO            RESET
     C                   MOVEL     SET1          ROUTNE
      *
      * IF THE 'SHIP DATE' SELECTOR HAS CHANGED, FLIP IT TO THE
      * CCYYMMDD FORMAT NEEDED FOR DATA COMPARISON FOR RETRIEVAL.  IF
      * IT IS AN INVALID DATE, THE RETURNED DATE WILL BE ZERO.
      *
     C     SVSMDY        IFNE      C1SMDY                                       IF DIFF DATE
     C                   Z-ADD     C1SMDY        PFRM8
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          WKCYMD
     C                   ENDIF                                                  IF DIFF DATE
      *
     C                   MOVEL     C1FSBO        SVFSBO                          E5190
     C                   MOVEL     C1BODS        SVBODS                             "
      *
     C                   Z-ADD     C1MVSN        SVMVSN
     C                   Z-ADD     C1SMDY        SVSMDY
      *
     C                   MOVEL     C1ORHG        SVORHG
     C                   Z-ADD     C1ORFS        SVORFS
     C                   MOVEL     C1ORBL        SVORBL
     C                   MOVEL     C1ORPP        SVORPP
      *
     C                   MOVEL     C1DNHG        SVDNHG
     C                   Z-ADD     C1DNFS        SVDNFS
     C                   MOVEL     C1DNBL        SVDNBL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * FIRST, EDIT ALL THE SELECTIONS ENTERED BY THE USER IN THE
      * SUBFILE OPTION FIELD TO SEE THAT VALID SELECTIONS WERE INPUT
      *
     C                   EXSR      $EDOP1
      *
      * IF THERE ARE NO ERRORS IN THE SUBFILE OPTIONS ENTERED
      * BY THE USER, THEN EXECUTE THE SUBROUTINE TO READ AND PROCESS
      * THE SUBFILE RECORDS SELECTED BY THE USER.  AFTER COMPLETING THE
      * SUBROUTINE, SET THE ROUTINE TO START OVER.
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
     C                   EXSR      $PROC1
      *
     C     RTNCD         IFEQ      '00'
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
     C                   ELSE
      *
      * ELSE THERE WERE ERRORS IN THE SUBFILE OPTIONS ENTERED, SO SET
      * TO REDISPLAY SCREEN WITH ERRORS TO USER
      *
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * CLEAR THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
     C                   Z-ADD     0             SVRRN1
      *
     C                   CLEAR                   HP429S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP429C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * REFRESH/FILL THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * SET POSITION IN TRANSFER DETAIL FILE TO START WITH 'SCHEDULED'
      * MOVEMENTS.
      *
     C     C1MVSN        IFEQ      0
     C     WKMSCD        SETLL     HSJ074F
     C                   ELSE
     C     K1L074        SETLL     HSJ074F
     C                   ENDIF
      *
      * THE FIELD 'RESET' IS A FLAG FIELD TO INDICATE WHETHER YOU ARE
      * ATTEMPTING TO REPOSITION THE USER CLOSE TO WHERE HE WAS IN THE
      * SUBFILE.
      * THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) WAS RETRIEVED EARLIER AND PUT IN WKFLOW.
      * ADD TO WKFLOW THE NUMBER OF LINES ON YOUR SUBFILE PAGE OR THE
      * NUMBER LESS 1 TO DETERMINE A SUBFILE RRN TO REPOSITION TO.
      * IF RESET IS 'NO', JUST START THE SUBFILE OVER
      *
     C     RESET         IFEQ      YES
     C     WKFLOW        ADD       WKRLIN        MAXRRN
     C                   ELSE
     C                   Z-ADD     WKPLIN        MAXRRN
     C                   ENDIF
      *
      * LOAD THE SUBFILE UNTIL YOU HAVE THE REQUIRED NUMBER OF RECORDS.
      * THE NUMBER WILL DEPEND ON WHETHER YOU ARE REPOSITIONING THE
      * SUBFILE OR NOT. STOP IF YOU GET TO THE END OF THE SUBFILE.
      *
     C     SVRRN1        DOUGE     MAXRRN
     C     *IN82         OREQ      *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * LOAD THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
     C                   Z-ADD     SVRRN1        HDRRN1
      *
      * SET THE COUNT OF RECORDS ON THE SUBFILE PAGE TO ZERO.
      *
     C                   Z-ADD     0             COUNT
      *
      * FILL THE SUBFILE UNTIL THE END OF THE DATABASE FILE OR YOU
      * HAVE FILLED ALL THE LINES ON THE SUBFILE PAGE. ONLY PROCESS
      * THE 'SCHEDULED' MOVEMENTS BY USING A READ EQUAL ON A WORKFIELD
      * SET UP WITH THE STATUS OF SC=SCHEDULED.
      *
     C                   SETOFF                                       82
     C     *IN82         DOUEQ     *ON                                          DO FILL LOOP
     C     COUNT         ORGE      WKPLIN
      *
     C     WKMSCD        READE     HSJ074F                                82
     C     *IN82         IFEQ      *OFF                                          IF READ
     c     mhorbo        ifeq      c1fsbo                                        E5190
      *
      * RETRIEVE ORIGIN AND DESTINATION HOG GROUP DATA SO IT
      * CAN BE USED IN MATCHING AGAINST THE USER'S SELECTORS
      *
     C     MDORSN        CHAIN     HSL034D                            91
     C     *IN91         IFEQ      *OFF                                         IF ORIGIN
     C                   MOVEL     HGHGCD        DFORHG
     C                   MOVEL     HGBLCD        DFORBL
     C                   ELSE
     C                   MOVE      *BLANK        DFORHG
     C                   MOVE      *BLANK        DFORBL
     C                   ENDIF                                                  IF ORIGIN
      *
     C     MDDNSN        CHAIN     HSL034D                            91
     C     *IN91         IFEQ      *OFF                                         IF DEST
     C                   MOVEL     HGHGCD        DFDNHG
     C                   MOVEL     HGBLCD        DFDNBL
     C                   ELSE
     C                   MOVE      *BLANK        DFDNHG
     C                   MOVE      *BLANK        DFDNBL
     C                   ENDIF                                                  IF DEST
      *
      * ONLY INCLUDE RECORDS THAT MATCH THE USER'S SELECTORS
      *
     C     C1SMDY        IFNE      0                                            IF SCHED
     C     WKCYMD        ANDNE     MHSCDT
     C                   ELSE
      *
     C     C1ORPP        IFNE      *BLANK                                       IF OR PHASE
     C     C1ORPP        ANDNE     MHORPP
     C                   ELSE
      *
     C     C1ORFS        IFNE      0                                            IF OR FARM
     C     C1ORFS        ANDNE     MHORFS
     C                   ELSE
      *
     C     C1ORHG        IFNE      *BLANK                                       IF OR GROUP
     C     C1ORHG        ANDNE     DFORHG
     C                   ELSE
      *
     C     C1ORBL        IFNE      *BLANK                                       IF OR BLDG
     C     C1ORBL        ANDNE     DFORBL
     C                   ELSE
      *
      * DESTINATION
      *
     C     C1DNFS        IFNE      0                                            IF DEST FARM
     C     C1DNFS        ANDNE     MHDNFS
     C                   ELSE
      *
     C     C1DNHG        IFNE      *BLANK                                       IF DEST GRP
     C     C1DNHG        ANDNE     DFDNHG
     C                   ELSE
      *
     C     C1DNBL        IFNE      *BLANK                                       IF DS BLDG
     C     C1DNBL        ANDNE     DFDNBL
     C                   ELSE
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
     C                   MOVE      *BLANK        DFOPT
     C                   Z-ADD     MHMVSN        DFMVSN
     C                   MOVEL     MHORPP        DFORPP
     C                   Z-ADD     MHORFS        DFORFS
     C                   Z-ADD     MHDNFS        DFDNFS
     C                   Z-ADD     MDSCHD        DFSCHD
      *
      *  FLIP 'SCHEDULED SHIP DATE' TO MMDDYY FORMAT
      *
     C                   Z-ADD     MHSCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DFSMDY
      *
     C                   WRITE     HP429S1
     C                   SETON                                        81
      *
      * FOR POSITIONING, PLACE THE SUBFILE RELATIVE RECORD NUMBER INTO
      * THE SFLRCDNBR FIELD (HDPAG1)
      *
     C                   Z-ADD     HDRRN1        HDPAG1
      *
     C                   ENDIF                                                  IF DS BLDG
     C                   ENDIF                                                  IF DS GROUP
     C                   ENDIF                                                  IF DS FARM
     C                   ENDIF                                                  IF OR BLDG
     C                   ENDIF                                                  IF OR GROUP
     C                   ENDIF                                                  IF OR FARM
     C                   ENDIF                                                  IF OR PHASE
     C                   ENDIF                                                  IF SCHED
     C                   ENDIF                                                  IF orbo E5190
     C                   ENDIF                                                  IF READ
     C                   ENDDO                                                  DO FILL LOOP
      *
      * IF NO RECORDS WERE WRITTEN TO THE SUBFILE, THEN ISSUE
      * THE STANDARD MESSAGE 'NO DATA TO DISPLAY'.
      *
     C     HDRRN1        IFEQ      0
     C                   MOVEL     YES           NORECS
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09002'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   MOVEL     NO            NORECS
     C                   ENDIF
      *
     C                   Z-ADD     HDRRN1        SVRRN1
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CHKBO - CHECK THE BUSINESS OFFICE ENTERED AS A RESTRICTOR    subr added E5190
      *----------------------------------------------------------------
      *
      * THE BUSINESS OFFICE ENTERED MUST:
      *    BE A VALID OFFICE IN THE BUSINESS OFFICE MASTER
      *    BE A 'LIVE PRODUCTION' DIVISION OFFICE
      *    HAVE A SECURITY LOCATION TO WHICH THE USER IS AUTHORIZED
      *
     C     $CHKBO        BEGSR
      *
      * CLEAR SUBFILE
      *
     C                   EXSR      $CLR1
      *
      * CHECK THE USER'S SECURITY TO THIS BUSINESS OFFICE.
      * SET INDVFL TO YES TO TELL SECURITY PROGRAM TO COMPARE THE
      * FUNCTION DIVISION CODE TO THE B.O. DIVISION CODE.
      *
     C                   MOVEL     YES           INDVFL
     C                   EXSR      $SEC
     C     OUBOFL        IFEQ      YES
     C                   SETON                                        31
     C                   ENDIF
     C                   MOVEL     NO            INDVFL                          RESET FL
      *
     C                   MOVEL     OUBODS        C1BODS
      *
     C                   ENDSR
      /EJECT
      *--------------------------------------------------------------------------------
      * READ CHANGED RECORDS IN SUBFILE AND EDIT FOR VALID SUBFILE OPTIONS FOR SCREEN 1
      *--------------------------------------------------------------------------------
      *
     C     $EDOP1        BEGSR
      *
     C                   Z-ADD     0             ERROR
      *
      * READ ALL THE CHANGED SUBFILE RECORDS EDITING FOR VALID SUBFILE
      * OPTIONS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO READC
     C                   READC     HP429S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SET OFF ERROR INDICATOR FOR SUBFILE SELECTOR
      *
     C                   SETOFF                                       30
      *
      * CONVERT OPTION USER ENTERED TO STANDARD FORMAT. FOR EXAMPLE,
      * A ' 1', '01', AND '1 ' MUST BE STANDARDIZED TO ' 1'.
      *
     C     WKOPT2        IFEQ      *BLANK                                       IF 2 = BLANK
     C                   MOVE      WKOPT1        WKOPT2
     C                   MOVE      *BLANK        WKOPT1
     C                   ENDIF                                                  IF 2 = BLANK
      *
     C     WKOPT1        IFEQ      *ZERO                                        IF 1 IS ZERO
     C                   MOVE      *BLANK        WKOPT1
     C                   ENDIF                                                  IF 1 IS ZERO
      *
      * TEST FOR VALID SUBFILE OPTION ENTRY ON ALL CHANGED RECORDS
      *
     C     DFOPT         IFNE      ' 2'                                         IF BAD OPT
     C     DFOPT         ANDNE     ' 4'
     C     DFOPT         ANDNE     ' 5'
     C     DFOPT         ANDNE     '  '
     C                   SETON                                        30
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09006'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
      * CHECK THE USER'S AUTHORITY TO THE FUNCTION
      *
     C     DFOPT         IFEQ      ' 2'                                         IF OPTION
     C     DFOPT         OREQ      ' 4'
     C     DFOPT         OREQ      ' 5'
      *
     C                   SELECT
     C                   when      dfopt = ' 2' or dfopt = ' 4'
     C                   MOVEL(P)  'HP129'       WKFNCD
      *
     C     DFOPT         WHENEQ    ' 5'
     C                   MOVEL(P)  'HP729'       WKFNCD
     C                   ENDSL
      *
     C                   EXSR      $SEC
      *
     C     SECFL         IFEQ      YES                                          IF PROBLEM
     C                   SETON                                        30
     C                   EXSR      $ERR1
     C                   ELSE
      *
      * PERFORM ADDITIONAL VALIDATIONS FOR OPTION 4=DELETE
      * 1) THE USER MUST HAVE AUTHORITY TO DELETE SCHEDULED MOVEMENTS.
      *
     C     DFOPT         IFEQ      ' 4'                                         IF DELETE
     C                   CALL      'HPSECD'
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM                    SDUSR
     C                   PARM      'SC'          XXMSCD
     C                   if        error <> 0
     C                   SETON                                        30
     C                   EXSR      $ERR1
     C                   endif
     C                   ENDIF                                                  IF DELETE
      *
     C                   ENDIF                                                  IF PROBLEM
     C                   ENDIF                                                  IF OPTION
     C                   ENDIF                                                  IF BAD OPT
      *
      * REWRITE THE SUBFILE RECORD WITH THE MODIFIED DATA TAG ON SO
      * IT WILL BE REREAD AND PROCESSED IN THE SUBROUTINE $PROC1.
      *
     C     ERROR         IFEQ      0
     C     DFOPT         ANDEQ     *BLANK
     C                   SETOFF                                       85
     C                   ELSE
     C                   SETON                                        85
     C                   ENDIF
      *
     C                   UPDATE    HP429S1
     C                   SETOFF                                       8530
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO READC
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * READ SUBFILE RECORDS AND PROCESS USER SELECTIONS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
     C                   Z-ADD     0             ERROR
     C                   MOVEL     '00'          RTNCD
      *
      * READ ALL THE CHANGED SUBFILE RECORDS AND PROCESS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP429S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
     C     RTNCD         IFEQ      '00'                                         IF ENTER
      *
      * IF USER SELECTED OPTION 2=REVISE, SET PARM FOR REVISE MODE
      * AND SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE.
      *
     C     DFOPT         IFEQ      ' 2'
     C                   MOVEL     YES           RESET
     C                   MOVEL     REVISE        MODE
     C                   CALL      'HP129'
     C                   PARM                    MODE
     C                   PARM                    DFMVSN
     C                   PARM                    RTNCD
     C                   ENDIF
      *
      *
      * IF USER SELECTED OPTION 4=DELETE, SET PARM FOR DELETE MODE
      * AND SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE.
      *
     C     DFOPT         IFEQ      ' 4'
     C                   MOVEL     YES           RESET
     C                   CALL      'HP129'
     C                   PARM      DELETE        MODE
     C                   PARM                    DFMVSN
     C                   PARM                    RTNCD
     C                   ENDIF
      *
      * IF USER SELECTED OPTION 5=VIEW TRANSFER,
      * SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE.
      *
     C     DFOPT         IFEQ      ' 5'
     C                   MOVEL     YES           RESET
     C                   CALL      'HP729'
     C                   PARM                    DFMVSN
     C                   PARM                    RTNCD
     C                   ENDIF
      *
      * UPON RETURNING FROM THE CALLED PROGRAM,
      * CLEAR THE SUBFILE OPTION SELECTOR ENTERED BY THE USER AND
      * REWRITE THE SUBFILE RECORD
      *
     C                   MOVE      *BLANK        DFOPT
     C                   SETOFF                                       85
     C                   ELSE
     C                   SETON                                        85
     C                   ENDIF                                                  IF ENTER
      *
     C                   UPDATE    HP429S1
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * F4 PROMPT ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      * IF BUSINESS OFFICE CODE                               E5190
      *
     C     FLD           WHENEQ    'C1FSBO'
     C                   MOVE      *BLANK        XXFSBO
     C                   MOVE      *BLANK        XXBODS
     C                   CALL      'HP503'
     C                   PARM                    XXFSBO
     C                   PARM                    XXBODS
     C                   PARM                    SVDVCD
     C                   SETON                                        51
     C     XXFSBO        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXFSBO        C1FSBO
     C                   MOVEL     XXBODS        C1BODS
     C                   ENDIF                                                  IF SELECTED
      *
      * ORIGIN HOG GROUP
      *
     C     FLD           WHENEQ    'C1ORHG'
     C                   move      C1fsbo        XXFSBO                          E5190
     C                   CALL      'HP541'
     C****               PARM      *blank        XXFSBO
     C                   PARM                    XXFSBO                          E5190
     C                   PARM      0             xxfscd
     C                   PARM      *blank        XXHGCD
     C                   SETON                                        52
      *
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        C1ORHG
     C                   ENDIF                                                  IF SELECTED
      *
      * ORIGIN FARM SITE
      *
     C     FLD           WHENEQ    'C1ORFS'
     C                   CALL      'HP5040'
     C                   PARM      *blank        xxaist
     C                   PARM      0             xxfscd
     C                   PARM      *blank        XXFSNM
     C                   SETON                                        53
     C     xxfscd        IFNE      0                                            IF SELECTED
     C                   Z-ADD     xxfscd        C1ORFS
     C                   ENDIF                                                  IF SELECTED
      *
      * ORIGIN PRODUCTION PHASE
      *
     C     FLD           WHENEQ    'C1ORPP'
     C                   CALL      'HP507'
     C                   PARM      *blank        XXPPCD
     C                   PARM      *blank        XXPPDS
     C                   SETON                                        54
     C     XXPPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPPCD        C1ORPP
     C                   ENDIF                                                  IF SELECTED
      *
      * DESTINATION HOG GROUP
      *
     C     FLD           WHENEQ    'C1DNHG'
     C                   move      c1fsbo        XXFSBO
     C                   CALL      'HP541'
     C*****              PARM      *blank        XXFSBO                           E5190
     C                   PARM                    XXFSBO                           e5190
     C                   PARM      0             xxfscd
     C                   PARM      *blank        XXHGCD
     C                   SETON                                        55
      *
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        C1DNHG
     C                   ENDIF                                                  IF SELECTED
      *
      * DESTINATION FARM SITE
      *
     C     FLD           WHENEQ    'C1DNFS'
     C                   CALL      'HP5040'
     C                   PARM      *blank        xxaist
     C                   PARM      0             xxfscd
     C                   PARM      *blank        XXFSNM
     C                   SETON                                        56
     C     xxfscd        IFNE      0                                            IF SELECTED
     C                   Z-ADD     xxfscd        C1DNFS
     C                   ENDIF                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------------------
      * SET THE SUBFILE TO REDISPLAY AT THE FIRST PAGE THAT HAS ERRORS ON SCREEN 1
      *---------------------------------------------------------------------------
      *
     C     $ERR1         BEGSR
      *
     C     ERROR         IFEQ      1
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * MANIPULATE DATES WITH DATE UTILITY
      *---------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * FLIP DATE FROM CCYYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZERO         ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * CALL SECURITY PROGRAM
      *---------------------------------------------------------------
      *
     C     $SEC          BEGSR
      *
      * SETUP THE FIELDS TO SEND INTO THE SECURITY PROGRAM
      *
     C                   MOVEL     WKFNCD        INFNCD                         PGM NAME
     C                   MOVEL     C1FSBO        INBOBO                         E5190
     C                   MOVE      *BLANK        OUBOBO                         BO CODE   E5190
     C                   MOVE      *BLANK        OUBODS                         BO DESC   E5190
     C                   MOVEL     NO            TERMFL            1            TERMINAL FLG
      *
     C                   CALL      'HPSEC  '
      **** IN
     C                   PARM                    MGID                           MSGID ARRAY
     C                   PARM                    MGDT                           MSGDTA DATA
     C                   PARM                    ERROR                          # OF ERRORS
     C                   PARM                    INFNCD           10            FUNC CODE
     C                   PARM                    INBOBO            5            BO CODE
     C                   PARM                    INDVFL            1            CHECK DIV?
     C                   PARM                    INBOFL            1            CHECK B.O.?
      **** OUT
     C                   PARM                    OUBOBO            5            BO CODE
     C                   PARM                    OUBODS           30            BO DESC.
     C                   PARM                    OUDVCD            5            DIV CODE
     C                   PARM                    SECFL             1            SEC FLAG
     C                   PARM                    OUBOFL            1            B.O. ERR FLG
     C                   PARM                    OURMFL            1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR MOVEMENT EVENT
      *
     C     K1L074        KLIST
     C                   KFLD                    WKMSCD
     C                   KFLD                    C1MVSN
      *
      * PARM LISTS
      *  NONE
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        MODE              7            PARM
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            NORECS            1            NO RECORDS
     C     *LIKE         DEFINE    IDFLOW        WKFLOW
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
     C     *LIKE         DEFINE    HDRRN1        SVRRN1
     C                   Z-ADD     0             COUNT             2 0
      *
      * DEFINE SAVE FIELDS
      *
     C     *LIKE         DEFINE    C1FSBO        SVFSBO                            E5190
     C     *LIKE         DEFINE    C1BODS        SVBODS                            E5190
     C     *LIKE         DEFINE    C1MVSN        SVMVSN
     C     *LIKE         DEFINE    C1ORHG        SVORHG
     C     *LIKE         DEFINE    C1ORFS        SVORFS
     C     *LIKE         DEFINE    C1ORBL        SVORBL
     C     *LIKE         DEFINE    C1ORPP        SVORPP
     C     *LIKE         DEFINE    C1DNHG        SVDNHG
     C     *LIKE         DEFINE    C1DNFS        SVDNFS
     C     *LIKE         DEFINE    C1DNBL        SVDNBL
     C     *LIKE         DEFINE    C1SMDY        SVSMDY
      *
      * DEFINE WORFIELDS
      *
     C     *LIKE         DEFINE    MHMSCD        WKMSCD
     C                   MOVEL     'SC'          WKMSCD
      *
      * DEFINE FIELDS FOR USE AS PARMS WHEN CALLING PROMPT PROGRAMS
      *
     C     *LIKE         DEFINE    C1BODS        XXBODS                          e5190
     C     *LIKE         DEFINE    C1FSbo        xxFSbo                          e5190
     C                   MOVE      *BLANK        XXAIST            1
     C     *LIKE         DEFINE    MHORBO        XXFSBO
     C     *LIKE         DEFINE    MHMSCD        XXMSCD
     C     *LIKE         DEFINE    MHORFS        XXFSCD
     C                   MOVE      *BLANK        XXFSNM           30
     C     *LIKE         DEFINE    MHORPP        XXPPCD
     C                   MOVE      *BLANK        XXPPDS           30
     C     *LIKE         DEFINE    HGHGCD        XXHGCD
     C                   MOVE      *BLANK        XXREPRINT         1
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS.
      *
     C                   Z-ADD     11            WKPLIN            2 0          SFLPAG(0011)
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM
     C                   EXSR      $CLMSG
      *
     C     *LIKE         DEFINE    OUDVCD        SVDVCD                          e5190
      *
      * EXECUTE THE SECURITY SUBROUTINE TO DETERMINE USER
      * AUTHORIZATIONS.
      *
     C                   MOVEL(P)  SDPGM         WKFNCD           10
     C                   MOVEL     NO            INBOFL
     C                   EXSR      $SEC
     C                   MOVEL     YES           INBOFL
      *
      * IF ERRORS ON INITIAL CALL, SET TERMINAL FLAG TO YES.
      *
     C     SECFL         IFEQ      YES
     C                   MOVEL     YES           TERMFL
     C                   ENDIF
      *
      * MOVE RETURNED FIELDS TO SCREEN (AND SAVE IN WORK      ????     dont know if need this E5190
      * FIELDS FOR USE WITH F5=REFRESH LOGIC)                                 "
      *                                                                      "
     C                   MOVEL     OUBOBO        C1FSBO                      "
     C                   MOVEL     OUBOBO        SVFSBO                      "
     C                   MOVEL     OUBODS        C1BODS                      "
     C                   MOVEL     OUBODS        SVBODS                       E5190
      *
      * SAVE THIS FUNCTION'S DIVISION CODE
      *
     C                   MOVEL     OUDVCD        SVDVCD
      *
     C                   ENDSR
      /EJECT
