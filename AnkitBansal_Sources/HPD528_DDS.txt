     A*%%TS  SD  20020722  160729  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD528 - SELECT FARM SITE BY FEED MILL BO
     A*  RPG PROGRAM:   HP528  - SELECT FARM SITE BY FEED MILL BO
     A*  CREATED BY:    MARY PALMER
     A*  DATE:          04/21/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP528R1
     A*%%TS  SD  20010521  134228  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 20 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP528R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP528R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP528S1                   SFL
     A*%%TS  SD  20010521  134228  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFSCD    R        O  8  1REFFLD(HSRREF/FSFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFFSNM    R        O  8  8REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A            DFCELL    R        O  8 38REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            DFPPCD    R        O  8 44REFFLD(FSPPCD)
     A            DFFMBO    R        O  8 55REFFLD(BOREC/BOBOBO *LIBL/HSP009)
     A            DFFSBO    R        O  8 64REFFLD(BOREC/BOBOBO *LIBL/HSP009)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP528C1                   SFLCTL(HP528S1)
     A*%%TS  SD  20020722  160729  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP528R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 26'SELECT FARM SITE'
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  5  1'Farm'
     A                                      DSPATR(HI)
     A                                  5 44'Prod'
     A                                      DSPATR(HI)
     A                                  5 53'Business Offices'
     A                                      DSPATR(HI)
     A                                  6  1'Site'
     A                                      DSPATR(HI)
     A                                  6  8'Description'
     A                                      DSPATR(HI)
     A                                  6 37'Cell'
     A                                      DSPATR(HI)
     A                                  6 44'Phase'
     A                                      DSPATR(HI)
     A                                  6 55'Mill'
     A                                      DSPATR(HI)
     A                                  6 64'Live'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  7  1REFFLD(FSFSCD)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  7 38REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1PPCD    R        B  7 44REFFLD(FSPPCD)
     A                                      DSPATR(HI)
     A            C1FMBO    R        B  7 55REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1FSBO    R        B  7 64REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A* COMMAND LINE
     A*--------------------------------------------------------------*
     A          R HP528K1
     A                                      OVERLAY
     A                                      WINDOW(HP528R1)
     A                                 19  1'F12=Previous'
