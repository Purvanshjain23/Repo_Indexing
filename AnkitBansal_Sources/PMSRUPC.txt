/*********************************************************************/
/* PROGRAM    : PMSRUPC                                             */
/* TITLE      : EXECUTE THE ESNDMAIL CMD TO FAX MULTIPLE BOLS -HPB ONLY  */
/* DATE       : 03/04/2008                                           */
/* Description: EXECUTE THE ESNDNMAIL COMMAND, ADD AND REMOVE THE    */
/*                ESEND LIBRARY, MONITOR FOR ERRORS                  */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    I     &FAX        FAX NUMBER                                   */
/*    I     &ESUBJECT   EMAIL SUBJECT                                */
/*********************************************************************/
/* Modifications:                                                    */
/*-------------------------------------------------------------------*/
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/* 4/22/08    RMC      USE DIFF PGM AND OVERLAY FOR EMAIL/FAX THAN PRINT */
/* 06/10/09   RMC      NEW OVERLAY FOR NON-AGRI BIODIESEL  HPBBOLFN.  */
/*                     THE OVERLAY FOR AGRI BIODIESEL IS HPBBOLFX  */
/* 09/23/21   DN       DO2212 - Removed PMUCPFR Front Overlay Offset */
/*                     Down & Across (.2). Overlay is now maintain   */
/*                     in FormDocs tool.                             */
/*                                                                   */
/*********************************************************************/
 PPSRUPC:    PGM        PARM(&RETURN &FAX &ESUBJECT +
          &JOBNAME  &OVERLAY &BKOVER  &CUSTNAME &CONTNAME &COMPNYN &BLOVER)
/*-------------------------------------------------------------------*/
/* Declare Variables                                                 */
/*-------------------------------------------------------------------*/
             DCL        VAR(&RETURN)   TYPE(*CHAR) LEN(7)
             DCL        VAR(&EADDRESS) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ESUBJECT) TYPE(*CHAR) LEN(25)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FAX) TYPE(*CHAR) LEN(12)     /*FAX PHONE # */

             DCL  VAR(&PRTF)     TYPE(*CHAR) LEN(10)
             DCL  VAR(&DESC)     TYPE(*CHAR) LEN(25)
             DCL  VAR(&SNDDAT)   TYPE(*DEC)  LEN(6 0)
             DCL  VAR(&STMTTYPE) TYPE(*CHAR) LEN(1)
             DCL  VAR(&OUTQ)     TYPE(*CHAR) LEN(10)
             DCL  VAR(&CONTNAME) TYPE(*CHAR) LEN(25)
             DCL  VAR(&CUSTNAME) TYPE(*CHAR) LEN(30)
             DCL  VAR(&COMPNYN)  TYPE(*DEC)  LEN(15 5)
             DCL  VAR(&EMAILMSG) TYPE(*CHAR) LEN(30) VALUE(*NONE)
             DCL  VAR(&ADDR)     TYPE(*CHAR) LEN(25)
             DCL  VAR(&COMP)     TYPE(*DEC)  LEN(3 0)
/*----------------------------------------------------------------- */
/* PARMS FOR THE RETRIEVE OVERLAY PARAMETERS PROGRAM                */
/*----------------------------------------------------------------- */
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(2) VALUE('FX')
             DCL        VAR(&DEPT) TYPE(*CHAR) LEN(6) VALUE('TRAFF ')
             DCL        VAR(&OVERLAY) TYPE(*CHAR) LEN(8)     /* FAX COVER OVL */
             DCL        VAR(&FAXPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FAXFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLOVER) TYPE(*CHAR) LEN(8) /* BOL FAX OVERLAY */
             DCL        VAR(&BKOVER) TYPE(*CHAR) LEN(8) /* BOL BACK OVERLAY */
             DCL        VAR(&ADDLOBJ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDPHONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDFAX) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OVOUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&DRAWER) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&PRTDEVTYPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LENGTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&WIDTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&CPI) TYPE(*DEC) LEN(3 1)
             DCL        VAR(&LPI) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&OVERFLOW) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&ROTATE) TYPE(*CHAR) LEN(5)
             DCL        VAR(&COMMENT1) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT2) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT3) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT4) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT5) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RECSTS) TYPE(*CHAR) LEN(1)



/* ERROR MESSAGE FIELDS                                                       */
             DCL        VAR(&MSG)   TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL)  TYPE(*DEC) LEN(5 0)
             DCL        VAR(&MSG2)  TYPE(*CHAR) LEN(512)

           CHGVAR VAR(&COMP) VALUE(&COMPNYN)

 /*----------------------------------------------------------------- */
 /* SET UP FAX NUMBER / COVER SHEET :                                */
 /*----------------------------------------------------------------- */
 /*----------------------------------------------------------------- */
 /* GET FAX SERVER EMAIL ADDRESS. THIS WILL BE THE EXTENSION TO      */
 /* SEABOARD'S SERVER, I.E.: @FAX.SEABOARDCORP.COM                   */
 /* THE CORRECT PHONE NUMBER FROM THE INPUT PARAMETERS WILL BE       */
 /* CONCATENATED TO IT AND THE COVER SHEET AND REPORT WILL THEN BE   */
 /* FAXED TO THAT PHONE NUMBER.                                      */
 /*----------------------------------------------------------------- */
             CHGVAR     VAR(&ADDR) VALUE('                        ')
             CALL       PGM(PPJXXFR) PARM(' ' 'FAXSERVER' &ADDR)
             CHGVAR     VAR(&EADDRESS) VALUE(&FAX *TCAT &ADDR)

 /*----------------------------------------------------------------- */
 /* THE COVER SHEET ATTACHMENT MUST BE SENT AS A *PDF TO PRINT THE LOGO */
 /*                                                                  */
 /*----------------------------------------------------------------- */
 /*----------------------------------------------------------------- */
 /* CALL THE GENERIC RETRIEVE COMPANY DOCUMENT DEFAULTS FIELDS       */
 /* PROGRAM. COMPANY, TYPE, DEPARTMENT, AND OVERLAY NAME ARE         */
 /* INPUT RESTRICTED, THE REST ARE OUTPUT PARMS.                     */
 /*----------------------------------------------------------------- */
              CALL       PGM(PMG9XFR) PARM(' ' &COMP &TYPE &DEPT +
                           &OVERLAY &FAXPGM &FAXFILE &ADDLOBJ +
                           &SENDPHONE &SENDFAX &OVOUTQ &HOLD &SAVE +
                           &DRAWER &PRTDEVTYPE &LENGTH &WIDTH &LPI +
                           &CPI &OVERFLOW &ROTATE &COMMENT1 +
                           &COMMENT2 &COMMENT3 &COMMENT4 &COMMENT5 +
                           &RECSTS)

             IF         COND(&OUTQ *EQ '     ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(&OVOUTQ))

 PRTCOVER:   OVRPRTF    FILE(&FAXFILE) DEVTYPE(&PRTDEVTYPE) +
                          PAGESIZE(&LENGTH &WIDTH) LPI(&LPI) +
                          CPI(&CPI) OVRFLW(&OVERFLOW) +
                          DRAWER(&DRAWER) PAGRTT(&ROTATE) +
                          FRONTOVL(&OVERLAY) OUTQ(&OUTQ) +
                          HOLD(&HOLD) SAVE(&SAVE)

  /* PRINT THE COVER SHEET FOR BOLS                               */
               CALL       PGM(&FAXPGM) PARM('' &COMP &TYPE &DEPT +
                            &OVERLAY &CUSTNAME &CONTNAME &FAX)

/*-------------------------------------------------------------------*/
/* EXECUTE OVERRIDE PRINTFILES                                       */
/*-------------------------------------------------------------------*/
/* DO2212 - Removed PMUCPFR Front Overlay Offset Down & Across (.2)  */
/* DO2212    OVRPRTF    FILE(PMUCPFR$) PAGESIZE(88 136) LPI(8) +
                          CPI(18) OVRFLW(88) DUPLEX(*YES) +
                          FRONTOVL(&BLOVER 0.2 .2) BACKOVL(&BKOVER +
                          0 0 *CONSTANT) USRDTA(&JOBNAME)            */
             OVRPRTF    FILE(PMUCPFR$) PAGESIZE(88 136) LPI(8) +
                          CPI(18) OVRFLW(88) DUPLEX(*YES) +
                          FRONTOVL(&BLOVER) BACKOVL(&BKOVER 0 0 +
                          *CONSTANT) USRDTA(&JOBNAME)
             OVRPRTF    FILE(PMQBPFR$) PAGESIZE(88 136) LPI(8) +
                          CPI(18) OVRFLW(88) FRONTOVL(HPBBOLBY .2) +
                          USRDTA(&JOBNAME)
/*-------------------------------------------------------------------*/
/* PRINT THE BOLS                                                    */
/*-------------------------------------------------------------------*/
 CALL PMSPXFR ('' &JOBNAME)
/*-------------------------------------------------------------------*/
/* EXECUTE THE COMMAND AND MONITOR FOR ERRORS                        */
/*-------------------------------------------------------------------*/
             ADDLIBLE   LIB(ESEND)
             MONMSG     MSGID(CPF2103 CPF9999)


 FXPROD:     ESNDMAIL   RECIPIENT(&EADDRESS) SUBJECT(&ESUBJECT) +
                          MSG(*NONE) MSGTYPE(*TEXT) ATTLIST((* +
                          *PDF *N &FAXFILE) (* *PDF *N PMUCPFR$ * +
                          *ALL *ALL))
             MONMSG     MSGID(EML0999) EXEC(GOTO CMDLBL(BYPROD))

 BYPROD:     ESNDMAIL   RECIPIENT(&EADDRESS) SUBJECT(&ESUBJECT) +
                          MSG(*NONE) MSGTYPE(*TEXT) ATTLIST((* +
                          *PDF *N &FAXFILE) (* *PDF *N PMQBPFR$ * +
                          *ALL *ALL))
             MONMSG     MSGID(EML0999) EXEC(GOTO CMDLBL(DELETE))

 DELETE:     DLTSPLF    FILE(*SELECT) SELECT(*CURRENT *ALL *ALL +
                          &JOBNAME)
             MONMSG     MSGID(CPF3309)
             DLTSPLF    FILE(&FAXFILE) SELECT(*ALL)
             MONMSG     MSGID(CPF3309)

             GOTO       CMDLBL(ENDPGM)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDPGM))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

 ENDPGM:     RMVLIBLE   LIB(ESEND)
             MONMSG     MSGID(CPF0000)

             ENDPGM
