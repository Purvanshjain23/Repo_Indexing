      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      HPS---Datamart
      * PROGRAM:     HP217-Datamart BGF: Build Period Production
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/22/10
      *
      * FUNCTION:    Stephanie Allen needs the Datamart Daily BGF Production file
      *              summed to the Period level...1 record per Farm/Period.
      *
      *              This data will be the start of the new Sow Monthly Cube.
      *              BUT, it isn't really "monthly"...it is "period".
      *
      *              This will run in the Datamart EOP logic for HPS.
      *
      ****************************************************************
      * MODIFICATIONS:
      *
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/12/16  Barb Gutierrez
      *           - Added company control file HSP0071 and changed some logic to
      *             accomodate multiple companies being processed.  E5752
      *           - changed hppb018 to hplb018a to key by company being processed.
      *           - changed hppb182 to hplb182a to deleted records by company being
      *             processed.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp0071   IF   E           K DISK
      *   Company Control File
      *
     F*hppb018   if   e           k disk
     Fhplb018a  if   e           k disk
      *   Datamart BGF: Farm site
      *
      *
     Fhplb082b  if   e           k disk
      *   Datamart BGF: Daily production  (records selected with open query)
      *
      *
     F*hppb182   uf a e           k disk
     Fhplb182a  uf a e           k disk
      *   Datamart BGF: Period production
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
     d svccly          s                   like(ccccly)
     d svcclp          s                   like(cccclp)
     d svlbpdt         s                   like(cclbpdt)
     d svlepdt         s                   like(cclepdt)
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D countavg        s              3  0
     D wkdays          s              5  1
      *
      * Workfields for date manipulation
      *
     D wkisoepdt       s               d   datfmt(*iso)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  Definition for external data area 'DALPER' which holds the last
      *  period that was closed in HPS.
      *---------------------------------------------------------------
      *
      *   Remove use of dalper.  e5752
     D*dalper          uds
     D* daccyy                 1      4  0
     D* daper                  5      6  0
     D* dabpdt                 7     14  0
     D* daepdt                15     22  0
     D* dappfl                24     24
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Read each record in the Datamart BGF Farm file selected out for the company
      * being processed in the open query.  (HPBLDCD$)
      *
     C     xxcocd        setll     hplb018a
     C                   dou       *in90 = *on                                  Main do loop
     C     xxcocd        reade     hplb018a                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Populate Farm fields that are a direct map from the Datamart Farm file.
      * Then, accumulate the Datamart Daily Production data.
      *
     C                   exsr      $directmap
     C                   exsr      $accum
      *
      * Write record
     C                   write     w2rec
     C                   clear                   w2rec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      * EOF Processing
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Populate Farm fields
      *----------------------------------------------------------------
      *
     C     $directmap    begsr
      *
      * Populate Period fields from the Data Area
      *   Populate with values retrieved from company control file.  e5752
      *
     C**                 z-add     daccyy        w2acyr
     C**                 z-add     daper         w2acpe
     C**                 z-add     daepdt        w280epdt
     C                   z-add     svccly        w2acyr
     C                   z-add     svcclp        w2acpe
     C                   z-add     svlepdt       w280epdt
     C                   move      wkisoepdt     w2epdt
      *
     C                   z-add     fsfscd        w2fscd
     C                   move      fscocd        w2cocd
     C                   move      fsdvcd        w2dvcd
     C                   move      fsrgcd        w2rgcd
     C                   move      fsfsnm        w2fsnm
     C                   move      fsfsbo        w2fsbo
     C                   move      fsfmbo        w2fmbo
     C                   move      fsdzcd        w2dzcd
     C                   move      fsppcd        w2ppcd
     C                   move      fsptcd        w2ptcd
     C                   move      fscrcd        w2crcd
     C                   z-add     fscell        w2cell
     C                   move      fscellds      w2cellds
     C                   z-add     fspod         w2pod
     C                   move      fspodds       w2podds
     C                   move      fsgtcd        w2gtcd
     C                   move      fsmgcd        w2mgcd
     C                   move      fsmgnm        w2mgnm
     C                   move      fssvcd        w2svcd
     C                   move      fssvnm        w2svnm
     C                   move      fssacd        w2sacd
     C                   move      fsbscd        w2bscd
     C                   z-add     fscphd        w2cphd
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Retrieve Datamart Daily Production data for the Farm
      *----------------------------------------------------------------
      *
      * Records for the Period were selected with an Open Query over Daily Date.
      *
     C     $accum        begsr
      *
     C                   move      yes           first
     C                   z-add     0             wkdays
     C                   z-add     0             countavg
      *
      *
     C     fsfscd        setll     hplb082b
      *
     C                   dou       *in91 = *on                                  Do accum
     C     fsfscd        reade     hplb082b                               91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   add       bdwnsv        w2wnsv
     C                   add       bdlwnsv       w2lwnsv
     C                   add       bdrtsv        w2rtsv
     C                   add       bdglsv        w2glsv
      *
     C                   add       bdabhd        w2abhd
     C                   add       bdglabhd      w2glabhd
     C                   add       bdnipqt       w2nipqt
      *
     C                   add       bdgllt        w2gllt
     C                   add       bdglbahd      w2glbahd
     C                   add       bdglsbhd      w2glsbhd
     C                   add       bdglmmhd      w2glmmhd
     C                   add       bdtolt        w2tolt
     C                   add       bdbahd        w2bahd
     C                   add       bdsbhd        w2sbhd
     C                   add       bdmmhd        w2mmhd
      *
     C                   add       bdtolshd      w2tolshd
     C                   add       bdewnlt       w2ewnlt
     C                   add       bdwnlt        w2wnlt
     C                   add       bdtownlt      w2townlt
     C                   add       bdtownhd      w2townhd
      *
     C                   add       bdarglhd      w2arglhd
     C                   add       bdtiglhd      w2tiglhd
     C                   add       bdslglhd      w2slglhd
     C                   add       bdddglhd      w2ddglhd
     C                   add       bdeuglhd      w2euglhd
     C                   add       bdtoglhd      w2toglhd
      *
     C                   add       bdarsohd      w2arsohd
     C                   add       bdtisohd      w2tisohd
     C                   add       bdslsohd      w2slsohd
     C                   add       bdddsohd      w2ddsohd
     C                   add       bdeusohd      w2eusohd
     C                   add       bdtosohd      w2tosohd
     C                   add       bdoosohd      w2oosohd
      *
     C                   add       bdarbohd      w2arbohd
     C                   add       bdslbohd      w2slbohd
     C                   add       bdddbohd      w2ddbohd
      *
     C                   add       bdcuhd        w2cuhd
     C                   add       bdculb        w2culb
     C                   add       bddoahd       w2doahd
     C                   add       bdkoahd       w2koahd
      *
     C                   add       bdmanhr       w2manhr
      *
      * Take Beginning Piglet Inventory from the first record for the Farm.
      *
     C                   if        first = yes
     C                   move      no            first
     C                   z-add     bdbgplhd      w2bgplhd
     C                   endif
      *
      *
      * Do not accumulate values for the fields below; we want the value from the last
      * record in the Period:
      *  1) Ending Piglet Inventory
      *  2) Open Gilt Pool-Eligible
      *  3) Open Sow Pool
      *  4) Scheduled Head from Transfers to WTF Groups
      *
     C                   z-add     bdenplhd      w2enplhd
     C                   z-add     bdopglhd      w2opglhd
     C                   z-add     bdopsohd      w2opsohd
     C                   z-add     bdschd        w2schd
      *
      *
      * Accumulate Average Age of Piglet Weaned.
      * And, if this daily record had a value for 'Average Age of Piglet Weaned'
      * then increment your counter.
      *
     C                   add       bdadwn        wkdays
     C                   if        bdadwn <> 0
     C                   add       1             countavg
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do accum
      *
      *
      * Calculate 'Average Age of Piglet Weaned' as:
      *  Accumulated daily averages / the number of days with age entries
      *
     C                   if        countavg <> 0
     C     wkdays        div(h)    countavg      w2adwn
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *  Pass in company.  E5752
      *
     C     *entry        plist
     C                   parm                    xxcocd            3
      *
     C     key01         klist
     C                   kfld                    xxcocd
     C                   kfld                    svlepdt
      *
      * Read the company control file for the company being passed in to retrieve
      * the last HPS closed year/period.      E5752
      *
     C     xxcocd        chain     hsp0071                            92
     C                   if        *in92 = *off                                 If exists
     C                   move      ccccly        svccly                         last close year
     C                   move      cccclp        svcclp                         last close period
     C                   move      cclbpdt       svlbpdt                        last close beg date
     C                   move      cclepdt       svlepdt                        last close end date
     C                   endif                                                  If exists
      *
      * You will need the Period End date in "date format" for the Datamart file.
      *  use date from comapny control file instead of dalper. e5752
      *
     C     *iso          move      svlepdt       wkisoepdt
      *
      *
      * Always delete all existing records for the Period from the Datamart file.
      *  should only be deleting the records for the company being processed. E5752
      *
     C*    svlepdt       setll     hppb182
     C     key01         setll     hplb182a
     C                   dou       *in91 = *on                                  Do deletes
     C*    svlepdt       reade     hppb182                                91
     C     key01         reade     hplb182a                               91
     C                   if        *in91 = *off
     C                   delete    w2rec
     C                   endif
     C                   enddo                                                  Do deletes
      *
     C                   endsr
      *
