     A*%%TS  SD  20140121  131517  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        OMS DEVELOPMENT MODEL
     A*  DISPLAY FILE:  PBJ8PVR#
     A*  TITLE:         Print Warehouse Aged Inventory PV Prompt & Validate
     A*  PROGRAMMER:    LeAnne Ramsey
     A*  CREATED:       09/17/13
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R PBJ8PR1
     A*%%TS  SD  20140121  131517  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(44)
     A                                      SETOF(48)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(59)
     A                                      SETOF(59)
     A                                      SETOF(60)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  1 24'Inventory Management System'
     A            SDUSR         10A  O  1 60
     A            UDATE          6Y 0O  1 72EDTWRD('  /  /  -')
     A                                  2  2'ENTER'
     A                                  2 13'Prompt for Warehouse Inventory--Ag-
     A                                      ed on Production Date'
     A                                      DSPATR(HI)
     A            SDSTM          6Y 0O  2 72EDTWRD('  :  :  ')
     A                                  4  5'Warehouse Type . . . . .'
     A            D1WHTYCD       2A  B  4 30
     A N30                                  DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  4 33'+'
     A                                  5  5'From Production Date . .'
     A            D1FMDY         6Y 0B  5 30EDTWRD('  /  /  -')
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                  6  5'To Production Date . . .'
     A            D1TMDY         6Y 0B  6 30EDTWRD('  /  /  -')
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A                                  7  5'Aging Days . . . . . . .'
     A            D1AGE          3Y 0B  7 30EDTCDE(M)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  8  5'Only print Items with an Available-
     A                                       to Sell quantity ..'
     A            D1YN           1A  B  8 60
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                  8 64'(Y=Yes, N=No)'
     A                                  9  5'Only print Summary Page ..........-
     A                                      ....................'
     A            D1SUMYN        1A  B  9 60
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                  9 64'(Y=Yes, N=No)'
     A                                 10  2'Optional Selections:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11  5'Select/Omit Warehouses .'
     A            D1SOFL         1A  B 11 30
     A N40                                  DSPATR(HI)
     A  40
     AO 60                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A                                 11 34'+'
     A                                 11 36'(Blank=All warehouses for the Type-
     A                                       above)'
     A                                 12 36'(S=Select or O=Omit the warehouses-
     A                                       below)'
     A                                 13 17'Warehouses .'
     A            D1WHCD1        3A  B 13 30
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 13 34'+'
     A            D1WHDS1       30A  O 13 37
     A            D1WHCD2        3A  B 14 30
     A N32                                  DSPATR(HI)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 14 34'+'
     A            D1WHDS2       30A  O 14 37
     A            D1WHCD3        3A  B 15 30
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                 15 34'+'
     A            D1WHDS3       30A  O 15 37
     A            D1WHCD4        3A  B 16 30
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                 16 34'+'
     A            D1WHDS4       30A  O 16 37
     A            D1WHCD5        3A  B 17 30
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                 17 34'+'
     A            D1WHDS5       30A  O 17 37
     A            D1WHCD6        3A  B 18 30
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                 18 34'+'
     A            D1WHDS6       30A  O 18 37
     A                                 20  5'From Item Code  . . . .'
     A            D1FITCD        7Y 0B 20 30EDTCDE(M)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A                                 20 39'+'
     A            D1FITDS       30A  O 20 41
     A                                 21  5'To Item Code  . . . . .'
     A            D1TITCD        7Y 0B 21 30EDTCDE(M)
     A N48                                  DSPATR(HI)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A                                 21 39'+'
     A            D1TITDS       30A  O 21 41
      *
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R PBJ8PK1
     A*%%TS  SD  20060418  143047  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt +'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R PBJ8PES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R PBJ8PEC                   SFLCTL(PBJ8PES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * FIELD LEVEL POP-UP WINDOWS FOR SYNON SELECTIONS
      *---------------------------------------------------------------
      * Warehouse Type
      * Condition: Fresh or Frozen
     A          R POPUP1                    TEXT('Fresh or Frozen          ')
     A                                      WINDOW(3 40 2 11 *NOMSGLIN)
     A                                      WDWBORDER( +
     A                                      (*COLOR BLU) +
     A                                      )
     A            P1WHTYCD       2Y 0B  1  1SNGCHCFLD
     A                                      CHECK(ER)
     A                                      CHOICE(1 'Fresh')
     A                                      CHOICE(2 'Frozen')
     A                                      CHCAVAIL((*COLOR WHT))
      *---------------------------------------------------------------
     A* Select/Omit Warehouses Flag
     A* Condition: *ALL values
     A          R POPUP2                    TEXT('*ALL values              ')
     A                                      WINDOW(10 40 3 11 *NOMSGLIN)
     A                                      WDWBORDER( +
     A                                      (*COLOR BLU) +
     A                                      )
     A            P2SOFL         2Y 0B  1  1SNGCHCFLD
     A                                      CHECK(ER)
     A                                      CHOICE(1 'Select')
     A                                      CHOICE(2 'Omit')
     A                                      CHOICE(3 'Blanks')
     A                                      CHCAVAIL((*COLOR WHT))
