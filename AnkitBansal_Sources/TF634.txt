      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF634
      * TITLE:       Invoice
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/12/05
      *
      * FUNCTION:  Batch program to print an Invoice.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp005    if   e           k disk
      *    Charge codes
      *
      *
     Ftfp006    if   e           k disk
      *    System modules
      *
      *
     Ftfp020    uf   e           k disk
      *    Invoice header
      *
      *
     Ftfl021c   uf   e           k disk
      *    Invoice charges
      *
      *
     Ftfp097    if   e           k disk
      *    Status codes
      *
      *
     Ftfp098    if   e           k disk
      *    Charge frequency codes
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
      *
      *
      * Control fields
      *
     D firstchg        s              1    inz('Y')
      *
     D smtotfl         s              1    inz('Y')
     D inctotfl        s              1    inz('Y')
      *
      *
     D svinfl          s                   like(ccinfl)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
     D wkpodt          s              8  0
      *
      *
      *
      * Print fields
      *
     D h1indtmdy       s              6  0
     D h1dudtmdy       s              6  0
     D h1taskds        s              7
      *
     D h2desc          s             15
      *
     D l1ccds          s                   like(ccccds)
     D l1smds          s                   like(smsmds)
     D l1icdtmdy       s              6  0
      *
     D t1desc          s             14
      *
     D t2desc          s             47
     D t2am            s                   like(icicam)
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D aram            s             11  2 dim(1)
     D arunit          s             11  4 dim(1)
      *
      *
      * Array index
      *
     D x               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldtask                 1      1
     D  ldinsn                 2      8  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Retrieve Invoice Header
      *
     C     ldinsn        chain     tfp020                             92
     C                   if        *in92 = *off                                 If header
      *
      * Flip dates from CCYYMMDD to MMDDYY
      *
     C                   exsr      $dates
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Retrieve/print Invoice Charges
      *
     C                   exsr      $charges
      *
      * Print "Pay This Amount" line
      *
     C                   exsr      $t2tot
      *
      * If this is the actual print of the invoice (not a Preview or Reprint),
      * then update the Invoice Header record with Post data.
      *
     C                   if        ldtask = 'F'
     C                   move      'P'           ihinstcd
     C                   z-add     wkpodt        ihpodt
     C                   update    ihrec
     C                   endif
      *
     C                   endif                                                  If header
      *
      * EOF processing
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print heading of "Memo Charges" or "Invoice Charges
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        icinfl = 'Y'
     C                   eval      h2desc = 'Invoice Charges'
     C                   else
     C                   eval      h2desc = 'Memo Charges'
     C                   endif
      *
      * Print both lines twice to make them BOLD.
      *
     C                   except    h2hdr
     C                   except    h2hdr
      *
     C                   except    blank
      *
     C                   except    h2under
     C                   except    h2under
      *
     C                   except    blank
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Invoice Charge Detail Line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/Print Invoice Charges
      *---------------------------------------------------------------
      *
     C     $charges      begsr
      *
     C                   z-add     1             x
      *
     C     ihinsn        setll     tfl021c
     C                   dou       *in91 = *on                                  Do charges
     C     ihinsn        reade     tfl021c                                91
     C                   if        *in91 = *off                                 If not EOF
      *
      * First time break logic
      *
     C                   select
     C                   when      firstchg = yes
     C                   move      no            firstchg
     C                   move      icinfl        svinfl
     C                   exsr      $h2hdr
      *
      * Break on Memo/Invoice: Print totals for section and headings for next section
      *
     C                   when      icinfl <> svinfl
     C                   exsr      $t1tot
     C                   move      icinfl        svinfl
     C                   exsr      $h2hdr
     C                   endsl
      *
      * Detail processing:
      *    1) Retrieve System Module description
      *    2) Retrieve Charge Code description
      *    3) Flip Invoice Charge Date from CCYYMMDD to MMDDYY
      *
     C                   exsr      $smds
     C                   exsr      $ccds
      *
     C                   if        icicdt <> 0
     C     *iso          test(d)                 icicdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      icicdt        wkisodate
     C     *mdy          move      wkisodate     l1icdtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Accumulate totals into arrays and print detail line
      *
     C                   add       icicam        aram
     C                   add       icunit        arunit
      *
      * If this is an "include in invoice" charge, accumulate into the
      * "Pay This Amount" total.
      *
     C                   if        icinfl = 'Y'
     C                   add       icicam        t2am
     C                   endif
      *
     C                   exsr      $l1dtl
      *
      * If this is the actual print of the invoice (not a Preview or Reprint),
      * then update the Invoice Charge records with a Posted status.
      *
     C                   if        ldtask = 'F'
     C                   move      'P'           icinstcd
     C                   update    icrec
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do charges
      *
      * Print Total Line for last section
      *
     C                   if        firstchg = no
     C                   exsr      $t1tot
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Charge Code Info
      *---------------------------------------------------------------
      *
     C     $ccds         begsr
      *
     C     iccccd        chain     tfp005                             92
     C                   if        *in92 = *off
     C                   move      ccccds        l1ccds
     C                   else
     C                   move      'Unknown'     l1ccds
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Charge Code Info
      *---------------------------------------------------------------
      *
     C     $smds         begsr
      *
     C     icsmcd        chain     tfp006                             92
     C                   if        *in92 = *off
     C                   movel     smsmds        l1smds
     C                   else
     C                   move      'Unknown'     l1smds
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Total Line for Memo and Invoice Sections
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   if        svinfl = 'N'
     C                   eval      t1desc = '   Memo Total:'
     C                   else
     C                   eval      t1desc = 'Invoice Total:'
     C                   endif
      *
     C                   except    t1tot
      *
      * Clear array entry
     C                   z-add     0             aram(x)
     C                   z-add     0             arunit(x)
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print the "Pay This Amount" line
      *---------------------------------------------------------------
      *
     C     $t2tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   eval      t2desc = 'PAYABLE IN U.S. DOLLARS     '
     C                                      + 'PAY THIS AMOUNT--->'
      *
     C                   z-add     2             x
     C                   except    t2tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Flip Invoice Dates
      *---------------------------------------------------------------
      *
     C     $dates        begsr
      *
      * Flip Invoice Date from CCYYMMDD to MMDDYY
      *
     C                   if        ihindt <> 0
     C     *iso          test(d)                 ihindt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihindt        wkisodate
     C     *mdy          move      wkisodate     h1indtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip Due Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        ihdudt <> 0
     C     *iso          test(d)                 ihdudt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihdudt        wkisodate
     C     *mdy          move      wkisodate     h1dudtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   select
     C                   when      ldtask = 'P'
     C                   eval      h1taskds = 'PREVIEW'
      *
     C                   when      ldtask = 'F'
      *
     C                   when      ldtask = 'R'
     C                   eval      h1taskds = 'REPRINT'
     C                   endsl
      *
      * Save system date to use as Post Date when updating the Invoice Header.
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wkpodt
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                                           65 'INVOICE'
     O                       h1taskds           132
      *
     O          e            h1hdr          1
     O                                           14 'SEABOARD FOODS'
      *
     O          e            h1hdr          1
     O                                           25 '9000 W. 67th    Suite 200'
      *
     O          e            h1hdr          1
     O                                           26 'Shawnee Mission, KS  66201'
     O                                          122 'INVOICE DATE'
     O                       h1indtmdy          132 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           50 'PHONE: 913-261-2600'
     O                                          122 'DUE DATE'
     O                       h1dudtmdy          132 '  /  /  '
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            6 'System'
     O                                           45 'Charge'
     O                                           86 'Charge'
     O                                           97 'Charge'
      *
     O          e            h1hdr       0  1
     O                                            6 'Module'
     O                                           31 'Module Description'
     O                                           44 'Code'
     O                                           70 'Charge Description'
     O                                           86 'Number'
     O                                           96 'Date'
     O                                          114 'Amount'
     O                                          131 'Units'
      *
     O          e            h1hdr          2
     O                       dash               132
      *
      *-------------------------------------------------------------
      * "Memo Charges" vs "Invoice Charges" heading
      *-------------------------------------------------------------
      *
     O          e            h2hdr       0  0
     O                       h2desc              15
      *
     O          e            h2under     0  0
     O                                           15 '---------------'
      *
      *-------------------------------------------------------------
      * Detail line for Invoice Charges
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       icsmcd               4
     O                       l1smds         b    33
     O                       iccccd              49
     O                       l1ccds         b    77
     O                       icicsn        z     86
     O                       l1icdtmdy      b    97 '  /  /  '
     O                       icicam        k    115
     O                       icunit        k    132
      *
      *
      *
      *-------------------------------------------------------------
      * Total lines
      *-------------------------------------------------------------
      *
     O          e            t1tot       0  1
     O                                          114 '--------------'
     O                                          131 '--------------'
      *
     O          e            t1tot       0  1
      *
     O                       t1desc              97
     O                       aram(x)       kb   115
     O                       arunit(x)     kb   132
      *
      *-------------------------------------------------------------
      * Pay This Amount
      *-------------------------------------------------------------
      *
     O          e            t2tot       5  1
     O                       t2desc              97
     O                       t2am          kb   115
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     o          e            blank       1
      *
