/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF426CL                                       */
/*  PROGRAM NAME:    AR ADJUSTMENT LISTING                         */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     01/23/08                                      */
/*                                                                 */
/*  FUNCTION:        THIS REPORT IS GENERATED:                     */
/*                    1) ON-DEMAND                                 */
/*                    2) FROM THE WEEKLY REVENUE CLOSE FUNCTION    */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                 */
/* 01/29/08  LEANNE RAMSEY                                         */
/*           WE HAVE ADDED MORE SELECTIONS TO THE LISTING. SO,     */
/*           WE WILL NOW DO AN OPEN QUERY TO SELECT RECORDS.       */
/*                                                                 */
/* 05/06/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*                                                                 */
/* 09/10/09  LEANNE RAMSEY                                         */
/*           ADDED REPORT SELECTIONS: DETAIL/SUMMARY               */
/*                                    AR CUSTOMER                  */
/*                                                                 */
/* 10/01/09  LEANNE RAMSEY                                         */
/*           ADDED REPORT SELECTIONS: G/L POST METHOD CODE         */
/*                                    PAYOUT CODE                  */
/*                                                                 */
/* 02/07/18  ROSE CENTONZE                                        */
/*           ADDED NEW REPORT FOR COMPANY 440 ONLY - TF6260 -      */
/*           CALLED FROM PMT TF426 WHEN 440 IS SELECTED.  WHEN     */
/*           COMPANY ISNT ENTERED, THEN ONLY PRINT 360/960 IN      */
/*           TF426.   FROM WEEKLY REVENUE CLOSE, PRINT BOTH TF626  */
/*           AND TF6260                                            */
/*                                                                 */
/* 03/08/18  DANNY NGUYEN    (S12308)                              */
/*           ADDED 'STF' VERBIAGE TO OUTGOING EMAIL FOR 440 &      */
/*           INCLUDED NEW DL 'TF Revenue Close-STF.                */
/*                                                                 */
/* 05/10/21  DANNY NGUYEN    (R17061)                              */
/*           GET E1LIVE & M3LIVE FLAG FROM COMPANY VALUES. IF      */
/*           E1LIVE=Y OR M3LIVE='P' THEN CALL CLONED PROGRAM       */
/*           TF6260E WHICH WILL NO LONGER USE/PRINT M3 DIMS.       */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(235)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&EMAILORDL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&LDPFCD)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&TSTPRDFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&E1LIVE) TYPE(*CHAR) LEN(1)  /* R17061 */
             DCL        VAR(&M3LIVE) TYPE(*CHAR) LEN(1)  /* R17061 */

             DCL        VAR(&LDFWECYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTWECYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDCONO)    TYPE(*DEC)  LEN(3)
             DCL        VAR(&LDAJTY)    TYPE(*CHAR) LEN(3)
             DCL        VAR(&LDCC)      TYPE(*CHAR) LEN(12)
             DCL        VAR(&LDOBJ)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&LDSUB)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&LDCUNO)    TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDDSFL)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDPMCD)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDPOCD)    TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDPFCD)    VALUE(%SST(*LDA 37 1))
             CHGVAR     VAR(&LDFWECYMD) VALUE(%SST(*LDA 52 8))
             CHGVAR     VAR(&LDTWECYMD) VALUE(%SST(*LDA 60 8))
             CHGVAR     VAR(&LDCONO)    VALUE(%SST(*LDA 71 3))
             CHGVAR     VAR(&LDAJTY)    VALUE(%SST(*LDA 74 3))
             CHGVAR     VAR(&LDCC)      VALUE(%SST(*LDA 77 12))
             CHGVAR     VAR(&LDOBJ)     VALUE(%SST(*LDA 89 6))
             CHGVAR     VAR(&LDSUB)     VALUE(%SST(*LDA 95 8))
             CHGVAR     VAR(&LDCUNO)    VALUE(%SST(*LDA 103 7))
             CHGVAR     VAR(&LDDSFL)    VALUE(%SST(*LDA 140 1))
             CHGVAR     VAR(&LDPMCD)    VALUE(%SST(*LDA 141 1))
             CHGVAR     VAR(&LDPOCD)    VALUE(%SST(*LDA 172 1))

/*----------------------------------------------------------------*/
/* FOR SENDING EMAILS:                                            */
/* RETRIEVE DATA AREA THAT LETS US KNOW WHETHER WE ARE IN TEST    */
/* OR PRODUCTION (ALWAYS DEFAULT TO THE "TEST" DISTRIBUTION LIST.)*/
/*----------------------------------------------------------------*/

             CHGVAR     VAR(&EMAILORDL) VALUE('TF Test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)

/*----------------------------------------------------------------*/
/* RETRIEVE/SET PRINT DEFAULTS                                    */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&LDPFCD = 'F') THEN(CHGVAR VAR(&HOLD) +
                          VALUE(*YES))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&LDPFCD *NE 'D') THEN(DO)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             ENDDO
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*--------------------------------------------------------------------------*/
/*  Extract data from the TFS AR Adjustments file                           */
/*--------------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 11)) VALUE('AJRWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 13 8)) VALUE(&LDFWECYMD)
             CHGVAR     VAR(%SST(&QDATA 22 16)) VALUE('*AND AJRWEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 39 8)) VALUE(&LDTWECYMD)

/* IF COMPANY                                                       */

             IF         COND(&LDCONO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 48 15)) VALUE('*AND AJCONO *EQ')
             CHGVAR     VAR(%SST(&QDATA 64 3)) VALUE(&LDCONO)
             ENDDO
             ELSE DO
             CHGVAR     VAR(%SST(&QDATA 48 15)) VALUE('*AND AJCONO *NE')
             CHGVAR     VAR(%SST(&QDATA 64 3)) VALUE(440)
             ENDDO

/* IF ADJUSTMENT TYPE                                               */

             IF         COND(&LDAJTY *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 68 17)) VALUE('*AND AJAJTY *EQ "')
             CHGVAR     VAR(%SST(&QDATA 85 3)) VALUE(&LDAJTY)
             CHGVAR     VAR(%SST(&QDATA 88 1)) VALUE('"')
             ENDDO

/* IF COST CENTER                                                   */

             IF         COND(&LDCC *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 90 15)) VALUE('*AND AJCC *EQ "')
             CHGVAR     VAR(%SST(&QDATA 105 12)) VALUE(&LDCC)
             CHGVAR     VAR(%SST(&QDATA 117 1)) VALUE('"')
             ENDDO

/* IF OBJECT                                                        */
             IF         COND(&LDOBJ *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 119 16)) VALUE('*AND AJOBJ *EQ "')
             CHGVAR     VAR(%SST(&QDATA 135 6)) VALUE(&LDOBJ)
             CHGVAR     VAR(%SST(&QDATA 141 1)) VALUE('"')
             ENDDO

/* IF SUBSIDIARY                                                    */
             IF         COND(&LDSUB *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 143 16)) VALUE('*AND AJSUB *EQ "')
             CHGVAR     VAR(%SST(&QDATA 159 8)) VALUE(&LDSUB)
             CHGVAR     VAR(%SST(&QDATA 167 1)) VALUE('"')
             ENDDO

/* IF A/R CUSTOMER                                                  */
             IF         COND(&LDCUNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 169 17)) VALUE('*AND AJARCUNO *EQ')
             CHGVAR     VAR(%SST(&QDATA 187 7)) VALUE(&LDCUNO)
             ENDDO

/* IF G/L POST METHOD CODE                                          */
             IF         COND(&LDPMCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 195 17)) VALUE('*AND AJPMCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 212 1)) VALUE(&LDPMCD)
             CHGVAR     VAR(%SST(&QDATA 213 1)) VALUE('"')
             ENDDO

/* IF PAYOUT CODE                                                   */
             IF         COND(&LDPOCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 215 17)) VALUE('*AND AJPOCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 232 1)) VALUE(&LDPOCD)
             CHGVAR     VAR(%SST(&QDATA 233 1)) VALUE('"')
             ENDDO


/*--------------------------------------------------------------------------*/
/*  Run the Detail or Summary or Both the Detail and Summary Versions       */
/*--------------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/******************************************************************************/
/*  RUN FOR 360 -  960                                                        */
/******************************************************************************/

NOTSTF:     IF         COND(&LDCONO *NE 440) THEN(DO)

             OVRDBF     FILE(TFL026A)  SHARE(*YES)
             OPNQRYF    FILE((TFL026A)) QRYSLT(&QDATA) KEYFLD(*FILE)  +
                          SEQONLY(*YES)
/* Detail */
             IF         COND(&LDDSFL *EQ 'D' *OR &LDDSFL *EQ 'B') +
                          THEN(DO)
             CALL       PGM(TF626) PARM('D')
             ENDDO

/* Summary */
             IF         COND(&LDDSFL *EQ 'S' *OR &LDDSFL *EQ 'B') +
                          THEN(DO)
             CALL       PGM(TF626) PARM('S')
             ENDDO

             CLOF       OPNID(TFL026A)
             DLTOVR     FILE(TFL026A)

/*--------------------------------------------------------------------------*/
/* If this is a "Final" run out of the Weekly Revenue Close                 */
/* (and you are in the "production" environment),                           */
/*    e-mail the report to a distribution list                              */
/*    dup the report to another spool file                                  */
/*--------------------------------------------------------------------------*/

             IF         COND(&LDPFCD *EQ 'F') THEN(DO)          /* If Final */

             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAILORDL) VALUE('TF Revenue +
                          Close-Admin'))

             ESNDMAIL   RECIPIENT(&EMAILORDL) SUBJECT('AR Adjustment +
                          Listing') MSG('Distribution List: TF +
                          Revenue Close-Admin') ATTLIST((* *PDF *N +
                          PRINT198 *))
             MONMSG     MSGID(CPF0000)

             IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(TFPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDDO                                              /* If Final */
             ENDDO                                              /* NOTSTF */

/******************************************************************************/
/*  RUN FOR 440                                                               */
/******************************************************************************/

             IF COND((&LDCONO *EQ 0) *AND (&LDPFCD *EQ 'D')) THEN(GOTO ENDIT)

/* FORCE  RUN FOR 440 IN WEEKLY REV CLOSE  */
             IF COND((&LDCONO *EQ 0) *AND (&LDPFCD *EQ 'P')) THEN(GOTO STFRUN)
             IF COND((&LDCONO *EQ 0) *AND (&LDPFCD *EQ 'F')) THEN(GOTO STFRUN)

            IF         COND(&LDCONO *EQ 440) THEN(DO)  /* WILL BE FROM PMT  */
STFRUN:

             CHGVAR     VAR(%SST(&QDATA 48 15)) VALUE('*AND AJCONO *EQ')
             CHGVAR     VAR(%SST(&QDATA 64 3)) VALUE(440)
             OVRDBF     FILE(TFL026A)  SHARE(*YES)
             OPNQRYF    FILE((TFL026A)) QRYSLT(&QDATA) KEYFLD(*FILE)  +
                          SEQONLY(*YES)

/* R17061 - Get E1LIVE Flag from Company Values */
             CALL       PGM(POMTXFR) PARM('' X'440F' 'E1LIVE' &E1LIVE)
/* R17061 - Get M3LIVE Flag from Company Values */
             CALL       PGM(POMTXFR) PARM('' X'440F' 'M3LIVE' &M3LIVE)

/* Detail */
             IF         COND(&LDDSFL *EQ 'D' *OR &LDDSFL *EQ 'B') +
                          THEN(DO)
    /* R17061 - Check E1LIVE Flag to call JDE E1 program version. */

             IF         COND((&M3LIVE *EQ 'Y') *OR (&M3LIVE *EQ +
                          'P')) THEN(DO)
             CALL       PGM(TF6260) PARM('D')
             ENDDO
             IF         COND((&E1LIVE *EQ 'Y') *OR (&E1LIVE *EQ +
                          'P')) THEN(DO)
             CALL       PGM(TF6260E) PARM('D')
             ENDDO
             ENDDO

/* Summary */
             IF         COND(&LDDSFL *EQ 'S' *OR &LDDSFL *EQ 'B') +
                          THEN(DO)
    /* R17061 - Check E1LIVE Flag to call JDE E1 program version. */
             IF         COND(&E1LIVE *EQ 'Y') THEN(DO)
             CALL       PGM(TF6260E) PARM('S')
             ENDDO
             ELSE       CMD(DO)
             CALL       PGM(TF6260) PARM('S')
             ENDDO
             ENDDO

             CLOF       OPNID(TFL026A)
             DLTOVR     FILE(TFL026A)

             IF         COND(&LDPFCD *EQ 'F') THEN(DO)          /* If Final */

 /*S12308-Added 'tf revenue close-stf' DL. */
             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAILORDL) VALUE('TF Revenue +
                          Close-Admin, TF Revenue Close-STF'))

 /*S12308    ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('AR Adjustment +
                          Listing') MSG('Distribution List: TF +
                          Revenue Close-Admin') ATTLIST((* *PDF *N +
                          PRINT198 *))                                    */
 /*S12308*/  ESNDMAIL   RECIPIENT(&EMAILORDL) SUBJECT('AR Adjustment +
                          Listing-STF-440') MSG('Distribution List: +
                          TF Revenue Close-Admin, TF Revenue +
                          Close-STF') ATTLIST((* *PDF *N PRINT198 +
                          *))
             MONMSG     MSGID(CPF0000)

             IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(TFPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDDO                                              /* If Final */
             ENDDO

ENDIT:       DLTOVR     FILE(*ALL)

             ENDPGM

