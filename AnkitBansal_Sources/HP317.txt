      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Cost Movements: Workfile: Build Driver Movement Detail File
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     04/16/03
      *
      * FUNCTION:    This function builds a driver file of transfer and sale movement detail
      *              records that will ultimately need to be costed.
      *
      ********************************************************************************************
      * MODIFICATIONS:
      ********************************************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ********************************************************************************************
      * FILE SPECIFICATIONS
      ********************************************************************************************
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsl064b   if   e           k disk
      *    Check detail
      *
      *
     Fhsp074    if   e           k disk
      *    Transfer movement header
      *
      *
     Fhsp075    if   e           k disk
      *    Transfer movement detail
      *
      *
     Fhsp084    if   e           k disk
      *    Sales movement header
      *
      *
     Fhsp085    if   e           k disk
      *    Sales movement detail
      *
      *
     Fhsp362    if a e           k disk
      *    Costed movements: Workfile-Movement Detail
      *
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Process transfer movements selected by open query
      *
     C                   exsr      $transfers
      *
      * Process sales movements selected by open query
      *
     C                   exsr      $sales
      *
      * EOF processing
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Process Transfer Movements
      *----------------------------------------------------------------
      *
     C     $transfers    begsr
      *
      * Read each transfer movement header record selected by the open query.
      * DO NOT process BGF-to-BGF transfers.
      * DO NOT process transfers with no quality or reject head.
      *
      * The open query has already selected transfers with:
      *      1) a received date in the current period
      *      2) a status of SH=Shipped
      *
     C     *loval        setll     hsp074
      *
     C                   dou       *in90 = *on                                  Do transfers
     C                   read      hsp074                                 90
     C                   if        *in90 = *off                                 If read
      *
     C                   if        mhorpp = 'BGF  ' and                         If BGF-to-BGF
     C                             mhdnpp = 'BGF  '
     C                   else
      *
      * Set up workfile fields populated from the header record
      *
     C                   move      'T'           wdmtcd
     C                   z-add     mhrcdt        wdrcdt
     C                   z-add     mhmvsn        wdmvsn
     C                   z-add     mhorfs        wdorfscd
     C                   move      mhorpp        wdorppcd
     C                   z-add     mhdnfs        wddnfscd
     C                   move      mhdnpp        wddnppcd
      *
      * Process transfer detail records
      *
     C     mhmvsn        setll     hsp075
      *
     C                   dou       *in91 = *on                                  Do detail
     C     mhmvsn        reade     hsp075                                 91
     C                   if        *in91 = *off and                             If not eof
     C                             (mdqlhd <> 0 or mdrjhd <> 0)
      *
     C                   z-add     mdlnno        wdlnno
     C                   z-add     mdorsn        wdorhgsn
     C                   z-add     mddnsn        wddnhgsn
      *
     C                   z-add     mddohd        wddohd
     C                   z-add     mddoahd       wddoahd
     C                   z-add     mdkoahd       wdkoahd
      *
     C     mdqlhd        add       mdrjhd        wdhd
     C     mdqllb        add       mdrjlb        wdlb
      *
     C                   write     wdrec
     C                   endif                                                  If not eof
     C                   enddo                                                  Do detail
      *
     C                   endif                                                  If BGF-to-BGF
     C                   endif                                                  If read
     C                   enddo                                                  Do transfers
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Process Sales Movements
      *----------------------------------------------------------------
      *
     C     $sales        begsr
      *
      * Read each sales movement header record selected by the open query.
      * The open query has already limited sales to those with:
      *      1) a received date in the current period
      *      2) a status of Fully Paid
      *      3) a sales type of Feeder Pig or Market
      *
     C     *loval        setll     hsp084
      *
     C                   dou       *in90 = *on                                  Do sales
     C                   read      hsp084                                 90
     C                   if        *in90 = *off                                 If read
      *
     C                   move      'S'           wdmtcd
     C                   z-add     shmvsn        wdmvsn
     C                   z-add     shrcdt        wdrcdt
     C                   z-add     shfscd        wdorfscd
      *
     C                   move      *blank        wddnppcd
     C                   z-add     0             wddnfscd
     C                   z-add     0             wddnhgsn
      *
      * Process sales  detail records
      *
     C     shmvsn        setll     hsp085
      *
     C                   dou       *in91 = *on                                  Do detail
     C     shmvsn        reade     hsp085                                 91
     C                   if        *in91 = *off                                 If not eof
      *
      * Retrieve group's Production Phase; do not process a detail line
      * out of a BGF group.
      *
     C     sghgsn        chain     hsl034d                            92
     C                   if        *in92 = *off and hgppcd <> 'BGF  '           If continue
      *
     C                   z-add     0             wdlnno
     C                   move      hgppcd        wdorppcd
     C                   z-add     sghgsn        wdorhgsn
      *
     C                   z-add     sglvhd        wdhd
     C                   z-add     sgdohd        wddohd
     C                   z-add     sgdohd        wddoahd
     C                   z-add     sglvlb        wdlb
      *
      *  Retrieve carcass pounds for movement/group
      *
     C                   z-add     0             wdcrlb
     C     key01         setll     hsl064b
      *
     C                   dou       *in93 = *on                                  Do check
     C     key01         reade     hsl064b                                93
     C                   if        *in93 = *off                                 If read
     C                   add       cdcrlb        wdcrlb
     C                   endif                                                  If read
     C                   enddo                                                  Do check
      *
     C                   write     wdrec
     C                   endif                                                  If continue
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do detail
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do sales
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    sgmvsn
     C                   kfld                    sghgsn
      *
      *
     C                   endsr
      /EJECT
