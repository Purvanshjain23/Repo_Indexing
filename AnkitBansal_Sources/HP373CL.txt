/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP373CL                                       */
/*  PROGRAM NAME:    PRELIMINARY EDITS FOR CLOSING PERIOD          */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/24/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED FROM THE EOP FUNCTION AND   */
/*                   ALSO FROM AN ON-DEMAND FUNCTION.              */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 02/26/16    Rose Centonze    E5290                              */
/*             Added company code parm and add to opnqry select    */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&ERROR &ppcocd)


             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(100)

             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PASS) TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 9 8))
             CHGVAR     VAR(&PASS) VALUE('F')


/*********************************************************************/
/*  CLEAR MOVEMENT WORKFILE                                          */
/*********************************************************************/

             CLRPFM     FILE(HSP320)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(CANTCLEAR))

/*********************************************************************/
/*  SETUP OPNQRY STATEMENTS                                          */
/*********************************************************************/

/*  SETUP OPNQRY STATEMENT FOR EXPENSE TICKETS                       */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('XTXEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND XTXEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND XTCOCD +
                          *EQ')                                    /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSP048) SHARE(*YES)
             OPNQRYF    FILE((HSP048)) QRYSLT(&QDATA) +
                          KEYFLD((XTFSCD) (XTRPCD) (XTXTSN)) +
                          SEQONLY(*YES)


/*  SETUP OPNQRY STATEMENT FOR FEED TICKET HEADER                 */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FHTKDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND FHTKDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

  /* E5290 JOIN TO HSP009 WITH THE REGION TO GET THE COMPANY,THEN SELECT  */

      /*     OPNQRYF    FILE((HSP037)) QRYSLT(&QDATA)            *ORIGINAL*  +
                          KEYFLD((FHFSCD) (FHTKNO) (FHTRCD)) +
                          SEQONLY(*YES)                            */
             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND BOCOCD +
                          *EQ')                                     /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSP037) SHARE(*YES)
             OPNQRYF    FILE((HSP037) (HSP009)) FORMAT(HSP037) +
                          QRYSLT(&QDATA) KEYFLD((FHFSCD) (FHTKNO) +
                          (FHTRCD)) JFLD((HSP037/FHRGCD +
                          HSP009/BORGCD)) SEQONLY(*YES)


/*  SETUP OPNQRY STATEMENT FOR DISPOSED HOG GROUPS                */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('HGDSDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND HGDSDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND HGCOCD +
                          *EQ')                                     /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSL034G) SHARE(*YES)
             OPNQRYF    FILE((HSL034G)) QRYSLT(&QDATA) +
                          KEYFLD((HGHGCD)) SEQONLY(*YES)


/*  SETUP OPNQRY STATEMENT FOR MOVEMENT EVENT FILE                */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MEMEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MEMEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

  /* E5290 JOIN TO HSP034 WITH THE SYSTEM HOG GROUP TO GET THE COMPANY, SEL */

             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND HGCOCD +
                          *EQ')                                     /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)
             OVRDBF     FILE(HSP058) SHARE(*YES)
     /*      OPNQRYF    FILE((HSP058)) QRYSLT(&QDATA) +         ORIGINAL  +
                          KEYFLD((MEMVSN) (MEODFL *DESCEND) +
                          (MEHGSN)) SEQONLY(*YES)                   */
             OPNQRYF    FILE((HSP058) (HSP034)) FORMAT(HSP058) +
                          QRYSLT(&QDATA) KEYFLD((MEMVSN) (MEODFL +
                          *DESCEND) (MEHGSN)) JFLD((HSP058/MEHGSN +
                          HSP034/HGHGSN *EQ)) SEQONLY(*YES)


/*********************************************************************/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*********************************************************************/

/* AS OF MARCH 2004, USERS ASKED US TO ALWAYS PUT OUTPUT ON SAVE/HOLD */

             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&HOLD) VALUE(*YES)

/* SET PARAMETERS FROM LDA                                           */

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*********************************************************************/
/*  CALL PROGRAM TO DO PRELIMINARY EDITS AND BUILD MOVEMENT WORKFILE */
/*********************************************************************/


             OVRPRTF    FILE(QPRINT) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD) SAVE(&SAVE) SHARE(*YES)
             CALL       PGM(HP320) PARM(&ERROR &PASS)
             DLTOVR     FILE(QPRINT)

             CLOF       OPNID(HSL034G)
             CLOF       OPNID(HSP037)
             CLOF       OPNID(HSP048)
             CLOF       OPNID(HSP058)

             DLTOVR     FILE(HSL034G)
             DLTOVR     FILE(HSP037)
             DLTOVR     FILE(HSP048)
             DLTOVR     FILE(HSP058)

             GOTO       CMDLBL(EXIT)

 CANTCLEAR:
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** Your CLOSE PERIOD function did +
                          not run because file HSP320 could not be +
                          cleared.  Please call IS in Kansas City +
                          to alert them to the problem. Thank you. +
                          *****') TOUSR(&USER)
             CHGVAR     VAR(&ERROR) VALUE('Y')

 EXIT:

             ENDPGM

