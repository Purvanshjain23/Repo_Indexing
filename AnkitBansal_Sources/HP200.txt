      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS Data Mart
      * PROGRAM:     HP200 - Build DataMart BGF Farms from HPS
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/02/00
      *
      * FUNCTION:    This file in the Data Mart will be re-created with
      *              each load of the Data Mart.
      *
      *              Only BGF farms are included.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE       PROGRAMMER
      *
      * 01//29/01  LeAnne Fedor
      *            Production type was removed from the Datamart Farm file.
      *            It is now in the Weekly Production file (HPPB092).
      *
      * 06/05/01  LeAnne Fedor
      *           Added new field 'multisite' to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Three fields (manager/supervisor/multisite) renamed in HPS Farm Site file.
      *           Manager and supervisor fields removed from HPS Hog Group file.
      *
      * 02/02/02  LeAnne Fedor
      *           Added a call to the new generic program that retrieves 'total square feet'
      *           for a farm.
      *
      * 02/06/02  LeAnne Fedor
      *           Added a 'pod' logic.
      *
      * 09/29/03  Barb Gutierrez
      *           We're making all datamart farm masters identical  Therefore we needed to bring
      *           the production type code back into this program.  We also added a sequence number
      *           for the production phase code so we could sort it in cognos according to the
      *           phase instead of alphabetical. (bgf/nur/gf versus bgf/gf/nur)
      *
      * 10/27/03  LeAnne Fedor
      *           Added 'County'.
      *
      * 12/28/04  LeAnne Fedor
      *           Added logic to calculate Farm Size.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp016    if   e           k disk
      *  Manager
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsl034a   if   e           k disk
      *  Hog group
      *
      *
     Fhsp019    if   e           k disk
      *  Farm buildings
      *
      *
     Fhppb018   o    e             disk    rename(fsrec:fsrec1) prefix(p1)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D process         s              1
     D start1          s              1  0
      *
      * Work fields
      *
     D wkalphfscd      s              5
     D wkalphcell      s              2
     D wkalphpod       s              3
      *
      *
      * Parm fields
      *
     D xxfssqft        s              7  0
     D xxsize          s                   like(p1fssize)
     D xxcphd          s                   like(p1fscphd)
     D xxppcd          s                   like(fsppcd)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process BGF farm records in the Hog Production System
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off and                             If not EOF
     C                             fsppcd = 'BGF'
      *
      * Populate fields that map directly file-to-file
      *
     C                   z-add     fsfscd        p1fsfscd
     C                   move      fscocd        p1fscocd
     C                   move      fsdvcd        p1fsdvcd
     C                   move      fsrgcd        p1fsrgcd
     C                   move      fsfsnm        p1fsfsnm
     C                   move      fsfsbo        p1fsfsbo
     C                   move      fsfmbo        p1fsfmbo
     C                   move      fsdzcd        p1fsdzcd
     C                   move      fsppcd        p1fsppcd
     C                   z-add     1             p1fsppcdsq
     C                   move      fsptcd        p1fsptcd
     C                   move      fscrcd        p1fscrcd
     C                   z-add     fscell        p1fscell
     C                   z-add     fspod         p1fspod
     C                   move      fsgtcd        p1fsgtcd
     C                   move      fspmgcd       p1fsmgcd
     C                   move      fscmgcd       p1fssvcd
     C                   move      fsmmgcd       p1fsmlcd
     C                   move      fscjd         p1fscjd
     C                   z-add     fspajd        p1fspajd
     C                   z-add     fsfajd        p1fsfajd
     C                   move      fscnty        p1fscnty
     C                   move      fssacd        p1fssacd
      *
      *
      * Call the generic program to retrieve total square feet on the farm.
      *
     C                   call      'HP8003'
     C                   parm                    fsfscd
     C                   parm      0             xxfssqft
      *
      *  Calculate hog capacity for the farm as:
      *      total square feet on the farm / target square feet per pig
      * And, if you have any Hog Capacity:
      *      calc Farm Size based on Production Phase/Hog Capacity
      *
     C                   if        fshdsqft <> 0                                If feet
     C     xxfssqft      div(h)    fshdsqft      p1fscphd
      *
     C                   if        p1fscphd <> 0
     C                   call      'HP8018'
     C                   parm      fsppcd        xxppcd
     C                   parm      p1fscphd      xxcphd
     C     p1fssize      parm      *blank        xxsize
     C                   endif
     C                   endif                                                  If feet
      *
      * Retrieve 'production manager name' and concatenate with code
      * into a single name field.
      *
     C     fspmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If found
     C                   eval      p1fsmgnm = %trim(fspmgcd) + ' ' +
     C                             %trim(mgmgnm)
     C                   endif                                                  If found
      *
      * Retrieve 'cell manager name' and concatenate with code
      * into a single name field.
      *
     C     fscmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If found
     C                   eval      p1fssvnm = %trim(fscmgcd) + ' ' +
     C                             %trim(mgmgnm)
     C                   endif                                                  If found
      *
      * Retrieve 'multisite manager name' and concatenate with code
      * into a single name field.
      *
     C     fsmmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If found
     C                   eval      p1fsmlnm = %trim(fsmmgcd) + ' ' +
     C                             %trim(mgmgnm)
     C                   endif                                                  If found
      *
      * Concatenate the word 'farm' and the farm site number into
      * a single 'description' field.
      *
     C                   movel     fsfscd        wkalphfscd
      *
     C                   move      yes           process
     C                   z-add     1             start1
      *
     C                   dow       process = yes and start1 <= 5                Do farm loop
     C                   if        %subst(wkalphfscd: start1: 1) = '0'          If replace
     C                   eval      %subst(wkalphfscd: start1: 1) = *blank
     C                   else
     C                   move      no            process
     C                   endif                                                  If replace
     C                   add       1             start1
     C                   enddo                                                  Do farm loop
      *
     C                   eval      p1fsfsds = 'FARM' + ' ' + %trim(wkalphfscd)
      *
      * Concatenate the word 'cell' and the cell number into
      * a single 'description' field.
      *
     C                   movel     fscell        wkalphcell
      *
     C                   move      yes           process
     C                   z-add     1             start1
      *
     C                   dow       process = yes and start1 <= 2                Do cell loop
     C                   if        %subst(wkalphcell: start1: 1) = '0'          If replace
     C                   eval      %subst(wkalphcell: start1: 1) = *blank
     C                   else
     C                   move      no            process
     C                   endif                                                  If replace
     C                   add       1             start1
     C                   enddo                                                  Do cell loop
      *
     C                   eval      p1fscellds = 'CELL' + ' ' + %trim(wkalphcell)
      *
      *
      * Concatenate the word 'pod' and the pod number into
      * a single 'description' field.
      *
     C                   movel     fspod         wkalphpod
      *
     C                   move      yes           process
     C                   z-add     1             start1
      *
     C                   dow       process = yes and start1 <= 3                Do pod loop
     C                   if        %subst(wkalphpod: start1: 1) = '0'           If replace
     C                   eval      %subst(wkalphpod: start1: 1) = *blank
     C                   else
     C                   move      no            process
     C                   endif                                                  If replace
     C                   add       1             start1
     C                   enddo                                                  Do pod  loop
      *
     C                   eval      p1fspodds = 'POD' + ' ' + %trim(wkalphpod)
      *
      *
      * Retrieve any hog group record for this farm and populate:
      *    1) breed stock
      *
     C     fsfscd        chain     hsl034a                            92
     C                   if        *in92 = *off
     C                   move      hgbscd        p1fsbscd
     C                   endif
      *
      *
      *
      * Write record
      *
     C                   write     fsrec1
     C                   clear                   fsrec1
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
