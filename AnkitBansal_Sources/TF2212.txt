      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF2212 -Datamart MARGIN: Build Weekly Mix Adjustments
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     02/07/08
      *
      * Function:    This program builds one record for each Week/TF Class Group.
      *              Only Finished Goods data is included.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/02/10  LeAnne Ramsey
      *           Added a field to TFP8015:
      *              ADWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      * 05/07/24  Santosh Patil P310149 - Field length is increased
      *           in TFP015. Function is recompiled only.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl015b   if   e           k disk    rename(mwrec:mwrecb) prefix(p1)
      *  Mix summary--weekly
      *  (LF selects only Finished Goods)
      *
      *
     Ftfp8015   o    e           k disk
      *  Datamart TFS: Weekly mix adjustments
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
     D svwedt          s                   like(mw80wedt)
     D svtfcgcd        s                   like(mwtfcgcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Workfields
      *
     D wkcono          s                   like(mwcono)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Definition for external data area DATFMARG
      *---------------------------------------------------------------
     D
     Ddatfmarg        uds
     D  da099dt               30     37  0
     D  dadmdt                80     87  0
     D  dasbmfl              100    100
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************************************
      * Mainline
      ****************************************************************************************
      *
      * The TFS Mix Summary records have both Seaboard and Triumph data in a single
      * record. For the Datamart file, we must write separate records for the 2 companies.
      * Since we will be processing few records for each week, we will make 2 passes
      * thru the file (instead of defining/using workfields for accumulating).
      *
     C                   z-add     360           wkcono
     C                   move      yes           first
     C                   exsr      $summarize
      *
     C                   z-add     960           wkcono
     C                   move      yes           first
     C                   exsr      $summarize
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------------
      * Process the TF Mix Summary Weekly records
      *-----------------------------------------------------------------------------------------
      *
     C     $summarize    begsr
      *
     C     dadmdt        setgt     tfl015b
      *
     C                   dou       *in90 = *on or p1mwwedt > da099dt            Do summary
     C                   read      tfl015b                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             p1mwwedt <= da099dt
      * Control break
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   exsr      $directmap
      *
     C                   when      p1mwwedt   <> svwedt or
     C                             p1mwtfcgcd <> svtfcgcd
     C                   exsr      $wrt8015
     C                   exsr      $directmap
     C                   endsl
      *
      * Detail processing
     C                   exsr      $detail
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do summary
      *
      * EOF processing
      *
     C                   if        first = no
     C                   exsr      $wrt8015
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Detail processing to accumulate values
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C                   select
     C                   when      wkcono = 360
     C                   add       p1mwspulb     mwpulb
     C                   add       p1mwspuam     mwpuam
     C                   add       p1mwspuslb    mwpuslb
     C                   add       p1mwspumam    mwpumam
     C                   add       p1mwspulam    mwpulam
     C                   add       p1mwspukam    mwpukam
     C                   add       p1mwspuiam    mwpuiam
     C                   add       p1mwspuoam    mwpuoam
     C                   add       p1mwspupam    mwpupam
     C                   add       p1mwspumgam   mwpumgam
     C                   add       p1mwspuwmgam  mwpuwmgam
     C                   add       p1mwpumixam   mwpumixam
     C                   add       p1mwstrpam    mwstrpam
     C                   add       p1mwputotam   mwputotam
      *
     C                   when      wkcono = 960
     C                   add       p1mwtpulb     mwpulb
     C                   add       p1mwtpuam     mwpuam
     C                   add       p1mwtpuslb    mwpuslb
     C                   add       p1mwtpumam    mwpumam
     C                   add       p1mwtpulam    mwpulam
     C                   add       p1mwtpukam    mwpukam
     C                   add       p1mwtpuiam    mwpuiam
     C                   add       p1mwtpuoam    mwpuoam
     C                   add       p1mwtpupam    mwpupam
     C                   add       p1mwtpumgam   mwpumgam
     C                   add       p1mwtpuwmgam  mwpuwmgam
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a Datamart "Weekly Mix Adjustment" records
      *---------------------------------------------------------------
      *
     C     $wrt8015      begsr
      *
     C                   write     mwrec
     C                   clear                   mwrec
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Set up Datamart fields and save control fields
      *--------------------------------------------------------------------------------------
      *
     C     $directmap    begsr
      *
      * Save control fields
     C                   z-add     p1mwwedt      svwedt
     C                   move      p1mwtfcgcd    svtfcgcd
      *
      * Set up Datamart fields that are a direct map from the Mix Summary--Weekly file
      *
     C                   z-add     wkcono        mwcono
     C                   move      p1mwitycd     mwitycd
     C                   move      p1mwtfcgcd    mwtfcgcd
     C                   move      p1mwtfclcd    mwtfclcd
     C                   move      p1mwistycd    mwistycd
     C                   z-add     p1mwisgrcd    mwisgrcd
     C                   z-add     p1mwisclcd    mwisclcd
     C                   move      p1mwmixgrp    mwmixgrp
      *
      * Dates
     C                   z-add     p1mwyr        mwyr
     C                   z-add     p1mwwk        mwwk
     C                   z-add     p1mwwbdt      mw80wbdt
     C                   z-add     p1mwwedt      mw80wedt
      *
      * Get dates into the "date" format.
      *
     C     *iso          move      mw80wbdt      wkisodate
     C                   move      wkisodate     mwwbdt
      *
     C     *iso          move      mw80wedt      wkisodate
     C                   move      wkisodate     mwwedt
     C                   move      mwwedt        mwwedtmdcy
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /eject
