     A*%%TS  SD  20030227  094349  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD750  VIEW FARM SITE
     A*  RPG PROGRAM:   HP750
     A*  CREATED BY:    ROSE CENTONZE
     A*  DATE:          04/04/94
     A*
     A*****************************************************************
     A* MODIFICATIONS
     A* 04/19/95  LEANNE FEDOR
     A*           ADDED 'CELL' TO SCREEN.
     A* 05/15/96  LEANNE FEDOR
     A*           REMOVED J.D. EDWARD ADDRESS FIELDS FOR FARM SITE.
     A*
     A* 02/10/16  SUSAN BLAKEY
     A*           ADD MTECH REFERENCE - FOR FARM REFERENCE
     A*           DISPLAY ONLY.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'previous')
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R HP750R1
     A*%%TS  SD  20030227  094349  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 29'VIEW FARM SITE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site:'
     A            DFFSCD    R        O  4 13REFFLD(HSRREF/FSFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  4 22'Name:'
     A            DFFSNM    R        O  4 28REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  4 58'Business office:'
     A            DFFSBO    R        O  4 75REFFLD(HSRREF/FSFSBO *LIBL/HSPREF)
     A                                  5  7'Cell:'
     A            DFCELL    R        O  5 16REFFLD(FSCELL)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  5 20'County:'
     A            DFCNTY    R        O  5 28REFFLD(HSRREF/@@CNTY *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFPOD     R        O  6 15REFFLD(HSRREF/@@POD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                  6 21'State:'
     A            DFSACD    R        O  6 28REFFLD(HSRREF/@@SACD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A                                  6 40'Farm Reference'
     A            DFMTEC    R        O  6 55REFFLD(HSRREF/@@MTECHREF *LIBL/HSPR-
     A                                      EF)
     A                                      DSPATR(HI)
     A                                  7  3'Defaults:'
     A                                  7 16'Transportation office:'
     A            DFTOCD    R        O  7 40REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFTONM    R        O  7 49REFFLD(HSRREF/@@TONM *LIBL/HSPREF)
     A                                  8 22'Production type:'
     A            DFPTCD    R        O  8 40REFFLD(HSRREF/FSPTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFPTDS    R        O  8 49REFFLD(HSRREF/PTPTDS *LIBL/HSPREF)
     A                                  9 21'Production phase:'
     A            DFPPCD    R        O  9 40REFFLD(HSRREF/FSPPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFPPDS    R        O  9 49REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A                                 10 27'Group type:'
     A            DFGTCD    R        O 10 40REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFGTDS    R        O 10 49REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                 11  2'Type of farm ...............:'
     A            DFTFCD    R        O 11 33REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFTFDS    R        O 11 49REFFLD(HSRREF/@@TFDS *LIBL/HSPREF)
     A                                 12  2'Consolidated farm ..........:'
     A            DFCNFSCD  R        O 12 33REFFLD(FSREC/FSCNFSCD *LIBL/HSP018)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                 13  2'Production manager .........:'
     A            DFPMGCD   R        O 13 33REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFPMGNM   R        O 13 49REFFLD(HSRREF/@@MGNM *LIBL/HSPREF)
     A                                 14  2'Cell manager ...............:'
     A            DFCMGCD   R        O 14 33REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFCMGNM   R        O 14 49REFFLD(HSRREF/@@MGNM *LIBL/HSPREF)
     A                                 15  2'Multisite manager ..........:'
     A            DFMMGCD   R        O 15 33REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFMMGNM   R        O 15 49REFFLD(HSRREF/@@MGNM *LIBL/HSPREF)
     A                                 16  2'G/L cost center ............:'
     A            DFCJD     R        O 16 33REFFLD(HSRREF/FSCJD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFCCDS        30A  O 16 49TEXT('JDE cost center description')
     A                                 17  2'Company relationship........:'
     A            DFCRCD    R        O 17 33REFFLD(HSRREF/FSCRCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFCRDS    R        O 17 49REFFLD(HSRREF/CRCRDS *LIBL/HSPREF)
     A                                 18  2'Feed mill business office ..:'
     A            DFFMBO    R        O 18 33REFFLD(HSRREF/FSFMBO *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFFMDS    R        O 18 49REFFLD(HSRREF/BOBODS *LIBL/HSPREF)
     A                                      TEXT('feed mill BUSINESS OFFICE DES-
     A                                      CRIPTION')
     A                                 19  2'Feed delivery zone .........:'
     A            DFDZCD    R        O 19 33REFFLD(HSRREF/FSDZCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFDZDS    R        O 19 49REFFLD(HSRREF/DZDZDS *LIBL/HSPREF)
     A                                 20  2'Farm site JDE number .......:'
     A            DFFAJD    R     Y  O 20 33REFFLD(HSRREF/FSFAJD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            DFFJNM        30A  O 20 49TEXT('JDE NAME')
     A                                 21  2'Producer JDE number ........:'
     A            DFPAJD    R     Y  O 21 33REFFLD(HSRREF/FSPAJD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            DFPJNM        30A  O 21 49TEXT('producer name from JDE')
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP750ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP750EC                   SFLCTL(HP750ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP750K1
     A*%%TS  SD  19950228  134012  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
