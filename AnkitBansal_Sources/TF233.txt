     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      TF Fees & Payments - Margin Adjustments
      * PROGRAM:     TF233
      * TITLE:       NVA Volume Margin Adjustments
      * PROGRAMMER:  Alice Brownfield
      * CREATED:     11/24/08
      *
      * FUNCTION:    The driver file for this program is TF Margin Adjustment Group Detail.
      *              The logic only processes records where:
      *                   TF Classification = NVA
      *                   Include in Volume = Yes
      *
      *              A record is output at the Margin Adjustment MIX group level.
      *
      *   Alice's NOTE:  by the time we got this installed they had discontinued the
      *                  volume adjustments for NVA's.  They may/will need this in
      *                  the future.  Since it really hasn't been fully run in parallel
      *                  mode we'll want to do that when they start using it again.
      *
      ***************************************************************************************
      * MODIFICATIONS:
      ***************************************************************************************
      * DATE      PROGRAMMER
      *
      * 12/01/08  LeAnne Ramsey
      *           File TFP050 renamed from Cold Carcass Pounds to Kill/Cut Data.
      *           Fields were added to the file.
      *           The 2-letter prefix was changed from "CP" to "KC".
      *
      * 05/02/12  LeAnne Ramsey (E2077)
      *           Damon Ginther is adding Riblets to the ByProduct side of this.
      *           To handle that, we added a new field to ROP125.
      *              YVT72APR-Special Trim Price/LB
      *           I have added code to populate the new field.
      *           BUT, we are still not using this for NVA (see Alice's note above).
      *
      * 09/05/12  LeAnne Ramsey (E2243)
      *           Recompile only.
      *           Per Damon G. we must now write more/special records to TFP014 for the
      *           Items that receive lbs/dollars from Skirt Meat Items!! So, I had to add
      *           a new field to TFP014:
      *                         ADSMSFL-Skirt Meat Split Flag
      *
      * 11/09/17  Danny Nguyen    - R12011-Kill Cut Data for STF
      *           File TFP050 was changed. Recompile only. No logic change.
      *
      * 02/07/22  Danny Nguyen    - DO2484-WI 479 STF Variance Reporting
      *           File TFP014 was changed. Recompile only. No logic change.
      *           Added fields:
      *             ADXPULB  - STF Produced Pounds
      *             ADXPUSLB - STF Produced Start Wgt
      *             ADXYPC   - STF STD YIELD %
      *             ADXPMPPC - STF PUMP %
      *             ADXSLLB  - STF SOLD LBS
      *             ADXSLSLB - STF SOLD START WEIGHT
      *
      * 10/10/22  R Centonze   W105621 - Increase price fields to handle > 999.99
      *         COPY @@SPU1MG TO  @@SPU1MG10 SIZE 10.6  -> FIELD TFP014.ADSPU1MG
      *         COPY @@TPU1MG TO  @@TPU1MG10 SIZE 10.6  -> FIELD TFP014.ADTPU1MG
      *         COPY @@SSL1MG TO  @@SSL1MG10 SIZE 10.6  -> FIELD TFP014.ADSSL1MG
      *         COPY @@SSL2MG TO  @@SSL2MG10 SIZE 10.6  -> FIELD TFP014.ADSSL2MG
      *         COPY @@TSL1MG TO  @@TSL1MG10 SIZE 10.6  -> FIELD TFP014.ADTSL1MG
      *         COPY @@TSL2MG TO  @@TSL2MG10 SIZE 10.6  -> FIELD TFP014.ADTSL2MG
      * 05/07/24  Santosh Patil P310149 - Field length is increased
      *           in TFP015. Function is recompiled only.
      *
      /EJECT
      ***************************************************************************************
      * FILE SPECIFICATION
      ***************************************************************************************
      *
     fppaorel4  if   e           k disk
      *  TF Margin adjustment group detail
      *
      *
     ftfp050    if   e           k disk
      *  Kill/cut data
      *
      *
     fomfarel1  if   e           k disk
      *  Item Structure Class Master file
      *
      *
     ftfp015    if   e           k disk
      *  TF MIX MA Transaction Summary
      *
      *
     ftfp014    if   e           k disk
      *  TF MA Transaction Detail
      *
      *
     frol121a   if   e           k disk
      *  TF ByProduct mix summary
      *
      *
     frop125    o    e           k disk
      *  TF ByProduct & NVA Volume
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkcgcd          s                   like(yscgcd)
     D wkprcd          s                   like(adprcd)
     D wkmco           s                   like(admco)
     D wkbninpr        s                   like(yvbninpr)
     D wktrgapr        s                   like(yvbninpr)
     D wkt72apr        s                   like(yvbninpr)
     D wkscclb         s                   like(yvscclb)
     D wktcclb         s                   like(yvtcclb)
     D wkacclb         s                   like(yvacclb)
      *
     D svrgnb          s                   like(aorgnb)
     D svi1t1          s                   like(aoi1t1)
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
     D  ldwedt                29     36  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Process each record in the TF Margin Adjustment Group Detail file where:
      *    1) TF Classification = NVA
      *    2) Include in Volume = Yes
      *
     C     'NVA'         setll     ppaorel4
      *
     C                   dou       *in90 = *on                                  Do loop
     C     'NVA'         reade     ppaorel4                               90
     C                   if        *in90 = *off and aosxsx = 'Y'                If not eof
      *
      * We only want to process this record if it is the 1st of a MIX group
      *    check IS Structure Type and MIX Sub group
      *
     C                   if        aorgnb <> svrgnb or aoi1t1 <> svi1t1         If process
     C                   move      aorgnb        svrgnb
     C                   move      aoi1t1        svi1t1
      *
      * If this is a NECKBONE group = get the Meat Cost for the base item (5412)
      *        1.  IS structure type = 700
      *        2.  Mix Sub Group = 'B'
      *
      * else ZERO out
      *
     C                   if        aorgnb = 700 and aoi1t1 = 'B'
     C                   exsr      $tfp014
     C                   else
     C                   z-add     *zeros        wkmco
     C                   endif
      *
      * Retrieve the TF MIX Transaction Summary records for this group
      *
     C                   exsr      $tfp015
      *
      * Write the TF NVA Volume record
      *
     C                   exsr      $rop125
      *
     C                   endif                                                  If process
     C                   endif                                                  If not eof
     C                   enddo                                                  Do loop
      *
      * EOF processing
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * TF Mix Summary file
      *---------------------------------------------------------------
      *
     C     $tfp015       begsr
      *
      * Retrieve the TF Mix Summary record for this Group:
      *   1) TF Classification
      *   2) Item Structure Type
      *   3) TF MIX Sub Group
      *   4) WeekEnding Year and Week
      *
     C     key02         chain     tfp015                             92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     mwspulb       yvspulb
     C                   z-add     mwtpulb       yvtpulb
     C                   z-add     mwapulb       yvapulb
     C                   z-add     mwspuslb      yvspuslb
     C                   z-add     mwtpuslb      yvtpuslb
     C                   z-add     mwapuslb      yvapuslb
      *
      * get the class description
     C     aorinb        chain     omfarel1                           93
     C                   if        *in93 = *off
     C                   move      fadetx        yvisclds
     C                   else
     C                   eval      yvisclds =
     C                             'No TF Class Description'
     C                   endif
      *
      * Aggregate Net Produced Price/Start LB =
      *   the total Mix Adjustment Margiin per LB of Start Weight for the TF Group
      *       + the Meat Cost retrieved (currently only used for Neckbones)
      *           currently wkmco will be ZERO for all others
      *
     C                   eval      yvanpupr = mwapu2mg + wkmco
      *
      * else if no 015 record
     C                   else
     C                   eval      yvisclds =
     C                             'No TF NVA Mix Summary'
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * TF MA Transaction Detail file - only executed for Neckbones
      *---------------------------------------------------------------
      *
     C     $tfp014       begsr
      *
      * Retrieve the Meat Cost of the BASE item (5412) for Neckbones
      *   Used later to calculate the Net Produced Value/start lbs
      *   1) Item
      *   2) WeekEnding Year and Week
      *
     C                   eval      wkprcd = 5412
     C     key03         chain     tfp014                             92
     C                   if        *in92 = *off
     C                   z-add     admco         wkmco
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write the TF NVA Volume record
      *---------------------------------------------------------------
      *
     C     $rop125       begsr
      *
     C                   eval      yvpgm = sdpgm
      *
     C                   z-add     ldwedt        yvwedt
     C                   z-add     ldyr          yvyr
     C                   z-add     ldwk          yvwk
      *
     C                   z-add     wkscclb       yvscclb
     C                   z-add     wktcclb       yvtcclb
     C                   z-add     wkacclb       yvacclb
      *
     C                   z-add     wkbninpr      yvbninpr
     C                   z-add     wktrgapr      yvtrgapr
     C                   z-add     wkt72apr      yvt72apr
      *
      * From TF Margin Adjustment Detail file
      *
     C                   move      aoflaa        yvtfclcd
     C                   move      aosvsx        yvtfcgcd
     C                   z-add     aorgnb        yvistycd
     C                   z-add     aorhnb        yvisgrcd
     C                   z-add     aorinb        yvisclcd
      *
      * Perform Calcs
     C                   exsr      $calc
      *
     C                   write     yvrec
     C                   clear                   yvrec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Calculations
      *---------------------------------------------------------------
      *
     C     $calc         begsr
      *
      * If we have a Agg Net Price we'll setup the equivalent.
      * Equivalent Net Produced Price/LB = BNIN Value
      *
     C                   if        yvanpupr <> 0
     C                   z-add     yvbninpr      yveqnpupr
     C                   endif
      *
      * Margin/LB
      *
     C     yvanpupr      sub       yveqnpupr     yvmgpr
      *
      * Margin Amount
     C                   eval(h)   yvsmgam = yvmgpr * yvspuslb
     C                   eval(h)   yvtmgam = yvmgpr * yvtpuslb
     C                   eval(h)   yvamgam = yvmgpr * yvapuslb
      *
      *
      * Incremental Margin/LB (aka: Average Incremental Margin/LB of Carcass Weight)
      *
     C                   if        yvacclb <> 0
     C                   eval(h)   yvincmgpr = yvamgam / yvacclb
     C                   endif
      *
      *
      * Incremental Margin Amount
      *
     C                   eval(h)   yvincmgam = yvincmgpr * yvtcclb
      *
      * Amount Due
     C                   eval(h)   yvsduam = yvtmgam - yvincmgam
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldwedt
     C                   kfld                    wkcgcd
      *
     C     key02         klist
     C                   kfld                    aoflaa
     C                   kfld                    aorgnb
     C                   kfld                    aoi1t1
     C                   kfld                    ldyr
     C                   kfld                    ldwk
      *
     C     key03         klist
     C                   kfld                    wkprcd
     C                   kfld                    ldyr
     C                   kfld                    ldwk
      *
      * Retrieve info used for calcs for all Volume records:
      *
      *    Retrieve Aggregate Net Produced Price/Lb for Meat Cost Group: Meat and BoneMeal
      *
     C                   eval      wkcgcd = 'BNIN'
     C     key01         chain     rol121a                            92
     C                   if        *in92 = *off
     C                   z-add     ysanpupr      wkbninpr
     C                   endif
      *
      *    Retrieve Aggregate Net Produced Price/Lb for Meat Cost Group: Regular Trim
      *
     C                   eval      wkcgcd = 'TRGA'
     C     key01         chain     rol121a                            92
     C                   if        *in92 = *off
     C                   z-add     ysanpupr      wktrgapr
     C                   endif
      *
      *    Retrieve Aggregate Net Produced Price/Lb for Meat Cost Group: Special Trim
      *
     C                   eval      wkcgcd = 'T72A'
     C     key01         chain     rol121a                            92
     C                   if        *in92 = *off
     C                   z-add     ysanpupr      wkt72apr
     C                   endif
      *
      * Retrieve Kill/Cut Data
      *
     C     ldwedt        chain     tfp050                             92
     C                   if        *in92 = *off
     C                   z-add     kcscclb       wkscclb
     C                   z-add     kctcclb       wktcclb
     C     kcscclb       add       kctcclb       wkacclb
     C                   endif
      *
     C                   endsr
      /eject
