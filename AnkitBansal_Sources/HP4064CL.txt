/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4064CL                                      */
/*  PROGRAM NAME:    UT: CLEAR TRANSFER INTERFACE FILES TO ALLOW   */
/*                   A REDO                                        */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     07/10/09                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4064.                                       */
/*=================================================================*/
/*  Modification History:                                          */
/*                                                                 */
/*  01/19/2017 JBB E7986  Add Business Office as a parameter.  If  */
/*                        Business Office is 'OKLIV', set files to */
/*                        HSP2061 and HSP2062.  If Business Office */
/*                        is 'IFLIV', set files to HSP2063 and     */
/*                        HSP2064.                                 */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&BUSOFC)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BUSOFC) TYPE(*CHAR) LEN(5)
             DCL        VAR(&RCVFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EDITFILE) TYPE(*CHAR) LEN(10)

/* SET FILE NAMES BASED ON BUSINESS OFFICE PARAMETER              */
             IF         COND(&BUSOFC = 'OKLIV') THEN(DO)
             CHGVAR     VAR(&RCVFILE) VALUE('HSP2061')
             CHGVAR     VAR(&EDITFILE) VALUE('HSP2062')
             ENDDO

             IF         COND(&BUSOFC = 'IFLIV') THEN(DO)
             CHGVAR     VAR(&RCVFILE) VALUE('HSP2063')
             CHGVAR     VAR(&EDITFILE) VALUE('HSP2064')
             ENDDO

/* RECEIVING FILE                                                  */

             ALCOBJ     OBJ((&RCVFILE *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** The Transfer Receiving File could +
                          not be cleared...the file is in use. +
                          *****') TOUSR(&USER)
             GOTO       CMDLBL(EDITING)
                       ENDDO

             CLRPFM     FILE(&RCVFILE)
             MONMSG     MSGID(CPF0000)

             DLCOBJ     OBJ((&RCVFILE *FILE *EXCL))
             MONMSG     MSGID(CPF0000)

EDITING:

             ALCOBJ     OBJ((&EDITFILE *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** The Transfer Editing File could +
                          not be cleared...the file is in use. +
                          *****') TOUSR(&USER)
             GOTO       CMDLBL(EXIT)
                        ENDDO

             CLRPFM     FILE(&EDITFILE)
             MONMSG     MSGID(CPF0000)

             DLCOBJ     OBJ((&EDITFILE *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
EXIT:
             ENDPGM
