      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP142
      * TITLE:       Create Hog Groups
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/21/99
      *
      * FUNCTION: This function creates multiple hog groups for a farm.
      *           There is no delete/update capability---only create.
      *
      *           NOTE: A record is deleted by blanking out the entry fields
      *                 on the subfile line.
      *
      *                 No file updates occur until the user takes
      *                 F9-Accept to accept his changes.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/31/00  LeAnne Fedor
      *           We have cannibalized the 'genetic line' field to be
      *           'sire line'.  Sire line will be populated by batch
      *           programs running at night. So, we have removed 'genetic
      *           line' from this program entirely.
      *
      * 02/21/01  LeAnne Fedor
      *           Production type can now be changed on the subfile line.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/18/01  LeAnne Fedor
      *           Added feed group code to control record only. This new field is only
      *           valued for BGF groups.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only...'hog capacity' removed from Farm Buildings file.
      *           'Square feet' fields added to Farm Site file.
      *
      * 07/24/02  LeAnne Fedor
      *           Added edits for various fields to enforce our new Production Phase concept.
      *
      * 11/26/02  LeAnne Fedor
      *           We have added 2 new fields to the Hog Group Master: stocking density code
      *                                                               stayed/moved code
      *           These fields are always set to blanks when a group is created....they are
      *           populated by the first transfer of hogs into the group.
      *
      * 09/21/04  LeAnne Fedor
      *           Average Days Old In is no longer an entry field. It is output only and
      *           we derive a value.
      *
      * 11/04/04  LeAnne Fedor
      *           Average Days Old In is once again input capable--but only kinda!
      *           The users are now required to enter ADOI for non-BGF Boar and GDU groups.
      *           For ALL other groups, they must leave the entry-capable ADOI at zero.
      *
      *           But, we have added a new field to the subfile: Default Average Days Old In.
      *           This field is output only and we populate it with hard-coded default values.
      *           We put it on the screen so the users could see the ADOI that will be
      *           defaulted for all those groups that they had to leave the entry-capable
      *           ADOI field zero.
      *
      *           We also added Stocking Density to the subfile. The users must key
      *           a value for Nursery Wean-to-Finish groups. BUT, they must leave Stocking
      *           Density blank for all other groups!
      *
      * 01/16/06  LeAnne Fedor
      *           To allow the users to assign multiple Bin Sets to a Room, we created
      *           a new file (HSP113-Rooms/Bin Sets) and logic to write records to the
      *           new file instead of plopping the "bin set" into the Room record when
      *           assigning Bin Sets to a Room in the Assign Bin Sets function.
      *           So, the edit here that a Room has been assigned a Bin Set has been
      *           changed to go against the new HSP113 file instead of HSP020-Building/Rooms.
      *
      * 02/21/08  LeAnne Ramsey
      *           We have changed our Purge function (which we have not ever run).
      *           We will now write the group record to a permanent purge file before
      *           deleting it from the Hog Group file. Why? Because Cognos/Datamart has
      *           a file at the Group level. We don't want the users to create new groups
      *           that have the same Hog Group Code as old/existing groups. So, we will
      *           now edit for a "hog group" in both HSP034-Hog Groups and our new
      *           permanent purge file HSP934-Purged Hog Groups.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd142    cf   e             workstn
     F                                     sfile(hp142s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fhsp010    if   e           k disk
      *    Production type
      *
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp019    if   e           k disk
      *    Farm buildings
      *
      *
     Fhsp020    if   e           k disk
      *    Building rooms
      *
      *
     Fhsp025    if   e           k disk
      *    Group type
      *
      *
     Fhsp031    if   e           k disk
      *    Breed stock
      *
      *
     Fhsp034    uf a e           k disk
      *    Hog group
      *
      *
     Fhsl034a   if   e           k disk    rename(hgrec:hgreca)
      *    Hog group
      *
      *
     Fhsp056    if   e           k disk
      *    Sex
      *
      *
     Fhsp113    if   e           k disk
      *    Rooms/Bin Sets
      *
      *
     Fhsp934    if   e           k disk    rename(hgrec:hgrec934)
      *    Purged hog groups
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D create          c                   'CREATE'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      * Define array/table for building/room used in subfile.
      * This array is used to edit that a specific building/room combination
      * is used only one time for a group
      *
     D bldroom         s             10a   dim(200)
      *
      * Define array/table for hog group code in subfile.
      * This array is used to edit that a specific group is used only one time
      * in the subfile.
      *
     D group           s              7a   dim(200)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D reset           s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      * Work fields
      *
     D wkbldroom       s             10
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
     D wkdiff          s                   like(count)
     D wkfld           s                   like(count)
     D wkrmdr          s                   like(count)
      *
      *
      * Parm fields
      *
     D xxcrdtmdy       s                   like(c1crdtmdy)
     D xxcrdtcymd      s                   like(hgcrdt)
     D xxfsbo          s                   like(hgfsbo)
     D xxptcd          s                   like(ptptcd)
     D xxptds          s                   like(ptptds)
     D xxppcd          s                   like(ppppcd)
     D xxppds          s                   like(ppppds)
     D xxadoi          s                   like(hgadoi)
     D xxalphfscd      s              5
     D xxblcd          s                   like(hgblcd)
     D xxblds          s                   like(fbblds)
     D xxrmcd          s                   like(hgrmcd)
     D xxfscd          s                   like(fsfscd)
     D xxgtcd          s                   like(gtgtcd)
     D xxgtds          s                   like(gtgtds)
     D xxbscd          s                   like(hgbscd)
     D xxbsds          s                   like(bsbsds)
     D xxsxcd          s                   like(sxsxcd)
     D xxsxds          s                   like(sxsxds)
     D xxfgcd          s                   like(hgfgcd)
     D xxcffl          s                   like(hgcffl)
     D xxsdcd          s                   like(hgsdcd)
     D xxsdds          s             15
      *
      *
      * Array indexes
      *
     D x               s              3  0
     D y               s              3  0
     D z               s              3  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *---------------------------------------------------------------------------------
      * Definition for external data area 'DAHGSN' for assigning the next group number
      *---------------------------------------------------------------------------------
      *
     Dnexthgsn         s              7  0 dtaara(dahgsn)
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
     C
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear/fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Set the page of the subfile to display
      *
     C                   select
     C                   when      wkflow = 0 and error = 0
     C                   z-add     1             hdpag1
      *
     C                   when      reset = yes
     C                   z-add     1             hdpag1
     C                   endsl
      *
      * Issue instructional messages
      *
     C                   select
     C                   when      first = yes
      *
      * You must have at least one valid record in the subfile.
      * (You will have a valid record if there is a building/room array entry.)
      *
     C                   when      y = 0 and error = 0
     C                   z-add     1             error
     C                   eval      mgid(e) = 'HS00156'
      *
      * If the user has at least one valid entry and no errors and
      * is not paging up or down, issue the 'accept changes' message.
      *
     C                   when      y > 0 and error = 0 and *in25 = *off
     C                   z-add     1             error
     C                   eval      mgid(e) = 'HS09026'
     C                   endsl
      *
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp142k1
     C                   write     hp142ec
     C                   exfmt     hp142c1
      *
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   eval      wkflow = idflow
     C                   eval      reset = no
      *
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      routne = exit
      *
     C                   when      *in12 = *on                                  F12-previous
     C                   eval      rtncd = '12'
     C                   eval      routne = exit
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   exsr      $set1
     C                   exsr      $edit1
     C                   eval      routne = scrn1
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
     C                   eval      routne = scrn1
      *
     C                   when      *in09 = *on                                  F9-accept
     C                   exsr      $edit1
     C                   if        error <> 0                                   If error
     C                   eval      routne = scrn1
     C                   else
     C                   exsr      $crt034
     C                   eval      routne = exit
     C                   endif                                                  If error
      *
     C                   when      *in25 = *on                                  F25-rollup
     C                   eval      wkdiff = idflow + wkplin
     C                   if        hdrrn1 >= wkdiff                             If something
     C                   else
     C                   eval      hdrrn1 = count
     C                   exsr      $setof
     C                   exsr      $clrln
     C                   do        wkplin                                       Do fill
     C                   exsr      $blank
     C                   enddo                                                  Do fill
     C                   endif                                                  If something
     C                   eval      routne = scrn1
     C                   other
      *
      * User pressed ENTER
      *
     C                   exsr      $edit1
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Edit input fields for screen 1
      *---------------------------------------------------------------
      *
     C     $edit1        begsr
      *
     C                   move      no            first
      *
      * Edit the subfile
      *
     C                   exsr      $eds1
      *
     C                   if        error = 0
     C                   eval      hdpag1 = 1
     C                   endif
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   eval      hdrrn1 = 0
     C                   eval      count = 0
      *
     C                   clear                   hp142s1
     C                   seton                                        83        sflclr
     C                   write     hp142c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Loading the subfile in this program is different.  First, you are always
      * in create mode.  Second, we want to load one subfile record with the
      * default values the user entered in the calling program and then load
      * the remainder of the subfile page with blank lines.
      *
      * Load one record in the subfile with the values that came in as parms.
      *
     C                   exsr      $load1
      *
      * Fill up the subfile page with blank lines if necessary
      *
     C                   if        wkplin <> 0 and wkplin <> count
     C     count         div       wkplin        wkfld
     C                   mvr                     wkrmdr
     C                   eval      wkdiff = wkplin - wkrmdr
     C                   exsr      $setof
     C                   exsr      $clrln
     C                   do        wkdiff
     C                   exsr      $blank
     C                   enddo
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * load subfile on screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   seton                                        85
      *
      * Set up subfile line fields
      *
     C                   eval      d1blcd = c1blcd
     C                   eval      d1rmcd = c1rmcd
     C                   eval      d1ptcd = c1ptcd
     C                   eval      d1ppcd = c1ppcd
     C                   eval      d1gtcd = c1gtcd
     C                   eval      d1sxcd = c1sxcd
     C                   eval      d1bscd = c1bscd
     C                   eval      d1cffl = c1cffl
     C                   z-add     c1adoi        d1dftadoi
      *
     C                   add       1             count
     C                   add       1             hdrrn1
      *
     C                   write     hp142s1
     C                   seton                                        81
     C                   eval      hdpag1 = hdrrn1
      *
      *
      * Put this building/room into the editing array.  This will allow
      * you check that at least one subfile entry is made.
      *
     C     xxblcd        cat       xxrmcd        wkbldroom
     C                   z-add     1             y
     C                   eval      bldroom(y) = wkbldroom
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * load blank subfile records for screen 1
      *---------------------------------------------------------------
      *
     C     $blank        begsr
      *
     C                   add       1             count
     C                   add       1             hdrrn1
      *
     C                   write     hp142s1
     C                   seton                                        81
     C                   eval      hdpag1 = hdrrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * clear subfile line
      *---------------------------------------------------------------
      *
     C     $clrln        begsr
      *
     C                   move      *blank        d1hgcd
     C                   move      *blank        d1blcd
     C                   move      *blank        d1rmcd
     C                   move      *blank        d1ptcd
     C                   move      *blank        d1ppcd
     C                   move      *blank        d1gtcd
     C                   move      *blank        d1sdcd
     C                   move      *blank        d1sxcd
     C                   move      *blank        d1bscd
     C                   move      *blank        d1cffl
     C                   z-add     0             d1adoi
     C                   z-add     0             d1dftadoi
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * read and edit changed subfile records on screen 1
      *---------------------------------------------------------------
      *
     C     $eds1         begsr
      *
      * Initialize the arrays and indexes used for editing
      *
     C                   eval      bldroom = *blank
     C                   eval      group = *blank
     C                   eval      x = 0
     C                   eval      y = 0
     C                   eval      z = 0
      *
      * Read/process all changed subfile records
      *
     C                   setoff                                       80
     C                   dou       *in80 = *on                                  Do process
     C                   readc     hp142s1                                80
     C                   if        *in80 = *off                                 If rec read
      *
      * Set on MDT
     C                   seton                                        85
      *
      * Set off error indicators in the subfile
      *
     C                   exsr      $setof
      *
      * If there are no user entries for the changed subfile record,
      * clear the output-only subfile fields
      *
     C                   if        d1blcd = *blank and                          If no entry
     C                             d1rmcd = *blank and
     C                             d1hgcd = *blank and
     C                             d1ptcd = *blank and
     C                             d1ppcd = *blank and
     C                             d1gtcd = *blank and
     C                             d1sdcd = *blank and
     C                             d1adoi = 0 and
     C                             d1bscd = *blank and
     C                             d1sxcd = *blank and
     C                             d1cffl = *blank
     C                   exsr      $clrln
     C                   else
      *
      * Validate:
      *  1) building
      *  2) room
      *  3) combination of building/room
      *  4) hog group
      *  5) production type
      *  6) production phase
      *  7) group type
      *  8) stocking density
      *  9) average days old in
      * 10) origin BGF farm site
      * 11) origin nursery farm site
      * 12) breed stock
      * 13) sex
      *
     C                   exsr      $blcd
     C                   exsr      $rmcd
      *
     C                   if        *in31 = *off and *in32 = *off
     C                   exsr      $combination
     C                   endif
      *
     C                   exsr      $hgcd
     C                   exsr      $ptcd
     C                   exsr      $ppcd
     C                   exsr      $gtcd
     C                   exsr      $sdcd
     C                   exsr      $adoi
     C                   exsr      $dftadoi
     C                   exsr      $bscd
     C                   exsr      $sxcd
     C                   exsr      $cffl
     C                   endif                                                  If no entry
      *
     C                   update    hp142s1
     C                   exsr      $setof
     C                   endif                                                  If rec read
     C                   enddo                                                  Do process
      * Set MDT
     C                   setoff                                       85
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * validate building
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) must exist on the specified farm site
      *
     C     $blcd         begsr
      *
     C                   select
     C                   when      d1blcd = *blank
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the building is valid on this farm site
     ‚*
     C     key01         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     d1blcd        mgw(1)
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * validate room
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $rmcd         begsr
      *
     C                   select
     C                   when      d1rmcd = *blank
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     ‚*
     C     key02         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     d1rmcd        mgw(1)
     C                   movea     d1blcd        mgw(6)
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     ‚*
      * Room must have an 'active' status
     ‚*
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     d1rmcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
      *
      * Room must have a bin set assigned to it.
      *
     C     key02         chain     hsp113                             92
     C                   if        *in92 = *on                                  If no bin set
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00162'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1rmcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no bin set
      *
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------
      * Validate building/room combination
      *-------------------------------------------------------------------------------
      *
      * Validations:
      *   1) There cannot already be a group in this building/room with a status of 'created'
      *   2) The building/room can appear in the subfile only one time.
      *
     C     $combination  begsr
      *
      * Check that there is not already a 'created' group in this building/room.
      *
     C     key02         setll     hsl034a
      *
     C                   dou       *in91 = *on or hggscd = 'CR'                 Do loop
     C     key02         reade     hsl034a                                91
     C                   if        *in91 = *off and hggscd = 'CR'               If not eof
     C                   seton                                        3132
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00295'
     C                   clear                   mgw
     C                   movea     hghgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not eof
     C                   enddo                                                  Do loop
      *
      * Check that building/room does not already exist in the subfile.
      *
     C     d1blcd        cat       d1rmcd        wkbldroom
      *
     C                   eval      x = 1
     C     wkbldroom     lookup    bldroom(x)                             92
     C                   if        *in92 = *on                                  If dup
     C                   seton                                        3132
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00133'
     C                   clear                   mgw
     C                   movea     d1blcd        mgw(1)
     C                   movea     d1rmcd        mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If dup
      *
      *
      * If not a duplidate, load the building/room combination into the array.
      *
     C                   if        *in31 = *off
     C                   add       1             y
     C                   eval      bldroom(y) = wkbldroom
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Validate hog group code
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) cannot already exist in Hog Group file
      *  3) cannot already exist in Purged Hog Group file
      *  4) must be unique in subfile
      *
     C     $hgcd         begsr
      *
     C                   select
     C                   when      d1hgcd = *blank
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     d1hgcd        chain(n)  hsp034                             92
     C                   if        *in92 = *off                                 If already exists
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00260'
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If already exists
      *
     C     d1hgcd        chain     hsp934                             92
     C                   if        *in92 = *off                                 If purged
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01233'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If purged
      *
      * Validate that group is unique in subfile
      *
     C                   if        *in30 = *off                                 If OK so far
     C                   eval      x = 1
     C     d1hgcd        lookup    group(x)                               92
     C                   if        *in92 = *on                                  If dup
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00157'
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If dup
     C                   endif                                                  If OK so far
      *
      *
      * If not a duplicate, load the group into the array.
      *
     C                   if        *in30 = *off
     C                   add       1             z
     C                   eval      group(z) = d1hgcd
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Validate production type
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) must be valid in master file
      *
     C     $ptcd         begsr
      *
     C                   select
     C                   when      d1ptcd = *blank
     C                   move      c1ptcd        d1ptcd
     C                   other
      *
      * Check that the production type exists in the master file
     ‚*
     C     d1ptcd        chain     hsp010                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00180'
     C                   clear                   mgw
     C                   movea     d1ptcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Validate production phase
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) must be valid in master file
      *
     C     $ppcd         begsr
      *
     C                   select
     C                   when      d1ppcd = *blank
     C                   move      c1ppcd        d1ppcd
     C                   other
      *
      * Check that the production phase exists in the master file
     ‚*
     C     d1ppcd        chain     hsp011                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00170'
     C                   clear                   mgw
     C                   movea     d1ppcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
      *
      * If production phase in the control record is BGF, then phase in the
      * subfile must be BGF
      *
     C                   select
     C                   when      c1ppcd = 'BGF  ' and d1ppcd <> 'BGF  '
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS01172'
     C                   endif
      *
     C                   when      (c1ppcd = 'NUR  ' or c1ppcd = 'GF   ') and
     C                              d1ppcd = 'BGF  '
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS01173'
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If not found
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * validate group type
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) must be valid in master file
      *
     C     $gtcd         begsr
      *
     C                   select
     C                   when      d1gtcd = *blank
     C                   move      c1gtcd        d1gtcd
     C                   other
      *
      * Check that the group type exists in the master file
     ‚*
     C     d1gtcd        chain     hsp025                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00137'
     C                   clear                   mgw
     C                   movea     d1gtcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
      *
      *
     C                   if        d1ppcd = 'BGF  ' and d1gtcd = 'W'
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01174'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1gtcd        mgw(1)
     C                   movea     d1ppcd        mgw(2)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Validate Stocking Density
      *----------------------------------------------------------------
      * Validations:
      *  1) Must be S=Single or D=Double for WTF Nursery groups
      *  2) Must be blank for all other groups
      *
     C     $sdcd         begsr
      *
     C                   select
     C                   when      d1ppcd = 'NUR  ' and d1gtcd = 'W'
     C                   if        d1sdcd <> 'S' and d1sdcd <> 'D'              If not S/D
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS00841'     mgid(e)
     C                   endif
     C                   endif                                                  If not S/D
     C                   other
      *
     C                   if        d1sdcd <> *blank
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS00843'     mgid(e)
     C                   endif
     C                   endif
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * validate breed stock code
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) must be valid in master file
      *
     C     $bscd         begsr
      *
     C                   select
     C                   when      d1bscd = *blank
     C                   move      c1bscd        d1bscd
     C                   other
      *
      * Check that the breed stock exists in the master file
     ‚*
     C     d1bscd        chain     hsp031                             92
     C                   if        *in92 = *off                                 If hit
     C                   move      bsbscd        d1bscd
     C                   else
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00251'
     C                   clear                   mgw
     C                   movea     d1bscd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If hit
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Validate sex code
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) must be valid in master file
      *
     C     $sxcd         begsr
      *
     C                   select
     C                   when      d1sxcd = *blank
     C                   move      c1sxcd        d1sxcd
      *
      * Check that the sex code exists in the master file
     ‚*
     C                   when      d1sxcd <> *blank
     C     d1sxcd        chain     hsp056                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00209'
     C                   clear                   mgw
     C                   movea     d1sxcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Validate Continuous Flow Flag
      *----------------------------------------------------------------
      * Validations:
      *  1) required
      *  2) must be Y=Yes or N=No
      *
     C     $cffl         begsr
     ‚*
     C                   select
     C                   when      d1cffl = *blank
     C                   move      c1cffl        d1cffl
      *
     C                   when      d1cffl <> yes and d1cffl <> no
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09106'
     C                   endif
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Validate Average Days Old In
      *----------------------------------------------------------------
      *
      * The users must now key ADOI for non-BGF Boar and GDU groups.
      * For everybody else, they must leave the field at zero.
      *
     C     $adoi         begsr
      *
     C                   select                                                 Select 1
     C                   when      d1ppcd <> 'BGF  ' and
     C                             (d1gtcd = 'B' or d1gtcd = 'G')
      *
     C                   select                                                 Select 2
     C                   when      d1adoi = 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS00844'     mgid(e)
     C                   endif
      *
     C                   when      d1adoi < 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   exsr      $err1
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   endsl                                                  Select 2
     C                   other
      *
     C                   if        d1adoi <> 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   exsr      $err1
     C                   add       1             error
     C                   movel     'HS00846'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   endsl                                                  Select 1
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Derive a default Average Days Old In
      *----------------------------------------------------------------
      *
      * For info only, we will derive a default ADOI to show on the screen;
      * except for Non-BGF Boar/GDU groups--which the users key an ADOI value.
      *
     C     $dftadoi      begsr
      *
     C                   select
     C                   when      d1ppcd <> 'BGF  ' and
     C                             (d1gtcd = 'B' or d1gtcd = 'G')
     C                   z-add     0             d1dftadoi
      *
     C                   when      d1ppcd = 'BGF  '
     C                   z-add     0             d1dftadoi
      *
     C                   when      d1ppcd = 'NUR  ' or
     C                             (d1ppcd = 'GF   ' and d1gtcd = 'W')
     C                   z-add     18            d1dftadoi
     C                   other
      *
     C                   z-add     67            d1dftadoi
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * set off the indicators on the subfile
      *----------------------------------------------------------------
      *
     C     $setof        begsr
      *
     C                   setoff                                       303132
     C                   setoff                                       333435
     C                   setoff                                       363738
     C                   setoff                                       39
     C                   setoff                                       515253
     C                   setoff                                       545556
     C                   setoff                                       5759
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Create hog groups
      *----------------------------------------------------------------
      *
     C     $crt034       begsr
      *
     C                   setoff                                       80
     C                   dou       *in80 = *on                                  Do writes
     C                   readc     hp142s1                                80
     C                   if        *in80 = *off and d1blcd <> *blank            If rec read
      *
     C                   clear                   hgrec
      *
      * Retrieve a system-assigned hog group number
      *
     C     *lock         in        nexthgsn
     C                   add       1             nexthgsn
     C                   eval      hghgsn = nexthgsn
     C                   out       nexthgsn
      *
     C                   z-add     c1fscd        hgfscd
     C                   eval      hggscd = 'CR'
     C                   eval      hgpufl = no
      *
      * Retrieve farm site defaults
      *
     C     c1fscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If off
     C                   movel     fsfsbo        hgfsbo
     C                   movel     fsppcd        hgppcd
     C                   movel     fscocd        hgcocd
     C                   movel     fsdvcd        hgdvcd
     C                   movel     fsrgcd        hgrgcd
     C                   movel     fscrcd        hgcrcd
     C                   endif                                                  If off
      *
     C                   movel     d1hgcd        hghgcd
     C                   movel     d1blcd        hgblcd
     C                   movel     d1rmcd        hgrmcd
     C                   movel     d1ptcd        hgptcd
     C                   movel     d1ppcd        hgppcd
     C                   movel     d1gtcd        hggtcd
     C                   movel     d1sdcd        hgsdcd
     C                   movel     d1bscd        hgbscd
     C                   movel     d1sxcd        hgsxcd
     C                   movel     d1cffl        hgcffl
     C                   z-add     xxcrdtcymd    hgcrdt
     C                   move      xxfgcd        hgfgcd
      *
      * Average Days Old In
      *
     C                   if        d1adoi <> 0
     C                   z-add     d1adoi        hgadoi
     C                   else
     C                   z-add     d1dftadoi     hgadoi
     C                   endif
      *
     C                   write     hgrec
     C                   endif                                                  If rec read
     C                   enddo                                                  Do writes
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * set off f4 indicators in the subfile records
      *----------------------------------------------------------------
      *
     C     $f4sflind     begsr
      *
     C                   setoff                                       80
     C                   dou       *in80 = *on                                  Do indicators off
     C                   readc     hp142s1                                80
     C                   if        *in80 = *off                                 If rec read
     C                   seton                                        85
     C                   setoff                                       515253
     C                   setoff                                       555657
     C                   setoff                                       59  46
     C                   update    hp142s1
     C                   endif                                                  If rec read
     C                   enddo                                                  Do indicators off
      * Set MDT
     C                   setoff                                       85
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * F4 prompt for screen 1
      *--------------------------------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      *--------------------------------------------------------------------------------------
      * Prompted fields in subfile
      *--------------------------------------------------------------------------------------
      *
      * Set off prompt indicators in all subfile records.
      *
     C                   exsr      $f4sflind
      *
     C                   select
      *
      *-----------------------------------------------------------------
      * Building
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1BLCD'
     C                   call      'HP523'
     C                   parm                    c1fscd
     C                   parm                    xxblcd
     C                   parm                    xxblds
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        xxblcd <> *blank
     C                   movel     xxblcd        d1blcd
     C                   endif
      *
      * Update subfile
      *
     C                   seton                                        8551
     C                   update    hp142s1
     C                   setoff                                       8551
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *
      *-----------------------------------------------------------------
      * Room
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1RMCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   call      'HP524'
     C                   parm                    c1fscd
     C                   parm                    d1blcd
     C                   parm                    xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        d1rmcd
     C                   endif
      * Update subfile
     C                   seton                                        8552
     C                   update    hp142s1
     C                   setoff                                       8552
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *
      *-----------------------------------------------------------------
      * Production type
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1PTCD'
     C                   call      'HP504'
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxptds
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        xxptcd <> *blank
     C                   move      xxptcd        d1ptcd
     C                   endif
      * Update subfile
     C                   seton                                        8553
     C                   update    hp142s1
     C                   setoff                                       8553
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *
      *-----------------------------------------------------------------
      * Production phase
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1PPCD'
     C                   call      'HP507'
     C                   parm      *blank        xxppcd
     C                   parm      *blank        xxppds
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   if        xxppcd <> *blank
     C                   move      xxppcd        d1ppcd
     C                   endif
      * Update subfile
     C                   seton                                        8554
     C                   update    hp142s1
     C                   setoff                                       8554
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *
      *-----------------------------------------------------------------
      * Group type
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1GTCD'
     C                   call      'HP531'
     C                   parm      *blank        xxgtcd
     C                   parm      *blank        xxgtds
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        xxgtcd <> *blank
     C                   move      xxgtcd        d1gtcd
     C                   endif
      * Update subfile
     C                   seton                                        8555
     C                   update    hp142s1
     C                   setoff                                       8555
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *
      *-----------------------------------------------------------------
      * Stocking density
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1SDCD' and d1ppcd = 'NUR  ' and
     C                                   d1gtcd = 'W'
     C                   call      'HP5024'
     C                   parm                    xxsdcd
     C                   parm                    xxsdds
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        xxsdcd <> *blank
     C                   movel     xxsdcd        d1sdcd
     C                   endif
      *
      * Update subfile
      *
     C                   seton                                        8559
     C                   update    hp142s1
     C                   setoff                                       8559
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *
      *-----------------------------------------------------------------
      * Breed stock
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1BSCD'
     C                   call      'HP516'
     C                   parm      *blank        xxbscd
     C                   parm      *blank        xxbsds
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        xxbscd <> *blank
     C                   move      xxbscd        d1bscd
     C                   endif
      * Update subfile
     C                   seton                                        8556
     C                   update    hp142s1
     C                   setoff                                       8556
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *
      *-----------------------------------------------------------------
      * Sex
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1SXCD'
     C                   call      'HP584'
     C                   parm      *blank        xxsxcd
     C                   parm      *blank        xxsxds
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        xxsxcd <> *blank
     C                   move      xxsxcd        d1sxcd
     C                   endif
      * Update subfile
     C                   seton                                        8557
     C                   update    hp142s1
     C                   setoff                                       8557
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *
      *-----------------------------------------------------------------
      * Continuous Flow
      *-----------------------------------------------------------------
      *
     C                   when      fld = 'D1CFFL'
     C                   call      'HP522'
     C                   parm      *blank        xxcffl
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        xxcffl <> *blank
     C                   move      xxcffl        d1cffl
     C                   endif
      * Update subfile
     C                   seton                                        8557
     C                   update    hp142s1
     C                   setoff                                       8557
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record thus causing the subfile record to be retrieved
      * and updated.) But, set the page of the subfile to be displayed
      * to the page on which the user took the prompt.
      *
     C                   eval      hdrrn1 = count
     C                   eval      hdpag1 = hdrr1
     C                   endif                                                  If hit
      *-----------------------------------------------------------------
      * Catch-all logic if user prompts on non-prompt fields
      *-----------------------------------------------------------------
      *
     C                   other
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09011'
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * routine to find/retrieve the subfile record to update
      *          when returning from a select program on screen 1
      *----------------------------------------------------------------
      *
      * HDRR1 is the hidden field associated with the keyword SFLCSRRRN
      * on the control record in the DSPF.
      * HDRRN1 is the value associated with the keyword 'SFILE' in
      * the F-specs for the workstation.
      *
     C     $rrn1         begsr
      *
     C                   setoff                                       97
      *
     C                   if        hdrr1 <> 0
     C                   eval      hdrrn1 = hdrr1
     C     hdrrn1        chain     hp142s1                            97
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------------------
      * set the subfile to redisplay at the first page that has errors on screen 1
      *----------------------------------------------------------------------------
      *
     C     $err1         begsr
      *
     C                   if        error = 1
     C                   eval      hdpag1 = hdrrn1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * seton last record and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm Lists
      *
     C     *entry        plist
     C     c1fsbo        parm                    xxfsbo
     C     c1fscd        parm                    xxfscd
     C     c1blcd        parm                    xxblcd
     C     c1rmcd        parm                    xxrmcd
     C     c1ptcd        parm                    xxptcd
     C     c1ppcd        parm                    xxppcd
     C     c1gtcd        parm                    xxgtcd
     C     c1sxcd        parm                    xxsxcd
     C     c1fgcd        parm                    xxfgcd
     C     c1cffl        parm                    xxcffl
     C     c1bscd        parm                    xxbscd
     C     c1adoi        parm                    xxadoi
     C     c1crdtmdy     parm                    xxcrdtmdy
     C                   parm                    xxcrdtcymd
     C                   parm                    rtncd
      *
     C                   eval      mode = create
     C                   eval      c1mode = create
      *
     C     key01         klist
     C                   kfld                    c1fscd
     C                   kfld                    d1blcd
      *
     C     key02         klist
     C                   kfld                    c1fscd
     C                   kfld                    d1blcd
     C                   kfld                    d1rmcd
      *
      * Only display 'feed group' in the control record when the incoming Production
      * Phase is 'BGF'
      *
     C                   if        xxppcd = 'BGF  '
     C                   seton                                        87
     C                   endif
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   eval      wkplin = 7                                   sflpag
     C                   eval      wkrlin = wkplin - 1
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
