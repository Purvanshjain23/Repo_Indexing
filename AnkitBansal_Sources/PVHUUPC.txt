/*********************************************************************/
/* PROGRAM : PVHUUPC                                                 */
/* DESC    : CRT BACKUP AFTER RESTORCL EXECUTE USER PROGRAM          */
/* DATE    : 10/16/2024                                              */
/* PROJECT : WI568 - WAREHOUSE INVENTORY MANAGEMENT (WIM)            */
/* BY      : DANNY NGUYEN                                            */
/* PURPOSE : Process will make a backup copies of the Item Balance   */
/*           and Item Balance Detail files for both Legacy & WIM     */
/*           after the Restoration job completes for Legacy & WIM.   */
/*                                                                   */
/* HOW USE : GoAnywhere Agent will invoke this program once WIM      */
/*           Restoration completes.                                  */
/*           Legacy Restoration calls PBP3XFR & will verify if all   */
/*           3 Plants Restoration has been 'Processed' to call this  */
/*           process.                                                */
/*                                                                   */
/* PARMS   : &SYSTEM - L=Legacy ; W=WIM                              */
/*           &CPYLIB - Copy Library for Backup; Valid Values:        */
/*                    PRKTESTF, PRKUATF, PRKFLIB                     */
/*           &COMPANY- Company Number.                               */
/*                                                                   */
/*********************************************************************/
/* MAINTENANCE LOG:                                                  */
/*-------------------------------------------------------------------*/
/* DATE     BY  PROJ      CHANGE DESCRIPTION                         */
/* -------- --- --------- ------------------------------------------ */
/*                                                                   */
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&SYSTEM &CPYLIB &COMPANY)

             DCL        VAR(&SYSTEM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CPYLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COMPANY) TYPE(*CHAR) LEN(3)

             DCL        VAR(&CPYLGYIB) TYPE(*CHAR) LEN(10) +
                          VALUE('CAB0REP') /* Copy To Legacy +
                          Item Balance File */
             DCL        VAR(&CPYLGYIBD) TYPE(*CHAR) LEN(10) +
                          VALUE('CAB1REP') /* Copy To Legacy +
                          Item Balance Detail File */
             DCL        VAR(&CPYWIMIB) TYPE(*CHAR) LEN(10) +
                          VALUE('PUCGCPP') /* Copy To WIM +
                          Item Balance Table */
             DCL        VAR(&CPYWIMIBD) TYPE(*CHAR) LEN(10) +
                          VALUE('PUCHCPP') /* Copy To WIM +
                          Item Balance Detail Table */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)



    /*If Legacy - Backup Legacy Files */
             IF         COND(&SYSTEM *EQ 'L') THEN(DO)

       /* Concat the Company Number to Filename. */
             CHGVAR     VAR(&CPYLGYIB) VALUE(&CPYLGYIB *TCAT &COMPANY)
             CHGVAR     VAR(&CPYLGYIBD) VALUE(&CPYLGYIBD *TCAT &COMPANY)

       /* Validate Copy to Library/Filename Exist. */
          /* Legacy Item Balance */
             CHKOBJ     OBJ(&CPYLIB/&CPYLGYIB) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             SNDPGMMSG  MSG('Copy To File ' *CAT &CPYLIB *CAT '/' +
                          *CAT &CPYLGYIB *TCAT ' not found.')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO
         /* Legacy Item Balance Detail */
             CHKOBJ     OBJ(&CPYLIB/&CPYLGYIBD) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             SNDPGMMSG  MSG('Copy To File ' *CAT &CPYLIB *CAT '/' +
                          *CAT &CPYLGYIBD *TCAT ' not found.')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

    /* No Errors then Copy to Backup Files */
       /* Legacy Item Balance */
             CPYF       FROMFILE(&CPYLIB/CAB0REP) +
                          TOFILE(&CPYLIB/&CPYLGYIB) +
                          MBROPT(*REPLACE) FROMRCD(1) INCREL((*IF +
                          B0AIC3 *EQ &COMPANY))
       /* Legacy Item Balance Detail */
             CPYF       FROMFILE(&CPYLIB/CAB1REP) +
                          TOFILE(&CPYLIB/&CPYLGYIBD) +
                          MBROPT(*REPLACE) FROMRCD(1) INCREL((*IF +
                          B1AIC3 *EQ &COMPANY))

             ENDDO      /* &SYSTEM = 'L' */

    /*If WIM - Backup WIM Files */
             IF         COND(&SYSTEM *EQ 'W') THEN(DO)

       /* Concat the Company Number to Filename. */
             CHGVAR     VAR(&CPYWIMIB) VALUE(&CPYWIMIB *TCAT &COMPANY)
             CHGVAR     VAR(&CPYWIMIBD) VALUE(&CPYWIMIBD *TCAT &COMPANY)

       /* Validate Copy to Library/Filename Exist. */
          /* WIM Item Balance */
             CHKOBJ     OBJ(&CPYLIB/&CPYWIMIB) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             SNDPGMMSG  MSG('Copy To File ' *CAT &CPYLIB *CAT '/' +
                          *CAT &CPYWIMIB *TCAT ' not found.')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO
         /* WIM Item Balance Detail */
             CHKOBJ     OBJ(&CPYLIB/&CPYWIMIBD) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             SNDPGMMSG  MSG('Copy To File ' *CAT &CPYLIB *CAT '/' +
                          *CAT &CPYWIMIBD *TCAT ' not found.')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

    /* No Errors then Copy to Backup Files */
       /* WIM Item Balance */
             CPYF       FROMFILE(&CPYLIB/PUCGCPP) +
                          TOFILE(&CPYLIB/&CPYWIMIB) +
                          MBROPT(*REPLACE) FROMRCD(1) INCREL((*IF +
                          CGAIC3 *EQ &COMPANY))
       /* WIM Item Balance Detail */
             CPYF       FROMFILE(&CPYLIB/PUCHCPP) +
                          TOFILE(&CPYLIB/&CPYWIMIBD) +
                          MBROPT(*REPLACE) FROMRCD(1) INCREL((*IF +
                          CHAIC3 *EQ &COMPANY))

             ENDDO      /* &SYSTEM = 'W' */


             GOTO       CMDLBL(END)


/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:
             ENDPGM
