      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPPAGE
      * TITLE:       GENERIC PIG AGES CALCULATION
      * PROGRAMMER:  ALICE BROWNFIELD
      * CREATED:     6/02/94
      *
      * NOTE:        THIS PROGRAM DOES NOT SET ON LR--IT USES RETURN.
      *              SO, IF YOUR PROGRAM CALLS THIS PROGRAM OR A
      *              PROGRAM THAT CALLS THIS PROGRAM, THEN YOU MUST
      *              CALL THE CLP TO RECLAIM RESOURCES AS YOU EXIT THE
      *              CALLING PROGRAM.
      *
      * FUNCTION: GENERIC PROGRAM TO BE CALLED FROM MULTIPLE PROGRAMS
      *           WHENEVER THE NEED IS TO KNOW THE AVERAGE AGE OF
      *           A GROUP OF PIGS.
      *
      *           THIS PROGRAM RECEIVES THE FOLLOWING PARMS:
      *                THE HOG GROUP NUMBER  -- XXHGSN
      *                A CUTOFF DATE         -- XXCYMD
      *
      *           THIS PROGRAM RETURNS THE FOLLOWING PARM:
      *                AVERAGE DAYS OLD  -- DAYOLD
      *
      *           IT READS THE GROUP HEADER TO GET THE DATE OPENED AND THE
      *              AGE "IN".
      *
      *           IT THEN SETS UP THE PARMS AND CALLS THE PROGRAM
      *              CALCULATE THE "PIGS IN" AMOUNT (HPMOVE).
      *
      *           AVERAGE GROUP AGE = AVERAGE DAYS IN GROUP
      *                               (PIG DAYS / PIGS IN)
      *                                         +
      *                               AGE "IN" DAYS ON THE
      *                                  GROUP HEADER.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 09/19/94  MARY PALMER - ADDED TRANSFER W/IN PHASE AN NEXT PHASE
      *                         PARMS TO HPMOVE.
      *
      * 01/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. SEX ADDED TO HOG GROUP FILE.
      *
      * 02/07/95  LEANNE FEDOR
      *           MODIFIED TO 'RETURN' INSTEAD OF SETTING ON LR.
      *
      * 03/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. ORIGIN BGF FARM SITE AND ORIGIN
      *           NURSERY FARM SITE ADDED TO HOG GROUP FILE.
      *
      * 04/18/95  LEANNE FEDOR
      *           RECOMPILE ONLY.  FARM SITE CHANGED TO NUMERIC FIELD.
      *
      * 07/05/95  LEANNE FEDOR
      *           CHANGED NAMES OF A FEW FIELDS TO FIT STANDARD
      *           NAMING CONVENTIONS.
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 08/07/96  LEANNE FEDOR
      *           INCREASED THE SIZE OF THE 'POUNDS' PARMS FOR HPMOVE
      *           FROM 5,0 TO 9,2.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 06/26/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * RETRIEVE THE 'OPENED' DATE FOR THE GROUP TO USE AS
      * THE 'FROM' DATE TO SEND TO THE PIG DAYS PROGRAM.
      *
     C     XXHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF                                         IF GOOD OPEN
     C     HGOPDT        ANDLT     XXCYMD
      *
     C                   Z-ADD     XXHGSN        YYHGSN
     C                   Z-ADD     HGOPDT        YYFYMD
     C                   Z-ADD     XXCYMD        YYTYMD
     C                   Z-ADD     0             YYPDAY
      *
     C                   CALL      'HPPDAY'
     C                   PARM                    YYHGSN
     C                   PARM                    YYFYMD
     C                   PARM                    YYTYMD
     C                   PARM                    YYPDAY
      *
      * WHEN THE PROGRAM RETURNS WE WILL HAVE THE TOTAL PIG DAYS
      * FOR THIS GROUP.
      *
      * NOW WE NEED TO KNOW THE PIGS "IN" HEAD COUNT. SO SETUP THE
      * PARMS AND CALL THE PROGRAM TO GET THE PIGS "IN" HEAD
      * COUNT.
     C                   Z-ADD     XXHGSN        ZZHGSN
     C                   Z-ADD     XXCYMD        ZZCYMD
      *
     C                   CALL      'HPMOVE'
     C                   PARM                    ZZHGSN                         GROUP
     C                   PARM                    ZZCYMD                         CUT-OFF DATE
      * HEAD
     C                   PARM                    GPIHD                          GROUP INV
     C                   PARM                    PINHD                          PIG IN
     C                   PARM                    TINHD                          TRSF IN
     C                   PARM                    TISPHD                         SAME PHASE
     C                   PARM                    TIDPHD                         DIFF PHASE
     C                   PARM                    POUHD                          PIG OUT
     C                   PARM                    TOUHD                          TRSF OUT
     C                   PARM                    TOSPHD                         SAME PHASE
     C                   PARM                    TODPHD                         DIFF PHASE
     C                   PARM                    RINHD                          REJECT IN
     C                   PARM                    ROUHD                          REJECT OUT
     C                   PARM                    QINHD                          QUALITY IN
     C                   PARM                    QOUHD                          QUALITY OUT
     C                   PARM                    MORHD                          MORTALITY
     C                   PARM                    INAHD                          INV ADJ
      * POUNDS
     C                   PARM                    PINLB                          PIG IN
     C                   PARM                    TINLB                          TRSF IN
     C                   PARM                    TISPLB                         SAME PHASE
     C                   PARM                    TIDPLB                         DIFF PHASE
     C                   PARM                    POULB                          PIG OUT
     C                   PARM                    TOULB                          TRSF OUT
     C                   PARM                    TOSPLB                         SAME PHASE
     C                   PARM                    TODPLB                         DIFF PHASE
     C                   PARM                    RINLB                          REJECT IN
     C                   PARM                    ROULB                          REJECT OUT
     C                   PARM                    QINLB                          QUALITY IN
     C                   PARM                    QOULB                          QUALITY OUT
     C                   PARM                    MORLB                          MORTALITY
      *
      * WHEN THE PROGRAM RETURNS WE WILL HAVE THE PIGS "IN" COUNT
      * CALCULATE THE AVERAGE DAYS IN THE GROUP
      *
     C     PINHD         IFNE      0
     C     YYPDAY        DIV(H)    PINHD         AVGDAY
     C                   ENDIF
      *
      * WE NEED TO ADD THE AVERAGE DAYS FOR THE GROUP TO THE
      * AVERAGE DAYS OLD "IN" DAYS TO GET A NEW AVERAGE
      * DAYS OLD.
      *
     C     AVGDAY        ADD       HGADOI        XXADOO
      *
      *  MOVE THE AVERAGE OLD DAYS "IN" INTO PARM FIELD TO PASS OUT
      *
     C                   Z-ADD     HGADOI        XXADOI
     C                   ENDIF                                                  IF GOOD OPEN
      *
      * RETURN TO CALLING PROGRAM
      *
     C                   RETURN
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * Parm lists
      *
     C     *ENTRY        PLIST
     C                   PARM                    XXHGSN
     C                   PARM                    XXCYMD
     C                   PARM                    XXADOO
     C                   PARM                    XXADOI
      *
      * DEFINE INPUT PARAMETERS FOR THIS PROGRAM
      *
     C     *LIKE         DEFINE    HGHGSN        XXHGSN
     C     *LIKE         DEFINE    HGOPDT        XXCYMD
     C     *LIKE         DEFINE    HGADOO        XXADOO
     C     *LIKE         DEFINE    HGADOI        XXADOI
      *
      * DEFINE PARAMETERS FOR THE PIG DAYS PROGRAM (HPPDAY)
      *
     C     *LIKE         DEFINE    HGHGSN        YYHGSN
     C     *LIKE         DEFINE    HGOPDT        YYFYMD
     C     *LIKE         DEFINE    HGOPDT        YYTYMD
     C                   Z-ADD     0             YYPDAY           11 0
      *
      * DEFINE PARAMETERS FOR THE MOVEMENT PROGRAM (HPMOVE)
      *
     C     *LIKE         DEFINE    HGHGSN        ZZHGSN
     C     *LIKE         DEFINE    HGOPDT        ZZCYMD
      * HEAD
     C                   Z-ADD     0             WKHD05            5 0
     C     *LIKE         DEFINE    WKHD05        GPIHD
     C     *LIKE         DEFINE    WKHD05        PINHD
     C     *LIKE         DEFINE    WKHD05        TINHD
     C     *LIKE         DEFINE    WKHD05        TISPHD
     C     *LIKE         DEFINE    WKHD05        TIDPHD
     C     *LIKE         DEFINE    WKHD05        POUHD
     C     *LIKE         DEFINE    WKHD05        TOUHD
     C     *LIKE         DEFINE    WKHD05        TOSPHD
     C     *LIKE         DEFINE    WKHD05        TODPHD
     C     *LIKE         DEFINE    WKHD05        RINHD
     C     *LIKE         DEFINE    WKHD05        ROUHD
     C     *LIKE         DEFINE    WKHD05        QINHD
     C     *LIKE         DEFINE    WKHD05        QOUHD
     C     *LIKE         DEFINE    WKHD05        MORHD
     C     *LIKE         DEFINE    WKHD05        INAHD
      * POUNDS
     C                   Z-ADD     0             WKLB92            9 2
     C     *LIKE         DEFINE    WKLB92        PINLB
     C     *LIKE         DEFINE    WKLB92        TINLB
     C     *LIKE         DEFINE    WKLB92        TISPLB
     C     *LIKE         DEFINE    WKLB92        TIDPLB
     C     *LIKE         DEFINE    WKLB92        POULB
     C     *LIKE         DEFINE    WKLB92        TOULB
     C     *LIKE         DEFINE    WKLB92        TOSPLB
     C     *LIKE         DEFINE    WKLB92        TODPLB
     C     *LIKE         DEFINE    WKLB92        RINLB
     C     *LIKE         DEFINE    WKLB92        ROULB
     C     *LIKE         DEFINE    WKLB92        QINLB
     C     *LIKE         DEFINE    WKLB92        QOULB
     C     *LIKE         DEFINE    WKLB92        MORLB
      *
      *  DEFINE WORK FIELDS
      *
     C     *LIKE         DEFINE    HGADOO        AVGDAY
      *
     C                   ENDSR
      /EJECT
