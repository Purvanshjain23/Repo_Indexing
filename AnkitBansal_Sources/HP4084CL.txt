/***************************************************************** */
/*š                                                              € */
/*š PROGRAM NUMBER:  HP4084CL                                    € */
/*š PROGRAM NAME:    CLEAR SCHEDULED MOVEMENT RECORDS FOR A REDO € */
/*š PROGRAMMER:      BRAD BADEN                                  € */
/*š PROJECT:         E010812                                     € */
/*š CREATE DATE:     09/14/2017                                  € */
/*š                                                              € */
/*š FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM      € */
/*š                  HP4084. THIS PROGRAM USES SQL TO DELETE     € */
/*š                  RECORDS FROM FILES HSP2081 AND HSP2082      € */
/*š                  FOR A BUSINESS OFFICE.                      € */
/*=================================================================*/
/*  Modification History:                                          */
/*  xx/xx/xxxx xxx xxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&BUSOFC)

             DCL        VAR(&BUSOFC) TYPE(*CHAR) LEN(5)

             DCL        VAR(&USER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&COMMIT)   TYPE(*CHAR) LEN(5)
             DCL        VAR(&JRNLSTS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SQLSTMT) TYPE(*CHAR) LEN(256)

          /*‚Retrieve user ID for messages */
             RTVJOBA    USER(&USER)

          /*‚Business Office name cannot be blank */
             IF         COND(&BUSOFC *NE '     ') THEN(DO)

          /*‚Reorganize physical files before allocated */
          /*‚This keeps them at a small size.           */
             RGZPFM     FILE(HSP2081)
             MONMSG     MSGID(CPF0000)

             RGZPFM     FILE(HSP2082)
             MONMSG     MSGID(CPF0000)

          /*‚Delete records in the Receiving file HSP2081 */
          /*‚for the Business Office passed into this CLP */
             ALCOBJ     OBJ((HSP2081 *FILE *EXCL))

          /*‚Get Journal Status to see if file is journaled */
             RTVOBJD    OBJ(HSP2081) OBJTYPE(*FILE) JRNSTS(&JRNLSTS)
             SELECT
             WHEN       COND(&JRNLSTS = '0') THEN(CHGVAR +
                          VAR(&COMMIT) VALUE('*NONE'))
             WHEN       COND(&JRNLSTS = '1') THEN(CHGVAR +
                          VAR(&COMMIT) VALUE('*CHG '))
             ENDSELECT


             CHGVAR     VAR(&SQLSTMT) VALUE('DELETE FROM HSP2081 +
                          WHERE U1BOCD = ''' || &BUSOFC |< ''' ')
             RUNSQL     SQL(&SQLSTMT) COMMIT(&COMMIT)

             DLCOBJ     OBJ((HSP2081 *FILE *EXCL))

          /*‚Delete records in the Editing file HSP2082   */
          /*‚for the Business Office passed into this CLP */
             ALCOBJ     OBJ((HSP2082 *FILE *EXCL))

          /*‚Get Journal Status to see if file is journaled */
             RTVOBJD    OBJ(HSP2082) OBJTYPE(*FILE) JRNSTS(&JRNLSTS)
             SELECT
             WHEN       COND(&JRNLSTS = '0') THEN(CHGVAR +
                          VAR(&COMMIT) VALUE('*NONE'))
             WHEN       COND(&JRNLSTS = '1') THEN(CHGVAR +
                          VAR(&COMMIT) VALUE('*CHG '))
             ENDSELECT

             CHGVAR     VAR(&SQLSTMT) VALUE('DELETE FROM HSP2082 +
                          WHERE E1BOCD = ''' || &BUSOFC |< ''' ')
             RUNSQL     SQL(&SQLSTMT) COMMIT(&COMMIT)

             DLCOBJ     OBJ((HSP2082 *FILE *EXCL))

          /*‚Commit the changes if running under commitment control */
             IF         COND(&JRNLSTS *EQ '1') THEN(COMMIT)

             ENDDO

 EXIT:       ENDPGM
