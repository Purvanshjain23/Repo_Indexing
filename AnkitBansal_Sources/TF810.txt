      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF810
      * TITLE:       Retrieve Next Product Exception Number
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     06/11/08
      *
      * FUNCTION:  This function retrieves the next 'system-assigned' Product Exception
      *            Number. FYI: Product Exception stuff is used in both TFS and
      *            Meat Costing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99
      *
      /EJECT
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parms
      *
     D xxsn            s              7  0
     D xxpfcd          s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      ****************************************************************
      * Data areas
      ****************************************************************
      *
      * We have 2 data areas: 1 for Temporary numbers and 1 for permanent numbers
      * Temporary: used when this is called from a Preliminary run of Meat Costing
      * Permanent: used when this is called from a Final run of Meat Costing or from
      *            the TFS interactive program for manual record entry
      *
     Dnexttmsn         s              7  0 dtaara(datftmsn)
     Dnextpesn         s              7  0 dtaara(datfpesn)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
     C                   select
      *
      * When "preliminary", get a "temporary" number
      *
     C                   when      xxpfcd = 'P'
     C     *lock         in        nexttmsn
     C                   add       1             nexttmsn
     C                   z-add     nexttmsn      xxsn
     C                   out       nexttmsn
     C                   other
      *
      * Get a "real" Product Exception Number
      *
     C     *lock         in        nextpesn
     C                   add       1             nextpesn
     C                   z-add     nextpesn      xxsn
     C                   out       nextpesn
     C                   endsl
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxpfcd
     C                   parm                    xxsn
      *
     C                   endsr
      /EJECT
