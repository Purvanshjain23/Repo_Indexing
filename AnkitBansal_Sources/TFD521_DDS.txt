     A*%%TS  SD  20060418  144417  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD521-SELECT WEEKLY PRODUCT REPORT/DOWNLOAD
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/16/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF521S1                   SFL
     A*%%TS  SD  20060313  170646  ISLRAMS     REL-V5R3M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1RPFL    R        O  5  4REFFLD(@@FLAG)
     A            D1RPDS        38A  O  5 10
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF521C1                   SFLCTL(TF521S1)
     A*%%TS  SD  20060418  144417  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0004)
     A                                      WINDOW(TF521EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'WEEKLY PRODUCT REPORT/DOWNLOAD'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Report'
     A                                      DSPATR(HI)
     A                                  4 10'Description'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R TF521K1
     A                                      WINDOW(TF521EC)
     A                                      OVERLAY
     A  43                              9  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 10  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF521ES                   SFL SFLMSGRCD(10)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R TF521EC                   SFLCTL(TF521ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 12 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  19980327  102611  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
