     A*%%TS  SD  20030716  093807  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD477
     A*  TITLE:         SPECIFY PERIOD CLOSE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/13/94
     A*****************************************************************
     A* 02/26/16  Rose Centonze   E5290
     A*           Added company to the screen
     A*
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP477R1
     A*%%TS  SD  20030716  093807  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(33)
     A                                      SETOF(35)
     A                                      SETOF(53)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'SPECIFY PERIOD CLOSE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Company   .......'
     A            D1COCD    R        B  4 21REFFLD(@@COCD)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                  6  2'Period.........:'
     A            DFPER     R        O  6 21REFFLD(@@PER)
     A                                      EDTCDE(M)
     A            DFBMDY    R     Y  O  6 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  6 36'to'
     A            DFEMDY    R     Y  O  6 39REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  8  2'Edit or final ..'
     A                                  8 19'?'
     A            DFEFFL    R        B  8 21REFFLD(@@FLAG)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFEFDS    R        O  8 26REFFLD(@@DS10)
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP477K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A  44N99     DFEFDS        10A  O 23 32
     A N99                             23 44'F17=Print Options'
     A N99                             23 63'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP477ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP477EC                   SFLCTL(HP477ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
