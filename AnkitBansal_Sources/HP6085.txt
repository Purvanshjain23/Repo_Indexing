      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6085
      * TITLE:       Feed Report for Created Groups
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/01/06
      *
      * FUNCTION:  Batch program to print feed data for groups with a Created status.
      *
      *            This report is generated:
      *              1) on-demand from the Preliminary EOP Edits
      *              2) automatically as part of the EOP function
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/28/08  LeAnne Ramsey
      *           Program abended on Array Index "Y" out of range.
      *           "Y" was not being initialized for each group.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp034    if   e           k disk
      *    Hog group (records selected/keyed in open query)
      *
      *
     Fhsj034l   if   e           k disk    rename(hgrec:hgrecl) prefix(p1)
      *  Hog group + Rooms/Bin sets + Bin set bins + Feed ticket detail
      *
      *
     Fhsl038w   if   e           k disk
      *    Feed ticket detail
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * ARRAYS AND TABLES
      *----------------------------------------------------------------
      *
      * Define array for holding feed ticket numbers
      *
736  D***************** tkno            s              7  0 dim(100)
736  D tkno            s              9  0 dim(100)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svtkno          s                   like(fdtkno)
     D svcrdt          s                   like(hgcrdt)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkfymd          s                   like(fdrtdt)
      *
      *
      * Indexs
      *
     D x               s              3  0
     D y               s              3  0
      *
      *
      * Print fields
      *
     d h1ef            s              9
      *
     D l1crdtmdy       s              6  0
     D l1am            s                   like(fdinam)
     D l1lb            s                   like(fdfdlb)
     D l1countd        s              3  0
     D l1countt        s              3  0
      *
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldeffl                19     19
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print report headings
      *
     C                   exsr      $h1hdr
      *
      * Read each Hog Group selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp034                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      no            prtfl
     C                   z-add     0             svtkno
     C                   z-add     0             tkno
     C                   z-add     0             y
     C                   z-add     0             l1countd
     C                   z-add     0             l1countt
      *
      * Flip Creat Date from CCYYMMDD to MMDDYY format
      *
     C                   z-add     0             l1crdtmdy
     C     *iso          test(d)                 hgcrdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      hgcrdt        wkisodate
     C     *mdy          move      wkisodate     l1crdtmdy
     C                   endif                                                  If OK date
      *
      * Read/sum each Feed Ticket Detail record for this group.
      *
     C                   exsr      $feed
      *
      * Read/count each Feed Ticket that will be allocated to this group.
      *
     C                   exsr      $allocate
      *
      * Print the detail line
      *
     C                   if        prtfl = yes
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------
      * EOF processing
      *---------------------
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Process each Feed Ticket Detail record for the group
      *---------------------------------------------------------------
      *
     C     $feed         begsr
      *
     C     hghgsn        setll     hsl038w
     C                   dou       *in91 = *on                                  Do feed
     C     hghgsn        reade     hsl038w                                91
     C                   if        *in91 = *off                                 If not eof
      *
     C                   move      yes           prtfl
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     hgcrdt        svcrdt
      *
     C                   when      hgcrdt <> svcrdt
     C                   except    blank
     C                   z-add     hgcrdt        svcrdt
     C                   endsl
      *
      * Count the number of Delivery Tickets
      *
     C                   if        fdtrcd = 'D' and
     C                             svtkno <> fdtkno
     C                   add       1             l1countd
     C                   z-add     fdtkno        svtkno
     C                   endif
      *
      * Accumulate Feed Amount as:
      *   Ingredient Amount + Manufacturing Amount + Mediction Amount
      *
     C                   add       fdinam        l1am
     C                   add       fdmfam        l1am
     C                   add       fdmdam        l1am
      *
      * Accumulate Pounds
     C                   add       fdfdlb        l1lb
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do feed
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------
      * Count the number of Unallocated Feed Tickets that will go to this group
      *--------------------------------------------------------------------------------
      *
     C     $allocate     begsr
      *
     C     hghgsn        setll     hsj034l
      *
     C                   dou       *in92 = *on                                  Do allocated
     C     hghgsn        reade     hsj034l                                92
     C                   if        *in92 = *off
      *
      * Determine the "FROM" date for this Feed Ticket.
      *
     C                   exsr      $frdt
      *
      * If the 'from date' is before the group's 'create date', we will not
      * allocate this ticket to this group---so, don't count the ticket.
      *
     C                   if        wkfymd < hgcrdt                              If don't count
     C                   else
      *
      * We are processing this "to be allocated" ticket.
      *
     C                   move      yes           prtfl
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     hgcrdt        svcrdt
      *
     C                   when      hgcrdt <> svcrdt
     C                   except    blank
     C                   z-add     hgcrdt        svcrdt
     C                   endsl
      *
      * We only want to count each ticket 1 time. So, we will put each
      * unique ticket into an array and check the array for duplicate
      * entries.
     C                   z-add     1             x
     C     p1fdtkno      lookup    tkno(x)                                92
     C                   if        *in92 = *off                                 If not there
     C                   add       1             l1countt
     C                   add       1             y
     C                   z-add     p1fdtkno      tkno(y)
     C                   endif                                                  If not there
     C                   endif                                                  If don't count
      *
     C                   endif
     C                   enddo                                                  Do allocated
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Determine the "from" date for the Feed Ticket Detail record
      *--------------------------------------------------------------------------------------
      *
      * The 'FROM' date will be the Reference Ticket Date for all tickets--
      * except for the "TO BIN" record on a Pickup Ticket--where it will be
      * the Ticket Date of the Pickup Ticket.
      *
      * The only way to determine the "TO BIN" records on Pickup Tickets is
      * that the pounds will be greater than zero--instead of negative.
      *
     C     $frdt         begsr
      *
     C                   if        p1fdtrcd = 'P' and
     C                             p1fdfdlb > 0
     C                   z-add     p1fdtkdt      wkfymd
     C                   else
     C                   z-add     p1fdrtdt      wkfymd
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Set indicator to print "yes" when there have been more than 2
      * deliveries.
     C                   if        l1countd > 2
     C                   seton                                        87
     C                   else
     C                   setoff                                       87
     C                   endif
      *
      * Print headings
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
      * Print detail
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      * Edit/Final/On-demand
      *
     C                   select
     C                   when      ldeffl = 'D'
     C                   movel(p)  'On-Demand'   h1ef
      *
     C                   when      ldeffl = 'E'
     C                   movel(p)  '  Edit   '   h1ef
      *
     C                   when      ldeffl = 'F'
     C                   movel(p)  '  Final  '   h1ef
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           72 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           71 'FEED REPORT FOR CREATED '
     O                                           77 'GROUPS'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          2
     O                       h1ef                65
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
      *
     O          e            h1hdr          1
     O                                           95 'Number of'
     O                                          126 'Number of'
      *
     O          e            h1hdr          1
     O                                            7 'Created'
     O                                           13 'Hog'
     O                                           25 'Farm'
     O                                           51 'Production'
     O                                           65 'Feed'
     O                                           81 'Feed'
     O                                           95 'Delivery'
     O                                          111 'More than'
     O                                          127 'Unallocated'
      *
      *
     O          e            h1hdr          1
     O                                            6 'Date'
     O                                           15 'Group'
     O                                           25 'Site'
     O                                           31 'Bldg'
     O                                           38 'Room'
     O                                           48 'Phase'
     O                                           65 'Amount'
     O                                           81 'Pounds'
     O                                           94 'Tickets'
     O                                          112 '2 Deliveries'
     O                                          125 'Tickets'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       l1crdtmdy            8 '  /  /  '
     O                       hghgcd              17
     O                       hgfscd        zb    25
     O                       hgblcd         b    32
     O                       hgrmcd         b    39
     O                       hgppcd         b    48
     O                       l1am          kb    66
     O                       l1lb          kb    82
     O                       l1countd      kb    92
     O               87                         108 'Yes'
     O                       l1countt      kb   124
      *
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
