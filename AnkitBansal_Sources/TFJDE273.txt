      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Triumph Foods / JD Edwards
      * PROGRAM:     TFJDE273
      * TITLE:       Upload to JDE: AR Adjustment
      * PROGRAMMER:  Alice Brownfied
      * CREATED:     10/08/09
      *
      *  FUNCTION:   This program uploads A/R Adjustment data to JDE.
      *
      *              The program is called from the Weekly Revenue Close function when
      *              it is a "Final" run.
      *
      *              Seaboard is Company 360
      *              Triumph  is Company 960
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/11/11  LeAnne Ramsey  (E1332)
      *           Since we are still NOT turning on/implementing the creation/post of the
      *           JDE batches...Kevin Henn does not want us to do this until the AR/CD Recon
      *           is settled...I moved the "account" logic into TF261 so that Jeff Sherbondy can
      *           see/review the 'accounts' on the TF661-AR Adjustment Report by GL Post Account.
     H*  4/22/2021   Rose Centonze               Scan:  SDN440
     H*                   Modify to use the new E1 tables:
     H*                   F0011  -->  E10011l1  when write here, write to e1b9cpp
     H*                   F0911  -->  E10911  --> new fields
     H*   in tfjde801, tfjde800 -->   X0010  -->  GET0010  - new logic  -goes to BSSV
      *         1 set edi trans no to gldoc
      *         2 GLKCO - document company - leave blank
      *         3 GLPKCO  GLOKCO  GLPSFX  - leave blank , not 000
      *         4 glrcnd - reconciled-leave Blank
      *         5 glag - should have the amount, not GLAA
      *         6 icndo, icodcn- increment when get document
      *         7 GL ENTRIES WILL HAVE EDI USER‚360- OMSJDESF or 960- OMSJDETF > xxedus  ***
      *               Per company value
      *  7/28/21          CHANGE all refs of company 362 to 320
      /EJECT
      *********************************************************************************************
      * File specifications
      *********************************************************************************************
      *
     F*f0011     uf a e           k disk                                            sdn440
     Fe10011l1  UF A E           K DISK
      *   JDE Batch control record
      *
      *
     F*f0911     uf a e             disk                                           sdn440
     Fe10911    uf a e             DISK
      *   JDE Account ledger
      *
      *
     Ftfl025a   uf   e           k disk
      *   AR Adjustment G/L post
      *
      *
     Fprint132  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d first           s              1    inz('Y')
     d svpcc           s                   like(appcc)
     d svpobj          s                   like(appobj)
     d svpsub          s                   like(appsub)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D xxccyymmdd      s              8  0                                      sdn440
     D xxsyssyndt      s              7  0                                      sdn440
      *
      *
      * Workfields
      *
     d wkam            s                   like(appostam)
     d wkuser          s                   like(gluser)
      *
     d wkdoccnt        s              5  0
     d wksdoccnt       s              5  0                                      SF line count sdn440
     d wktdoccnt       s              5  0                                      TF line count SDN440
     d wkbatcnt        s              5  0
     d wksbatcnt       s              5  0                                      sf doc count  sdn440
     d wktbatcnt       s              5  0                                      TF doc count  sdn440
     d wkbatam         s             15  2
     d wkbatval        s             15  0
     d wkbatnbr        s                   like(glicu)
     d wknumco         s              3  0 inz(960)
      *
     d wksco5a         s              5
     d wksbatnbr       s                   like(glicu)
     d wksdocnbr       s                   like(gldoc)
      *
     d wktco5a         s              5
     d wktbatnbr       s                   like(glicu)
     d wktdocnbr       s                   like(gldoc)
      *
     d wktime          s             12  0
     d wkdate          s              6  0
      *
      *
      * Parm fields
      *
     d xxapfl          s              1
     D xxdt            s              8  0
     D xxyr            s              4  0
     D xxpe            s              2  0
     D xxwk            s              2  0
      *
      *
      * For call to JDE date routine
      *
     d xxsidat         s              6
     d*xxedat          s              8                                          sdn440
     d xxedat          s              6  0                                       sdn440
     d xxffmt          s              7
     d xxtfmt          s              7
     d xxsep           s              7
     d xxertst         s              1
     d xxctry          s              2
      *
      * For call to JDE account routine
      *
     d xxbatnbr        s                   like(glicu)
     d xxdocnbr        s                   like(gldoc)
     D xxedus          s                   like(gledus)
      *
      *
      * Print fields
      *
     d h1mdy           s              6  0
      *
     d h2conm          s              8
     d h2batnbr        s                   like(glicu)
     d h2docnbr        s                   like(gldoc)
      *
     d l1pcono         s                   like(appcono)
     d l1acct          s             47
     d l1desc          s             30
     d l1dbam          s                   like(wkam)
     d l1cram          s                   like(wkam)
      *
     d t1dbam          s                   like(wkam)
     d t1cram          s                   like(wkam)
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Break CCYY into CC and YY fields
      *---------------------------------------------------------------
      *
     D wkyear          ds
     D   wkccyy                       4  0
     D   wkcc                         2  0 overlay(wkccyy:1)
     D   wkyy                         2  0 overlay(wkccyy:3)
      *
      *----------------------------------------------------------------
      * LDA - local data area
      *----------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldcedt                22     29  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *-------------------------------------------------------------------------------------
      * Mainline
      *-------------------------------------------------------------------------------------
      *
      * Print report heading
      *
     C                   exsr      $h1hdr
      *
      * Seaboard/Triumph batches
      *
     C                   exsr      $seaboard
     C                   exsr      $triumph
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------
      * Print and write JDE Account Ledger records for Seaboard
      *----------------------------------------------------------------
      *
     C     $seaboard     begsr
      *
      * Print the Seaboard heading
      *
     C                   eval      h2conm = 'Seaboard'
     C                   z-add     wksbatnbr     h2batnbr
     C                   z-add     wksdocnbr     h2docnbr
     C                   except    h2hdr
     C                   except    blank
      *
      * Set up some defaults
      *
     C                   eval      glexa = 'TF AR Adj Share'
     C                   eval      wkuser = sdusr
      *
      * Read the records for SBF (360 & 320) for the WeekEnding Date
      *
      * Company 360
     C                   move      yes           first
     C                   z-add     360           wknumco
     C                   exsr      $read025
      * Company 320
     C                   move      yes           first
     C                   z-add     320           wknumco
     C                   exsr      $read025
      *
      * Write Seaboard Batch Control record
      *
     C                   z-add     wksbatnbr     wkbatnbr
     C                   z-add     wksbatcnt     wkbatcnt                       sdn440 SF doc count
     C                   eval      xxedus = xxedussf                            sdn440
     C                   exsr      $wrtbat
      *
      * Print Total line
     C                   exsr      $t1tot
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Print and write JDE Account Ledger records for Triumph
      *----------------------------------------------------------------
      *
     C     $triumph      begsr
      *
      * Initialize totals
     C                   z-add     0             t1dbam
     C                   z-add     0             t1cram
      *
      * Print 3 blank lines so that we have some white space between the
      * Seaboard section and the Triumph section.
      *
     C                   except    blank
     C                   except    blank
     C                   except    blank
      *
      * Print the Triumph heading
      *
     C                   eval      h2conm = 'Triumph'
     C                   z-add     wktbatnbr     h2batnbr
     C                   z-add     wktdocnbr     h2docnbr
     C                   except    h2hdr
     C                   except    blank
      *
      * Set up some defaults; then, process the Triumph records.
      *
     C                   eval      wkuser = 'TFGLPOST  '
     C                   eval      glexa = 'TF AR Adj Share'
      *
     C                   move      yes           first
     C                   z-add     960           wknumco
     C                   exsr      $read025
      *
      * Write Triumph Batch Control record
      *
     C                   z-add     wktbatnbr     wkbatnbr
     C                   z-add     wktbatcnt     wkbatcnt                       sdn440 tF doc count
     C                   eval      xxedus = xxedustf                            sdn440
     C                   exsr      $wrtbat
      *
      * Print Total line
     C                   exsr      $t1tot
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Process records from TFP025 - print and write to 911 at break
      *----------------------------------------------------------------
      *
     C     $read025      begsr
      *
      * Read the records for the WeekEnding Date for the Posting Company
      *
     C     key02         setll     tfl025a
      *
     C                   dou       *in90 = *on                                  Do loop
     C     key02         reade     tfl025a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   exsr      $setup
      *
     C                   when      svpcc  <> appcc  or
     C                             svpobj <> appobj or
     c                             svpsub <> appsub
     C                   exsr      $wrt911
     C                   exsr      $l1dtl
     C                   exsr      $setup
     c                   endsl
      *
      * Detail processing
     C                   add       appostam      wkam
      *
      * Sum debits for batch total
      *
     C                   if        wkam > 0
     C                   add       wkam          wkbatam
     C                   endif
      *
      * Update AR Adjustment GL Post record with Batch Number
      *
     C                   z-add     h2batnbr      apglbatno
     C                   update    aprec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
      * Last record processing
      *
     C                   exsr      $wrt911
     C                   exsr      $l1dtl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Setup fields when CostCenter/Object/Sub change
      *----------------------------------------------------------------
      *
     C     $setup        begsr
      *
      * Initialize accumulator
      *
     C                   z-add     0             wkam
      *
      * Save Control fields
      *
     C                   move      appcc         svpcc
     C                   move      appobj        svpobj
     C                   move      appsub        svpsub
      *
      * Set up JDE fields
     C                   move      appcc         glmcu
     C                   move      appobj        globj
     C                   move      appsub        glsub
     C                   move      appaid        glaid
     C                   move      appaidds      glaid
      *
     C                   move      apglcono      glco
     C                   move      apglacct      glani
     C                   eval      gluser = wkuser
     C                   eval      gltorg = wkuser
      *
      * Set up print fields
     C                   z-add     appcono       l1pcono
     C                   move      appaidds      l1desc
      *
      * Populate the 'account' field for printing.
      *
      *       No error (ie: We ultimately got a Structured Account Number)
      *
     C                   select
     C                   when      apglacct <> *blank
     C                   setoff                                       87
     C                   eval      l1acct = %trim(appcc)  + '.' +
     C                                      %trim(appobj) + '.' +
     C                                      %trim(appsub)
      *
      *       Error on Account ID Retrieval (ie: using CC/Obj/Sub we did not get an ID)
      *
     C                   when      appaid = *blank
     C                   seton                                        87
     C                   eval      l1acct = 'Unknown CC/Obj/Sub: ' +
     C                                      %trim(appcc)  + '.' +
     C                                      %trim(appobj) + '.' +
     C                                      %trim(appsub)
     C                   other
      *
      *       Error on CC/Obj/Sub (ie: using Account ID we did not find the CC/Obj/Sub)
      *
     C                   seton                                        87
     C                   eval      l1acct = 'Unknown Account ID: ' +
     C                                      %trim(appaid)
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a JDE Account Ledger record
      *---------------------------------------------------------------
      *
     C     $wrt911       begsr
      *
     C                   select
     C                   when      h2conm = 'Seaboard'
     C                   z-add     wksbatnbr     glicu
     C**                 move      wksco5a       glkco
     C                   move      *blanks       glkco                          sdn440
     C                   z-add     wksdocnbr     gldoc
     C                   add       1             wksdoccnt                      sdn440 SF line count
     C                   z-add     wksdoccnt     wkdoccnt                       sdn440
     C                   moveL     xxedussf      gledus                         sdn440
      *
     C                   when      h2conm = 'Triumph'
     C                   z-add     wktbatnbr     glicu
     C**                 move      wktco5a       glkco
     C                   move      *blanks       glkco                          sdn440
     C                   z-add     wktdocnbr     gldoc
     C                   add       1             wktdoccnt                      sdn440 TF line count
     C                   z-add     wktdoccnt     wkdoccnt                       sdn440
     C                   moveL     xxedustf      gledus                         sdn440
     C                   endsl
      *
     C**                 add       1             wkdoccnt                sdn440  accum above per com
     C                   z-add     wkdoccnt      gljeln
      * SDN440 added for new fields in e10911
     C                   movel     glicu         gledbt                         edi batch no
     C                   movel     gldoc         gledtn                         edi trans #->gldoc
     C                   z-add     gljeln        gledln                         edi line no
     C                   movel     '0'           gledsp                         edi success procd
     C                   movel     'A'           gledtc                         edi trans action
     C                   movel     'J'           gledtr                         edi trans type
     C                   move      xxsyssyndt    glabdt                         Date updated
     C                   time                    glabtm                         Time Last Updated
     C                   movel     'TFJDE273 '   glakvn                         pgm  Last Updated
     C                   movel     gluser        glajvn                         user id created
      *
      * Get the amount into the JDE non-decimal format by multiplying by 100.
      *
     C**   100           mult      wkam          glaa                               SDN440
     C                   Z-ADD     wkam          glag                               sdn440
      *
      * Set up print fields
      *
     C                   if        wkam > 0
     C                   z-add     wkam          l1dbam
     C                   else
     C                   z-add     wkam          l1cram
     C                   endif
      *
      * Accumulate Debit/Credit amounts for totals
      *
     C                   add       l1dbam        t1dbam
     C                   add       l1cram        t1cram
      *
      * Write record
     C                   write     i0911
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a batch record
      *---------------------------------------------------------------
      *
     C     $wrtbat       begsr
      *
      *  Put the "amount" into the JDE non-decimal format.
      *
     C     100           mult      wkbatam       wkbatval
      *
      * Write a record to batch control file
      *
     C**   key01         chain     f0011                              92
     C     KEY01         CHAIN     I0011l1                            92
     C                   if        *in92 = *on                                  If no hit
     C                   z-add     wkbatcnt      icndo
     C                   z-add     wkbatcnt      icdocn
     C**                 z-add     wkbatval      icaicu                           sdn440
     C                   move      wkbatval      icaicu                           no dec-sdn440
     C                   z-add     wkbatval      icame
      *
     C                   z-add     glicu         icicu
     C                   move      *blank        icist
     C**                 movel     wkuser        icuser                           sdn440
     C                   MOVEL     xxedus        ICUSER                           sdn440
     C                   move      glicut        icicut
     C                   move      gldicj        icdicj
     C                   move      'A'           iciapp
     C                   move      'Y'           icbal
     C                   move      ' '           icbalj
     C                   move      'N'           icpob
     C                   move      'Y'           iciboi
      *
     C**                 write     i0011                                        sdn440
     C                   WRITE     I0011l1                                      sdn440
     C*
     C* WRITE RUN LOG HEADER WORKFILE - WILL BE READ IN CL TF410CL TO PUSH BATCHES TO ZPROCESSOR
     C*
     C                   call      'E1LCXFR'
     c                   parm                    RETURN            7
     C                   parm                    icICUT                         BATCH TYPE
     C                   parm                    icICU                          BATCH NUMBER
     C                   parm                    icuser                         USER
     C                   parm                    glco                           company
     C                   endif                                                  If no hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Get dates into JDE format
      *---------------------------------------------------------------
      *
     C     $date         begsr
      *
     C*** sdn440         call      'X0028'
     C*                  parm                    xxsidat
     C*                  parm      *blank        xxedat
     C*                  parm                    xxffmt
     C*                  parm      '*JUL   '     xxtfmt
     C*                  parm      '*NONE'       xxsep
     C*                  parm      *blank        xxertst
     C*                  parm      *blank        xxctry
      *
     C                   call      'E10028A'
     C                   parm                    xxccyymmdd
     C                   parm      0             xxedat
      *
     c                   move      xxedat        xxsidat
     C                   move      xxsidat       #sidat            6
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    glicut
     C                   kfld                    wkbatnbr
      *
     C     key02         klist
     C                   kfld                    ldcedt
     C                   kfld                    wknumco
      *
     C                   time                    rtime
      *
      * Flip the Week End to MMDDYY format.
      *
     C     *iso          test(d)                 ldcedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ldcedt        wkisodate
     C     *mdy          move      wkisodate     h1mdy
     C                   endif                                                  If OK date
      *
      * Retrieve/save a batch Number for:
      *   1) Triumph
      *   2) Seaboard
     C                   call      'TFJDE800'
     C     wksbatnbr     parm      0             xxbatnbr
      *
     C                   call      'TFJDE800'
     C     wktbatnbr     parm      0             xxbatnbr
      *
      *
      * Retrieve/save a Document Number for:
      *   1) Triumph
      *   2) Seaboard
     C                   call      'TFJDE801'
     C     wksdocnbr     parm      0             xxdocnbr
     c                   add       1             wksbatcnt                       sdn440 SF doc count
      *
     C                   call      'TFJDE801'
     C     wktdocnbr     parm      0             xxdocnbr
     c                   add       1             wktbatcnt                       sdn440 TF doc count
      *
      *
      * Set up/save Company numbers as both 3,0 numeric and 5 alpha fields.
      *
     C                   move      *zero         wksco5a
     C                   move      '360'         wksco5a
      *
     C                   move      *zero         wktco5a
     C                   move      '960'         wktco5a
      *
      * Set up defaults for records in F0911-Account Ledger File.
      *
      * Get system date into the format required by JDE by calling JDE's routine.
      *
     C                   time                    wktime
     C                   move      wktime        wkdate
     C                   move      wkdate        xxsidat
     C                   move      '*SYSVAL'     xxffmt
      *
     C     *mdy          movel     udate         wkisodate                       sdn440
     C                   movel     wkisodate     xxccyymmdd                      sdn440
     C                   call      'HP8008'                                      sdn440
     C                   parm                    xxccyymmdd                      sdn440
     C                   parm      0             xxsyssyndt
     C                   exsr      $date                                         sdn440
     C                   move      xxsidat       glupmj
     C                   move      xxsidat       gldsyj
     C                   move      xxsidat       gldicj
      *
      * Retrieve the Accounting Year/Period associated with the Date.
      *
     C                   call      'TF815'
     C                   parm      ldcedt        xxdt
     C                   parm      'A'           xxapfl
     C                   parm      0             xxyr
     C     glpn          parm      0             xxpe
     C                   parm      0             xxwk
      *
     C                   z-add     xxyr          wkccyy
     C                   z-add     wkcc          glctry
     C                   z-add     wkyy          glfy
      *
      * Get Week End Date into JDE format by calling JDE date routine.
      *
     C                   move      ldcedt        xxsidat
     C                   move      '*YMD   '     xxffmt
     C     *cymd         move      ldcedt        wkisodate                         sdn440
     c**                 move      wkisodate     xxccyymmdd                        sdn440
     C                   move      ldcedt        xxccyymmdd                        sdn440
     C                   exsr      $date
     C                   move      xxsidat       gldgj
     C                   move      xxsidat       gldsvj
      *
      * Set up some defaults
      *
     C                   movel     sdjob         gljobn
     C                   time                    glticu
     C                   time                    glupmt
      *
     C                   move      '2'           glam
     C                   move      'AA'          gllt
     C** sdn440          move      '00000'       glpkco
     C** sdn440          move      '00000'       glokco
     C** sdn440          move      '000'         glpsfx
     C                   move      '     '       glpkco                         sdn440
     c                   move      '     '       glokco                         sdn440
     C                   move      '   '         glpsfx                         sdn440
     C                   move      'JE'          gldct
     C                   move      'G '          glicut
     C*** sdn440         move      'R'           glrcnd
     C                   move      ' '           glrcnd                           sdn440
     C                   move      'D'           glcrrm                           sdn440
     C                   move      'USD'         glcrcd                           sdn440
     C                   movel     'TFJDE273  '  glpid                            sdn440
      *
      *
      * Get company value OMSJDE for User ID to use for gledus
      *
     c                   movel     'OMSJDEUSR '  xxval
     c                   call      'PUQ6XFR '
     C                   PARM                    XXRTN             7
     C                   PARM      360           xxnumco           3 0
     C                   PARM                    XXVAL            10
     C                   PARM                    XXEDUSSF         10             360 user id
     c                   movel     'OMSJDEUSR '  xxval
     c                   call      'PUQ6XFR '
     C                   PARM                    XXRTN             7
     C                   PARM      960           xxnumco
     C                   PARM                    XXVAL            10
     C                   PARM                    XXEDUSTF         10             960 user id
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           80 'AR ADJUSTMENT POST REPORT'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           75 'JOURNAL ENTRIES'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          122 'PAGE'
     O                       page          z    132
      *
     O          e            h1hdr          2
     O                                           14 'Week End date:'
     O                       h1mdy          b    24 '  /  /  '
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            5 'Post'
     O          e            h1hdr          1
     O                                            7 'Company'
     O                                           24 'Account Number'
     O                                           70 'Description'
     O                                          105 'Debit'
     O                                          123 'Credit'
      *
     O          e            h1hdr       0  2
     O                       dash               132
      *
      *
      *-------------------------------------------------------------
      * Company header
      *-------------------------------------------------------------
      *
     O          e            h2hdr       0  0
     O                       h2conm               8
     O                                           25 'JDE Batch Nbr:'
     O                       h2batnbr      z     35
     O                                           54 'JDE Document Nbr:'
     O                       h2docnbr      z     64
      *
     O          e            h2hdr       0  1
     O                       h2conm               8
     O                                           25 'JDE Batch Nbr:'
     O                       h2batnbr      z     35
     O                                           54 'JDE Document Nbr:'
     O                       h2docnbr      z     64
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  1
     O                       l1pcono       zb     4
     O               87                           9 '*'
     O                       l1acct         b    57
     O                       l1desc         b    89
     O                       l1dbam        kb   106
     O                       l1cram        kb   124
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       0  1
     O                                          105 '--------------'
     O                                          123 '--------------'
      *
     O          e            t1tot       0  0
     O                       t1dbam        kb   106
     O                       t1cram        kb   124
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       0  1
      /eject
      *
