      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP302 - WRITE FEED TICKET ALLOCATION RECORDS FROM WORKFILES
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/16/94
      *
      * FUNCTION: THIS PROGRAM READS THE TWO FEED TICKET ALLOCATION
      *           WORKFILES AND WRITES THE ALLOCATION RECORDS TO THE
      *           FEED TICKET DETAIL FILE.
      *
      *           THIS PROGRAM IS SUBMITTED BY HP468CL WHICH IS CALLED
      *           FROM HP468 THROUGH QCMDEXC.
      *
      *           THE WORKFILES ARE CREATED IN HP301-BUILD WORKFILES.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/30/94  LEANNE FEDOR
      *           REPLACED HSP038 WITH HSL038I WHICH SELECTS ONLY
      *           THE T=TO BE ALLOCATED RECORDS.  THIS WILL CORRECT
      *           A BUG WITH THE PICKUP TICKET ALLOCATIONS.
      *
      * 09/12/94  LEANNE FEDOR
      *           REMOVED THE FOLLOWING TIME FIELDS FROM THE FEED
      *           TICKET HEADER AND DETAIL FILE.
      *                 COSTED TIME
      *                 ALLOCATED TIME
      *                 G/L POST TIME
      *                 EOP POST TIME
      *
      * 04/26/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 03/09/98  LEANNE FEDOR
      *           RECOMPILE ONLY. FEED POUNDS AND FEED AMOUNT FIELDS
      *           INCREASED IN SIZE PER NATHAN MALONE.
      *
      * 01/30/01  LeAnne Fedor
      *           Production type was removed from the feed ticket files.
      *           So, removed all production type logic from program.
      *
      * 12/09/02  LeAnne Fedor
      *           Production Phase was removed from the feed ticket detail file.
      *
      * 04/21/08  LeAnne Ramsey
      *           No logic changes; cleaned up code (upper/lower case, ILE conventions).
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      *************************************************************************************
      * FILE SPECIFICATIONS
      *************************************************************************************
      *
     Fhsp301    if   e           k disk
      *  Feed ticket workfile
      *
      *
     Fhsp302    if   e           k disk
      *  Group workfile
      *
      *
     Fhsl038i   uf a e           k disk
      *  Feed ticket detail (LF selects To Be Allocated)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **************************************************************************************
      * MAIN LINE
      **************************************************************************************
      *
      * Read each Feed Ticket Detail record in the Ticket Workfile.
      *
      * If the ticket record in the workfile has been 'allocated',
      * make sure that the original Feed Ticket Detail record still exists in
      * the Feed Ticket Detail file. If so, process the allocation.
      *
      * (NOTE: There are no checks that the records match. If the user has changed
      * a Feed Ticket Detail record, it will be overlaid with the values in the
      * workfile for that Line Number.)
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp301                                 90
     C                   if        *in90 = *off and wtalfl = 'A'                If not EOF
      *
     C     key01         chain     hsl038i                            91
     C                   if        *in91 = *off                                 If exists
     C                   delete    fdrec
      *
      * Set up values from the Feed Ticket record in the Workfile
      *
     C                   movel     wtrgcd        fdrgcd
     C                   movel     wtfmbo        fdfmbo
     C                   z-add     wtfscd        fdfscd
      *
     C                   z-add     wttkno        fdtkno
     C                   movel     wttrcd        fdtrcd
     C                   z-add     wtlnno        fdlnno
     C                   z-add     wttkdt        fdtkdt
     C                   movel     wtalfl        fdalfl
     C                   z-add     wtaldt        fdaldt
      *
     C                   z-add     wtrtno        fdrtno
     C                   z-add     wtrtdt        fdrtdt
      *
     C                   movel     wtbncd        fdbncd
     C                   movel     wtrncd        fdrncd
     C                   movel     wtadcd        fdadcd
     C                   movel     wtftcd        fdftcd
      *
     C                   z-add     wtrcdt        fdrcdt
     C                   z-add     wtmcdt        fdmcdt
      *
     C                   exsr      $group
     C                   endif                                                  If exists
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Retrieve/Process Group Records
      *--------------------------------------------------------------------------------------
      *
     C     $group        begsr
      *
     C     key02         setll     hsp302
      *
     C                   dou       *in92 = *on                                  Do group
     C     key02         reade     hsp302                                 92
     C                   if        *in92 = *off                                 If not EOF
      *
     C                   movel     wgblcd        fdblcd
     C                   movel     wgrmcd        fdrmcd
     C                   z-add     wghgsn        fdhgsn
      *
     C                   z-add     wginam        fdinam
     C                   z-add     wgmfam        fdmfam
     C                   z-add     wgmdam        fdmdam
     C                   z-add     wgdlam        fddlam
     C                   z-add     wgfdlb        fdfdlb
      *
     C                   write     fdrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do group
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Initialization subroutine
      *--------------------------------------------------------------------------------------
      *
     C     $inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wtfmbo
     C                   kfld                    wttkno
     C                   kfld                    wttrcd
     C                   kfld                    wtlnno
      *
     C     key02         klist
     C                   kfld                    wttkno
     C                   kfld                    wttrcd
     C                   kfld                    wtlnno
      *
     C                   endsr
      /EJECT
