      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3056
      * TITLE:       Build Workfile: Daily/Weekly Mortality Variance Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/08/03
      *
      * FUNCTION:  Batch program to build workfile for listing.
      *
      *            This listing is submitted from HP4056-Specify.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/28/03  LeAnne Fedor
      *           Rewrote.
      *           The program originally used 2 Datamart Files:
      *                    HPPF103-Datamart FIN: Reported Daily Activity
      *                    HPPF075-Datamart FIN: Weekly Group Detail
      *           It now uses:
      *                    HSP103-Reported Daily Activity
      *                    HSP068-Killed/Dead Hogs
      *
      * 08/24/05  LeAnne Fedor
      *           Recompile only. New field "Man Hours" added to Reported Daily
      *           Activity file.
      *
      * 05/15/09  LeAnne Ramsey
      *           Per Sami Wilson, we are removing Projected Destroyed Head.
      *
      * 04/21/10  LeAnne Ramsey
      *           Per Sami Wilson, remove the last 3 columns from the Report:
      *             Total Weekly Vs Projected
      *              Daily   Weekly  Variance
      *              Head    Head     Head
      *           So, I have removed these from the workfile.
      *           Also, Sami only wants records where ther is a "Total Mortality
      *           Variance Head".
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fhsl068c   if   e           k disk
      *   Killed/Dead Hogs
      *   (records selected in open query)
      *
      *
     Fhsp103    if   e           k disk
      *   Reported Daily Activity
      *   (records selected in open query)
      *
      *
     Fhsp3056   uf a e           k disk
      *  Workfile: Daily/Weekly Mortality Variance
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     d procfl          s              1    inz('Y')
     d dailyfl         s              1    inz('Y')
     d weeklyfl        s              1    inz('Y')
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcdyr                 1      4  0
     D  ldcdwk                 5      6  0
      *
     D  ldppcd                41     45
     D  ldcell                46     47  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Use the Farm Master as the driver file.
      *
     C     *loval        setll     hsp018
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      yes           procfl
      *
      *
      * Check to see if this farm matches the user's selections on Production
      * Phase and Cell.
     C                   exsr      $match
      *
      * If you are processing this farm.
      *
     C                   if        procfl = yes                                 If process
      *
     C                   move      no            dailyfl
     C                   move      no            weeklyfl
      *
      * Retrieve Daily and Weekly data for the farm.
      *
     C                   exsr      $daily
     C                   exsr      $weekly
      *
      * If you had daily or weekly records, write a record for the
      * farm to the workfile.
      *
     C                   if        dailyfl = yes or weeklyfl = yes
     C                   exsr      $wrt3056
     C                   endif
      *
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------
      * Check that Farm matches user's selections
      *--------------------------------------------------------------------------
      *
     C     $match        begsr
      *
      * Cell
     C                   if        ldcell <> 0 and ldcell <> fscell
     C                   move      no            procfl
     C                   endif
      * Production phase
     C                   if        ldppcd <> *blank and ldppcd <> fsppcd
     C                   move      no            procfl
     C                   endif
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------
      * Process Reported Daily Activity records
      *--------------------------------------------------------------------------
      *
     C     $daily        begsr
      *
     C     fsfscd        setll     hsp103
      *
     C                   dou       *in91 = *on                                  Do daily
     C     fsfscd        reade     hsp103                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   move      yes           dailyfl
     C                   add       rdddhd        w1ddhd
     C                   add       rddshd        w1dshd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do daily
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------
      * Process Killed/Dead Hogs
      *--------------------------------------------------------------------------
      *
     C     $weekly       begsr
      *
     C     fsfscd        setll     hsl068c
      *
     C                   dou       *in91 = *on                                  Do weekly
     C     fsfscd        reade     hsl068c                                91
     C                   if        *in91 = *off                                 If not EOF
     C                   move      yes           weeklyfl
      *
     C                   select
     C                   when      kddsfl = 'Y'
     C                   add       kdkdhd        w1wshd
     C                   other
     C                   add       kdkdhd        w1wdhd
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do weekly
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------
      * Write workfile record
      *--------------------------------------------------------------------------
      *
     C     $wrt3056      begsr
      *
     C                   z-add     ldcdyr        w1cdyr
     C                   z-add     ldcdwk        w1cdwk
      *
     C                   z-add     fsfscd        w1fscd
     C                   move      fsfsnm        w1fsnm
     C                   move      fsppcd        w1ppcd
      *
      * Calculate 'mortality' head
      *
     C     w1ddhd        add       w1dshd        w1dmhd
     C     w1wdhd        add       w1wshd        w1wmhd
      *
      *
      * Calculate variance values as:
      *   daily - weekly
      *
     C     w1ddhd        sub       w1wdhd        w1vdhd
     C     w1dshd        sub       w1wshd        w1vshd
     C     w1dmhd        sub       w1wmhd        w1vmhd
      *
      *
      * Write workfile record ONLY when there is a Total Mortality Variance Head.
      *
     C                   if        w1vmhd <> 0
     C                   write     w1rec
     C                   endif
      *
      * Always clear record and accumulators
      *
     C                   clear                   w1rec
      *
     c                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C                   endsr
      /EJECT
