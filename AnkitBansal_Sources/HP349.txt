      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP349
      * TITLE:       BUILD WORKFILE FOR DOA REPORT BY FARM--SUMMARY
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/19/95
      *
      *  FUNCTION:   THIS BATCH PROGRAM IS CALLED FROM HP455CL.
      *              IT BUILDS WORKFILE HSP349 THAT IS USED IN THE
      *              REPORT PROGRAM HP649.
      *
      *              THE USER'S SUBMISSION CRITERIA ARE PASSED IN THE
      *              LDA FROM HP455.  AN OPEN QUERIES ARE USED IN THE
      *              CL TO MAKE AN INITIAL SELECTION OF DATA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/06/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO SALES MOVEMENT HEADER FILE.
      *
      * 02/13/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO TRANSFER MOVEMENT HEADER FILE.
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      * 12/18/02  LeAnne Fedor
      *           Production Phase/Type were removed from Sales Movement Header file.
      *           Modified logic here to use group production phase/type in Sales
      *           section.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP074    IF   E           K DISK
      *  TRANSFER MOVEMENT HEADER   (records selected in OPNQRY)
      *
      *
     FHSP075    IF   E           K DISK
      *  TRANSFER MOVEMENT DETAIL   (records selected in OPNQRY)
      *
      *
     FHSP084    IF   E           K DISK
      *  SALES MOVEMENT HEADER      (records selected in OPNQRY)
      *
      *
     FHSP085    IF   E           K DISK
      *  SALES MOVEMENT DETAIL      (records selected in OPNQRY)
      *
      *
     FHSP349    UF A E           K DISK
      *    WORKFILE
      *
      ****************************************************************
      * Definition specs
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
      *
      *
      * Workfields
      *
     D wkhgsn          s                   like(hghgsn)
      *
      *----------------------------------------------------------------
      * Local data area
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDPPCD                22     26
     D  LDGTCD                47     47
     D  LDPTCD                48     52
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * DATA FOR THE WORKFILE IS RETRIEVED IN 2 STEPS.
      *     STEP 1: DOA HOGS FROM TRANSFERS
      *     STEP 2: DOA HOGS FROM SALES
      *
     C                   exsr      $transfers
     C                   exsr      $sales
      *
     C                   seton                                        lr
      /EJECT
      *-------------------------------------------------------------------
      * Process Transfer Movements
      *-------------------------------------------------------------------
      *
      * READ ALL TRANSFER MOVEMENTS SELECTED BY THE OPEN QUERY. FOR
      * EACH MOVEMENT, RETRIEVE/PROCESS ALL ASSOCIATED DETAIL RECORDS.
      *
      * AN OPEN QUERY OVER THE DETAIL RECORDS WILL HAVE LIMITED THE
      * DATA TO THE USER-SELECTED HOG GROUP. BUT, IF THE USER LIMITED THE
      * REPORT TO A SPECIFIC 'GROUP TYPE', COMPARE THE GROUP'S GROUP TYPE
      * TO THE USER'S SELECTED TYPE BEFORE PROCESSING THE DETAIL RECORD.
      *
     C     $transfers    begsr
      *
     C     *LOVAL        SETLL     HSP074
      *
     C     *IN90         DOUEQ     *ON                                          DO TRAN DOA
     C                   READ      HSP074                                 90
     C     *IN90         IFEQ      *OFF                                         IF TRAN DOA
      *
     C     MHMVSN        SETLL     HSP075
     C     *IN91         DOUEQ     *ON                                          DO TRAN DTL
     C     MHMVSN        READE     HSP075                                 91
     C     *IN91         IFEQ      *OFF                                         IF TRAN DTL
     C                   move      yes           procfl
      *
      *
      * Check 'group type' if the user has limited the report to a single type.
      *
     C                   if        ldgtcd <> *blank                             If 1 type
     C                   z-add     mdorsn        wkhgsn
     C                   exsr      $gtcd
     C                   endif                                                  If 1 type
      *
      *
      * PROCESS THE ORIGIN OR SHIPPED OUT SIDE OF THE TRANSFER.
      * THE ORIGIN SITE HAD HOGS ARRIVING DEAD AT THE DESTINATION.
      *
     C                   if        procfl = yes                                 If process
     C     KEY01         CHAIN     HSP349                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       MDDOHD        WFDOHD
     C                   ADD       MDDOLB        WFDOLB
     C                   UPDATE    WFREC
     C                   ELSE
     C                   MOVEL     MHORPT        WFPTCD
     C                   MOVEL     MHORPP        WFPPCD
     C                   Z-ADD     MHORFS        WFFSCD
     C                   Z-ADD     MDDOHD        WFDOHD
     C                   Z-ADD     MDDOLB        WFDOLB
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
     C                   endif                                                  If process
      *
     C                   ENDIF                                                  IF TRAN DTL
     C                   ENDDO                                                  DO TRAN DTL
      *
     C                   ENDIF                                                  IF TRAN DOA
     C                   ENDDO                                                  DO TRAN DOA
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------
      * Retrieve a group's group type and compare to the user's selection
      *-------------------------------------------------------------------
      *
     C     $gtcd         begsr
      *
     C     wkhgsn        chain     hsl034d                            92
     C                   if        *in92 = *on or hggtcd <> ldgtcd
     C                   move      no            procfl
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------
      * Process Sales Movements
      *-------------------------------------------------------------------
      *
      * READ ALL SALES MOVEMENTS SELECTED BY THE OPEN QUERY. FOR
      * EACH MOVEMENT, RETRIEVE/PROCESS ALL ASSOCIATED DETAIL RECORDS.
      *
      * AN OPEN QUERY OVER THE DETAIL RECORDS WILL HAVE LIMITED THE
      * DATA TO THE USER-SELECTED HOG GROUP. BUT, FURTHER GROUP COMPARISONS
      * WITH USER'S SELECTIONS ARE REQUIRED.
      *
     C     $sales        begsr
      *
     C     *LOVAL        SETLL     HSP084
      *
     C     *IN90         DOUEQ     *ON                                          DO SALE DOA
     C                   READ      HSP084                                 90
     C     *IN90         IFEQ      *OFF                                         IF SALE HDR
      *
      * PROCESS THE ORIGIN OR SHIPPED OUT SIDE OF THE SALE.
      * THE ORIGIN SITE HAD HOGS ARRIVING DEAD AT THE CUSTOMER SITE.
      *
     C     SHMVSN        SETLL     HSP085
     C     *IN91         DOUEQ     *ON                                          DO SALE DTL
     C     SHMVSN        READE     HSP085                                 91
     C     *IN91         IFEQ      *OFF                                         IF SALE DTL
      *
      * Retrieve group data and compare to user selections.
      *
     C                   z-add     sghgsn        wkhgsn
     C                   exsr      $hgcd
      *
      * Process this group
      *
     C                   if        procfl = yes                                 If process
     C     KEY02         CHAIN     HSP349                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       SGDOHD        WFDOHD
     C                   ADD       SGDOLB        WFDOLB
     C                   UPDATE    WFREC
     C                   ELSE
     C                   MOVEL     HGPTCD        WFPTCD
     C                   MOVEL     HGPPCD        WFPPCD
     C                   Z-ADD     SHFSCD        WFFSCD
     C                   Z-ADD     SGDOHD        WFDOHD
     C                   Z-ADD     SGDOLB        WFDOLB
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
     C                   endif                                                  If process
      *
     C                   ENDIF                                                  IF SALE DTL
     C                   ENDDO                                                  DO SALE DTL
      *
     C                   ENDIF                                                  IF SALE HDR
     C                   ENDDO                                                  DO SALE DOA
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Group info
      *---------------------------------------------------------------
      *
     C     $hgcd         begsr
      *
     C     sghgsn        chain     hsl034d                            92
      *
     C                   select
     C                   when      *in92 = *on
     C                   move      no            procfl
      *
     C                   when      (ldgtcd <> *blank and hggtcd <> ldgtcd) or
     C                             (ldptcd <> *blank and hgptcd <> ldptcd) or
     C                             (ldppcd <> *blank and hgppcd <> ldppcd)
     C                   move      no            procfl
     C                   other
      *
     C                   move      yes           procfl
     C                   endsl
      *
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR WORKFILE FOR DOA'S FROM TRANSFERS
      *
     C     KEY01         KLIST
     C                   KFLD                    MHORPT
     C                   KFLD                    MHORPP
     C                   KFLD                    MHORFS
      *
      * KEY FOR WORKFILE FOR DOA'S FROM SALES
      *
     C     KEY02         KLIST
     C                   KFLD                    HGPTCD
     C                   KFLD                    HGPPCD
     C                   KFLD                    SHFSCD
      *
     C                   ENDSR
      /EJECT
