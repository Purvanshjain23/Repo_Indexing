/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4700CL                                      */
/*  PROGRAM NAME:    LISTINGS FROM WORK WITH BUDGET TEMPLATES      */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     02/09/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4700-WORK WITH BUDGET TEMPLATES.            */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(213)

             DCL        VAR(&LDREPORT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDBTCD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&LDBACD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCSCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDBTWK) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDTFCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDSIZE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&LDAIST) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDREPORT) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDBTCD) VALUE(%SST(*LDA 2 6))
             CHGVAR     VAR(&LDBACD) VALUE(%SST(*LDA 8 1))
             CHGVAR     VAR(&LDCSCD) VALUE(%SST(*LDA 9 1))
             CHGVAR     VAR(&LDBTWK) VALUE(%SST(*LDA 10 5))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 15 2))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 17 5))
             CHGVAR     VAR(&LDTFCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDSIZE) VALUE(%SST(*LDA 27 6))
             CHGVAR     VAR(&LDAIST) VALUE(%SST(*LDA 33 1))


/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                     */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* BUDGET TEMPLATE LISTING                                          */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'T') THEN(DO)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6700)
             ENDDO

/*------------------------------------------------------------------*/
/* TEMPLATE/FARM BUDGET VARIANCE LISTING                            */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'F') THEN(DO)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(165) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6702)

             ENDDO

/*------------------------------------------------------------------*/
/* LISTING OF UNLINKED FARMS                                        */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'U') THEN(DO)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80)  LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6704)

             ENDDO

/*------------------------------------------------------------------*/
/* LISTING OF TEMPLATES  (WITH USER SELECTIONS)                     */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'L') THEN(DO)

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('THBTDS *NE +
                          " "')

/*  IF A SINGLE TEMPLATE                                         */

             IF         COND(&LDBTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND THBTCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 33 6)) VALUE(&LDBTCD)
             CHGVAR     VAR(%SST(&QDATA 39 1)) VALUE('"')
             ENDDO

/*  IF BUDGET AVAILABITY CODE                                    */

             IF         COND(&LDBACD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 41 17)) VALUE('*AND THBACD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 58 1)) VALUE(&LDBACD)
             CHGVAR     VAR(%SST(&QDATA 59 1)) VALUE('"')
             ENDDO

/*  IF CREATION SCHEDULE CODE                                    */

             IF         COND(&LDCSCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 61 17)) VALUE('*AND THCSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 78 1)) VALUE(&LDCSCD)
             CHGVAR     VAR(%SST(&QDATA 79 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                          */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 81 17)) VALUE('*AND THPPCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 98 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 103 1)) VALUE('"')
             ENDDO

/*  IF TYPE OF FARM                                              */

             IF         COND(&LDTFCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 105 17)) VALUE('*AND THTFCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 122 5)) VALUE(&LDTFCD)
             CHGVAR     VAR(%SST(&QDATA 127 1)) VALUE('"')
             ENDDO

/*  IF SIZE                                                      */

             IF         COND(&LDSIZE *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 129 17)) VALUE('*AND THSIZE *EQ "')
             CHGVAR     VAR(%SST(&QDATA 146 6)) VALUE(&LDSIZE)
             CHGVAR     VAR(%SST(&QDATA 152 1)) VALUE('"')
             ENDDO

/*  IF STATUS                                                    */

             IF         COND(&LDAIST *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 154 17)) VALUE('*AND THAIST *EQ "')
             CHGVAR     VAR(%SST(&QDATA 171 1)) VALUE(&LDAIST)
             CHGVAR     VAR(%SST(&QDATA 172 1)) VALUE('"')
             ENDDO

/*  IF WEEKS                                                     */
             IF         COND(&LDBTWK *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 174 15)) VALUE('*AND THBTWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 190 5)) VALUE(&LDBTWK)
             ENDDO

/*  IF CELL                                                      */
             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 196 15)) VALUE('*AND THCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 212 2)) VALUE(&LDCELL)
             ENDDO

             OVRDBF     FILE(HSP185) SHARE(*YES)
             OPNQRYF    FILE((HSP185)) QRYSLT(&QDATA) KEYFLD(*FILE)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6701)

             CLOF       OPNID(HSP185)
             ENDDO


             DLTOVR     FILE(*ALL)

             ENDPGM

