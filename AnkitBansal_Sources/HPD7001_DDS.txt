     A*%%TS  SD  20010306  132625  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7001
     A*  TITLE:         VIEW BGF FARROWING AND PREG CHECKED POSITIVE DATA
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          08/05/00
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7001S1                  SFL
     A*%%TS  SD  20010306  132625  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1EVYR    R        O  9  2REFFLD(HSRREF/@@EVYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1EVWK    R        O  9 11REFFLD(HSRREF/@@EVWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1EVFL    R        O  9 23REFFLD(HSRREF/@@EVFL *LIBL/HSPREF)
     A            D1GLEVHD  R        O  9 35REFFLD(HSRREF/@@GLEVHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1EVHD    R        O  9 44REFFLD(HSRREF/@@EVHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1FSCD    R        O  9 53REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            D1CELL    R        O  9 62REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1SVYR    R        O  9 68REFFLD(HSRREF/@@SVYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1SVWK    R        O  9 75REFFLD(HSRREF/@@SVWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7001C1                  SFLCTL(HP7001S1)
     A*%%TS  SD  20010306  132625  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDWEDT    R        H      REFFLD(BPWEDT)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 14'VIEW BGF FARROWING AND PREG CHECKE-
     A                                      D POSITIVE DATA'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ..'
     A N99                              4 21'?'
     A            C1FSBO    R        B  4 23REFFLD(FSFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N99                               DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A                                  6  2'--- Event ---'
     A                                      DSPATR(HI)
     A                                  6 20'Farrowed/'
     A                                      DSPATR(HI)
     A                                  6 37'Gilt'
     A                                      DSPATR(HI)
     A                                  6 45'Group'
     A                                      DSPATR(HI)
     A N99                              6 55'?'
     A                                      DSPATR(HI)
     A                                  6 68'-Serviced-'
     A                                      DSPATR(HI)
     A                                  7  2'Year'
     A                                      DSPATR(HI)
     A                                  7 10'Week'
     A                                      DSPATR(HI)
     A                                  7 18'Preg Check Pos'
     A                                      DSPATR(HI)
     A                                  7 37'Head'
     A                                      DSPATR(HI)
     A                                  7 46'Head'
     A                                      DSPATR(HI)
     A                                  7 54'Farm'
     A                                      DSPATR(HI)
     A                                  7 61'Cell'
     A                                      DSPATR(HI)
     A                                  7 68'Year'
     A                                      DSPATR(HI)
     A                                  7 74'Week'
     A                                      DSPATR(HI)
     A N99        C1EVYR    R        B  8  2REFFLD(HSRREF/@@EVYR *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N99        C1EVWK    R        B  8 11REFFLD(HSRREF/@@EVWK *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1EVFL    R        B  8 23REFFLD(HSRREF/@@EVFL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A N99        C1FSCD    R        B  8 53REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A N99        C1CELL    R        B  8 62REFFLD(FSCELL)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A N99        C1SVYR    R        B  8 68REFFLD(HSRREF/@@SVYR *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N99        C1SVWK    R        B  8 75REFFLD(HSRREF/@@SVWK *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7001K1
     A*%%TS  SD  20000905  091220  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A N99                             23 22'F5=Refresh'
     A N99                             23 34'F7=Listing'
     A N99                             23 46'F17=Print Options'
     A N99                             23 65'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7001ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7001EC                  SFLCTL(HP7001ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
