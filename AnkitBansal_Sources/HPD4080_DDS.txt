     A*%%TS  SD  20121015  102400  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4080
     A*  TITLE:         SPECIFY OPTIONS FOR ACTIVITY VARIANCE REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/02/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4080R1
     A*%%TS  SD  20121015  102400  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 17'SPECIFY OPTIONS FOR ACTIVITY VARIA-
     A                                      NCE REPORT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Saturday date .....'
     A            D1WEMDY   R        B  4 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  5  2'Shipped/reported ..'
     A N99                              5 22'?'
     A N99        D1SRFL         1A  B  5 24
     A N41                                  DSPATR(HI)
     A  49
     AO 41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A            D1SRDS         8A  O  5 35
     A                                  6  2'Report/Download ...'
     A N99                              6 22'?'
     A N99        D1RPFL    R        B  6 24REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N30                                  DSPATR(HI)
     A  50
     AO 30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A            D1RPDS        45A  O  6 35
     A                                  7  2'Email address .....'
     A            D1EMAIL   R        B  7 24REFFLD(TFREF/@@EMAIL *LIBL/TFPREF)
     A                                      CHGINPDFT(UL LC FE)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A                                  8  2'Business office ...'
     A N99                              8 22'?'
     A N99        D1FSBO    R        B  8 24REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  51
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1BODS    R        O  8 35REFFLD(BOBODS)
     A                                  8 67'(Blank = All)'
     A                                  9  2'Production type ...'
     A N99                              9 22'?'
     A N99        D1PTCD    R        B  9 24REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A N32                                  DSPATR(HI)
     A  52
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1PTDS    R        O  9 35REFFLD(PTREC/PTPTDS *LIBL/HSP010)
     A                                  9 67'(Blank = All)'
     A                                 10  2'Production phase ..'
     A N99                             10 22'?'
     A N99        D1PPCD    R        B 10 24REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A N33                                  DSPATR(HI)
     A  53
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1PPDS    R        O 10 35REFFLD(PPREC/PPPPDS *LIBL/HSP011)
     A                                 10 67'(Blank = All)'
     A                                 11  2'Group type ........'
     A N99                             11 22'?'
     A N99        D1GTCD    R        B 11 24REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A N34                                  DSPATR(HI)
     A  54
     AO 34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1GTDS    R        O 11 35REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                 11 67'(Blank = All)'
     A                                 12  2'Type of farm ......'
     A N99                             12 22'?'
     A N99        D1TFCD    R        B 12 24REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A N39                                  DSPATR(HI)
     A  59
     AO 39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A            D1TFDS    R        O 12 35REFFLD(HSRREF/@@TFDS *LIBL/HSPREF)
     A                                 12 67'(Blank = All)'
     A                                 13  2'Cell ..............'
     A N99        D1CELL    R        B 13 24REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                 13 67'(Blank = All)'
     A                                 14  2'Farm site .........'
     A N99                             14 22'?'
     A N99        D1FSCD    R        B 14 24REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            D1FSNM    R        O 14 35REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                 14 67'(Blank = All)'
     A                                 15  2'Hog group .........'
     A N99                             15 22'?'
     A N99        D1HGCD    R        B 15 24REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A N36                                  DSPATR(HI)
     A  56
     AO 36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                 15 67'(Blank = All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4080K1
     A*%%TS  SD  20021002  110842  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit Report'
     A N99                             23 40'F17=Print Options'
     A N99                             23 59'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4080ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4080EC                  SFLCTL(HP4080ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
