      *
      * SYSTEM:      ACCOUNT RECEIVABLE SYSTEM (PORK)
      * PROGRAM:     AR601
      * TITLE:       DAILY AGING REPORT BY SALES ROUTE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     01/13/97
      *
      *  FUNCTION:   THIS PROGRAM PRINTS THE DAILY AGING REPORT
      *              BY SALES ROUTE.
      *
      *              IT RUNS OVER WORKFILES CREATED IN RPG PROGRAM
      *              AR300 WHICH IS SUBMITTED THROUGH CLP PROGRAM
      *              AR600CLP.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/16/98  LEANNE FEDOR
      *           REMVOED CUSTOMER TYPE/DESCRIPTION FROM ENTRY PARMS.
      *
      * 11/26/01  BARB GUTIERREZ
      *           moveD THE PRINT OF THE INVOICE DETAIL OVER TO THE
      *           LEFT TO ACCOMODATE FOR LARGER P.O. NUMBERS.
      *
      * 02/20/02  LEANNE FEDOR
      *           IF CUSTOMER NOTES EXIST IN FILE POAVCPP PRINT AN '*'
      *           AFTER THE CUSTOMER NUMBER TO SHOW NOTES EXIST.
      *           IF INVOICE NOTES EXIST IN FILE POBUREP PRINT AN '*'
      *           AFTER THE INVOICE NUMBER TO SHOW NOTES EXIST.
      *
      * 02/27/02  LeAnne Fedor
      *           There are 2 new report selections:
      *            1) detail or summary
      *            2) include chargebacks
      *
      * 11/04/02  LeAnne Fedor
      *           Added new field 'statement frequency' to Customer Heading line
      *           Added new report selector: Parent customer to the Report Headi
      *
      * 01/15/04  LeAnne Fedor
      *           The user can now limit the report to up to 5 different Invoice
      *           added these new selectors to the heading.
      *
      * 08/23/05  Sharon Zurn
      *           -Print "All" in the heading if all company is blank.
      *           -Print company number on the invoice dtl line.
      *           -Reduce the chargeback comments to 25 to allow for the
      *            company number.
      *           -Create print field for the chargeback comments now that
      *            it is smaller.
      *            sz050823
      *
      * 12/14/05  Barb Gutierrez
      *           -Fixed problems with the logic for printing of all companies o
      *            selected company.
      *
      * 03/06/05  LeAnne Fedor
      *           Replaced "QPRINT" with "PRINT198".
      *           Changed "heading" logic to print Company/Cut-off Date on each
      *           page of the report. Prior to this the user selections printed
      *           on the first page only.
      *
      * 11/29/06  LeAnne Ramsey
      *           Added additional parm of "age code".
      *
      * 07/24/07  LeAnne Ramsey
      *           We increased the From/To Days fields in workfile ARP303 from 2
      *           3,0 because we were truncating "120" days to "20" days in the
      *           headings. So, in this program, tweaked the positions in the co
      *           headings to handle the 3,0 fields.
      * 05/16/08  Barb Gutierrez
      *           Recompile only due to new HPB environment.
      *
      * 09/17/08  LeAnne Ramsey
      *           Replaced file: POBUREL4-AR Header Notes
      *               with file: PMEDCPL1-AR Header Note Header
      *
      * 06/10/16  Brad Baden
      *           Added the Include Rebill flag to the parameter list.  Also inc
      *           rebill processing to the end for each group processed.
      *
      * 09/11/20  Brad Baden
      *           Increase Customer PO# size from 13 to 15
      *
      * 06/29/23  Eric L        (3000027)
      *           RECOMPILE ONLY - ARP300 DB CHANGES
      * 10/06/23  Eric L        (3000252)
      *           RECOMPILE ONLY - ARP301 DB CHANGES
      *
      * 07/15/24  Eric L        (3015255)
      *           Add actual shipped date ($ISHDT)to the report
      *
      /EJECT
      *******************************************************************
      * FILE SPECIFICATIONS
      *******************************************************************
      *
     Farl300b   if   e           k disk
      *  Workfile: Customer headings
      *
      *
     Farp301    if   e           k disk
      *  Workfile: Invoice detail
      *
      *
     Farl301a   if   e           k disk    rename($irec:$ireca)
      *  Workfile: Invoice detail
      *
      *
     Farp302    if   e           k disk
      *  Workfile: Customer totals
      *
      *
     Farp303    if   e           k disk
      *  Workfile: Report totals
      *
      *
     Farp304    if   e           k disk
      *  Workfile: Sales route totals
      *
      *
     Fpoavcpl1  if   e           k disk
      *  Customer comments
      *
      *
     Fpmedcpl1  if   e           k disk
      *  A/R header note header
      *
      *
     Fprint198  o    f  188        printer oflind(*inof)
      *
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D only            c                   'O'
     D CBandRB         c                   'Chargebacks and Rebills Only'
     D CBonly          c                   '      Chargebacks Only      '
     D RBonly          c                   '        Rebills Only        '
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D wkcmty          s              2    inz('AR')
      *
      *
      * Save fields
      *
     d svr4cd          s                   like($cr4cd)
     d svmzna          s                   like($cmzna)
      *
      *
      * Workfields
      *
     D wkcast          s              1
     D wkiast          s              1
     D wkngna          s             25
     d wkline          s                   like($tline)
      *
      *
      * Parms
      *
     d xxdt            s              7  0
     d xxanc7          s              7  0
     d xxparentno      s              7  0
     d xx0bal          s              1
     d xxdtlsum        s              1
     d xxinclcb        s              1
     d xxinclrb        s              1
     d xxsuffix1       s              2
     d xxsuffix2       s              2
     d xxsuffix3       s              2
     d xxsuffix4       s              2
     d xxsuffix5       s              2
     d xxagecode       s              6
      *
     d xxaytx          s                   like($caytx)
     d xxparentnm      s                   like($caytx)
     d xxaic3          s                   like($caic3)
     d xxr4cd          s                   like($cr4cd)
     d xxmzna          s                   like($cmzna)
      *
      *
      * Print fields
      *
     d dash            s            179    inz(*all'-')
     d dash2           s            100    inz(*all'-')
     d rtime           s              6  0
     d $h0bal          s              3
     d $hdtlsum        s              7
     d $hinclcb        s              4
     d $hinclrb        s              4
      *
      *---------------------------------------------------------------
      *  DATA STRUCTURES
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      *
      /EJECT
      *******************************************************************
      * Mainline
      *******************************************************************
      *
      * Process all Customer records in the Customer Headings workfile.
      *
     C     *loval        setll     arl300b
      *
     C                   dou       *in82 = *on
     C                   read      arl300b                                82
     C                   if        *in82 = *off
      *
     C                   if        first = yes
     C                   movel     $cr4cd        svr4cd
     C                   movel     $cmzna        svmzna
     C                   except    hdrrpt
     C                   movel     no            first
     C                   setoff                                       78
     C                   seton                                        87
     C                   endif
      *
      * Break logic for Sales Route.
      *   1) Print totals for saved sales route
      *   2) Page break and print headings for next sales route
      *
     C                   if        $cr4cd <> svr4cd
     C                   exsr      $tots
     C                   except    hdrrpt
     C                   endif
      *
      * Print headings for Customer
      *
     C                   exsr      $cust
      *
      * If the user selected the 'Detail' Version of the report, then process/pr
      * all invoices for the Customer.
      *
      * If user selected to run ageing for all companies, process report by cust
      * Otherwise, process report by selected company
      *
     C                   if        xxdtlsum = 'D'
      *
     C                   if        xxaic3 = 0
     C                   exsr      $bycust
     C                   else
     C                   exsr      $byco
     C                   endif
      *
     C                   endif
      *
      * Print total lines for the Customer
      *
     C                   exsr      $totc
      *
     C                   endif
     C                   enddo
      *
      *-----------------
      * EOF PROCESSING
      *-----------------
      *
      * If no detail matched the selections, print the 'End-of-Report'
      * message.
     C                   if        first = yes
     C                   except    hdrrpt
     C                   except    endrpt
     C                   else
      *
      * Print Sales Route total
      *
     C                   if        *inof = *on
     C                   except    hdrrpt
     C                   endif
     C                   exsr      $tots
      *
      * Page break and print report total
      * (Note: Set off the indicator that prints the Sales Route Code/Descriptio
      *  at the top of the page.)
      *
     C                   setoff                                       75
     C                   except    hdrrpt
     C                   except    totrpt
      *
      * End-of-Report message
      *
     C                   if        *inof = *on
     C                   except    hdrrpt
     C                   endif
     C                   except    endrpt
     C                   endif
      *
      * Exit program
     C                   seton                                        lr
      /EJECT
      *-----------------------------------------------------------------
      * Customer Headings
      *-----------------------------------------------------------------
      *
     C     $cust         begsr
      *
      * Set the indicator that will print "HOLD" in the Credit Memo column if
      * the "order Entry HOld Status' is Yes.
      *
     C                   if        $cqnst = yes
     C                   seton                                        79
     C                   else
     C                   setoff                                       79
     C                   endif
      *
      * Chain out to the Customer Comments file. If notes exist, print an
      * '*' after the Customer Number.
      *
     C     key4          chain     poavcpl1                           92
     C                   if        *in92 = *off
     C                   move      '*'           wkcast
     C                   else
     C                   move      ' '           wkcast
     C                   endif
      *
      * Write the Customer Headings
      *
     C                   if        *inof = *on
     C                   except    hdrrpt
     C                   endif
     C                   except    hdrcus
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------
      * Process invoices by Company/Customer
      *-----------------------------------------------------------------
      *
     C     $byco         begsr
      *
     C     key1          setll     arp301
      *
     C                   dou       *in83 = *on
     C     key1          reade     arp301                                 83
     C                   if        *in83 = *off
     C                   exsr      $invdtl
     C                   endif
     C                   enddo
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------
      * Process invoices by Customer
      *-------------------------------------------------------------------------
      *
     C     $bycust       begsr
      *
     C     $canc7        setll     arl301a
      *
     C                   dou       *in84 = *on
     C     $canc7        reade     arl301a                                84
     C                   if        *in84 = *off
     C                   exsr      $invdtl
     C                   endif
     C                   enddo
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------
      * Process Invoice Detail
      *-----------------------------------------------------------------
      *
     C     $invdtl       begsr
      *
      * Chain to the AR Header Note Header file. If notes exist, print an "*"
      * after the Invoice Number
      *
     C     key5          chain     pmedcpl1                           92
     C                   if        *in92 = *off
     C                   move      '*'           wkiast
     C                   else
     C                   move      ' '           wkiast
     C                   endif
      *
      * Move the Chargeback comments to a smaller field to print
      *
     C                   if        $icest = 'CB'
     C                   clear                   wkngna
     C                   movel     $ngna         wkngna
     C                   seton                                        73
     C                   else
     C                   setoff                                       73
     C                   endif
      *
      *
     C                   if        *inof = *on
     C                   except    hdrrpt
     C                   endif
     C                   except    dtlinv
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Print Customer Total Lines
      *----------------------------------------------------------------
      *
     C     $totc         begsr
      *
      * Write the first customer total line
      *
     C                   z-add     1             wkline
     C     key2          chain     arp302                             92
     C                   if        *in92 = *off
     C                   if        *inof = *on
     C                   except    hdrrpt
     C                   endif
     C                   except    totc1
     C                   endif
      *
      *
      * Print 'Applied Credit' line for Customer
      *
     C                   z-add     2             wkline
     C     key2          chain     ARP302                             92
     C                   if        *in92 = *off
     C                   if        *inof = *on
     C                   except    hdrrpt
     C                   endif
     C                   except    totc2
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Print Sales Route total lines
      *----------------------------------------------------------------
      *
     C     $tots         begsr
      *
     C     key3          chain     arp304                             92
     C                   if        *in92 = *off
      *
     C                   if        *inof = *on
     C                   except    hdrrpt
     C                   endif
     C                   except    totsls
     C                   endif
      *
     C                   movel     $cr4cd        svr4cd
     C                   movel     $cmzna        svmzna
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parms
     C     *entry        plist
     C                   parm                    xxaic3
     C                   parm                    xxdt
     C                   parm                    xxanc7
     C                   parm                    xxaytx
     C                   parm                    xxparentno
     C                   parm                    xxparentnm
     C                   parm                    xxr4cd
     C                   parm                    xxmzna
     C                   parm                    xx0bal
     C                   parm                    xxdtlsum
     C                   parm                    xxinclcb
     C                   parm                    xxinclrb
     C                   parm                    xxsuffix1
     C                   parm                    xxsuffix2
     C                   parm                    xxsuffix3
     C                   parm                    xxsuffix4
     C                   parm                    xxsuffix5
     C                   parm                    xxagecode
      *
      * Key lists
      *
     C     key1          klist
     C                   kfld                    xxaic3
     C                   kfld                    $canc7
      *
     C     key2          klist
     C                   kfld                    $caic3
     C                   kfld                    $canc7
     C                   kfld                    wkline
      *
     C     key3          klist
     C                   kfld                    $caic3
     C                   kfld                    svr4cd
      *
     C     key4          klist
     C                   kfld                    $canc7
     C                   kfld                    wkcmty
      *
     C     key5          klist
     C                   kfld                    $caic3
     C                   kfld                    $iconb
     C                   kfld                    $icest
      *
      * Retrieve Time for heading
      *
     C                   time                    rtime
      *
      * Set the indicator that limits the printing of the report selections to
      * the first page of the report.
      *
     C                   seton                                        78
      *
      * If the user has not limited the report to a single customer, set the
      * indicator to print "all" in the Report Heading.
      *
     C                   if        xxanc7 = 0
     C                   seton                                        77
     C                   endif
      *
      * If the user has not limited the report to a single Parent Customer,
      * set the indicator to print "all" in the Report Heading.
      *
     C                   if        xxparentno = 0
     C                   seton                                        71
     C                   endif
      *
      * If the user has selected "all" sales routes,
      * set the indicator to print "all" in the Report Heading.
      *
     C                   if        xxr4cd = *blank
     C                   seton                                        75
     C                   endif
      *
      * If the user has not limited the report to a single Company,
      * set the indicator to print "all" in the Report Heading.
      *
     C                   if        xxaic3 = 0
     C                   seton                                        80
     C                   endif
      *
      * Set up the Heading field for the 'Print Zero Balances'
      *
     C                   if        xx0bal = yes
     C                   movel     'YES'         $h0bal
     C                   else
     C                   movel     'NO'          $h0bal
     C                   endif
      *
      * Set up the Heading field for the  'Detail or Summary' Version
      *
     C                   if        xxdtlsum = 'D'
     C                   movel     'Detail '     $hdtlsum
     C                   seton                                        76
     C                   else
     C                   movel     'Summary'     $hdtlsum
     C                   endif
      *
      * Set up control indicator for the Invoice Suffix selections
      *
     C                   if        xxsuffix1 = *blank and
     C                             xxsuffix2 = *blank and
     C                             xxsuffix3 = *blank and
     C                             xxsuffix4 = *blank and
     C                             xxsuffix5 = *blank
     C                   seton                                        70
     C                   endif
      *
      * Set up Heading field for the 'Include Chargebacks'
      *
     C                   select
     C                   when      xxinclcb = yes
     C                   movel     'YES'         $hinclcb
      *
     C                   when      xxinclcb = no
     C                   movel     'NO'          $hinclcb
      *
     C                   when      xxinclcb = only
     C                   movel     'ONLY'        $hinclcb
     C                   seton                                        72
     C                   if        xxdtlsum = 'D'
     C                   seton                                        74
     C                   endif
     C                   endsl
      *
      * Set up Heading field for the 'Include Rebills'   E5262  Brad Baden
      *
     C                   select
     C                   when      xxinclrb = yes
     C                   movel     'YES'         $hinclrb
      *
     C                   when      xxinclrb = no
     C                   movel     'NO'          $hinclrb
      *
     C                   when      xxinclrb = only
     C                   movel     'ONLY'        $hinclrb
     C                   seton                                        69
     C                   if        xxdtlsum = 'D'
     C                   seton                                        74
     C                   endif
     C                   endsl
      *
      *
      * Retrieve the Company record from the Report Workfile.
      *
     C                   if        xxaic3 = 0
     C                   read      arp303                                 92
     C                   else
     C     xxaic3        chain     arp303                             92
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADINGS
      *-------------------------------------------------------------
      *
     Oprint198  E            hdrrpt         1 01
     O                       SDPGM               10
      *
     O                                           92 'Accounts Receivable'
     O                                         +  1 'System'
     O                                          169 'Date'
     O                       UDATE         Y    179
      *
      *
     O          E            hdrrpt         1
     O                       SDUSR               10
     O                                           87 'Daily Aging Report'
     O                                         +  1 'By Sales Route'
     O                                          169 'Time'
     O                       RTIME              179 '  :  :  '
      *
      *
     O          E            hdrrpt         1
     O*              72                          94 'Chargebacks Only'
     O              N69 72   CBonly              98
     O               69N72   RBonly              98
     O               69 72   CBandRB             98
     O                                          169 'Page'
     O                       PAGE          Z    179
      *
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'Company:'
     O              N80      xxaic3        Z     24
     O               80                          24 'ALL'
     O              N80      $RADTX              60
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'Cutoff date:'
     O                       $RCMDY              29 '  /  /  '
      *
     O          E    78      HDRRPT      0  1
     O                                           20 'Age code:'
     O                       xxagecode           27
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'Print zero balances:'
     O                       $H0BAL              24
      *
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'A/R customer number:'
     O               77                          24 'ALL'
     O              N77      XXANC7        Z     28
     O              N77      XXAYTX              60
      *
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'A/R parent customer:'
     O               71                          24 'ALL'
     O              N71      XXPARENTNO    Z     28
     O              N71      XXPARENTNM          60
      *
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'Sales route code:'
     O               75                          24 'ALL'
     O              N75      XXR4CD              24
     O              N75      XXMZNA              60
      *
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'Detail or Summary:'
     O                       $HDTLSUM            28
      *
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'Include chargebacks:'
     O                       $HINCLCB            25
      *
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'Include rebills:'
     O                       $HINCLRB            25
      *
      *
     O          E    78      hdrrpt      0  1
     O                                           20 'Invoice suffix:'
     O               70                          24 'ALL'
     O              n70      xxsuffix1           23
     O              n70      xxsuffix2           26
     O              n70      xxsuffix3           29
     O              n70      xxsuffix4           32
     O              n70      xxsuffix5           35
      *
      *
     O          E    75      hdrrpt      0  1
     O                                           81 'Sales route:'
     O                       SVR4CD              85
     O                       SVMZNA             117
      *
      *  The Company/Cutoff Date below are not printed on the "first" page.
      *  They are printed only on pages subsequent to the "first" page.
      *
     O          E    87      HDRRPT      0  0
     O                                           20 'Company:'
     O              N80      XXAIC3        Z     24
     O               80                          24 'ALL'
     O              N80      $RADTX              60
      *
     O                                           74 'Cutoff date:'
     O                       $RCMDY              84 '  /  /  '
      *
      *
      *
      *
      *
     O          E            hdrrpt      1  0
     O                       DASH               187
      *
      *
     O          E            hdrrpt      1  0
     O                                            8 'Invoice'
     O                                           17 'Order'
     O                                           25 'Due'
     O                                           32 'Ship'
     O                                           43 'P.O.'
      *
      * COLUMN 1 IS THE 'CURRENT' COLUMN
     O                                           64 'Current'
      *
      * AGING COLUMN 2
     O                       $R2FR         Z     74
     O                                           76 '-'
     O                       $R2TO         Z     80
      *
      *
      * AGING COLUMN 3
     O                       $R3FR         Z     90
     O                                           92 '-'
     O                       $R3TO         Z     96
      *
      *
      * AGING COLUMN 4
     O                       $R4FR         Z    107
     O                                          109 '-'
     O                       $R4TO         Z    113
      *
      *
      * AGING COLUMN 5
     O                       $R5FR         Z    124
     O                                          126 '-'
     O                       $R5TO         Z    130
      *
      *
      * AGING COLUMN 6
     O                                          144 'Over -'
     O                       $R5TO         Z    147
      *
     O                                          166 'Credit Memos/'
     O                                          186 'Sales'
      *
      *
     O          E            hdrrpt      1  0
     O                                            7 'Number'
     O                                           18 'Number'
     O                                           26 'Date'
     O                                           33 'Date'
     O                                           45 'Number'
     O                                           78 'Days'
     O                                           94 'Days'
     O                                          111 'Days'
     O                                          128 'Days'
     O                                          144 'Days'
     O                                          163 'On Account'
     O                                          181 'Co.'
     O                                          186 'Prsn'
      *
      *
     O          E            hdrrpt      1  2
     O                       DASH               187
      *
      *-------------------------------------------------------------------------
      * CUSTOMER HEADINGS
      *-------------------------------------------------------------------------
     O          E            HDRCUS      0  0
      *
     O                       $CANC7        Z      7
     O                       WKCAST               9
     O                       $CAYTX              40
     O                                           49 'Contact:'
     O                       $CNJNA              80
     O                                           91 'Telephone:'
     O                       $CI0TX             117
     O                                          122 'Fax:'
     O                       $CI1TX             148
     O               79                         156 '*HOLD*'
      *
      *
     O          E   N74      HDRCUS      1  0
     O                       $CaddR              33
     O               76                          48 'Last Pymt Date:'
     O               76      $CLPDT              57 '  /  /  '
     O               76                          77 'High A/R Bal/Date:'
     O               76      $CALVA        J     93
     O               76      $CHBDT             102 '  /  /  '
     O               76                         128 'YTD Sales Invoiced:'
     O               76      $CVPVA        J    144
     O               76                         152 'Stmt:'
     O               76      $BUNCS             154
      *
      *
     O          E    76N74   HDRCUS      1  1
     O                                           16 'Parent:'
     O                       $CBTC7        Z     24
     O                       $CPNAM              55
     O                                           77 'Parent Credit Limit:'
     O                       $CS2NB        J     93
     O                                          128 'MTD/YTD Avg Days to Pay:'
     O                       $CMADP        J    136
     O                       $CYADP        J    144
      *
      *-------------------------------------------------------------------------
      * DETAIL LINE FOR INVOICE
      *-------------------------------------------------------------------------
      *
     O          E            DTLINV      1
      *
     O                       $ICONB        Z      7
     O                       $ICEST              10
     O                       WKIAST              11
     O                       $ICAC7        Z     18
     O                       $IDUDT              27 '  /  /  '
     O                       $ISHDT         B    36 '  /  /  '
     O                       $IBCNA              52
      *
     O                       $IC1AM        KB    69
     O                       $IC2AM        KB    84
     O                       $IC3AM        KB   101
     O                       $IC4AM        KB   118
     O                       $IC5AM        KB   135
     O                       $IC6AM        KB   152
     O              n73      $IOAAM        KB   169
     O               73      wkngna         B   178
     O                       $iaic3         B   183
     O                       $IBCCD         B   188
      *
      *-------------------------------------------------------------------------
      * CUSTOMER TOTAL LINES
      *-------------------------------------------------------------------------
      *
      * CUSTOMER TOTAL LINE 1
      *
     O          E    76      TOTC1       1
     O                       DASH2              143
      *
     O*         E   N72      TOTC1       1
     O          E   N72N69   TOTC1       1
     O                                           16 'Customer:'
     O                       $TANC7        Z     24
     O                       $TR4CD              30
     O                       $TC1AM        K     69
     O                       $TC2AM        K     84
     O                       $TC3AM        K    101
     O                       $TC4AM        K    118
     O                       $TC5AM        K    135
     O                       $TC6AM        K    152
     O                       $TOAAM        K    169
      *
      *
      * CUSTOMER TOTAL LINE 2
      *
     O          E            TOTC2       1
     O              N72                          16 'Credit Applied:'
     O               72                          16 'Customer:'
     O               69                          16 'Customer:'
     O               72      $TANC7        Z     24
     O               69      $TANC7        Z     24
     O               72      $TR4CD              30
     O               69      $TR4CD              30
     O                       $TC1AM        K     69
     O                       $TC2AM        K     84
     O                       $TC3AM        K    101
     O                       $TC4AM        K    118
     O                       $TC5AM        K    135
     O                       $TC6AM        K    152
     O                                          169 'Tot. Due:'
     O                       $TDAM         K    185
     O                                          188 '***'
      *
     O          E            TOTC2       1  1
      *
     O                       DASH               188
      *
      *-------------------------------------------------------------------------
      * SALES ROUTE TOTAL
      *-------------------------------------------------------------------------
      *
     O          E            TOTSLS      1
     O                                           16 'Sales Route:'
     O                       $SR4CD              21
      *
     O                       $SC1AM        K     69
     O                       $SC2AM        K     84
     O                       $SC3AM        K    101
     O                       $SC4AM        K    118
     O                       $SC5AM        K    135
     O                       $SC6AM        K    152
      *
     O                                          169 'Tot. Due:'
     O                       $SDAM         K    185
      *
      *
     O          E            TOTSLS      1
     O                                           14 '% of Total:'
     O                       $SC1PC        K     69
     O                       $SC2PC        K     84
     O                       $SC3PC        K    101
     O                       $SC4PC        K    118
     O                       $SC5PC        K    135
     O                       $SC6PC        K    152
      *
      *
      *-------------------------------------------------------------------------
      * REPORT TOTAL
      *-------------------------------------------------------------------------
      *
     O          E            TOTRPT      2
     O                                           14 'Report Totals:'
      *
     O                       $RC1AM        K     69
     O                       $RC2AM        K     84
     O                       $RC3AM        K    101
     O                       $RC4AM        K    118
     O                       $RC5AM        K    135
     O                       $RC6AM        K    152
      *
     O                                          169 'Tot. Due:'
     O                       $RDAM         K    185
      *
      *
     O          E            TOTRPT      1
     O                                           14 '% of Total:'
     O                       $RC1PC        K     69
     O                       $RC2PC        K     84
     O                       $RC3PC        K    101
     O                       $RC4PC        K    118
     O                       $RC5PC        K    135
     O                       $RC6PC        K    152
      *
      *-------------------------------------------------------------------------
      * END OF REPORT
      *-------------------------------------------------------------------------
      *
     O          E            ENDRPT      2
     O                                           20 '** END OF REPORT **'
