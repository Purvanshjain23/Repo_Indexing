      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP653 - Daily Death Report by Cell
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/10/01
      *
      * FUNCTION:    Prints the daily deaths for farms sequenced by cell/farm.
      *              One line is printed per farm showing accumulated values.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/06/01  LeAnne Fedor
      *           'Group type' added as a new report option. Also, new 'square feet'
      *           fields added to Farm Site file requiring a recompile.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsj068f   if   e           k disk
      *    farm site + daily dead + hog groups  (Records selected by open query)
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D lbs             s             11  0 dim(3)
     D hd              s             11  0 dim(3)
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svfscd          s                   like(fsfscd)
     D svfsnm          s                   like(fsfsnm)
     D svcell          s                   like(fscell)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfsbo                 1      5
     D  ldfymd                 6     13  0
     D  ldtymd                14     21  0
     D  ldppcd                22     26
     D  ldfscd                27     31  0
     D  ldsdfl                32     32
     D  ldgtcd                47     47
     D  ldptcd                48     52
     D  ldhgsn                53     59  0
     D  ldfmdy                60     65  0
     D  ldtmdy                66     71  0
      *
     D  ldbods                72    101
     D  ldptds               102    111
     D  ldppds               112    141
     D  ldfsnm               142    166
     D  ldhgcd               167    173
     D  ldrpfl               174    174
     D  ldsdds               175    181
     D  ldgtds               182    196
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read all records selected by open query. Sum records by farm and
      * print one line for each farm.
      *
     C                   exsr      $hrhdr
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsj068f                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        first = yes                                  If first
     C                   z-add     fsfscd        svfscd
     C                   move      fsfsnm        svfsnm
     C                   z-add     fscell        svcell
     C                   move      no            first
     C                   endif                                                  If first
      *
      * Control break logic
      *
     C                   select
     C                   when      fscell <> svcell
     C                   exsr      $tftot
     C                   exsr      $tctot
      *
     C                   when      fsfscd <> svfscd
     C                   exsr      $tftot
     C                   endsl
      *
      * Detail processing.....accumulate farm and cell totals into arrays
      *
     C                   add       kdkdlb        lbs
     C                   add       kdkdhd        hd
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
      * Print last farm line, cell total, report total
      *
     C                   exsr      $tftot
     C                   exsr      $tctot
     C                   exsr      $trtot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print farm total line
      *---------------------------------------------------------------
      *
      * Since all group data for a farm is rolled into a single farm record,
      * the 'farm total' is, in effect, the detail line for this report.
      *
     C     $tftot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $hrhdr
     C                   endif
      *
     C                   except    tftot
      *
     C                   z-add     fsfscd        svfscd
     C                   movel     fsfsnm        svfsnm
      *
      * Set off the indicator that  controls printing of the cell number on
      * the detail line.  We will only print it on the first farm record for
      * each cell.
     C                   setoff                                       87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print cell total line
      *---------------------------------------------------------------
      *
     C     $tctot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $hrhdr
     C                   endif
      *
     C                   except    tctot
      *
     C                   z-add     fscell        svcell
      *
      * Set on the indicator that controls printing of the cell number on
      * the detail line.  We will print the cell number on the first farm
      * record for the cell.
      *
     C                   seton                                        87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report total line
      *---------------------------------------------------------------
      *
     C     $trtot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $hrhdr
     C                   endif
      *
     C                   except    trtot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $hrhdr        begsr
      *
     C                   except    hrhdr
      *
      *
      * Set on the indicator that controls printing of the cell number on
      * the detail line.  We want to print the cell number on the first farm
      * record for the cell and on the first line after a page break.
      *
     C                   seton                                        87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
      * Set up report headings
      *
      * Business office
      *
     C                   if        ldfsbo = *blank
     C                   movel     'ALL'         ldfsbo
     C                   endif
      *
      * Production type
      *
     C                   if        ldptcd = *blank
     C                   movel     'ALL'         ldptcd
     C                   endif
      *
      * Production phases
      *
     C                   if        ldppcd = *blank
     C                   movel     'ALL'         ldppcd
     C                   endif
      *
      * Farm site
      *
     C                   if        ldfscd = 0
     C                   seton                                        81
     C                   endif
      *
      * Hog group
      *
     C                   if        ldhgcd = *blank
     C                   movel     'ALL'         ldhgcd
     C                   endif
      *
      *
      * Group type
      *
     C                   if        ldgtcd = *blank
     C                   seton                                        82
     C                   endif
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            hrhdr          1 01
     O                       sdpgm               10
     O                                           74 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            hrhdr          1
     O                       sdusr               10
     O                                           55 'DAILY'
     O                                           76 'DEATH REPORT BY CELL'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            hrhdr          1
     O                                          122 'PAGE'
     O                       page          Z    132
      *
     O          e            hrhdr       1  1
     O                                           19 'BUSINESS OFFICE ...'
     O                       ldfsbo              26
     O                       ldbods              61
      *
     O          e            hrhdr          1
     O                                           19 'FROM DEATH DATE ...'
     O                       ldfmdy        Y     29
      *
     O          e            hrhdr          1
     O                                           19 'TO DEATH DATE .....'
     O                       ldtmdy        Y     29
      *
     O          e            hrhdr          1
     O                                           19 'PRODUCTION TYPE ...'
     O                       ldptcd              26
     O                       ldptds              41
      *
     O          e            hrhdr          1
     O                                           19 'PRODUCTION PHASE ..'
     O                       ldppcd              26
     O                       ldppds              61
      *
     O          e            hrhdr          1
     O                                           19 'GROUP TYPE ........'
     O              n82      ldgtcd              22
     O               82                          24 'ALL'
     O                       ldgtds              46
      *
     O          e            hrhdr          1
     O                                           19 'FARM SITE .........'
     O              n81      ldfscd        M     27
     O               81                          24 'ALL'
     O                       ldfsnm              56
      *
     O          e            hrhdr          2
     O                                           19 'HOG GROUP .........'
     O                       ldhgcd              28
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            hrhdr          1
     O                                           17 'FARM'
     O                                           69 'DEAD'
     O                                           85 'DEAD'
      *
     O          e            hrhdr          1
     O                                            5 'CELL'
     O                                           17 'SITE'
     O                                           69 'HEAD'
     O                                           85 'POUNDS'
      *
     O          e            hrhdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Farm site line  (prints the total for a farm)
      *-------------------------------------------------------------
      *
     O          e            tftot          1
     O               87      svcell        z      5
     O                       svfscd        z     17
     O                       svfsnm         b    50
     O                       hd (1)        kb    70
     O                       lbs(1)        kb    86
      *
      *
      *-------------------------------------------------------------
      * Cell total
      *-------------------------------------------------------------
      *
     O          e            tctot          1
     O                                           70 '-----------'
     O                                           86 '-----------'
     O          e            tctot          2
     O                       hd (2)        kb    70
     O                       lbs(2)        kb    86
      *
      *-------------------------------------------------------------
      * Report total
      *-------------------------------------------------------------
      *
     O          e            trtot       1  1
     O                                           54 'REPORT TOTAL'
     O                       hd (3)        kb    70
     O                       lbs(3)        kb    86
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
