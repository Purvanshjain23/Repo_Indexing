      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6052
      * TITLE:       Weekly BGF Datamart Production Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/23/01
      *
      * FUNCTION:  Batch program to list the records in the Datamart file.
      *            This listing is submitted from HP4051-Specify Options for
      *            BGF Datamart Edit Listings.
      *
/*    * The users are printing these listings for review prior to publishing the
/* TH * Datamart data to the world!!
      *
/* US * So, in the submission CLP, we replicate the COGNOS file generation so that
/* SO * we will have an exact match for COGNOS.  However, we create
/* WE * the Datamart files in QTEMP and we do not send the files to NIBBLE!
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/06/01  LeAnne Fedor
      *           Recompile only. Sire line added to Datamart: Weekly Production file.
      *
      * 12/06/01  LeAnne Fedor
      *           Recompile only. Added: number of doses, report PCP sire line, Report
      *           farr sire line to file Datmart BGF: Weekly Production.
      *
      * 01/03/02  LeAnne Fedor
      *           Recompile only. Added: Report Farrowed Born Alive to Datamart BGF file:
      *           Weekly Production.
      *
      * 10/14/04  LeAnne Fedor
      *           Added new field: Report Fallout Rate Gilt Group
      *
      * 12/28/04  LeAnne Fedor
      *           Recompile only. New field 'Farm Size' added to Datamart Farm Site
      *           file.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart BGF: Weekly Production file.
      *
      * 02/07/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *                    Added: Transferred In Open Gilts
      *                  Removed: Total Lactation Pounds
      *
      * 07/05/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *           Added 1 new field: Average Daily Beginning Piglet Inventory
      *
      * 08/23/05  LeAnne Fedor
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Man hours
      *
      * 03/23/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *
      * 04/24/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *                 Cognos Report Grouping
      *                 Weaning Cognos Report Grouping
      *                 Report PCP Cognos Report Grouping
      *                 Report Farrowed Cognos Report Grouping
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Total Gilt Weight @ 28 Weeks
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields deleted from Weekly Production file.
      *                           BPPSGLSV - Nbr Gilts Bred w/Passed Date
      *                           BPWSLB   - Weekly Ending Lbs Supplement Used
      *                           BPP2LT   - Total P2 Litters Farrowed
      *                           BPTDGB   - Total Days Gilt Breeding Age
      *                           BPIOGLHD - Transferred in Open Gilts
      *                           BPOOGLHD - Gilts Transferred Out Open
      *                           BP17WNLT - 17+ Day Weaned Sows
      *                           BPTGLLB  - Total Gilt Weight @ 28 Weeks
      *
      * 05/28/09  LeAnne Ramsey
      *           Recompile only:
      *           Added new field "Farrowing Crates" to:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 09/01/09  LeAnne Ramsey
      *           Recompile only. Added new field "Report Total Litters Farrowed" to:
      *                 HPPB092-Datamart BGF: Weekly Production
      *
      * 01/14/10  LeAnne Ramsey
      *           Recompile only. Changed text on BPLVHD and BPLVLB in:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 05/15/12  LeAnne Ramsey  (E2107)
      *           Change the calc of 'Report Post Ultrasound Fallout Rate' (l1rpultra)
      *           per Sami Wilson.
      *
      * 06/04/12  LeAnne Ramsey  (E2107)
      *           Change the calc of 'Report Gilt Group Fallout Rate (l1rpglfall)
      *           per Sami Wilson. She forgot to tell us to change this when we made
      *           the Ultrasound change of 5/15/12. We are only changing the divisor
      *               from BPFPGLHD-Report Farrowed PCP Head Gilt to
      *               to   BPFSGLHD-Report Farrowed Services Gilt
      *
      * 01/21/21  ISE             TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhppb018   if   e           k disk
      *   Datamart BGF: Farm Site
      *   (file keys overridden in submission CL)
      *
      *
     Fhplb092b  if   e           k disk
      *   Datamart BGF: Weekly Production
      *   (records selected by query in submission CL)
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D svcellds        s                   like(fscellds)
      *
      *
      * Work fields
      *
     D wkbornhd        s              5  0
     D wkfld155        s             15  5
     D wkfld150        s             15  0
      *
      *
      * Print fields
      *
     D l1tosv          s              5  0
     D l1ltwnhd        s              5  2
     D l1ltbahd        s              5  2
     D l1ltglbahd      s              5  2
     D l1ltbornhd      s              5  2
     D l1tolspr        s              5  2
     D l1still         s              5  1
     D l1mumm          s              5  1
     D l1rpcon         s              5  2
     D l1rpultra       s              5  2
     D l1rpglfall      s              5  2
     D l1rpfarr        s              5  2
     D l1abort         s              5  2
     D l1mort          s              5  2
     D l1female        s              5  2
     D l1weight        s              5  1
      *
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcdyr                 1      4  0
     D  ldcdwk                 5      6  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
593  C*    *loval        setll     hppb018
593  C     *start        setll     hppb018
     C                   except    h1hdr
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hppb018                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C     fsfscd        chain     hplb092b                           92
     C                   if        *in92 = *off
      *
     C                   if        svcellds <> fscellds
     C                   seton                                        87
     C                   move      fscellds      svcellds
     C                   endif
      *
     C                   exsr      $calc
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   seton                                        87
     C                   endif
      *
     C                   except    l1dtl
     C                   setoff                                       87
     C                   movel     yes           prtfl
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------
      * Calculate values for printing
      *---------------------------------------------------------------------
      *
     C     $calc         begsr
      *
      * Several edits require 'total' born; sum alive, stillborn and mummy head and
      * store in workfield.
      *
     C                   z-add     bpbahd        wkbornhd
     C                   add       bpsbhd        wkbornhd
     C                   add       bpmmhd        wkbornhd
      *
      *
      * Calculate total services
      *
     C                   eval      wkfld150 = bpwnsv + bplwnsv + bprtsv + bpglsv
     C                   z-add     wkfld150      l1tosv
      *
      *
      * Calculate 'report conception rate' as:
      *     report PCP head group / Report PCP services group
      *
     C                   if        bprsgrsv <> 0
     C     bprpgrhd      div       bprsgrsv      wkfld155
     C     wkfld155      mult(h)   100           l1rpcon
     C                   endif
      *
      *
      * Calculate 'report fallout rate post ultrasound' as:
      *    (report farrowing PCP head group - report farrowing head group) /
      *                                 report farrowed services group
      *
     C                   if        bpfsgrhd <> 0
     C                   eval      wkfld155 = (bpfpgrhd - bprfgrhd) / bpfsgrhd
     C     wkfld155      mult(h)   100           l1rpultra
     C                   endif
      *
      *
      * Calculate 'report fallout rate gilt group' as:
      *    (report farrowed PCP head gilt - report farrowed head gilt) /
      *                                 report farrowed services gilt
      *
     C                   if        bpfsglhd <> 0
     C                   eval      wkfld155 = (bpfpglhd - bprfglhd) / bpfsglhd
     C     wkfld155      mult(h)   100           l1rpglfall
     C                   endif
      *
      *
      * Calculate 'report farrowing rate' as:
      *     report farrowing head group / Report farrowing services group
      *
     C                   if        bpfsgrhd <> 0
     C     bprfgrhd      div       bpfsgrhd      wkfld155
     C     wkfld155      mult(h)   100           l1rpfarr
     C                   endif
      *
      *
      * Calculate 'abortion rate' as:
      *     number of abortions / (last 16 weeks services / 16)
      *
     C     bp16sv        div       16            wkfld155
     C                   if        wkfld155 <> 0
     C     bpabhd        div       wkfld155      wkfld155
     C     wkfld155      mult(h)   100           l1abort
     C                   endif
      *
      *
      *
      * If 'total litters farrowed' is not zero, calculate the following:
      *   1) 'Born alive per litter' as:
      *                             total born alive / total litters farrowed
      *   2) 'Total born per litter' as:
      *                             (born alive + stillborn + mummies)
      *                               / total litters farrowed
      *
     C                   if        bptolt <> 0                                  If litters
     C                   eval      wkfld155 = bpbahd / bptolt
     C                   z-add(h)  wkfld155      l1ltbahd
      *
     C                   eval      wkfld155 =  wkbornhd / bptolt
     C                   z-add(h)  wkfld155      l1ltbornhd
     C                   endif                                                  If litters
      *
      *
      * Calculate 'Gilt born alive per litter' as:
      *                             total gilt born alive / gilt litters farrowed
      *
     C                   if        bpgllt <> 0                                  If litters
     C     bpglbahd      div(h)    bpgllt        l1ltglbahd
     C                   endif                                                  If litters
      *
      *
      * Calculate percents:
      *   % stillborn as:  total stillborn /  total born
      *   % mummies as:    total mummies /  total born
      *
     C                   if        wkbornhd <> 0
      *
     C                   eval      wkfld155 = 100 * (bpsbhd / wkbornhd)
     C                   z-add(h)  wkfld155      l1still
      *
     C                   eval      wkfld155 = 100 * (bpmmhd / wkbornhd)
     C                   z-add(h)  wkfld155      l1mumm
     C                   endif
      *
      *
      * Calculate % pre-wean mortality as:
      *   total pre-wean losses / total born alive
      *
     C                   if        bpbahd <> 0
     C                   eval      wkfld155 = 100 * (bptolshd / bpbahd)
     C                   z-add(h)  wkfld155      l1tolspr
     C                   endif
      *
      *
      * Calculate '% total piglet mortalities' as:
      *    (total piglet mortality / total born alive) * 100
      *                where total piglet mortality is:
      *    total prewean losses + rejected head from transfers + quality head from transfers +
      *    live head from sales - total weaned head
      *
     C                   if        bpbahd <> 0
     C                   eval      wkfld155 = ((bptolshd + bpdohd) -
     C                                        (bprjhd + bpqlhd + bplvhd +
     C                                         bpdohd - bptownhd)) / bpbahd
     C     wkfld155      mult(h)   100           l1mort
     C                   endif
      *
      *
      * Calculate 'pigs weaned per sow weaned' as:
      * (rejected head + quality head + live head from sales) / total sows weaned
      *
     C                   if        bptownlt <> 0
     C                   eval      wkfld150 = bprjhd + bpqlhd + bplvhd
     C     wkfld150      div(h)    bptownlt      l1ltwnhd
     C                   endif
      *
      *
      * Calculate 'piglets weaned per female per year':
      * ((rejected head + quality head + live head from sales) * 52)) /
      *          gilt and sow ending inventory
      *
     C                   if        bpengshd <> 0
     C                   eval      wkfld155 = ((bprjhd + bpqlhd + bplvhd) * 52)
     C                                        / bpengshd
     C                   z-add(h)  wkfld155      l1female
     C                   endif
      *
      *
      * Calculate 'average weight piglets weaned':
      *  (received pounds + live pounds) /
      *         (rejected head + quality head + live head from sales)
      *
     C                   eval      wkfld150 = bprjhd + bpqlhd + bplvhd
     C                   if        wkfld150 <> 0
     C                   eval      wkfld155 = (bprclb + bplvlb) / wkfld150
     C                   z-add(h)  wkfld155      l1weight
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Rretrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                          115 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          105 'WEEKLY BGF DATAMART'
     O                                          124 ' PRODUCTION LISTING'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e            h1hdr          1
     O                                           17 'Calendar Year ..:'
     O                       ldcdyr        z     24
      *
      *
     O          e            h1hdr          2
     O                                           17 'Calendar Week ..:'
     O                       ldcdwk        z     24
      *
     O          e            h1hdr          1
     O                                           60 '------Report-----'
     O                                          115 'Gilt'
     O                                          122 '%'
     O                                          153 '%'
     O                                          167 'Pigs'
     O                                          178 'Piglets'
     O                                          188 'Average'
     O                                          198 'Average'
      *
     O          e            h1hdr          1
     O                                           38 'Report'
     O                                           60 '--Fallout Rates--'
     O                                           69 'Report'
     O                                           76 '%'
     O                                           97 'Total'
     O                                          106 'Born'
     O                                          115 'Born'
     O                                          124 'Still'
     O                                          130 '%'
     O                                          140 '%'
     O                                          155 'Total'
     O                                          168 'Weaned/'
     O                                          178 'Weaned/'
     O                                          186 'Age'
     O                                          198 'Weight'
      *
     O          e            h1hdr          1
     O                                           27 'Total'
     O                                           40 'Conception'
     O                                           50 'Post'
     O                                           60 'Gilt'
     O                                           71 'Farrowing'
     O                                           78 'Breed'
     O                                           88 'Abortion'
     O                                           97 'Born'
     O                                          106 'Alive'
     O                                          115 'Alive'
     O                                          124 'Borns'
     O                                          133 'Mummies'
     O                                          144 'Pre-Wean'
     O                                          156 'Piglet'
     O                                          166 'Sow'
     O                                          178 'Female/'
     O                                          188 'Piglets'
     O                                          198 'Piglets'
      *
      *
     O          e            h1hdr          0
     O                                           28 'Services'
     O                                           37 'Rate'
     O                                           53 'Ultrasound'
     O                                           60 'Group'
     O                                           68 'Rate'
     O                                           78 'Back'
     O                                           86 'Rate'
     O                                           97 '/Litter'
     O                                          106 '/Litter'
     O                                          115 '/Litter'
     O                                          124 '/Litter'
     O                                          133 '/Litter'
     O                                          146 'Mortalities'
     O                                          159 'Mortalities'
     O                                          167 'Weaned'
     O                                          176 'Year'
     O                                          188 'Weaned'
     O                                          198 'Weaned'
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e    87      l1dtl       1  1
     O                       dash               198
      *
     O          e            l1dtl       1  1
     O               87      fscellds             7
     O                       fsfsds              19
      *
     O                       l1tosv        kb    28
     O                       l1rpcon       kb    39
     O                       l1rpultra     kb    51
     O                       l1rpglfall    kb    61
     O                       l1rpfarr      kb    70
     O                       bpbredpr      kb    79
     O                       l1abort       kb    87
     O                       l1ltbornhd    kb    98
     O                       l1ltbahd      kb   106
     O                       l1ltglbahd    kb   116
     O                       l1still       kb   124
     O                       l1mumm        kb   133
     O                       l1tolspr      kb   144
     O                       l1mort        kb   157
     O                       l1ltwnhd      kb   168
     O                       l1female      kb   178
     O                       bpadwn        kb   188
     O                       l1weight      kb   198
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
