     A*%%TS  SD  20140313  103746  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        AS/400
     A*  DISPLAY FILE:  MPD452
     A*  TITLE:         SPECIFY LISTING OF INVOICES NOT POSTED TO JDE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/29/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP452R1
     A*%%TS  SD  20140313  103746  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(31)
     A                                      SETOF(34)
     A                                      SETOF(50)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 15'SPECIFY LISTING OF INVOICES NOT PO-
     A                                      STED TO JDE'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  5  2'Accounting company .'
     A                                  5 23'?'
     A            D1ACONO   R        B  5 25REFFLD(@@ACONO)
     A                                      EDTCDE(K)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1ACONM       30A  O  5 33
     A                                  6  2'JDE vendor number ..'
     A            D1JDVNNO  R        B  6 25REFFLD(PHREC/PHJDVNNO *LIBL/MPP102)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A                                  6 65'(Blank for All)'
     A            D1JDVNNM  R        O  7 25REFFLD(PHREC/PHJDVNNM *LIBL/MPP102)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP452K1
     A*%%TS  SD  20140312  134716  ISLRAMS     REL-V6R1M0  5761-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP452ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R MP452EC                   SFLCTL(MP452ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
