/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          DAILYS                                        */
/*  PROGRAM NUMBER:  OM301CL                                       */
/*  PROGRAM NAME:    WORKFILE: BUILD SALES HISTORY FOR DAILYS COST */
/*                             REPORTS                             */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     06/27/07                                      */
/*                                                                 */
/*  FUNCTION: DAILYS WILL NOW BE USING THE SYNON SALES HISTORY FILE*/
/*            BUT, THE "COST" FILES WILL NOT YET BE CONVERTED TO   */
/*                   SYNON FILES.                                  */
/*            SO, WE ARE CREATING WORKFILE LOGIC THAT CAN BE USED  */
/*                BY MULTIPLE, EXISTING DAILYS "COST" REPORTS.     */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&AIC3 &WMQY)

             DCL        VAR(&QDATA)   TYPE(*CHAR) LEN(80)
             DCL        VAR(&AIC3)    TYPE(*DEC)  LEN(3)
             DCL        VAR(&WMQY)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&RETURN)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&FRSYNDT) TYPE(*DEC)  LEN(7)
             DCL        VAR(&TOSYNDT) TYPE(*DEC)  LEN(7)


/* Call a Synon program to get the From/To Dates for data extraction.  */
/* Pass in the Week/Month/Quarter/Year value to tell the Synon program */
/* which 'date range' you need. Also, pass in Company.                 */

             CALL       PGM(PMI2XFR) PARM(&RETURN &AIC3 &WMQY  +
                          &FRSYNDT &TOSYNDT)

/* Using the returned Synon dates, extract Sales History data where:    */
/*     Actual Ship Date is in date range                                */
/*     Weight billed is not zero                                        */
/*     Company is 363 (Missoula) or 364 (Salt Lake)                     */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('F4GNDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 7)) VALUE(&FRSYNDT)
             CHGVAR     VAR(%SST(&QDATA 20 15)) VALUE('*AND F4GNDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 36 7)) VALUE(&TOSYNDT)

             CHGVAR     VAR(%SST(&QDATA 44 17)) VALUE('*AND F4BYWG +
                          *NE 0')

             CHGVAR     VAR(%SST(&QDATA 62 15)) VALUE('*AND F4AIC3 +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 78 3)) VALUE(&AIC3)

             OVRDBF     FILE(OMHSTPM9) SHARE(*YES)
             OPNQRYF    FILE((OMHSTPM9)) QRYSLT(&QDATA) KEYFLD(*FILE)  +
                          SEQONLY(*YES)

/* Create the workfile for extracted Sales History data. Then,         */
/* call the RPGLE program to write Sales History workfile records      */
/* summarized to the 'Item' level.                                     */
             DLTOBJ     OBJ(QTEMP/PMA6CPL1) TYPE(*FILE)
             MONMSG     MSGID(CPF0000)

             DLTOBJ     OBJ(QTEMP/PMA6CPP) TYPE(*FILE)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(PMA6CPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJ  OBJ(PMA6CPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(OM301)

             CLOF       OPNID(OMHSTPM9)
             DLTOVR     FILE(*ALL)

             ENDPGM

