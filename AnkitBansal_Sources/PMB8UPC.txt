/*********************************************************************/
/* PROGRAM    : PMB8UPC                                              */
/* TITLE      : EMAIL WITH ATTACHMENT TO RECIPIENT                   */
/* DATE       : 11/10/2006                                           */
/* Description: EXECUTE THE ESNDMAIL COMMAND TO SEND AN ATTACHED     */
/*                REPORT TO A RECIPIENT                              */
/*                REPORT CAN BE IN ONE OF FOUR FORMATS:              */
/*                   *PDF, *TEXT, *RTF, *HTML                        */
/*                ESNDMAIL DOES NOT SEND *XLS REPORTS                */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    I     &PRTF       PRINTER FILE BEING EMAILED                   */
/*    I     &DESC       TEXT FOR MESSAGE DESCRIPTION                 */
/*    I     &EMAILFMT   EMAIL FORMAT                                 */
/*                        FORMAT CAN BE *PDF, *TEXT, *RTF, *HTML     */
/*    I     &EMAIL      EMAIL ADDRESS BEING EMAILED TO               */
/*********************************************************************/
/* Modifications:                                                    */
/*-------------------------------------------------------------------*/
/* 01/27/2021 DN  S17042 - Scan & replace '$' with a 'blank' in      */
/*    variable &PRTF & use it as the attached filename. Append '.pdf'*/
/*    to the attached filename.                                      */
/*    Calls PUSQXFR.                                                 */
/* 08/31/2010 LJB E000915  CHANGE THE SUBJECT AND EMAIL MESSAGE      */
/*      TO INCLUDE A "DO NOT REPLY" MESSAGE. EMAIL SENDER HAS        */
/*      BEEN CHANGED TO CREDIT_DEPT@SEABOARDFOODS.COM FOR OMS.       */
/*********************************************************************/
 PMB8UPC:    PGM        PARM(&PRTF &DESC &EMAIL &EMAILFMT)

/*-------------------------------------------------------------------*/
/* Declare Variables                                                 */
/*-------------------------------------------------------------------*/
             DCL        VAR(&PRTF)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&DESC)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&EMAIL)    TYPE(*CHAR) LEN(50)
             DCL        VAR(&EMAILFMT) TYPE(*CHAR) LEN(5)
             DCL        VAR(&EMAILMSG) TYPE(*CHAR) LEN(90)
             DCL        VAR(&INPSTRNG) TYPE(*CHAR) LEN(80)
             DCL        VAR(&OUTSTRNG) TYPE(*CHAR) LEN(80)
             DCL        VAR(&ATCHFNME) TYPE(*CHAR) LEN(14)

/* ERROR MESSAGE FIELDS                                                       */
             DCL        VAR(&MSG)   TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL)  TYPE(*DEC) LEN(5 0)
             DCL        VAR(&MSG2)  TYPE(*CHAR) LEN(512)

 E000915:    CHGVAR     VAR(&EMAILMSG) VALUE(&DESC *TCAT '-DO NOT +
                          REPLY-Unmonitored email box')

/* S17042 - Scan & Replace '$' with a 'Blank' in &PRTF Variable. */
             IF         COND(&PRTF *NE *BLANK) THEN(DO)
             CHGVAR     VAR(&INPSTRNG) VALUE(&PRTF)
             CALL       PGM(PUSQXFR) PARM('' &INPSTRNG &OUTSTRNG)
             CHGVAR     VAR(&ATCHFNME) VALUE(&OUTSTRNG)
             CHGVAR     VAR(&ATCHFNME) VALUE(&ATCHFNME *TCAT '.pdf')
             ENDDO

/*-------------------------------------------------------------------*/
/* SEND A COPY OF THE SPOOLED FILE AS AN ATTACHMENT TO THE EMAIL     */
/* ADDRESS THAT IS PASSED IN                                         */
/*-------------------------------------------------------------------*/

 PDF_FORMAT: IF         COND(&EMAILFMT *EQ '*PDF') THEN(DO)
    /* S17042 - USE NEW FILENAME WITHOUT '$' AS ATTACHMENT. */
         /*  ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&EMAILMSG) +
                          MSG(&EMAILMSG) MSGTYPE(*TEXT) ATTLIST((* +
                          *PDF *N &PRTF))                            */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&EMAILMSG) +
                          MSG(&EMAILMSG) MSGTYPE(*TEXT) +
                          ATTLIST((&ATCHFNME *PDF *N &PRTF))

             MONMSG     MSGID(CPF9999 EML9999) EXEC(GOTO +
                          CMDLBL(MSGTRAP))
             ENDDO

 TEXT_FMT:   IF         COND(&EMAILFMT *EQ '*TEXT') THEN(DO)
    /* S17042 - USE NEW FILENAME WITHOUT '$' AS ATTACHMENT. */
         /*  ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&EMAILMSG) +
                          MSG(&EMAILMSG) MSGTYPE(*TEXT) ATTLIST((* +
                          *TEXT *N &PRTF))                           */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&EMAILMSG) +
                          MSG(&EMAILMSG) MSGTYPE(*TEXT) +
                          ATTLIST((&ATCHFNME *TEXT *N &PRTF))
             MONMSG     MSGID(CPF9999 EML9999) EXEC(GOTO +
                          CMDLBL(MSGTRAP))
             ENDDO

 RTF_FORMAT: IF         COND(&EMAILFMT *EQ '*RTF') THEN(DO)
    /* S17042 - USE NEW FILENAME WITHOUT '$' AS ATTACHMENT. */
         /*  ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&EMAILMSG) +
                          MSG(&EMAILMSG) MSGTYPE(*TEXT) ATTLIST((* +
                          *RTF *N &PRTF))                            */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&EMAILMSG) +
                          MSG(&EMAILMSG) MSGTYPE(*TEXT) +
                          ATTLIST((&ATCHFNME *RTF *N &PRTF))
             MONMSG     MSGID(CPF9999 EML9999) EXEC(GOTO +
                          CMDLBL(MSGTRAP))
             ENDDO

 HTML_FMT:   IF         COND(&EMAILFMT *EQ '*HTML') THEN(DO)
    /* S17042 - USE NEW FILENAME WITHOUT '$' AS ATTACHMENT. */
         /*  ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&EMAILMSG) +
                          MSG(&EMAILMSG) MSGTYPE(*TEXT) ATTLIST((* +
                          *HTML *N &PRTF))                           */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&EMAILMSG) +
                          MSG(&EMAILMSG) MSGTYPE(*TEXT) +
                          ATTLIST((&ATCHFNME *HTML *N &PRTF))
             MONMSG     MSGID(CPF9999 EML9999) EXEC(GOTO +
                          CMDLBL(MSGTRAP))
             ENDDO

             GOTO       CMDLBL(ENDPGM)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDPGM))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

 ENDPGM:     ENDPGM
