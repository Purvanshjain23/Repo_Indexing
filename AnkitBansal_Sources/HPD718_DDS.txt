     A*%%TS  SD  20010718  112557  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD718
     A*  TITLE:         VIEW EXPENSE TICKET
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/10/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP718S1                   SFL
     A*%%TS  SD  20010718  112557  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFALBO    R        O 13  2REFFLD(XTALBO)
     A            DFFSCD    R        O 13 13REFFLD(XTFSCD)
     A                                      EDTCDE(M)
     A            DFHGCD    R        O 13 21REFFLD(HGHGCD)
     A            DFXTAM    R        O 13 30REFFLD(XTXTAM)
     A                                      EDTCDE(K)
     A            DFXTQT    R        O 13 45REFFLD(XTXTQT)
     A                                      EDTCDE(K)
     A            DFXTLB    R        O 13 58REFFLD(XTXTLB)
     A                                      EDTCDE(K)
     A            DFXTHD    R        O 13 73REFFLD(XTXTHD)
     A                                      EDTCDE(K)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP718C1                   SFLCTL(HP718S1)
     A*%%TS  SD  20010718  112557  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 29'VIEW EXPENSE TICKET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Ticket number .....:'
     A            C1XTSN    R        O  4 24REFFLD(XTXTSN)
     A                                      EDTCDE(M)
     A                                  5  2'Begin date ........:'
     A            C1BMDY    R        O  5 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  6  2'End date ..........:'
     A            C1EMDY    R        O  6 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  7  2'Expense code ......:'
     A            C1EXCD    R        O  7 24REFFLD(XTEXCD)
     A                                  8  2'Allocation method .:'
     A            C1AMCD    R        O  8 24REFFLD(XTAMCD)
     A                                  9  2'Status ............:'
     A            C1XSCD    R        O  9 24REFFLD(XTXSCD)
     A                                  9 30'--------------------- TOTALS ------
     A                                      ---------------'
     A                                      DSPATR(HI)
     A            C1XTAM    R        O 10 30REFFLD(XTXTAM)
     A                                      EDTCDE(K)
     A            C1XTQT    R        O 10 45REFFLD(XTXTQT)
     A                                      EDTCDE(K)
     A            C1XTLB    R        O 10 58REFFLD(XTXTLB)
     A                                      EDTCDE(K)
     A            C1XTHD    R        O 10 73REFFLD(XTXTHD)
     A                                      EDTCDE(K)
     A                                 11  2'Allocated'
     A                                      DSPATR(HI)
     A                                 11 14'Farm'
     A                                      DSPATR(HI)
     A                                 11 22'Hog'
     A                                      DSPATR(HI)
     A                                 12  2'Office'
     A                                      DSPATR(HI)
     A                                 12 14'Site'
     A                                      DSPATR(HI)
     A                                 12 21'Group'
     A                                      DSPATR(HI)
     A                                 12 36'Amount'
     A                                      DSPATR(HI)
     A                                 12 46'Quantity'
     A                                      DSPATR(HI)
     A                                 12 64'Pounds'
     A                                      DSPATR(HI)
     A                                 12 75'Head'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP718K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP718ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP718EC                   SFLCTL(HP718ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
