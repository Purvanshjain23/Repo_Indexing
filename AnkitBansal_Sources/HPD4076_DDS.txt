     A*%%TS  SD  20030109  162312  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4076
     A*  TITLE:         SPECIFY OPTIONS FOR FARMS/BINS LISTING
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/06/00
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4076R1
     A*%%TS  SD  20020128  094628  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 17'SPECIFY OPTIONS FOR FARMS/BINS LIS-
     A                                      TING'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ...'
     A N99                              4 22'?'
     A N99        D1FSBO    R        B  4 24REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A N33                                  DSPATR(HI)
     A  53
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1BODS    R        O  4 31REFFLD(BOBODS)
     A                                  4 63'(Blank for All)'
     A                                  5  2'Production type ...'
     A N99                              5 22'?'
     A N99        D1PTCD    R        B  5 24REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A N30                                  DSPATR(HI)
     A  50
     AO 30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A            D1PTDS    R        O  5 31REFFLD(PTREC/PTPTDS *LIBL/HSP010)
     A                                  5 63'(Blank for All)'
     A                                  6  2'Production phase ..'
     A N99                              6 22'?'
     A N99        D1PPCD    R        B  6 24REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  51
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1PPDS    R        O  6 31REFFLD(PPREC/PPPPDS *LIBL/HSP011)
     A                                  6 63'(Blank for All)'
     A                                  7  2'Cell ..............'
     A N99        D1CELL    R        B  7 24REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                  7 63'(Blank for All)'
     A                                  8  2'Farm status .......'
     A N99                              8 22'?'
     A            D1FSST    R        B  8 24REFFLD(HSRREF/FSAIST *LIBL/HSPREF)
     A N37                                  DSPATR(HI)
     A  57
     AO 37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                  8 63'(Blank for All)'
     A                                  9  2'Farm site .........'
     A N99                              9 22'?'
     A N99        D1FSCD    R        B  9 24REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  52
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1FSNM    R        O  9 31REFFLD(FSFSNM)
     A                                  9 63'(Blank for All)'
     A                                 10  2'Room status .......'
     A N99                             10 22'?'
     A            D1RMST    R        B 10 24REFFLD(HSRREF/BRRMST *LIBL/HSPREF)
     A N36                                  DSPATR(HI)
     A  56
     AO 36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                 10 63'(Blank for All)'
     A                                 11  2'Shared bins only ..'
     A N99                             11 22'?'
     A            D1YNFL    R        B 11 24REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N35                                  DSPATR(HI)
     A  55
     AO 35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4076K1
     A*%%TS  SD  19980601  163600  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit Listing'
     A N99                             23 41'F17=Print Options'
     A N99                             23 60'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4076ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4076EC                  SFLCTL(HP4076ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
