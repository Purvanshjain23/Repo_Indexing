     A*%%TS  SD  20030109  141907  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD150  REVISE FARM SITE
     A*  RPG PROGRAM:   HP150
     A*  CREATED BY:    ROSE CENTONZE
     A*  DATE:          03/09/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 63 IS ON IF HOG GROUPS HAVE ALREADY BEEN ASSIGNED TO
     A*               THE FARM
     A*  INDICATOR 64 IS ON WHEN THE VERSION IS 'PRODUCTION' AND WE ARE
     A*               PROTECTING ALMOST ALL FIELDS
     A*
     A*  MODIFICATIONS:
     A*  07/14/20 DN  P408   - ADDED PREMISE ID.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF04(04 'prompt')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62N64                            CF11(11 'DELETE')
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R HP150R1
     A*%%TS  SD  20030109  141907  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
P408 A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(70)
     A                                      SETOF(79)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFVERSION R        O  2  2REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                  2 29'REVISE FARM SITE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  3  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ......'
     A            DFFSCD    R        B  4 20REFFLD(HSRREF/FSFSCD *LIBL/HSPREF)
     A N31N60N62
     A N64                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  60
     A  62
     A  64                                  DSPATR(PR)
     A N60N62N64                            DSPATR(UL)
     A                                      EDTCDE(M)
     A                                  4 33'Farm reference.'
     A            DFMTEC    R        B  4 50REFFLD(HSRREF/@@MTECHREF *LIBL/HSPR-
     A                                      EF)
     A                                      CHECK(LC)
     A    N62N64                            DSPATR(HI)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A                                  5  2'Farm site description ......'
     A            DFFSNM    R        B  5 33REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A N32N62N64                            DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A                                  6  2'County .....................'
     A            DFCNTY    R        B  6 33REFFLD(HSRREF/FSCNTY *LIBL/HSPREF)
     A                                      CHECK(LC)
     A N48N62N64                            DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A                                  7  2'State ......................'
     A            DFSACD    R        B  7 33REFFLD(@@SACD)
     A N39N62N64                            DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A            DFSANM    R        O  7 47REFFLD(HSRREF/@@NM30 *LIBL/HSPREF)
     A                                  8  2'Transport office (default)..'
     A N62                              8 31'?'
     A            DFTOCD    R        B  8 33REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A N49N62                               DSPATR(HI)
     A  79
     AO 49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            DFTONM    R        O  8 47REFFLD(HSRREF/@@TONM *LIBL/HSPREF)
     A                                  9  2'Production type  (default)..'
     A N62N64                           9 31'?'
     A            DFPTCD    R        B  9 33REFFLD(HSRREF/FSPTCD *LIBL/HSPREF)
     A N33N62N64                            DSPATR(HI)
     A  53
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A            DFPTDS    R        O  9 47REFFLD(HSRREF/PTPTDS *LIBL/HSPREF)
     A                                 10  2'Production phase (default)..'
     A N62N63N64                       10 31'?'
     A            DFPPCD    R        B 10 33REFFLD(HSRREF/FSPPCD *LIBL/HSPREF)
     A N34N62N63
     A N64                                  DSPATR(HI)
     A  54
     AO 34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  62 63
     AO 64                                  DSPATR(PR)
     A N62N63N64                            DSPATR(UL)
     A            DFPPDS    R        O 10 47REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A                                 11  2'Group type ......(default)..'
     A N62                             11 31'?'
     A            DFGTCD    R        B 11 33REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A N47N62                               DSPATR(HI)
     A  57
     AO 47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            DFGTDS    R        O 11 47REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                 12  2'Type of farm ...............'
     A N62                             12 31'?'
     A            DFTFCD    R        B 12 33REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A N30N62                               DSPATR(HI)
     A  70
     AO 30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            DFTFDS    R        O 12 47REFFLD(HSRREF/@@TFDS *LIBL/HSPREF)
     A                                 13  2'Cell .......................'
     A            DFCELL    R        B 13 33REFFLD(FSCELL)
     A N37N62                               DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                 14  2'Pod ........................'
     A            DFPOD     R        B 14 33REFFLD(HSRREF/@@POD *LIBL/HSPREF)
     A N38N62                               DSPATR(HI)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
P408 A                                 14 47'Premise ID'
  |  A            DFPRM     R        B 14 58REFFLD(HSRREF/@@PRCD *LIBL/HSPREF)
  |  A N41N62                               DSPATR(HI)
  |  A  41                                  DSPATR(PC)
  |  A  41                                  DSPATR(RI)
  |  A  62                                  DSPATR(PR)
P408 A N62                                  DSPATR(UL)
     A                                 15  2'Consolidated farm ..........'
     A N62                             15 31'?'
     A            DFCNFSCD  R        B 15 33REFFLD(HSRREF/@@CNFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N36N62                               DSPATR(HI)
     A  56
     AO 36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            DFCNFSNM  R        O 15 47REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                 16  2'G/L cost center ............'
     A N62N64                          16 31'?'
     A            DFCJD     R        B 16 33REFFLD(HSRREF/FSCJD *LIBL/HSPREF)
     A N44N62N64                            DSPATR(HI)
     A  52
     AO 44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A                                      CHECK(RB)
     A            DFCCDS        30A  O 16 47TEXT('JDE cost center description')
     A                                 17  2'Company relationship........'
     A N62N64                          17 31'?'
     A            DFCRCD    R        B 17 33REFFLD(HSRREF/FSCRCD *LIBL/HSPREF)
     A N35N62N64                            DSPATR(HI)
     A  55
     AO 35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A            DFCRDS    R        O 17 47REFFLD(HSRREF/CRCRDS *LIBL/HSPREF)
     A                                 18  2'Feed mill business office ..'
     A N62N64                          18 31'?'
     A            DFFMBO    R        B 18 33REFFLD(HSRREF/FSFMBO *LIBL/HSPREF)
     A N40N62N64                            DSPATR(HI)
     A  58
     AO 40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A            DFFMDS    R        O 18 47REFFLD(HSRREF/BOBODS *LIBL/HSPREF)
     A                                      TEXT('feed mill BUSINESS OFFICE DES-
     A                                      CRIPTION')
     A                                 19  2'Feed delivery zone .........'
     A N62N64                          19 31'?'
     A            DFDZCD    R        B 19 33REFFLD(HSRREF/FSDZCD *LIBL/HSPREF)
     A N42N62N64                            DSPATR(HI)
     A  59
     AO 42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A            DFDZDS    R        O 19 47REFFLD(HSRREF/DZDZDS *LIBL/HSPREF)
     A                                 20  2'Farm site JDE number .......'
     A N62N63N64                       20 31'?'
     A            DFFAJD    R     Y  B 20 33REFFLD(HSRREF/FSFAJD *LIBL/HSPREF)
     A N45N62N63
     A N64                                  DSPATR(HI)
     A  50
     AO 45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A  63
     AO 62
     AO 64                                  DSPATR(PR)
     A N62N63N64                            DSPATR(UL)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A            DFFJNM        30A  O 20 47TEXT('farm site vendor name from JD-
     A                                      E')
     A                                 21  2'Producer JDE number ........'
     A N62N64                          21 31'?'
     A            DFPAJD    R     Y  B 21 33REFFLD(HSRREF/FSPAJD *LIBL/HSPREF)
     A N46N62N64                            DSPATR(HI)
     A  51
     AO 46                                  DSPATR(PC)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A                                      EDTCDE(Z)
     A  46                                  DSPATR(RI)
     A            DFPJNM        30A  O 21 47TEXT('producer name from JDE')
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP150ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP150EC                   SFLCTL(HP150ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP150K1
     A*%%TS  SD  19940318  115721  ISRCENT     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61N64                       23 34'F11=Delete'
