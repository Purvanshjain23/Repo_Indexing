/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PDXCUPC                                       */
/*  PROGRAM NAME:    Print List With Company                       */
/*  PROGRAMMER:      Purva Patel                                   */
/*  CREATE DATE:     02/11/96                                      */
/*                                                                 */
/*  FUNCTION:        This CLP is submitted with an Execute Message */
/*                   from THE ITEM AVERAGE PRODUCTION PRINT PROMPT */
/*                                                                 */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  R CENTONZE   04/24/97  ADDED PARM TO PRINT REPORT ONLY OR UPDATE */
/*                         UNIT WEIGHT                             */
/*  Santosh      11/13/24  Task: SR3013276 CM: S313276             */
/*                         Logic is added to email the report to a */
/*                         distribution list                       */

/*                                                                 */
/*******************************************************************/
             PGM        PARM(&RTN &PDEV &HOLD &SAVE &XCOPIES &PGM +
                          &PGMPRTF &APP &RPTTYP &XCOMP &CWTSTS  +
                          &XCLASS &XGROUP &XTYPE &UPD)

/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* Print Options                                                   */
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDEV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PGMPRTF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&APP) TYPE(*CHAR) LEN(6)

/* Function Specific                                               */
             DCL        VAR(&RPTTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&XCOMP) TYPE(*CHAR) LEN(15)
             DCL        VAR(&CWTSTS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&XTYPE) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XGROUP) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XCLASS) TYPE(*CHAR) LEN(15)
             DCL        VAR(&UPD) TYPE(*CHAR) LEN(1)

/*-----------------------------------------------------------------*/
/*                    PROGRAM VARIABLES                            */
/*-----------------------------------------------------------------*/
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)

             DCL        VAR(&COMMAND) TYPE(*CHAR) LEN(512)

             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&TYPE) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&GROUP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&CLASS) TYPE(*DEC) LEN(3 0)

/* S313276 Changes Begin */
             DCL        VAR(&DISTSCH) TYPE(*CHAR) LEN(30) VALUE(' ')
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&EMLMSG) TYPE(*CHAR) LEN(50) VALUE(' ')
/* S313276 Changes End */

/*-----------------------------------------------------------------*/
/*                     INITIALIZATION                              */
/*-----------------------------------------------------------------*/
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)
             CHGVAR     VAR(&TYPE) VALUE(&XTYPE)
             CHGVAR     VAR(&GROUP) VALUE(&XGROUP)
             CHGVAR     VAR(&CLASS) VALUE(&XCLASS)

/* CONVERT COMPANY */
             IF COND(&XCOMP *EQ ' ') +
              THEN(DO)
                 CHGVAR     VAR(&COMP) VALUE('0')
              ENDDO
             ELSE +
              CMD(DO)
                 CHGVAR     VAR(&COMP) VALUE(&XCOMP)
              ENDDO

/* CONVERT TYPE    */
             IF COND(&XTYPE *EQ ' ') +
              THEN(DO)
                 CHGVAR     VAR(&TYPE) VALUE('0')
              ENDDO
             ELSE +
              CMD(DO)
                 CHGVAR     VAR(&TYPE) VALUE(&XTYPE)
              ENDDO

/* CONVERT GROUP   */
             IF COND(&XGROUP *EQ ' ') +
              THEN(DO)
                 CHGVAR     VAR(&GROUP) VALUE('0')
              ENDDO
             ELSE +
              CMD(DO)
                 CHGVAR     VAR(&GROUP) VALUE(&XGROUP)
              ENDDO

/* CONVERT CLASS   */
             IF COND(&XCLASS *EQ ' ') +
              THEN(DO)
                 CHGVAR     VAR(&CLASS) VALUE('0')
              ENDDO
             ELSE +
              CMD(DO)
                 CHGVAR     VAR(&CLASS) VALUE(&XCLASS)
              ENDDO

/*-----------------------------------------------------------------*/
/*                           MAIN                                  */
/*-----------------------------------------------------------------*/
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMPRTF) OUTQ(&PDEV) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

             CALL       PGM(&PGM) PARM(' ' &COMP &CWTSTS +
                       &CLASS &GROUP &TYPE &RPTTYP &APP &UPD)

/* S313276 Changes Begin */
             RTVJOBA    JOB(&JOB)
             IF         COND(&JOB = 'UPDUNTWGT') THEN(DO)
/* Get Email Distribution List */
             CALL       PGM(POMTXFR) PARM((' ') (&COMP) +
                          ('ITMAVGPRD') (&DISTSCH))

             IF         COND(&DISTSCH *NE ' ') THEN(DO)
             CHGVAR     VAR(&COMP) VALUE(&XCOMP)
             CHGVAR     VAR(&EMLMSG) VALUE('Unit Weight Variance +
                          Report for ' *BCAT &XCOMP *CAT '.')

/* Email the Report */
             ESNDMAIL   RECIPIENT(&DISTSCH) SUBJECT('Unit Weight +
                          Report') MSG(&EMLMSG) ATTLIST((* *PDF *N +
                          PDXEPFR$))
             ENDDO
             ENDDO
/* S313276 Changes End */

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:
             DLTOVR     FILE(*ALL)

             ENDPGM
