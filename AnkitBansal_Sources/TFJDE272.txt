      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Triumph Foods / JD Edwards
      * PROGRAM:     TFJDE272
      * TITLE:       Upload Claim Adjustment to JDE
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/07/06
      *
      *  FUNCTION:   This program uploads Claim Adjustment data to JDE.
      *
      *              The program is called from the Weekly Revenue Close function when
      *              it is a "Final" run.
      *
      *              Seaboard is Company 360
      *              Triumph  is Company 960
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/30/06  LeAnne Ramsey
      *           Added a "week" parm on the call to the generic "calendar" program.
      *
      * 11/13/07  LeAnne Ramsey
      *           Logic was blanking out the Week-ending Date in the header after
      *           printing. SO, it was only printing on the first page. Removed
      *           the "blank-after" edit from the output line.
      *
      * 01/21/08  LeAnne Ramsey
      *           Added a parm of A=Accounting on the call to generic program TF815
      *           so that we will get back the Accounting Year/Period instead of the
      *           Production (HPS) Year/Period. (The program will return zero for the
      *           Accounting week---which we don't use.)
      *
      * 05/11/09  LeAnne Ramsey
      *           Changed the LDA positions for Printing (hold/save/outq/copies).
      *
      * 05/28/09  LeAnne Ramsey
      *           Added Company and Report totals for 3 columns:
      *             1) Total Gross Claim Amount
      *             2) Seaboard Net Claim Amount
      *             5) Triumph Net Claim Amount
      *
      * 01/23/17  Brad Baden    E8287
      *           Added parameters for companies 360, 320, and 960.
      *           Added processing for those companies.
      *
      *  RMC E8287  3/23/17 Fix M3 parallel not working for JDE write to f0911
      *  RMC E8287  5/03/17 Dont create batch header for 360/960 if they are live with M3
      *  RMC S11799 10/03/17 XXCO gets blanked out if the JDE entry isnt found, so move
      *                      xxnumco into glco if that happens so it will be there for the
      *                      M3GL checks       Scan: S11799
      *  RMC S14761 4/01/19 on the 1910 account, init the glsub. it was using what was last
      *                     in it causing it to post incorrect to m3 since the beginning --
      *                     fields m3gbsub and m3gbsbl
     H*  4/22/2021  Rose Centonze               Scan:  SDN440
     H*                   Modify to use the new E1 tables:
     H*                   F0011  -->  E10011l1  when write here, write to e1b9cpp
     H*                   F0901  -->  E10901
     H*                   F0911  -->  E10911  --> new fields
     H*   in tfjde801, tfjde800 -->   X0010  -->  GET0010  - new logic  -goes to BSSV
      *         1 set edi trans no to gldoc
      *         2 GLKCO - document company - leave blank
      *         3 GLPKCO  GLOKCO  GLPSFX  - leave blank , not 000
      *         4 glrcnd - reconciled-leave Blank
      *         5 glag - should have the amount, not GLAA
      *         6 icndo, icodcn- increment when get document
      *         7 GL ENTRIES WILL HAVE EDI USER‚360- OMSJDESF or 960- TFGLPOST > xxedus  ***
      *               Per company value
      *
      *  7/28/21          CHANGE all refs of company 362 to 320
      *
      /EJECT
      ****************************************************************
      * File specifications
      ****************************************************************
      *
     F*f0011     uf a e           k disk
     Fe10011l1  uf a e           k disk                                          sdn440
      *   JDE Batch control record
      *
      *
     F*f0901     if   e             disk
     Fe10901    if   e             disk                                          sdn440
      *   JDE Account master
      *
      *
     F*f0911     uf a e             disk
     Fe10911    uf a e             disk                                          sdn440
      *   JDE Account ledger
      *
      *
     Ftfp312    if   e           k disk
      *    Workfile: Claim adjustment Upload to JDE
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d return          s              7
     d first           s              1    inz('Y')
     d svcono          s                   like(w1cono)
     d svco5a          s                   like(w1co5a)
     d svbatnbr        s                   like(w1batnbr)
     d svdocnbr        s                   like(w1docnbr)
     d svsdoc          s                   like(w1docnbr)                       sdn440 SF doc#
     d svtdoc          s                   like(w1docnbr)                       sdn440 TF doc#
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Workfields
      *
     d wkuser          s                   like(gluser)
      *
     d wkbatnbr        s                   like(glicu)
     d wksbatnbr       s                   like(glicu)
     d wktbatnbr       s                   like(glicu)
      *
     d wkbatcnt        s              5  0
     d wksbatcnt       s              5  0                                      sf doc count  sdn440
     d wktbatcnt       s              5  0                                      TF doc count  sdn440
     d wkdoccnt        s              5  0
     d wksdoccnt       s              5  0                                      SF line count sdn440
     d wktdoccnt       s              5  0                                      TF line count SDN440
      *
     d wkaid           s              8
     D wkmcu           s                   like(gmmcu)
     D wksub           s                   like(gmsub)
     D wkobj           s                   like(gmobj)
     d wkani           s             29
      *
     d wkbatval        s             15  0
     d wksoffam        s             15  2
     d wktoffam        s             15  2
     d wkam            s             15  2
      *
     d wktime          s             12  0
     d wkdate          s              6  0
      *
      * M3 Journal Entry Program Parms
     d m3p0rtn         s              7
     d m3aco           s              3  0
     d m3gbmcu         s             12
     d m3gbobj         s              6
     d m3gbsub         s              8
     d m3gbsbl         s              8
     d m3gbamt         s             15  2
     d m3gbqty         s             15  0
     d m3date          s              8  0
     d m3deschdr       s             40
     d m3descdtl       s             40
      *
     d wkcymd          s              8  0
      *
     d wkm3gl          s              1
     d wkjdegl         s              1
      *
      * Parm fields
      *
      * ENTRY
     D xxm3lv360       s              1                                         M3 Live for 360  Y/N
     D xxm3lv320       s              1                                         M3 Live for 320  Y/N
     D xxm3lv960       s              1                                         M3 Live for 960  Y/N
      *
     D xxacctco        s              3  0
     D xxnumco         s              3  0
      *
     D xxedus          s                   like(gledus)                         sdn440
     d xxsubtype       s              1
     d xxbalsheet      s              1
      *
     d xxapfl          s              1
     D xxdt            s              8  0
     D xxyr            s              4  0
     D xxpe            s              2  0
     D xxwk            s              2  0
      *
      *
      * For call to JDE date routine
      *
     d xxsidat         s              6
     d*xxedat          s              8                                          sdn440
     d xxedat          s              6  0                                       sdn440
     d xxffmt          s              7
     d xxtfmt          s              7
     d xxsep           s              7
     d xxertst         s              1
     d xxctry          s              2
     D xxccyymmdd      s              8  0                                      sdn440
     D xxsyssyndt      s              7  0                                      sdn440
      *
      *
      * For call to JDE account routine
      *
     d xxbatnbr        s                   like(glicu)
     d xxsy            s              4
     d xxidx           s              2
     d xxsym           s              1
     d xxomod          s              1
     d xximod          s              1
     d xxerrm          s              4
      *
     d xxglcode        s              8
     d xxani           s                   like(glani)
     d xxicu           s                   like(glicu)
     d xxdoc           s                   like(gldoc)
      *
      *
      * For call to M3 posting program
      *
     d xxp0rtn         s              7
     d xxaco           s              3  0
     d xxgbmcu         s             12
     d xxgbobj         s              6
     d xxgbsub         s              8
     d xxgbsbl         s              8
     d xxgbamt         s             15  2
     d xxgbqty         s             15  0
     d xxdate          s              8  0
     d xxdeschdr       s             40    inz('TFS Claim Adjustment ')
     d xxdescdtl       s             40    inz('TFS Claim Adjustment')
      *
      * Print fields
      *
     d h1mdy           s              6  0
      *
     d h2conm          s              8
     d h2batnbr        s                   like(glicu)
     d h2docnbr        s                   like(gldoc)
      *
     d l1acct          s             28
     d l1desc          s             30
     d l1dbam          s                   like(w1dbam)
     d l1cram          s                   like(w1cram)
      *
     d t1desc          s             14
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * Arrays
      ****************************************************************
      *
     D aragclam        s             11  2 dim(3)
     D arsnclam        s             11  2 dim(3)
     D artnclam        s             11  2 dim(3)
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *
     d i0901ds         ds           500
     d xxco                                like(gmco)
     d xxaid                               like(gmaid)
     d xxmcu                               like(gmmcu)
     d xxobj                               like(gmobj)
     d xxsub                               like(gmsub)
     d xxans                               like(gmans)
     d xxdl01                              like(gmdl01)
     d xxlda                               like(gmlda)
     d xxbpc                               like(gmbpc)
     d xxpec                               like(gmpec)
     d xxbill                              like(gmbill)
     d xxcrcd                              like(gmcrcd)
     d xxum                                like(gmum)
     d xxr001                              like(gmr001)
     d xxr002                              like(gmr002)
     d xxr003                              like(gmr003)
     d xxr004                              like(gmr004)
     d xxr005                              like(gmr005)
     d xxr006                              like(gmr006)
     d xxr007                              like(gmr007)
     d xxr008                              like(gmr008)
     d xxr009                              like(gmr009)
     d xxr010                              like(gmr010)
     d xxr011                              like(gmr011)
     d xxr012                              like(gmr012)
     d xxr013                              like(gmr013)
     d xxr014                              like(gmr014)
     d xxr015                              like(gmr015)
     d xxr016                              like(gmr016)
     d xxr017                              like(gmr017)
     d xxr018                              like(gmr018)
     d xxr019                              like(gmr019)
     d xxr020                              like(gmr020)
     d xxr021                              like(gmr021)
     d xxr022                              like(gmr022)
     d xxr023                              like(gmr023)
     d xxobja                              like(gmobja)
     d xxsuba                              like(gmsuba)
     d xxwcmp                              like(gmwcmp)
     d xxcct                               like(gmcct)
     d xxerc                               like(gmerc)
     d xxhtc                               like(gmhtc)
     d xxqlda                              like(gmqlda)
     d xxccc                               like(gmccc)
     d xxfmod                              like(gmfmod)
     d xxuser                              like(gmuser)
     d xxpid                               like(gmpid)
     d xxjobn                              like(gmjobn)
     d xxupmj                              like(gmupmj)
     d xxupmt                              like(gmupmt)
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Break CCYY into CC and YY fields
      *---------------------------------------------------------------
      *
     D wkyear          ds
     D   wkccyy                       4  0
     D   wkcc                         2  0 overlay(wkccyy:1)
     D   wkyy                         2  0 overlay(wkccyy:3)
      *
      *----------------------------------------------------------------
      * LDA - local data area
      *----------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldcyr                  1      4  0
     D  ldcwk                  5      6  0
     D  ldcbdt                 7     14  0
     D  ldcbsyn               15     21  0
     D  ldcedt                22     29  0
     D  ldcesyn               30     36  0
     D  ldpfcd                37     37
      *
      * Set up date fields that are used in generating reports that are generated
      * on-demand as well as from this close function.
      *
     D  ldfmdy                39     44  0
     D  ldtmdy                45     50  0
     D  ldfcymd               52     59  0
     D  ldtcymd               60     67  0
      *
     D  ldoutq               401    410
     D  ldhold               411    414
     D  ldsave               415    418
     D  ldcopy               419    419  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *-------------------------------------------------------------------------------------
      * Mainline
      *-------------------------------------------------------------------------------------
      *
      * Print report heading
      *
     C                   exsr      $h1hdr
      *
      * Process/print each record in the workfile.
      *
     C     *loval        setll     tfp312
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp312                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     w1cono        svcono
     C                   move      w1co5a        svco5a
     C                   z-add     w1batnbr      svbatnbr
     C                   z-add     w1docnbr      svdocnbr
     C                   exsr      $h2hdr
      *
     C                   when      svcono <> w1cono
     C                   exsr      $offset
     C                   z-add     1             x
     C                   evalr     t1desc = 'Company Total:'
     C                   exsr      $t1tot
     C                   z-add     w1cono        svcono
     C                   move      w1co5a        svco5a
     C                   z-add     w1batnbr      svbatnbr
     C                   z-add     w1docnbr      svdocnbr
     C                   exsr      $h2hdr
     C                   endsl
      *
     C**                 add       1             wkdoccnt                       sdn440
     c                   if        w1cono = 960                                 sdn440 TF line cnt
     C                   add       1             wktdoccnt
     C                   z-add     wktdoccnt     wkdoccnt                       sdn440
     c                   else
     C                   add       1             wksdoccnt                      sdn440 SF line cnt
     C                   z-add     wksdoccnt     wkdoccnt                       sdn440
     c                   endif
      *
      * Write the JDE Account Ledger record
      *
     C                   exsr      $acctreg
      *
      * Set up print fields and print the workfile record
      *
     C                   z-add     w1cram        l1cram
     C                   z-add     w1dbam        l1dbam
      *
     C                   seton                                        87
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------
      * EOF processing
      *----------------
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
     C                   exsr      $offset
     C                   exsr      $batch
      * Company total
     C                   z-add     1             x
     C                   evalr     t1desc = 'Company Total:'
     C                   exsr      $t1tot
      * Report total
     C                   except    blank
     C                   seton                                        88
     C                   z-add     2             x
     C                   evalr     t1desc = 'Report Total:'
     C                   exsr      $t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------
      * Retrieve regular Account info (ie: not "offset" account)
      *----------------------------------------------------------------
      *
     C     $acctreg      begsr
      *
     C                   move      *blank        wkaid
     C                   move      *blank        wkmcu
     C                   move      *blank        wkobj
     C                   move      *blank        wksub
      *
     C                   select
     C                   when      w1cono = 960
     C                   eval      gluser = 'TFGLPOST'
     C                   eval      gltorg = 'TFGLPOST'
     C                   z-add     w1batnbr      wktbatnbr
     C                   add       w1am          wktoffam
     C                   z-add     wktdoccnt     wkdoccnt                          sdn440 TF line #
     c* SDN440 keep doc count per company
     c                   if        svtdoc <> w1docnbr                              sdn440
     c                   add       1             wktbatcnt                         sdn440
     c                   z-add     w1docnbr      svtdoc                            sdn440 SaveTF doc
     C**                 z-add     0             wktdoccnt                         sdn440 TF line #
     c                   end                                                       sdn440
     C                   other
      *
     C**                 eval      gluser = sdusr
     C**                 eval      gltorg = sdusr
     C                   eval      gluser = xxedussf
     C                   eval      gltorg = xxedussf
     C                   z-add     w1batnbr      wksbatnbr
     C                   add       w1am          wksoffam
     C                   z-add     wksdoccnt     wkdoccnt                          sdn440 SF line #
     c* SDN440 keep doc count per company
     c                   if        svsdoc <> w1docnbr                              sdn440
     c                   add       1             wksbatcnt                         sdn440
     c                   z-add     w1docnbr      svsdoc                            sdn440 SaveTF doc
     C**                 z-add     0             wksdoccnt                         sdn440 SF line #
     c                   end                                                       sdn440
     C                   endsl
      *
      *
      * Retrieve Account I.D. associated with Company/"TFAJCL" from the
      * GL Master Codes file.
      *
     C                   call      'PKTIXFR'
     C                   parm                    return
     C                   parm      w1cono        xxnumco
     C                   parm      'TFAJCL  '    xxglcode
     C     wkaid         parm      *blank        xxaid
     C                   parm      *blank        xxsubtype
     C                   parm      *blank        xxbalsheet
     C                   parm      *zero         xxacctco
      *
      * Retrieve cost center/object/sub associated with Account ID just retrieved.
      * (1 = Short Act I.D., 9 = Individual cost center/object/sub.)
      *
     C**                 call      'X0901'                                         sdn440
     C                   call      'E1I0901'
     C                   parm                    xxsym
     C                   parm      '9'           xxomod
     C                   parm      '1'           xximod
     C                   parm      wkaid         xxani
     C     wkmcu         parm      *blank        xxmcu
     C     wkobj         parm      *blank        xxobj
     C     wksub         parm      *blank        xxsub
     C                   parm      *blank        xxerrm
     C                   parm                    i0901ds
      *
      * If retrieval was successful,
      *  1) replace the SUB with the GL Sub Code of the record you want to write
      *  2) go back to JDE with the new combination of Cost Center/Object/Sub
      *     to get the Account ID associated with that combination
      *
      *
     C                   if        xxerrm = *blank                              If successful
     C                   movel     w1glsub       wksub
      *
     C** sdn440          call      'X0901'
     C                   CALL      'E1I0901'
     C                   parm                    xxsym
     C                   parm      '1'           xxomod
     C                   parm      '9'           xximod
     C     wkaid         parm      *blank        xxani
     C                   parm      wkmcu         xxmcu
     C                   parm      wkobj         xxobj
     C                   parm      wksub         xxsub
     C                   parm      *blank        xxerrm
     C                   parm                    i0901ds
     C                   endif                                                  If successful
      *
      * Company number needs to be the company that is associated with this
      * cost center/object/sub code.
      *  S11799 :  add code to ensure company being there.
      *
     C                   move      xxco          glco
     c     glco          ifeq      '     '
     c                   move      xxmcu         wrkcomp           3
     c                   move      '00000'       glco
     c                   move      wrkcomp       glco
     c                   end
      *
      *
      * Hardcode explanation and plop "amount" into a workfield
      *
     C                   eval      glexa = 'TF Claim Adjustment'
     C                   z-add     w1am          wkam
      *
      * Write a JDE record---even if the account retrieval was NOT successful
      *
     C                   exsr      $wrt911
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a JDE Account Ledger record
      *---------------------------------------------------------------
      *
     C     $wrt911       begsr
      *
     C                   z-add     svbatnbr      glicu
     C                   z-add     svdocnbr      gldoc
     C**                 move      svco5a        glkco
     C                   move      *blanks       glkco                             sdn440
     C                   select
     c                   when      svco5a  = '00360'                               sdn440
     C                   move      xxedussf      gledus                            sdn440
     c                   when      svco5a  = '00960'                               sdn440
     C                   move      xxedustf      gledus                            sdn440
     C                   endsl                                                     sdn440
      *
     C                   move      wkmcu         glmcu
     C                   move      wkobj         globj
     C                   move      wksub         glsub
     C                   move      wkaid         glaid
      *
      * Retrieve the structured Account Number.
      *
     C                   move      *blank        wkani
      *
     C** sdn440          call      'X0901'
     C                   CALL      'E1I0901'
     C                   parm                    xxsym
     C                   parm      '2'           xxomod
     C                   parm      '9'           xximod
     C     wkani         parm                    xxani
     C                   parm      glmcu         xxmcu
     C                   parm      globj         xxobj
     C                   parm      glsub         xxsub
     C                   parm                    xxerrm
     C                   parm                    i0901ds
      *
     C                   move      xxdl01        l1desc
      *
      * Populate the Account Number
      *
     C                   if        xxerrm = *blank
     C                   movel     wkani         glani
     C                   else
     C                   move      *blank        glani
     C                   endif
      *
      * Increase the journal entry line number
      *
     C                   z-add     wkdoccnt      gljeln
      *
      * Remove the decimal
      *
     C**   100           mult      wkam          glaa                               sdn440
     C                   z-add     wkam          glag                               sdn440
      *
      ** Add M3 logic here
     C                   eval      wkm3gl = 'N'
      *
     C                   select
     C                   when      glco = '00360' and
     C                             (xxm3lv360 = 'Y' or xxm3lv360 = 'P')
     C                   eval      wkm3gl = 'Y'
      *
     C                   when      glco = '00320' and
     C                             (xxm3lv320 = 'Y' or xxm3lv320 = 'P')
     C                   eval      wkm3gl = 'Y'
      *
     C                   when      glco = '00960' and
     C                             (xxm3lv960 = 'Y' or xxm3lv960 = 'P')
     C                   eval      wkm3gl = 'Y'
     C                   endsl
     C*
      ** Add Jde logic here
     C                   eval      wkjdegl = 'N'
      *
     C                   select
     C                   when      glco = '00360' and
     C                             (xxm3lv360 = 'N' or xxm3lv360 = 'P')
     C                   eval      wkjdegl = 'Y'
      *
     C                   when      glco = '00320' and
     C                             (xxm3lv320 = 'N' or xxm3lv320 = 'P')
     C                   eval      wkjdegl = 'Y'
      *
     C                   when      glco = '00960' and
     C                             (xxm3lv960 = 'N' or xxm3lv960 = 'P')
     C                   eval      wkjdegl = 'Y'
     C                   endsl
      *
      ** Write entry to M3 if company is live for M3, otherwise write to JDE
     C                   if        wkm3gl = 'Y'

     C                   eval      m3p0rtn   = *blanks                          Return code
     C                   eval      m3aco     = %dec(glco:3:0)                   JDE Company
     C                   eval      m3gbmcu   = glmcu                            Cost Center
     C                   eval      m3gbobj   = globj                            Object
     C                   eval      m3gbsub   = glsub                            Sub
     C                   eval      m3gbsbl   = *blanks                          Subledger
     C                   eval      m3gbamt   = wkam                             $ amount
     C                   eval      m3gbqty   = glu                              Units
     C                   eval      m3date    = ldcedt                           Date
     C                   eval      m3deschdr = xxdeschdr                        Header description
     C                   eval      m3descdtl = l1desc                           Detail description

     C                   call      'PWAXUPR'
     C                   parm      m3p0rtn       xxp0rtn
     C                   parm      m3aco         xxaco
     C                   parm      m3gbmcu       xxgbmcu
     C                   parm      m3gbobj       xxgbobj
     C                   parm      m3gbsub       xxgbsub
     C                   parm      m3gbsbl       xxgbsbl
     C                   parm      m3gbamt       xxgbamt
     C                   parm      m3gbqty       xxgbqty
     C                   parm      m3date        xxdate
     C                   parm                    m3deschdr
     C                   parm                    m3descdtl
      *
      ** If Company is 320, then write reversing entries to companies 360 and 320 in M3
      *
     C                   if        m3aco = 320
      *
      ** Company 320 reversing entry
     C                   eval      m3p0rtn   = *blanks                          Return code
     C                   eval      m3gbobj   = '1910  '                         Object
     C                   eval      m3gbsub   = *blanks                          sub init 4/01/19
     C                   eval      m3gbsbl   = *blanks                          Subledger
     C                   eval      m3gbmcu   = '         320'                   Cost Center
     C                   eval      m3gbamt   = 0 - wkam                         $ amount
     C                   eval      m3gbqty   = 0 - glu                          Units
     C                   eval      m3deschdr = xxdeschdr                        Header description
     C                   eval      m3descdtl = l1desc                           Detail description

     C                   call      'PWAXUPR'
     C                   parm      m3p0rtn       xxp0rtn
     C                   parm      m3aco         xxaco
     C                   parm      m3gbmcu       xxgbmcu
     C                   parm      m3gbobj       xxgbobj
     C                   parm      m3gbsub       xxgbsub
     C                   parm      m3gbsbl       xxgbsbl
     C                   parm      m3gbamt       xxgbamt
     C                   parm      m3gbqty       xxgbqty
     C                   parm      m3date        xxdate
     C                   parm                    m3deschdr
     C                   parm                    m3descdtl
      *
      ** Company 360 reversing entry
     C                   eval      m3p0rtn   = *blanks                          Return code
     C                   eval      xxaco     =  360
     C                   eval      m3gbobj   = '1910  '                         Object
     C                   eval      m3gbmcu   = '         360'                   Cost Center
     C                   eval      m3gbsub   = *blanks                          sub init 4/01/19
     C                   eval      m3gbsbl   = *blanks                          Subledger
     C                   eval      m3gbamt   = wkam                             $ amount
     C                   eval      m3gbqty   = glu                              Units
     C                   eval      m3deschdr = xxdeschdr                        Header description
     C                   eval      m3descdtl = l1desc                           Detail description

     C                   call      'PWAXUPR'
     C                   parm      m3p0rtn       xxp0rtn
     C                   parm                    xxaco                              360
     C                   parm      m3gbmcu       xxgbmcu
     C                   parm      m3gbobj       xxgbobj
     C                   parm      m3gbsub       xxgbsub
     C                   parm      m3gbsbl       xxgbsbl
     C                   parm      m3gbamt       xxgbamt
     C                   parm      m3gbqty       xxgbqty
     C                   parm      m3date        xxdate
     C                   parm                    m3deschdr
     C                   parm                    m3descdtl
     C                   endif                                                  if m3aco = 320
     C                   endif                                                  if wkm3gl= Y

     C**********               else
      ** Write entry to JDE
     C                   if        wkjdegl = 'Y'
      *
      * SDN440 added for new fields in e10911
     C                   movel     glicu         gledbt                         edi batch no
     C                   movel     gldoc         gledtn                         edi trans #->gldoc
     C                   z-add     gljeln        gledln                         edi line no
     C                   movel     '0'           gledsp                         edi success procd
     C                   movel     'A'           gledtc                         edi trans action
     C                   movel     'J'           gledtr                         edi trans type
     C                   move      xxsyssyndt    glabdt                         Date updated
     C                   time                    glabtm                         Time Last Updated
     C                   movel     'TFJDE272 '   glakvn                         pgm  Last Updated
     C                   movel     gluser        glajvn                         user id created
      *
     C                   write     i0911
     C                   endif                                                  if wkjdegl = 'Y'
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Write an offset record
      *--------------------------------------------------------------------------------------
      *
     C     $offset       begsr
      *
     C                   select
     C                   when      svcono = 360
     C                   eval      xxglcode = 'OFFAR320'
     C     -1            mult      wksoffam      wkam
     C                   add       1             wksdoccnt                       sdn440
     C                   z-add     wksdoccnt     wkdoccnt                        sdn440
      *
     C                   when      svcono = 960
     C                   eval      xxglcode = 'OFFAR960'
     C     -1            mult      wktoffam      wkam
     C                   add       1             wktdoccnt                       sdn440
     C                   z-add     wktdoccnt     wkdoccnt                        sdn440
     C                   endsl
      *
     C****               add       1             wkdoccnt           sdn440 add per company above
      *
      * Clear account workfields.
      *
     C                   move      *blank        wkaid                          Account ID
     C                   move      *blank        wkmcu                          Cost Center
     C                   move      *blank        wkobj                          Object
     C                   move      *blank        wksub                          Sub Code
      *
      *
      * Retrieve Account I.D. from the GL Master Codes file.
      *
     C                   call      'PKTIXFR'
     C                   parm                    return
     C                   parm      960           xxnumco
     C                   parm                    xxglcode
     C     wkaid         parm      *blank        xxaid
     C                   parm      *blank        xxsubtype
     C                   parm      *blank        xxbalsheet
     C                   parm      *zeros        xxacctco
      *
      * Retrieve cost center/object/sub associated with Account ID just retrieved.
      * (1 = Short Act I.D., 9 = Individual cost center/object/sub.)
      *
     C** sdn440          call      'X0901'
     C                   CALL      'E1I0901'
     C                   parm                    xxsym
     C                   parm      '9'           xxomod
     C                   parm      '1'           xximod
     C                   parm      wkaid         xxani
     C     wkmcu         parm      *blank        xxmcu
     C     wkobj         parm      *blank        xxobj
     C     wksub         parm      *blank        xxsub
     C                   parm      *blank        xxerrm
     C                   parm                    i0901ds
      *
      * Hardcode explanation
      *
     C                   eval      glexa = 'TF Claim Adjustment--Offset'
      *
      * Company number needs to be the company that is associated with this
      * cost center/object/sub code.
      *
      *  S11799 :  add code to ensure company being there.
      *
     C                   move      xxco          glco
     c     glco          ifeq      '     '
     c                   move      xxmcu         wrkcomp           3
     c                   move      '00000'       glco
     c                   move      wrkcomp       glco
     c                   end
      *
      * Write record even if account retrieval was NOT successful
      *
     C                   exsr      $wrt911
      *
      * Set up Credit/Debit print field and print
      *
     C                   if        wkam < 0
     C                   z-add     wkam          l1cram
     C                   else
     C                   z-add     wkam          l1dbam
     C                   endif
      *
     C                   setoff                                       87
     C                   exsr      $l1dtl
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Write 2 JDE Batch Control records: 1 for Seaboard, 1 for Triumph
      *--------------------------------------------------------------------------------------
      *
     C     $batch        begsr
      *
      * Write a JDE Batch Control record for Triumph
      *   1) the batch amount is just the Offset amount with no
      *      sign flipping; but, remove the decimal position.
      *
     C     100           mult      wktoffam      wkbatval
     C                   z-add     wktbatnbr     wkbatnbr
     C                   z-add     wktbatcnt     wkbatcnt                        sdn440
     C                   eval      wkuser = 'TFGLPOST'
     C                   eval      xxedus = xxedustf                            sdn440
     C                   exsr      $wrtbat
      *
      * Write a JDE Batch Control record for Seaboard
      *   1) the batch amount is just the Offset amount with no
      *      sign flipping; but, remove the decimal position.
      *
     C     100           mult      wksoffam      wkbatval
     C                   z-add     wksbatnbr     wkbatnbr
     C                   z-add     wksbatcnt     wkbatcnt                        sdn440
     C***                eval      wkuser = sdusr
     C                   eval      wkuser = XXEDUSSF                             11-02-21
     C                   eval      xxedus = xxedussf                             sdn440
     C                   exsr      $wrtbat
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a batch record
      *---------------------------------------------------------------
      *
     C     $wrtbat       begsr
      *
      * Write a batch header record to batch control file
      *
      ** Write batch header only company is still live with JDE           E8287
      *
     C                   if        wkjdegl = 'Y'
     C**   key01         chain     f0011                              92         sdn440
     C     KEY01         CHAIN     I0011l1                            92
     C                   if        *in92 = *on                                  If no hit
     C                   z-add     wkbatcnt      icndo
     C                   z-add     wkbatcnt      icdocn
     C                   z-add     wkbatval      icaicu
     C                   z-add     wkbatval      icame
     C** sdn440          z-add     wkbatval      icaicu
     C                   move      wkbatval      icaicu                           no dec-sdn440
      *
     C                   z-add     wkbatnbr      icicu                             sdn440
     C                   move      *blank        icist
     C**                 movel     wkuser        icuser
     C                   MOVEL     xxedus        ICUSER
     C                   movel     wkuser        icuser
     C                   move      glicut        icicut
     C                   move      gldicj        icdicj
     C                   move      'A'           iciapp
     C                   move      'Y'           icbal
     C                   move      ' '           icbalj
     C                   move      'N'           icpob
     C                   move      'Y'           iciboi
      *
     C** sdn440          write     i0011
     C                   WRITE     I0011l1
     C*
     C* WRITE RUN LOG HEADER WORKFILE - WILL BE READ IN CL _____   TO PUSH BATCHES TO ZPROCESSOR
     C*
     C                   call      'E1LCXFR'
     c                   parm                    RETURN            7
     C                   parm                    icICUT                         BATCH TYPE
     C                   parm                    icICU                          BATCH NUMBER
     C                   parm                    icuser                         USER
     C                   parm                    glco                           company
      *
     C                   endif                                                  If no hit
     C                   endif                                                  If no hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Get dates into JDE format
      *---------------------------------------------------------------
      *
     C     $date         begsr
      *
     C*** sdn440         call      'X0028'
     C*                  parm                    xxsidat
     C*                  parm      *blank        xxedat
     C*                  parm                    xxffmt
     C*                  parm      '*JUL   '     xxtfmt
     C*                  parm      '*NONE'       xxsep
     C*                  parm      *blank        xxertst
     C*                  parm      *blank        xxctry
      *
     C                   call      'E10028A'
     C                   parm                    xxccyymmdd
     C                   parm      0             xxedat
      *
     c                   move      xxedat        xxsidat
     C                   move      xxsidat       #sidat            6
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Company heading
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   z-add     svbatnbr      h2batnbr
     C                   z-add     svdocnbr      h2docnbr
      *
     C                   select
     C                   when      svcono = 360
     C                   eval      h2conm = 'Seaboard'
      *
     C                   when      svcono = 960
     C                   eval      h2conm = 'Triumph'
     C                   endsl
      *
     C                   except    h2hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   select
     C                   when      wkaid = *blank
     C                   eval      l1acct = 'Unknown'
     C                   eval      l1desc = 'Error on Account ID retrieval'
      *
     C                   when      xxerrm = *blank
     C                   eval      l1acct = %trim(wkmcu) + '.' +
     C                                      %trim(wkobj) + '.' +
     C                                      %trim(wksub)
     C                   other
     C                   eval      l1acct = 'Unknown'
     C                   eval      l1desc = 'Cost Center/Obj/Sub error'
     C                   endsl
      *
     C                   if        *inof = *on
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    l1dtl
      *
      * Accumulate for totals
     C                   if        *in87 = *on
     C                   add       w1agclam      aragclam
     C                   add       w1snclam      arsnclam
     C                   add       w1tnclam      artnclam
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
      * Clear array entries
     C                   z-add     0             aragclam(x)
     C                   z-add     0             arsnclam(x)
     C                   z-add     0             artnclam(x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxm3lv360
     C                   parm                    xxm3lv320
     C                   parm                    xxm3lv960
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    glicut
     C                   kfld                    wkbatnbr
      *
      * Flip the Week-Ending Date to MMDDYY format.
      *
     C     *iso          test(d)                 ldcedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ldcedt        wkisodate
     C     *mdy          move      wkisodate     h1mdy
     C                   endif                                                  If OK date
      *
     C                   time                    rtime
      *
      * Get system date into the format required by JDE by calling JDE's routine.
      *
     C                   time                    wktime
     C                   move      wktime        wkdate
     C                   move      wkdate        xxsidat
     C                   move      '*SYSVAL'     xxffmt
      *
      * Set up defaults for records in F0911-Account Ledger File
      *
     C     *mdy          movel     udate         wkisodate                       sdn440
     C                   movel     wkisodate     xxccyymmdd                      sdn440
     C                   call      'HP8008'                                      sdn440
     C                   parm                    xxccyymmdd                      sdn440
     C                   parm      0             xxsyssyndt
     C                   exsr      $date
     C                   move      xxsidat       glupmj
     C                   move      xxsidat       gldsyj
     C                   move      xxsidat       gldicj
      *
      * Retrieve the Accounting Year/Period associated with the Week-Ending Date.
      *
     C                   call      'TF815'
     C                   parm      ldcedt        xxdt
     C                   parm      'A'           xxapfl
     C                   parm      0             xxyr
     C     glpn          parm      0             xxpe
     C                   parm      0             xxwk
      *
     C                   z-add     xxyr          wkccyy
     C                   z-add     wkcc          glctry
     C                   z-add     wkyy          glfy
      *
      * Get Weekending Date into JDE format by calling JDE date routine.
      *
     C                   move      ldcedt        xxsidat
     C                   move      '*YMD   '     xxffmt
     c                   move      ldcedt        xxccyymmdd                        sdn440
     C                   exsr      $date
     C                   move      xxsidat       gldgj
     C                   move      xxsidat       gldsvj                         Service Tax date
      *
      * Set up some defaults
      *
     C                   movel     sdjob         gljobn
     C                   time                    glticu
     C                   time                    glupmt
      *
     C                   move      '2'           glam
     C                   move      'AA'          gllt
     C** sdn440          move      '00000'       glpkco
     C** sdn440          move      '00000'       glokco
     C** sdn440          move      '000'         glpsfx
     C                   move      '     '       glpkco                         sdn440
     c                   move      '     '       glokco                         sdn440
     C                   move      '   '         glpsfx                         sdn440
     C                   move      'JE'          gldct
     C                   move      'G '          glicut
     C*** sdn440         move      'R'           glrcnd
     C                   move      ' '           glrcnd                           sdn440
     C                   move      'D'           glcrrm                           sdn440
     C                   move      'USD'         glcrcd                           sdn440
     C                   movel     'TFJDE272  '  glpid                            sdn440
      *
      *
      * Get company value OMSJDE for User ID to use for gledus                  sdn440
      *
     c                   movel     'OMSJDEUSR '  xxval
     c                   call      'PUQ6XFR '
     C                   PARM                    XXRTN             7
     C                   PARM      360           xxnumco           3 0
     C                   PARM                    XXVAL            10
     C                   PARM                    XXEDUSSF         10             360 user id
     c                   movel     'OMSJDEUSR '  xxval
     c                   call      'PUQ6XFR '
     C                   PARM                    XXRTN             7
     C                   PARM      960           xxnumco
     C                   PARM                    XXVAL            10
     C                   PARM                    XXEDUSTF         10             960 user id
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          106 'CLAIM POST REPORT'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          105 'JOURNAL ENTRIES'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e            h1hdr          2
     O                                           17 'Week-ending date:'
     O                       h1mdy               28 '  /  /  '
      *
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                          100 'GL '
     O                                          110 'Product'
     O                                          120 'Claim'
     O                                          126 'Line'
     O                                          132 'Memo'
     O                                          140 'CR/DB'
     O                                          163 'Total Gross'
     O                                          181 'Seaboard Net'
     O                                          197 'Triumph Net'
      *
     O          e            h1hdr       0  1
     O                                           14 'Account Number'
     O                                           41 'Description'
     O                                           76 'Debit'
     O                                           94 'Credit'
     O                                          100 'Sub'
     O                                          108 'Code'
     O                                          119 'Nbr'
     O                                          126 'Nbr'
     O                                          132 'Type'
     O                                          142 'Memo Nbr'
     O                                          163 'Claim Amount'
     O                                          181 'Claim Amount'
     O                                          197 'Claim Amount'
      *
     O          e            h1hdr       0  2
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Company header
      *-------------------------------------------------------------
      *
     O          e            h2hdr       0  0
     O                       h2conm               8
     O                                           25 'JDE Batch Nbr:'
     O                       h2batnbr      z     35
     O                                           54 'JDE Document Nbr:'
     O                       h2docnbr      z     64
      *
     O          e            h2hdr       0  2
     O                       h2conm               8
     O                                           25 'JDE Batch Nbr:'
     O                       h2batnbr      z     35
     O                                           54 'JDE Document Nbr:'
     O                       h2docnbr      z     64
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  1
     O                       l1acct         b    28
     O                       l1desc         b    60
     O                       l1dbam        kb    77
     O                       l1cram        kb    95
     O               87      w1glsub            100
     O               87      w1prcd        z    110
     O               87      w1clno        z    120
     O               87      w1cllnno      z    126
     O               87      w1mmty             131
     O               87      w1mmno        z    142
     O               87      w1agclam      k    164
     O               87      w1snclam      k    182
     O               87      w1tnclam      k    198
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       0
     O              n88                         163 '--------------'
     O              n88                         181 '--------------'
     O              n88                         197 '--------------'
      *
     O          e            t1tot       1
     O                       t1desc             147
     O                       aragclam(x)   k    164
     O                       arsnclam(x)   k    182
     O                       artnclam(x)   k    198
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       0  1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1
     O                                           20 'There were no claim '
     O                                           40 'adjustments to post.'
      /eject
