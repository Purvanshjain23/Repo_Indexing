     A*%%TS  SD  19960329  111853  ISLRAMS     REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD416
     A*  TITLE:         SPECIFY DELETED MOVEMENT LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/28/96
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A                                      CA12(12 'PREVIOUS')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP416R1
     A*%%TS  SD  19960328  154648  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(60)
     A                                      SETOF(66)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 18'SPECIFY DELETED MOVEMENT LISTING O-
     A                                      PTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Deleted by user ...............'
     A                                  4 34'?'
     A            DFUSCD    R     A  B  4 36REFFLD(@@USCD)
     A N30                                  DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFUSNM    R        O  4 48REFFLD(@@DS30)
     A                                  5  2'From deleted date .............'
     A            DFFDMD    R     Y  B  5 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To deleted date ...............'
     A            DFTDMD    R     Y  B  6 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'From scheduled date ...........'
     A            DFFSMD    R     Y  B  7 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  8  2'To scheduled date .............'
     A            DFTSMD    R     Y  B  8 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  9  2'Movement type .................'
     A                                  9 34'?'
     A            DFMTCD    R     A  B  9 36REFFLD(@@MTCD)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFMTDS    R        O  9 48REFFLD(MTMTDS)
     A                                 10  2'Origin business office ........'
     A                                 10 34'?'
     A            DFORBO    R        B 10 36REFFLD(BOBOBO)
     A N32                                  DSPATR(HI)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFORBD    R        O 10 48REFFLD(BOBODS)
     A                                 11  2'Origin production type ........'
     A                                 11 34'?'
     A            DFORPT    R        B 11 36REFFLD(@@PTCD)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFORTD    R        O 11 48REFFLD(PTPTDS)
     A                                 12  2'Origin production phase .......'
     A                                 12 34'?'
     A            DFORPP    R        B 12 36REFFLD(@@PPCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFORPD    R        O 12 48REFFLD(PPPPDS)
     A                                 13  2'Origin farm site ..............'
     A                                 13 34'?'
     A            DFORFS    R        B 13 36REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFORFN    R        O 13 48REFFLD(FSFSNM)
     A                                 14  2'Destination business office ...'
     A                                 14 34'?'
     A            DFDNBO    R        B 14 36REFFLD(BOBOBO)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDNBD    R        O 14 48REFFLD(BOBODS)
     A                                 15  2'Destination production type ...'
     A                                 15 34'?'
     A            DFDNPT    R        B 15 36REFFLD(@@PTCD)
     A N37                                  DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDNTD    R        O 15 48REFFLD(PTPTDS)
     A                                 16  2'Destination production phase ..'
     A                                 16 34'?'
     A            DFDNPP    R        B 16 36REFFLD(@@PPCD)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDNPD    R        O 16 48REFFLD(PPPPDS)
     A                                 17  2'Destination farm site .........'
     A                                 17 34'?'
     A            DFDNFS    R        B 17 36REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDNFN    R        O 17 48REFFLD(FSFSNM)
     A                                 18  2'Customer number ...............'
     A                                 18 34'?'
     A            DFCVNO    R        B 18 36REFFLD(@@AJD)
     A N40                                  DSPATR(HI)
     A  40
     AO 60                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFCNAM        30A  O 18 48
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP416K1
     A*%%TS  SD  19960329  111853  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44                             23 22'F8=Submit'
     A                                 23 33'F12=Previous'
     A N99                             23 47'F17=Print Options'
     A N99                             23 65'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP416ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP416EC                   SFLCTL(HP416ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
