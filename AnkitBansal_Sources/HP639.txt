      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP639
      * TITLE:       Costed Transfers into BGF Farms
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/30/02
      *
      * FUNCTION:  This report is a batch EOP report as well as being on-demand.
      *            It runs over a workfile built in HP339.
      *
      *********************************************************************************
      * MODIFICATIONS:
      *********************************************************************************
      * DATE      PROGRAMMER
      *
      * 08/19/03  LeAnne Fedor
      *           The users no longer want the EOP version limited to GDU groups. SO,
      *           we now include origin GDU groups; and, we sort by group type.
      *
      * 11/10/03  LeAnne Fedor
      *           Added 'per head' and 'per pound' fields to the total lines for Gary Albright.
      *           He asked that we "bring on down the transfer costs per head and per pound
      *           so that it takes the total amount and divides it by the total head and lbs."
      *
      * 04/22/16  Barb Gutierrez
      *           Changed the position of ldfsnm in the LDA to accomodate adding of LDCOCD.
      *           ldfsnm was in positions 185 - 209.  Now it is 235 - 259.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp025    if   e           k disk
      *  Group type
      *
      *
     Fhsp339    if   e           k disk
      *  Workfile: Costed transfers into BGF farms
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      * Print array for totals
      *
     D arhd            s             11  0 dim(3)
     D arlb            s             11  0 dim(3)
     D aram            s             11  0 dim(3)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Save/Control fields
      *
     D first           s              1    inz('Y')
     D svorfscd        s                   like(wtorfscd)
     D svorgtcd        s                   like(wtorgtcd)
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D rtime           s              6  0
     D dash            s            132    inz(*all'-')
      *
     D h1title         s             31
     D h1ef            s              9
     d h1fsbo          s                   like(ldfsbo)
     d h1ptcd          s                   like(ldptcd)
     d h1ppcd          s                   like(ldppcd)
     d h1gtcd          s              3
     d h1crcd          s                   like(ldcrcd)
     d h1gtds          s                   like(gtgtds)
      *
     D l1trlb          s              9  0
     D l1trhd          s              9  0
     D l1tram          s              9  0
     D l1rcdtmdy       s              6  0
      *
     D t1desc          s             21
     D t1hdrt          s                   like(wthdrt)
     D t1lbrt          s                   like(wtlbrt)
      *
      *
      * Array index
      *
     D x               s              1  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldbpdt                 1     08  0
     D  ldepdt                 9     16  0
     D  ldper                 17     18  0
     D  ldeffl                19     19
     D  ldccyy                20     23  0
     D  ldbmdy                60     66  0
     D  ldemdy                70     76  0
     D  lderfl                80     80
      *
     D  ldfsbo                87     91
     D  ldptcd                92     96
     D  ldppcd                97    101
     D  ldcell               102    103  0
     D  ldcrcd               104    108
     D  ldfscd               109    113  0
     D  ldgtcd               114    114
      *
     D  ldbods               115    144
     D  ldptds               145    154
     D  ldppds               155    184
     D  ldcocd               200    202
     D  ldgtds               210    224
     D  ldcrds               225    234
     D  ldfsnm               235    259
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      *
      * Read/print all records in the workfile.
      *
     C     *loval        setll     hsp339
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp339                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   z-add     wtorfscd      svorfscd
     C                   move      wtorgtcd      svorgtcd
     C                   exsr      $gtds
     C                   move      no            first
     C                   exsr      $h1hdr
      *
     C                   when      wtorgtcd <> svorgtcd
     C                   exsr      $fstot
     C                   exsr      $gttot
     C                   seton                                        of
      *
     C                   when      wtorfscd <> svorfscd
     C                   exsr      $fstot
     C                   endsl
      *
      * We will print the head, pounds, amount values as whole numbers.
      *
     C                   z-add(h)  wttram        l1tram
     C                   z-add(h)  wttrhd        l1trhd
     C                   z-add(h)  wttrlb        l1trlb
      *
      * Flip Received Date to MMDDYY format
      *
     C                   exsr      $rcdt
      *
      * Add values to total arrays
      *
     C                   add       l1tram        aram
     C                   add       l1trhd        arhd
     C                   add       l1trlb        arlb
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      *
     C                   select
     C                   when      first = yes
     C                   seton                                        97
     C                   exsr      $h1hdr
     C                   except    nodata
     C                   other
      *
      * Print out total lines for:
      * 1) last farm site
      * 2) last group type
      * 3) report
     C                   exsr      $fstot
     C                   exsr      $gttot
      *
     C                   eval      t1desc = '        Report Total:'
     C                   z-add     3             x
     C                   exsr      $t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Flip Received Date to MMDDYY format
      *---------------------------------------------------------------
      *
     C     $rcdt         begsr
      *
     C     *iso          test(d)                 wtrcdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wtrcdt        wkisodate
     C     *mdy          move      wkisodate     l1rcdtmdy
     C                   endif                                                  If OK date
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve Group Type description for printing
      *---------------------------------------------------------------
      *
     C     $gtds         begsr
      *
     C     svorgtcd      chain     hsp025                             92
     C                   if        *in92 = *off
     C                   move      gtgtds        h1gtds
     C                   else
     C                   movel(p)  'Unknown'     h1gtds
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Farm Site total
      *---------------------------------------------------------------
      *
     C     $fstot        begsr
      *
     C                   z-add     1             x
      *
     C                   eval      t1desc = ' Total for Farm Site:'
     C                   seton                                        87
      *
     C                   exsr      $t1tot
      *
     C                   z-add     wtorfscd      svorfscd
     C                   setoff                                       87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Group Type total
      *---------------------------------------------------------------
      *
     C     $gttot        begsr
      *
     C                   z-add     2             x
      *
     C                   eval      t1desc = 'Total for Group Type:'
     C                   seton                                        89
      *
     C                   exsr      $t1tot
      *
     C                   move      wtorgtcd      svorgtcd
     C                   setoff                                       89
      *
     C                   exsr      $gtds
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Calculate 'per head' and 'per pound' rates.
      *
     C                   if        arhd(x) <> 0
     C     aram(x)       div(h)    arhd(x)       t1hdrt
     C                   endif
      *
     C                   if        arlb(x) <> 0
     C     aram(x)       div(h)    arlb(x)       t1lbrt
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Setup Headings for User's report selection
      *
     C                   eval      h1title = 'Costed Transfers into BGF Farms'

     C                   select
     C                   when      ldeffl = 'D'
     C                   movel(p)  'ON-DEMAND'   h1ef
     C                   seton                                        98
      *
     C                   when      ldeffl = 'E'
     C                   movel(p)  '  EDIT   '   h1ef
      *
     C                   when      ldeffl = 'F'
     C                   movel(p)  '  FINAL  '   h1ef
     C                   endsl
      *
     C                   if        ldfsbo = *blank
     C                   movel     'All'         h1fsbo
     C                   else
     C                   movel     ldfsbo        h1fsbo
     C                   endif
      *
     C                   if        ldptcd = *blank
     C                   movel     'All'         h1ptcd
     C                   else
     C                   movel     ldptcd        h1ptcd
     C                   endif
      *
     C                   if        ldppcd = *blank
     C                   movel     'All'         h1ppcd
     C                   else
     C                   movel     ldppcd        h1ppcd
     C                   endif
      *
     C                   if        ldfscd = 0
     C                   seton                                        86
     C                   endif
      *
     C                   if        ldcell = 0
     C                   seton                                        88
     C                   endif
      *
     C                   if        ldgtcd = *blank
     C                   movel     'All'         h1gtcd
     C                   else
     C                   movel     ldgtcd        h1gtcd
     C                   endif
      *
     C                   if        ldcrcd = *blank
     C                   movel     'All'         h1crcd
     C                   else
     C                   movel     ldcrcd        h1crcd
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           75 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1title             80
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          1
     O                       h1ef                69
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          2
     O                                            7 'PERIOD:'
     O                       ldper         z     11
     O                       ldbmdy        y     21
     O                                           24 'TO'
     O                       ldemdy        y     33
      *
      *
     O          e            h1hdr          1
     O               98                          24 'ORIGIN GROUP SELECTIONS:'
     O               98                          84 'COMPANY RELATIONSHIP:'
     O               98      h1crcd              91
     O               98      ldcrds             103
      *
      *
     O          e    98      h1hdr          1
     O                                           17 'BUSINESS OFFICE:'
     O                       h1fsbo              24
     O                       ldbods              57
      *
     O                                           84 'CELL:'
     O              n88      ldcell        z     91
     O               88                          89 'All'
      *
      *
     O          e    98      h1hdr          1
     O                                           17 'PRODUCTION TYPE:'
     O                       h1ptcd              24
     O                       ldptds              37
      *
     O                                           84 'FARM SITE:'
     O              n86      ldfscd        z     91
     O               86                          89 'All'
     O                       ldfsnm             118
      *
      *
     O          e    98      h1hdr          2
     O                                           17 'PRODUCTION PHASE:'
     O                       h1ppcd              24
     O                       ldppds              57
      *
     O                                           84 'GROUP TYPE:'
     O                       h1gtcd              89
     O                       ldgtds             108
      *
      *-------------------------------------------------------------
      * Column heading lines
      *-------------------------------------------------------------
      *
      *
     O          e            h1hdr          1
     O                                           26 '--------- ORIGIN ---------'
     O                                           27 '-'
     O                                           40 'DESTINATION'
      *
     O                                           92 '--------- TRANSFER '
     O                                          107 'COSTS ---------'
      *
      *
     O          e            h1hdr          1
     O                                            6 'FARM'
     O                                           11 'HOG'
     O                                           27 '-- GROUP --'
      *
     O                                           36 'FARM'
     O                                           55 'HEAD'
     O                                           70 'POUNDS'
      *
     O                                           82 'PER HEAD'
     O                                           93 'PER POUND'
     O                                          107 'AMOUNT'
      *
     O                                          122 'RECEIVED'
     O                                          132 'MOVEMENT'
      *
     O          e            h1hdr          1
     O                                            6 'SITE'
     O                                           13 'GROUP'
     O                                           22 'STATUS'
     O                                           27 'TYPE'
      *
     O                                           36 'SITE'
     O                                           55 'OUT'
     O                                           70 'OUT'
      *
     O                                          120 'DATE'
     O                                          132 'NUMBER'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
     O          e   n97      h1hdr          1
     O                                           18 'Origin group type:'
     O                       h1gtds              35
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       wtorfscd      mb     7
     O                       wtorhgcd       b    15
     O                       wtorgscd       b    21
     O                       wtorgtcd       b    25
      *
     O                       wtdnfscd      mb    37
      *
     O                       l1trhd        kb    55
     O                       l1trlb        kb    71
      *
     O                       wthdrt        kb    82
     O                       wtlbrt        kb    93
     O                       l1tram        kb   108
      *
     O                       l1rcdtmdy      b   122 '  /  /  '
     O                       wtmvsn        zb   132
      *
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e    87      t1tot       1
     O                                           55 '-------------'
     O                                           71 '-------------'
     O                                          108 '-------------'
      *
     O          e            t1tot       1  2
     O                       t1desc         b    24
     O               87      svorfscd      zb    30
     O               89      svorgtcd       b    30
     O                       arhd(x)       kb    55
     O                       arlb(x)       kb    71
     O                       t1hdrt        kb    82
     O                       t1lbrt        kb    93
     O                       aram(x)       kb   108
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /eject
