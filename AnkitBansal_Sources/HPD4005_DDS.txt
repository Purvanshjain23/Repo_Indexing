     A*%%TS  SD  20070926  111019  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4005
     A*  TITLE:         WORK WITH BGF TARGET TABLES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/18/00
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF10(10 'COPY')
     A                                      CF14(14 'MULTIPLE FARMS')
     A                                      CF15(15 'VIEW FARMS')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF19(19 'SINGLE FARMS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4005S1                  SFL
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1OPT     R        B 11  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1TTBCD   R        O 11  7REFFLD(@@TTBCD)
     A            D1TTBDS   R        O 11 15REFFLD(@@TTBDS)
     A            D1TARGETFLR        O 11 53REFFLD(@@FLAG)
     A            D1FARMFL  R        O 11 71REFFLD(@@FLAG)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4005C1                  SFLCTL(HP4005S1)
     A*%%TS  SD  20070925  161235  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH BGF TARGET TABLES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  4 22'7=Work with Target Years'
     A                                  4 48'10=View Farms'
     A                                  6 53'?'
     A                                      DSPATR(HI)
     A                                  6 71'?'
     A                                      DSPATR(HI)
     A                                  7  7'Target'
     A                                      DSPATR(HI)
     A                                  7 50'Targets'
     A                                      DSPATR(HI)
     A                                  7 66'Target Table'
     A                                      DSPATR(HI)
     A                                  8  7'Table'
     A                                      DSPATR(HI)
     A                                  8 49'Exist for'
     A                                      DSPATR(HI)
     A                                  8 65'Currently Used'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  7'Code'
     A                                      DSPATR(HI)
     A                                  9 15'Description'
     A                                      DSPATR(HI)
     A                                  9 48'Target Table'
     A                                      DSPATR(HI)
     A                                  9 68'by Farms'
     A                                      DSPATR(HI)
     A            C1TTBCD   R        B 10  7REFFLD(@@TTBCD)
     A                                      DSPATR(HI)
     A            C1TARGETFLR        B 10 53REFFLD(@@FLAG)
     A                                      DSPATR(HI)
     A  59                                  DSPATR(PC)
     A            C1FARMFL  R        B 10 71REFFLD(@@FLAG)
     A                                      DSPATR(HI)
     A  58                                  DSPATR(PC)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP4005K1
     A*%%TS  SD  20070926  111019  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             20  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 21  2'F3=Exit'
     A                                 21 15'F6=Create'
     A                                 21 27'F14=Assign Multiple Tables/Farms'
     A                                 22  2'F4=Prompt'
     A                                 22 15'F7=Listing'
     A                                 22 27'F15=View Farms for Tables'
     A                                 22 63'F17=Print Options'
     A                                 23  2'F5=Refresh'
     A                                 23 14'F10=Copy'
     A                                 23 27'F19=Work with Single Farm/Tables'
     A                                 23 63'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4005ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4005EC                  SFLCTL(HP4005ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
