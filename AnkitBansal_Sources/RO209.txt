      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO209- Update Item Standard Cost for Overridden Items
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     07/30/08
      *
      * Function:    When a Final of the Weekly Meat Cost function runs, we call this program
      *              to update the Std Meat Cost in the Synon Item Standard Cost file for
      *              Company/Week-ending Date/Item for Items in the Meat Cost Override file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 01/21/21  ISE                TSN593
      *           Replaced *loval with *start
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpoaxcpl1  uf   e           k disk
      *  Item standard cost
      *
      *
     Frol016a   if   e           k disk
      *  Meat cost overrides (LF selects Active records)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields
      *
     D wkfrpr          s                   like(axfrpr)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldwedt                29     36  0
     D  ldwesyn               37     43  0
      *
     D  ldcono                82     84  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ******************************************************************************************
      * Mainline
      ******************************************************************************************
      *
      * Read each Active record in the Meat Cost Override file. Process the record
      * if the Effective Date is on/before the Meat Cost WeekEnding date.
      *
593  C*    *loval        setll     rol016a
593  C     *start        setll     rol016a
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      rol016a                                90
     C                   if        *in90 = *off and moefdt <= ldwedt            If not EOF
      *
      * Retrieve Item Standard Cost record for the Override Item using:
      *    1) Company
      *    2) Override Item
      *    3) Week-Ending Date
      *
     C     key02         chain(n)  poaxcpl1                           92
     C                   if        *in92 = *off                                 If hit on over
     C                   z-add     axfrpr        wkfrpr
      *
      * Retrieve the Item Standard Cost record for the Item being overridden
      * and update it with the Meat Cost from the Override Item.
      * If no Synon record exists, DO NOT write a record.
      *
     C     key01         chain     poaxcpl1                           92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     wkfrpr        axfrpr
     C                   update    @axcphi
     C                   endif                                                  If hit
      *
     C                   endif                                                  If hit on over
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcono
     C                   kfld                    moitcd
     C                   kfld                    ldwesyn
      *
     C     key02         klist
     C                   kfld                    ldcono
     C                   kfld                    moovitcd
     C                   kfld                    ldwesyn
      *
     C                   endsr
      /eject
