/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT SALES REPORT BY CO/DATE RANGE/ITEM CLASS/      */
/*             ITEM GROUP/ITEM TYPE. (COPIED FROM OMNPPCLP)         */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - SHEILA SCHERTZ                                      */
/*  DATE      - 07/19/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &TYPE       - SALES TYPE                                */
/*          &GRUP       - SALES GROUP                               */
/*          &CLSS       - SALES CLASS                               */
/*          &DTLSUM     - DETAIL OR SUMMARY                         */
/*          &EXPSLS     - EXPORT SALES                              */
/*          &FLAG1      - INCLUDE ALL PRODUCTS                      */
/*          &FLAG2      - INCLUDE CM/DM                             */
/********************************************************************/

 POETPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL +
                          &TYPEL &GRUPL &CLSSL &OUTQ &HOLD &SAVE +
                          &COPIESL &NIGHTQ &DTLSUM &EXPSLS &FLAG1 +
                          &FLAG2 &SALRT)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &TYPE      *DEC    (3 0)          /*  SALES TYPE     */
        DCL    &TYPEL     *DEC                   /*  FOR PASSING    */
        DCL    &GRUP      *DEC    (3 0)          /*  SALES GROUP    */
        DCL    &GRUPL     *DEC                   /*  FOR PASSING    */
        DCL    &CLSS      *DEC    (3 0)          /*  SALES CLASS    */
        DCL    &CLSSL     *DEC                   /*  FOR PASSING    */
        DCL    &OUTQ      *CHAR   10             /*  OUTPUT QUEUE   */
        DCL    &NIGHTQ    *CHAR   10             /*  NIGHT JOB QUEUE*/
        DCL    &SAVE      *CHAR   4              /*  SAVE SPOOL     */
        DCL    &HOLD      *CHAR   4              /*  HOLD SPOOL     */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &DTLSUM    *CHAR   1               /*  DETAIL/SUMMARY*/
        DCL    &EXPSLS    *CHAR   1               /*  EXPORT SALES  */
        DCL    &FLAG1     *CHAR   1               /*  INCL. ALL PROD*/
        DCL    &FLAG2     *CHAR   1               /*  INCL. CM/DM   */
        DCL    &SALRT     *CHAR   3
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &USER      *CHAR   10              /*  USER ID       */
        DCL    &MSGRTN    *CHAR   10              /*  MSG QUEUE     */
        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY FROM DATE  */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY EXPORT     */
        DCL     &E        *CHAR   95              /* QRY ALL        */
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TYPE) VALUE(&TYPEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&GRUP) VALUE(&GRUPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&CLSS) VALUE(&CLSSL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')


/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR AREA/BRK WORK FILE BUILD  */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             IF         COND(&EXPSLS *EQ 'N') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4U2ST *NE ' *CAT +
                         '"Y"')
             ENDDO
             IF         COND(&EXPSLS *EQ 'E') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4U2ST *EQ ' *CAT +
                         '"Y"')
             ENDDO


             CHGVAR     VAR(&E) VALUE(&A *BCAT &B *BCAT &C *BCAT &D)

             OVRDBF     FILE(OMHSTPLC) SHARE(*YES)

             OPNQRYF    FILE((OMHSTPLC)) OPTION(*ALL) +
                          QRYSLT(&E) KEYFLD(*FILE)

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

/*  THIS USED TO SPECIFY DRAWER(2)                                */
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

        /*   CALL       PGM(&PGM) PARM('' &FDATE &TDATE &TYPE &GRUP +
                    &CLSS &FLAG1 &FLAG2 &DTLSUM &EXPSLS &SALRT)  */
             CALL       PGM(&PGM) PARM('' &FDATE &TDATE &FLAG1 +
                          &FLAG2 &DTLSUM &EXPSLS &CLSS &GRUP &TYPE +
                          &COMP &SALRT &DTLSUM)
             CLOF       OPNID(OMHSTPLC)
             DLTOVR     FILE(OMHSTPLC)


 END:        ENDPGM
