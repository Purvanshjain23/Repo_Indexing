/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  EDIPCC                                        */
/*  PROGRAM NAME:    EDI CARCASS SUMMARY & DETAIL                  */
/*  PROGRAMMER:      JOHN ERICSON                                  */
/*  CREATE DATE:     ??/??/??                                      */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/* 10/18/01 CHANGED TO SEND THE FILES TO A FTP SITE INSTEAD OF     */
/*          THE EDIPC IF THE TRADING PARTNER IS GE 10000.          */
/*                                                                 */
/* 11/14/01 COMMENTED OUT DOWNLOAD TO EDIPC.                       */
/* 10/15/10 CHANGE THE .TXT TO .XLS                                */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&CO &DATEIN &FUN)

/* INPUT PARAMETERS*/
             DCL        VAR(&CO) TYPE(*CHAR) LEN(6)      /* TRADING PARTNER  */
             DCL        VAR(&DATEIN) TYPE(*CHAR) LEN(6) /* YMD-KILL +
                          DATE */
             DCL        VAR(&FUN) TYPE(*CHAR) LEN(1)     /* FUNCTION         */

/* FTP VARIABLES       */
             DCL        VAR(&FTPDIR) TYPE(*CHAR) LEN(64) /* FTP DIRECTORY     */

             DCL        VAR(&CS) TYPE(*CHAR) LEN(64)     /* SUMMARY PCFILE    */
             DCL        VAR(&CD) TYPE(*CHAR) LEN(64)     /* DETAIL PCFILE     */

             DCL        VAR(&PCCS) TYPE(*CHAR) LEN(12)   /* SUMMARY FILE NAME */
             DCL        VAR(&PCCD) TYPE(*CHAR) LEN(12)   /* DETAIL FILE NAME  */

             DCL        VAR(&FTPCS) TYPE(*CHAR) LEN(64)  /* SUMMARY FILE NAME */
             DCL        VAR(&FTPCD) TYPE(*CHAR) LEN(64)  /* DETAIL FILE NAME  */

             DCL        VAR(&DATE) TYPE(*CHAR) LEN(6)    /* JOB DATE          */
             DCL        VAR(&SEQ) TYPE(*CHAR) LEN(1)     /* SEQUENCE NUMBER   */
             DCL        VAR(&CONUM) TYPE(*DEC) LEN(6 0)  /* COMPANY NUMERIC   */
             DCL        VAR(&DATE8) TYPE(*CHAR) LEN(8)   /* YYMD FORMAT DATE  */


/*-----------------------------------------------------------------*/
/* INITIALIZE                                                      */
/*-----------------------------------------------------------------*/
/* CONVERT TRADING PARTNER TO NUMERIC */
             CHGVAR     VAR(&CONUM) VALUE(&CO)

/* RETRIEVE & CONVERT JOB DATE        */
/*           RTVJOBA    DATE(&DATE) */
             /* CONVERT DATE TO YMD FORMAT */
/*           CHGVAR     VAR(&DATE) VALUE(%SST(&DATE 5 2) *CAT +
                          %SST(&DATE 1 4)) */
             /* CONVERT DATE TO YYMD FORMAT */
             CVTDAT     DATE(&DATEIN) TOVAR(&DATE8) FROMFMT(*YMD) +
                          TOFMT(*YYMD) TOSEP(*NONE)

/* FORMAT PC FILE NAME ON THE AS400  */
             CHGVAR     VAR(&CS) VALUE('GUY.IS/S' *CAT &CO)
             CHGVAR     VAR(&CD) VALUE('GUY.IS/D' *CAT &CO)
             CALL       PGM(HPEEDIPCCR) PARM(&CO &DATEIN &SEQ)

/* ADD FTP LIBRARY TO THE THE LIBRARY LIST */
             ADDLIBLE   LIB(FTP_REMOTE) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)
/*-----------------------------------------------------------------*/
/* MAIN LINE                                                       */
/*-----------------------------------------------------------------*/

/* DOWNLOAD TO FTP SITE ON THE SERVER */
             IF         COND(&CONUM *GE 10000) THEN(DO)
             IF         COND(&SEQ *EQ ' ') THEN(CHGVAR VAR(&SEQ) +
                          VALUE(0))
             CHGVAR     VAR(&FTPDIR) VALUE('/PRODCR_ACCTS/' *CAT &CO)
             CHGVAR     VAR(&FTPCS) VALUE('SUM_' *CAT &DATE8 *CAT '_' +
                          *CAT &SEQ *TCAT '.XLS')
             CHGVAR     VAR(&FTPCD) VALUE('DTL_' *CAT &DATE8 *CAT '_' +
                          *CAT &SEQ *TCAT '.XLS')
             FTP_DOC2   FUNCTION(*SEND) IDIR(&FTPDIR) IFILE(&FTPCS) +
                          HDOC(&CS) URL(ftp.seaboardpork.com) +
                          USER('PDINETPRRP') PASS('prodcr36') +
                          ASCII(*NO)
             FTP_DOC2   FUNCTION(*SEND) IDIR(&FTPDIR) IFILE(&FTPCD) +
                          HDOC(&CD) URL(ftp.seaboardpork.com) +
                          USER('PDINETPRRP') PASS('prodcr36') +
                          ASCII(*NO)
             ENDDO
/*-----------------------------------------------------------------*/
/* END PROGRAM                                                     */
/*-----------------------------------------------------------------*/
             RMVLIBLE   LIB(FTP_REMOTE)
             MONMSG     MSGID(CPF0000)

             ENDPGM
