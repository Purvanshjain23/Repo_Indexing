/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4056CL                                      */
/*  PROGRAM NAME:    DAILY/WEEKLY MORTALITY VARIANCE REPORT        */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     09/08/03                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4056-SPECIFY OPTIONS.                       */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(44)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 15 8))


/*------------------------------------------------------------------*/
/* BUILD A QUERY OVER THE REPORTED DAILY ACTIVITY FILE              */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA)  VALUE(*BLANK)

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('RDDYDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND RDDYDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


             OVRDBF     FILE(HSP103) SHARE(*YES)
             OPNQRYF    FILE((HSP103)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-----------------------------------------------------------------*/
/* SET UP AND RUN OPEN QUERY OVER KILLED/DEAD FILE FOR DATE RANGE  */
/*-----------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('KDKDDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND KDKDDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


             OVRDBF     FILE(HSL068C) SHARE(*YES)
             OPNQRYF    FILE((HSL068C)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-----------------------------------------------------------------*/
/* CREATE WORKFILE IN QTEMP AND CALL BUILD PROGRAM                 */
/*-----------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3056) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP3056)


/*-----------------------------------------------------------------*/
/* DELETE FILE OVERRIDES                                           */
/*-----------------------------------------------------------------*/

             CLOF       OPNID(HSL068C)
             DLTOVR     FILE(HSL068C)

             CLOF       OPNID(HSP103)
             DLTOVR     FILE(HSP103)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(117) COPIES(&COPYS) HOLD(&HOLD) +
                          SHARE(*YES)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  CALL REPORT PROGRAM                                              */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6056)

             DLTOVR     FILE(*ALL)

             ENDPGM

