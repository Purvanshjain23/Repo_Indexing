      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     Weekly Revenue Close: Update Function Control File
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/15/04
      *
      *              As the last step of each Weekly Close, we will update the Function
      *              Control file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/30/06  LeAnne Ramsey
      *           Changed logic to use the Datamart Calendar file.
      *
      * 01/21/08  LeAnne Ramsey
      *           Added a parm of P=Production on the call to generic program TF815.
      *           A "P" will continue to return the HPS Year/Period/Week to us.
      *           (The JDE programs that call TF815 will now send in A=Accounting to
      *           return Accounting Year/Period.)
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp099    uf   e           k disk
      *    Function control
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
     D wkcldt          s              8  0
      *
      *
      * Parm fields
      *
     d xxerror         s              1
     d xxpfcd          s              1
     D xxyr            s              4  0
     D xxpe            s              2  0
     D xxwk            s              2  0
     D xxdt            s              8  0
     d xxapfl          s              1
      *
     D xxwbdt          s              8  0
     D xxwbsyndt       s              7  0
     D xxwbmdy         s              6  0
      *
     D xxwedt          s              8  0
     D xxwesyndt       s              7  0
     D xxwemdy         s              6  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Retrieve Function Control record.
      *
     C     'REVENUE   '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If hit
      *
      * Perform the "Final" updates when:
      *  1) there are no errors
      *  2) it is a Final run
      *
     C                   if        xxerror = no and xxpfcd = 'F'
     C                   exsr      $final
     C                   endif
      *
      * Always initialize control fields.
      *
     C                   move      *blank        fnsbfl
     C                   move      *blank        fnpfcd
      *
     C                   update    fnrec
     C                   endif                                                  If done
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Updating when it is a clean Final run.
      *---------------------------------------------------------------
      *
     C     $final        begsr
      *
      * Populate Close Date
     C                   z-add     wkcldt        fncldt
      *
      * Move 'current week' data to 'last week' data.
      *
     C                   z-add     fncyr         fnlyr
     C                   z-add     fncwk         fnlwk
     C                   z-add     0             fnlpe
      * Begin dates
     C                   z-add     fncbdt        fnlbdt
     C                   z-add     fncbmdy       fnlbmdy
     C                   z-add     fncbsyn       fnlbsyn
      * End dates
     C                   z-add     fncedt        fnledt
     C                   z-add     fncemdy       fnlemdy
     C                   z-add     fncesyn       fnlesyn
      *
      * Retrieve the data for the next week.
      *
     C                   exsr      $next
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve all the calendar data for the 'next' current week
      *---------------------------------------------------------------
      *
     C     $next         begsr
      *
      * Add 7 days to the Current Ending Date to get the next Ending Date
      *
     C                   movel     fncedt        wkisodate
     C                   adddur    7:*days       wkisodate
     C                   move      wkisodate     xxdt
      *
      * Retrieve the Production Year/Week for this "new" date.
      *
     C                   call      'TF815'
     C                   parm                    xxdt
     C                   parm      'P'           xxapfl
     C     fncyr         parm      0             xxyr
     C                   parm      0             xxpe
     C     fncwk         parm      0             xxwk
      *
      * Set Period to zero.
      *
     C                   z-add     0             fncpe
      *
      * Retrieve the Beginning/Ending dates for the new Week.
      *
     C                   call      'TF818'
     C                   parm                    xxyr
     C                   parm                    xxwk
     C     fncbdt        parm      0             xxwbdt
     C     fncbsyn       parm      0             xxwbsyndt
     C     fncbmdy       parm      0             xxwbmdy
     C     fncedt        parm      0             xxwedt
     C     fncesyn       parm      0             xxwesyndt
     C     fncemdy       parm      0             xxwemdy
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxerror
     C                   parm                    xxpfcd
      *
      * Save date for Closed Date
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wkcldt
      *
     C                   endsr
      /eject
