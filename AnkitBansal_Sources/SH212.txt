      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH212 - Datamart Sales: Build Sales/Broker Pricing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/14/09
      *
      * FUNCTION: This program builds Sales/Broker Pricing data for Dailys.
      *           It runs over DPMFLIB....there is no JRDATA/DFDATA processing.
      *
      *           This program is called from the Dailys Weekly Price Update.
      *           Each time this program is called it will delete/write Datamart
      *           records for the WeekEnding Date.
      *
      ****************************************************************
      * Date      Programmer
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpme7cpl1  if   e           k disk
      *  Sales/Broker pricing (this workfile only holds data for the week being processed)
      *
      *
     Fshp212    uf a e           k disk
      *  Datamart: Sales/Broker pricing
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Parms
      *
     D xx70wedt        s                   like(e7s7dt)
      *
      *
      * Workfields for date manipulation
      *
     D wknulldt        s                   like(e7wedt)   inz(D'0001-01-01')
     D wkwedt          s                   like(e7wedt)
     D wk80wedt        s                   like(e780wedt)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Delete existing Datamart records for the week
      *
     C     wk80wedt      setll     shp212
     C                   dou       *in91 = *on                                  Do delete
     C     wk80wedt      reade     shp212                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   delete    e7rec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do delete
      *
      * Build Datamart records for the week.
      * Process all records in the Synon file doing a one-to-one write to
      * the Datamart file.
      *
     C     *loval        setll     pme7cpl1
      *
     C                   dou       *in91 = *on                                  Do write
     C                   read      pme7cpl1                               91
     C                   if        *in91 = *off                                 If not EOF
     C                   move      'D'           e7corg
     C                   z-add     wk80wedt      e780wedt
     C                   move      wkwedt        e7wedt
     C                   write     e7rec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do write
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      *  Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xx70wedt
      *
      *
      * Format incoming Week-ending date from a Synon 7,0 format to:
      *   1) a date format
      *   2) an 8,0 format
      *
     C     *cymd         test(d)                 xx70wedt               92
     C                   if        *in92 = *off
     C     *cymd         move      xx70wedt      wkwedt
     C     *iso          move      wkwedt        wk80wedt
     C                   else
     C                   move      wknulldt      wkwedt
     C                   z-add     0             wk80wedt
     C                   endif
      *
     C                   endsr
      /EJECT
