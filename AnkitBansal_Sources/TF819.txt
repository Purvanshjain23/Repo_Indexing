      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF819
      * TITLE:       Retrieve Sunday On/Before a Date
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/14/06
      *
      * FUNCTION:  Given an incoming date, this program retrieves the Sunday prior to that
      *            date. If the incoming date is Sunday, then no prior date is retrieved.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcalcald   if   e           k disk
      *  Calendar file
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields
      *
     D wkcocd          s              3  0 inz(350)
      *
      * Parms
      *
     D xxdt            s              8  0
     D xxsundt         s              8  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * First, see if the incoming date exists in the Calendar file.
      * If it does, continue.
      *
     C     key01         chain     calcald                            92
     C                   if        *in92 = *off                                 If good incoming
      *
     C                   if        catdtdow = 1                                 If Sunday
     C                   z-add     xxdt          xxsundt
     C                   else
      *
     C     key01         setll     calcald
      *
     C                   dou       *in90 = *on or xxsundt <> 0                  Do loop
     C     wkcocd        readpe    calcald                                90
     C                   if        *in90 = *off and catdtdow = 1                If not eof
     C                   z-add     catdt08       xxsundt
     C                   endif                                                  If not eof
     C                   enddo                                                  Do loop
      *
     C                   endif                                                  If Sunday
     C                   endif                                                  If good incoming
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxdt
     C                   parm                    xxsundt
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkcocd
     C                   kfld                    xxdt
      *
      *
     C                   endsr
      /EJECT
