/***************************************************************** */
/*                                                                 */
/*  ENVIRONMENT:     PORK DIVISION                                 */
/*  SYSTEM:          INTERFACE MP2 TO JDE                          */
/*  PROGRAM NUMBER:  MP651CL                                       */
/*  PROGRAM NAME:    MP2: LIST OF OVERRIDDEN ITEMS                 */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     06/10/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED:                         */
/*                    1) ON DEMAND                                 */
/*                    2) FROM THE INVOICE POST FUNCTION            */
/*                                                                 */
/*                   THIS LISTING LOGIC EXTRACTS DATA BASED ON     */
/*                   'JDE BATCH NUMBER'. THUS, IT ONLY WORKS FOR   */
/*                   'POSTED' INVOICES.                            */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 10/10/2002  BARB GUTIERREZ                                      */
/*                   CORRECTED DISTRIBUTION LIST NAME ON           */
/*                   ESNDFILE CMD.                                 */
/*                                                                 */
/* 12/05/2002  BARB GUTIERREZ                                      */
/*                   DO NOT SEND REPORT TO DISTRIBUTION LIST IF    */
/*                   THEIR WAS NO OVERRIDDEN ITEMS.                */
/*                                                                 */
/* 08/09/2005  BARB GUTIERREZ                                      */
/*                   CHANGED EMAIL ADDRESS FROM SEABOARDPORK.COM TO */
/*                   SEABOARDFOODS.COM PER COMPANY NAME CHANGE.     */
/*                                                                 */
/* 10/20/2006  LEANNE RAMSEY                                       */
/*                   RECOMPILE ONLY---JUST CHANGED SOME TEXT.      */
/*                                                                 */
/*                                                                 */
/* 06/14/07    LEANNE RAMSEY                                       */
/*                   WE WILL NOW EMAIL THE REPORT TO AN ESEND      */
/*                   DISTRIBUTION LIST.                            */
/*                                                                 */
/* 03/13/08  LEANNE RAMSEY                                         */
/*           REPLACE QPRINT WITH PRINT198.                         */
/*                                                                 */
/* 03/27/08  LEANNE RAMSEY                                         */
/*           THE ATTACMENT DID NOT EMAIL BECAUSE I MISSED CHANGING */
/*           QPRINT TO PRINT198 IN THE ESEND COMMAND; I HAVE NOW   */
/*           CHANGED IT.                                           */
/*                                                                 */
/* 05/15/20  ROSE CENTONZE                                         */
/*           ADDED EMAIL LIST MP2 TEST FOR TESTING JDE CHANGES     */
/*                                                                 */
/* 05/17/21  DANNY NGUYEN - S17060                                 */
/*           ADDED &SAVE=*YES OPTION BEFORE PRINTING MP651 REPORT. */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&BATNO)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(65)

             DCL        VAR(&ALREADY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FLAG)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DATA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BATNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&FMDY) TYPE(*DEC) LEN(6)
             DCL        VAR(&TMDY) TYPE(*DEC) LEN(6)
             DCL        VAR(&JDVNNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&JDVNNM) TYPE(*CHAR) LEN(40)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(10)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&GRPPRF) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&FLAG) VALUE('B')


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER INVOICE HEADER FILE FOR THE BATCH */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('IHPOFL +
                          *EQ "Y"')
             CHGVAR     VAR(%SST(&QDATA 16 16)) VALUE('*AND IHBATNO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&BATNO)

             OVRDBF     FILE(MPP104)  SHARE(*YES)
             OPNQRYF    FILE((MPP104)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER INVOICE DETAIL FILE               */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  ALWAYS SELECT ONLY RECORDS WHERE THE USER 'OVERRODE' THE AMOUNT   */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('IDOVFL +
                          *EQ "Y"')

             OVRDBF     FILE(MPP105)  SHARE(*YES)
             OPNQRYF    FILE((MPP105)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------ -----------------------------*/
/*  CREATE WORKFILE                                                 */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(MPP351) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*------------------------------------------------------------------*/
/*  CALL PROGRAM TO BUILD WORKFILE                                  */
/*------------------------------------------------------------------*/

             CALL       PGM(MP351)

/*------------------------------------------------------------------*/
/*  DELETE FILE OVERRIDES                                           */
/*------------------------------------------------------------------*/

             CLOF       OPNID(MPP104)
             DLTOVR     FILE(MPP104)

             CLOF       OPNID(MPP105)
             DLTOVR     FILE(MPP105)

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/
    /* S17060 - ADDED OPTION TO SAVE REPORT SPOOLED FILE */

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD) SAVE(*YES)

/*------------------------------------------------------------------*/
/* CALL PRINT PROGRAM                                               */
/*------------------------------------------------------------------*/

             CALL       PGM(MP651) PARM(&FLAG &DATA &BATNO &FMDY +
                          &TMDY &JDVNNO &JDVNNM)


/* IF YOU HAD OVERRIDE DATA, THEN E-MAIL THE OVERRIDE LISTING       */

             IF         COND(&DATA *EQ 'Y')  THEN(DO)

             RTVJOBA    JOB(&JOBNAME) USER(&USER) NBR(&JOBNBR)
             RTVUSRPRF  USRPRF(&USER) GRPPRF(&GRPPRF)

             CHGVAR     VAR(&ALREADY) VALUE('N')
             ADDLIBLE   LIB(ESEND) POSITION(*LAST)
             MONMSG     MSGID(CPF2103) EXEC(DO) /* already in libl */
             CHGVAR     VAR(&ALREADY) VALUE('Y')

             IF         COND(&GRPPRF *NE 'SBDPGMR') THEN(DO)
             ESNDFILE   RECIPIENT(MP2) SUBJECT('MP2/JDE: Override +
                          Listing') MSG('An Override Listing has +
                          been generated during a Post of MP2 +
                          Invoices to JDE and emailed to all +
                          individuals on Distribution List: MP2.') +
                          TYPE(*SPLPDF) SPLF(PRINT198) +
                          JOB(&JOBNBR/&USER/&JOBNAME) SPLNBR(2)
             ENDDO

             IF         COND(&GRPPRF *EQ 'SBDPGMR') THEN(DO)
             ESNDFILE   RECIPIENT('mp2 test') SUBJECT('MP2/JDE: +
                          Override Listing-TEST') MSG('An Override +
                          Listing has been generated during a Post +
                          of MP2 Invoices to JDE and emailed to all +
                          individuals on Distribution List: MP2.') +
                          TYPE(*SPLPDF) SPLF(PRINT198) +
                          JOB(&JOBNBR/&USER/&JOBNAME) SPLNBR(2)
             ENDDO
             ENDDO
             IF         COND(&ALREADY *EQ 'N') THEN(DO)
             RMVLIBLE   LIB(ESEND)
             ENDDO
             ENDDO

             DLTOVR     FILE(*ALL)

             ENDPGM

