     A*%%TS  SD  20170607  165012  ISLBUSE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5063 - Select Feed Mill Type Code
     A*  RPG PROGRAM:   HP5063 -  Select Feed Mill Type Code
     A*  CREATED BY:    Lara Buser
     A*  DATE:          06/06/2017
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP5063R1
     A*%%TS  SD  20170606          ISLBUSE     REL-V2R2M0  5738-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 10 29)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5063R2
     A*%%TS  SD  20170606  135528  ISLBUSE     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP5063R3
     A*%%TS  SD  20170606  135528  ISLBUSE     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5063S1                  SFL
     A*%%TS  SD  20170607  165012  ISLBUSE     REL-V7R1M0  5770-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFMLTCD   R        O  4  1REFFLD(@@MLTCD)
     A            DFMLTDS   R        O  4  5REFFLD(@@MLTDS)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5063C1                  SFLCTL(HP5063S1)
     A*%%TS  SD  20170606  152822  ISLBUSE     REL-V2R2M0  5738-PW1
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0002)
     A                                      WINDOW(HP5063R1)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  2'SELECT FEED MILL TYPE '
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A          R HP5063K1
     A*%%TS  SD  20170606  152822  ISLBUSE     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP5063R1)
     A                                  9  1'F12=Previous'
