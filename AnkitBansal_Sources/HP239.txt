      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Datamart
      * PROGRAM:     HP239 - Build Cost Category File
      * PROGRAMMER:  Barb Gutierrez
      * CREATED:     08/27/03
      *
      * FUNCTION: This program consolidates HSP050, HSP051, HSP052, HSP005
      *           and HSP006 into one file for ease of use in cognos.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL052A   IF   E           K DISK
      *  Expense codes   (Key: execcd, exescd, exexcd)
     FHSL051A   IF   E           K DISK
      *  Expense Sub Class Codes  (Key:  esescd, eseccd)
     FHSP050    IF   E           K DISK
      *  Expense Class code file (Key: ececcd)
     FHSP006    IF   E           K DISK
      *  Report Code  (Key:  rprpcd)
     FHSP005    IF   E           K DISK
      *  Report Category Code  (Key:  cacacd)
      *
     Fhppa039   o    e             disk
      *    Datamart ALL: Cost Category File
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkcacd          s                   like(rpcacd)
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * Process each record in the expense code file (HSP052)
      *
     C     *loval        setll     exrec
      *
     C                   dou       *in90 = *on                                  MAIN DO
     C                   read      exrec                                  90
     C                   if        *in90 = *off                                 IF NOT EOF
      *
      * Retrieve the expense sub class stuff.  (HSP051)
      *
     C     exescd        chain     esrec                              91
     C                   if        *in91 = *off
     C                   movel     esescd        ccexsccd                      sub class code
     C                   movel     esesds        ccexscds                      sub class desc
     C                   z-add     esesseq       ccexscsq                      sub class seq #
     C                   else
     C                   move      *blanks       ccexsccd                      sub class code
     C                   move      *blanks       ccexscds                      sub class desc
     C                   z-add     0             ccexscsq                      sub class seq #
     C                   endif
      *
      * Retrieve the expense class (HSP050)
      *
     C     execcd        chain     ecrec                              92
     C                   if        *in92 = *off
     C                   movel     ececcd        ccexclcd                      exp class code
     C                   movel     ececds        ccexclds                      exp class desc
     C                   z-add     ececseq       ccexclsq                      sub class seq #
     C                   else
     C                   move      *blanks       ccexclcd                      exp class code
     C                   move      *blanks       ccexclds                      exp class desc
     C                   z-add     0             ccexclsq                      sub class seq #
     C                   endif
      *
      * Retrieve the report code (HSP006)
      *
     C     exrpcd        chain     rprec                              93
     C                   if        *in93 = *off
     C                   movel     rprpcd        ccrpcd                        report code
     C                   movel     rprsds        ccrpcsds                      short desc
     C                   movel     rprlds        ccrpclds                      long desc
     C                   z-add     rpseq         ccrpcsq                       seq
      *
      * Move the report category code into a work field.  Their are some report
      * codes that need moved into another report category code other than whats in the record.
      *
     C                   movel     rpcacd        wkcacd                        rpt cat cd
      *
     C                   if        rprpcd = 'TAXES'
     C                   movel(p)  'OTCST'       wkcacd                        rpt cat cd
     C                   z-add     40.00         ccrpcsq                       seq
     C                   endif
     C***
     C                   if        rprpcd = 'INS  '
     C                   movel(p)  'OTCST'       wkcacd                        rpt cat cd
     C                   z-add     50.00         ccrpcsq                       seq
     C                   endif
     C***
     C                   if        rprpcd = 'DEP  '
     C                   movel(p)  'OTCST'       wkcacd                        rpt cat cd
     C                   z-add     10.00         ccrpcsq                       seq
     C                   endif
     C***
     C                   if        rprpcd = 'LEASE'
     C                   movel(p)  'OTCST'       wkcacd                        rpt cat cd
     C                   z-add     20.00         ccrpcsq                       seq
     C                   endif
     C***
     C                   if        rprpcd = 'MISC '
     C                   movel(p)  'OTCST'       wkcacd                        rpt cat cd
     C                   z-add     30.00         ccrpcsq                       seq
     C                   endif
      *
      * Move the report category code into a work field.  Their are some report
      * codes that need moved into another report category code other than whats in the record.
      *
     C                   if        rprpcd = 'FCONS'
     C                   movel(p)  'OPCST'       wkcacd                        rpt cat cd
     C                   endif
      *
     C                   if        rprpcd = 'CULLS'
     C                   movel(p)  'OPCST'       wkcacd                        rpt cat cd
     C                   endif
      *
      * Retrieve the report category code  (HSP005)
      *
     C     wkcacd        chain     carec                              94
     C                   if        *in94 = *off
     C                   movel     cacacd        ccrpcacd                      rpt cat cd
     C                   movel     cacads        ccrpcads                      rpt cat cd desc
     C                   z-add     caseq         ccrpccsq                      rpt cat cd seq
     C*********   --------------------------
     C                   else
     C                   move      *blanks       ccrpcacd                      rpt cat cd
     C                   move      *blanks       ccrpcads                      rpt cat cd desc
     C*********          z-add     0             ccrpccsq                      rpt cat cd seq
     C                   endif
      *
     C                   else
      *
     C                   move      *blanks       ccrpcd                        report code
     C                   move      *blanks       ccrpcsds                      short desc
     C                   move      *blanks       ccrpclds                      long desc
     C                   z-add     0             ccrpcsq                       seq
     C                   endif
      *
     C                   movel     exexcd        ccexcd                        expense code
     C                   movel     exexds        ccexcdds                      exp code desc
     C                   z-add     exexseq       ccexcdsq                      exp code seq
     C                   write     ccrec
     C                   endif                                                  IF NOT EOF
     C                   enddo                                                  MAIN DO
      *
     C                   seton                                        LR
      /EJECT
      *----------------------------------------------------------------
      *  INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C                   ENDSR
