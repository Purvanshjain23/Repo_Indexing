     A*%%TS  SD  20090818  134556  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4066
     A*  TITLE:         SPECIFY-MOVE RECEIVED CULLSALE DATA TO EDITING
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          08/18/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP4066R1
     A*%%TS  SD  20090818  134556  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 15'SPECIFY-MOVE RECEIVED CULL SALE DA-
     A                                      TA TO EDITING'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  5  2'This function will move the upload-
     A                                      ed data from the Receiving File to'
     A                                  6  2'the Editing file. During this step-
     A                                      , Movement Numbers will be assigned'
     A                                  7  2'and Building/Room/Group fields wil-
     A                                      l be populated using our upload rul-
     A                                      es.'
     A                                  8  2'After this step completes, continu-
     A                                      e with the Menu Option to Edit (Mai-
     A                                      ntain)'
     A                                  9  2'the Uploaded Cull Sales. (Just FYI-
     A                                      , the Receiving File will be empty -
     A                                      at'
     A                                 10  2'the end of this move.)'
     A                                 12  2'Business Office.....'
     A                                      COLOR(BLU)
     A            SDBOBO         5A  O 12 23
     A                                      DSPATR(HI)
     A            SDBODS        30A    12 30
     A                                      COLOR(BLU)
     A                                 14  2'Take F8-Submit to submit the move -
     A                                      from Receiving to Editing.'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP4066K1
     A*%%TS  SD  20041011  083140  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP4066ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP4066EC                  SFLCTL(HP4066ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
