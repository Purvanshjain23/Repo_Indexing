/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF462CL                                       */
/*  PROGRAM NAME:    AR ON ACCOUNT BALANCE LISTING                 */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     11/05/09                                      */
/*                                                                 */
/*  FUNCTION:        THIS REPORT IS GENERATED:                     */
/*                    1) ON-DEMAND                                 */
/*                                                                 */
/* We have talked about running this report as part of the         */
/* Revenue Close function when it is an EOP week. So, just in      */
/* case, I have left the Preliminary/Final logic intact in this    */
/* CL.                                                             */
/*******************************************************************/
/* Modification history:                                           */
/* 03/21/2017 Brad Baden E9718 - Restrict processing to companies  */
/*                               360 and 960 only with QRYSLT.     */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(150)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&EMAIL)     TYPE(*CHAR) LEN(38)
             DCL        VAR(&LDPFCD)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&TSTPRDFL)  TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDSYNDT)   TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDCONO)    TYPE(*DEC)  LEN(3)
             DCL        VAR(&LDCUNO)    TYPE(*DEC)  LEN(7)

             CHGVAR     VAR(&LDPFCD)    VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDSYNDT)   VALUE(%SST(*LDA 30 7))
             CHGVAR     VAR(&LDCONO)    VALUE(%SST(*LDA 71 3))
             CHGVAR     VAR(&LDCUNO)    VALUE(%SST(*LDA 103 7))

/*----------------------------------------------------------------*/
/* FOR SENDING EMAILS:                                            */
/* RETRIEVE DATA AREA THAT LETS US KNOW WHETHER WE ARE IN TEST    */
/* OR PRODUCTION (ALWAYS DEFAULT TO THE "TEST" DISTRIBUTION LIST.)*/
/*----------------------------------------------------------------*/

             CHGVAR     VAR(&EMAIL) VALUE('TF Test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)

/*----------------------------------------------------------------*/
/* RETRIEVE/SET PRINT DEFAULTS                                    */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&LDPFCD = 'F') THEN(CHGVAR VAR(&HOLD) +
                          VALUE(*YES))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&LDPFCD *NE 'D') THEN(DO)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             ENDDO
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))


             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
/*--------------------------------------------------------------------------*/
/*  EXTRACT DATA FROM THE AR HEADER FILE                                    */
/*--------------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* Transaction Date must be On/Before the user's CutOff Date            */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BDB4DT *LE')
             CHGVAR     VAR(%SST(&QDATA 12 7)) VALUE(&LDSYNDT)

/* Adjustment Type is On Account                                        */

             CHGVAR     VAR(%SST(&QDATA 20 21)) VALUE('*AND BDUHCD +
                          *EQ "OA "')

/* Only select records where 1) Remaining Balance Due is zero or       */
/*                           2) Paid-In-Full Date is after CutOff Date */

             CHGVAR     VAR(%SST(&QDATA 42 34)) VALUE('*AND (BDBGVA +
                          *NE 0 *OR BDH4DT *GT ')
             CHGVAR     VAR(%SST(&QDATA 76 7)) VALUE(&LDSYNDT)
             CHGVAR     VAR(%SST(&QDATA 83 1)) VALUE(')')

/* Customer                                                           */
             IF         COND(&LDCUNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 85 15)) VALUE('*AND BDANC7 *EQ')
             CHGVAR     VAR(%SST(&QDATA 101 7)) VALUE(&LDCUNO)
             ENDDO

/* Company                                                         */
             IF         COND(&LDCONO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 109 15)) VALUE('*AND BDAIC3 *EQ')
             CHGVAR     VAR(%SST(&QDATA 125 3)) VALUE(&LDCONO)
             ENDDO

             IF         COND(&LDCONO *EQ 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 109 40)) VALUE('*AND (BDAIC3 *EQ +
                          360 *OR BDAIC3 *EQ 960)')
             ENDDO

             OVRDBF     FILE(ARBDCPL0) SHARE(*YES)
             OPNQRYF    FILE((ARBDCPL0)) QRYSLT(&QDATA) KEYFLD(*FILE)  +
                          SEQONLY(*YES)

/*--------------------------------------------------------------------------*/
/*  Run the Report                                                          */
/*--------------------------------------------------------------------------*/

             CALL       PGM(TF662)

             CLOF       OPNID(ARBDCPL0)
             DLTOVR     FILE(ARBDCPL0)

/*--------------------------------------------------------------------------*/
/* NOTE: This report is currently only ON-DEMAND...but we have talked       */
/*       seriously about running it in the Revenue Close function. So,      */
/*       I have left all that code intact.                                  */
/* If this is a "Final" run out of the Weekly Revenue Close                 */
/* (and you are in the "production" environment),                           */
/*    e-mail the report to a distribution list                              */
/*--------------------------------------------------------------------------*/

             IF         COND(&LDPFCD *EQ 'F') THEN(DO)          /* If Final */

             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                        VAR(&EMAIL) VALUE('TF Revenue Close-Admin'))

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('AR Adjustment +
                          Listing') MSG('Distribution List: TF +
                          Revenue Close-Admin') ATTLIST((* *PDF *N +
                          PRINT198 *))
             MONMSG     MSGID(CPF0000)
             ENDDO                                              /* If Final */

             DLTOVR     FILE(*ALL)

             ENDPGM

