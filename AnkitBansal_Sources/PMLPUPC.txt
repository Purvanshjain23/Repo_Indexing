/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT CUSTOMER ACCRUAL STMTS OVER HIST DTL ACCRUAL  */
/*                                                                  */
/*  COMPANY   - SEABOARD FOODS                                      */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - ROSE CENTONZE                                       */
/*  DATE      - 08/10/07                                            */
/*                                                                  */
/*  PROGRAM   - PRT CUSTOMER ACCRUAL STMTS  UP - HIST DTL ACCRUAL   */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                             */
/*          &COPIES     - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &RPT1       - REPORT SEL 1   (PASSED)                   */
/*          &ACCRTYP    - ACCRUAL TYPE                              */
/********************************************************************/
/* MODIFICATIONS                                                    */
/* SHARON ZURN     03/25/08                                         */
/*                 FIXED PROBLEM WHEN SELECTING ONE CUST. ALL THE   */
/*                 CUSTOMERS WERE PRINTING. ADDED SHIPTOL AND       */
/*                 &F.                                              */
/********************************************************************/
 PMLPUPC:    PGM        PARM(&RETURN &OUTQ &HOLD &SAVE &COMPL +
               &FDATEL &TDATEL &COPIESL &SHIPTOL &ACCRTYP &ACTPER +
                          &ACTYR)

        DCL    &RETURN    *CHAR   7               /*  RETURN CODE   */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &FDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)           /*  FROM DATE     */
        DCL    &TDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &TDATE     *DEC    (7 0)           /*  THROUGH DATE  */
        DCL    &SHIPTOL   *DEC                    /* SHIP TO PASSING*/
        DCL    &SHIPTO    *DEC    (7 0)           /* SHIP TO        */
        DCL    &RPT1       *CHAR   (1)            /* REPORT 1       */
        DCL    &OUTQ      *CHAR   10              /*  OUTPUT QUEUE  */
        DCL    &HOLD       *CHAR   (4)            /* Hold Status    */
        DCL    &SAVE       *CHAR   (4)            /* Save Status    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &NIGHTQ    *CHAR   10              /*  NIGHT JOB QUE.*/
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &ACCRTYP   *CHAR   2               /* ACCRUAL TYPE   */
        DCL    &SHIPTOA   *CHAR   7               /* SHIP TO ALPHA  */
        DCL    &ACTPER    *DEC   (2 0)            /* ACCTNG MO   */
        DCL    &ACTYR     *DEC   (4 0)            /* ACCTNG YR    */

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    VAR(&PGMO) TYPE(*CHAR) LEN(10) /* REPORT PRINT FILE  */
        DCL     &A        *CHAR   20              /* QRY ACT SHIP FROM */
        DCL     &B        *CHAR   25              /* QRY ACT SHP TO */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &E        *CHAR   25              /* QRY ACCRL TYPE */
        DCL     &F        *CHAR   25              /* QRY CUSTOMER   */
        DCL     &D        *CHAR   120             /* QRY ALL        */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)
/********************************************************************/
             CHGVAR     VAR(&COMP) VALUE(&COMPL) /* Change parm +
                          company number to 3.0 numeric */
             CHGVAR     VAR(&COMPA) VALUE(&COMP) /* Change numeric +
                          company number to alpha */
             CHGVAR     VAR(&FDATE) VALUE(&FDATEL) /* Change parm +
                          from date to 7.0 numeric date */
             CHGVAR     VAR(&FDATEA) VALUE(&FDATE) /* Change numeric +
                          date to alpha */
             CHGVAR     VAR(&TDATE) VALUE(&TDATEL) /* Change parm to +
                          date to 7.0 numeric date */
             CHGVAR     VAR(&TDATEA) VALUE(&TDATE) /* Change numeric +
                          to date to alpha */
             CHGVAR     VAR(&COPIES) VALUE(&COPIESL) /* Change parm +
                          number of copies to 3.0 numeric */

             CHGVAR     VAR(&SHIPTO) VALUE(&SHIPTOL) /* Change +
                          parm SHIP TO to 7.0 numeric date */
             CHGVAR     VAR(&SHIPTOA) VALUE(&SHIPTO) /* Change +
                          numeric ship to to alpha */
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR REPORT                    */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('ICPTDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND ICPTDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND ICAIC3 *EQ ' *CAT +
                         &COMPA)

             CHGVAR     VAR(&E) VALUE('*AND ICOKCD *EQ "' *CAT +
                         &ACCRTYP *CAT '"')

             IF         COND(&SHIPTO *GT 0) THEN(DO)
             CHGVAR     VAR(&F) VALUE('*AND ICBKC7 *EQ ' *CAT +
                         &SHIPTOA)
             ENDDO

             CHGVAR     VAR(&D) VALUE(&A *BCAT &B *BCAT &C *BCAT &E +
                          *BCAT &F)


/*********************************************************************/
/*   SELECT QUERY BY REPORT BEING RUN                                */
/*********************************************************************/
 ENDQRYSTMT: OVRDBF     FILE(PDICCPL7) SHARE(*YES)
             OPNQRYF    FILE((PDICCPL7)) QRYSLT(&D) KEYFLD(*FILE)


/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             OVRPRTF    FILE(PMLOPFR$) OUTQ(&OUTQ) COPIES(&COPIES) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(PMLOPFR) PARM('' &COMP &ACCRTYP &ACTPER &ACTYR)
             GOTO       CMDLBL(END)

/*********************************************************************/
/*----------------------RECEIVE MESSAGES----------------------------*/
/*********************************************************************/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))


 END:        ENDPGM
