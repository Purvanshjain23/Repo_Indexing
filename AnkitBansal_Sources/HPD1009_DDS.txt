     A*%%TS  SD  20050127  074152  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1009
     A*  TITLE:         REVISE SIRE LINE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/25/01
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1009R1
     A*%%TS  SD  20050127  074152  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 30'REVISE SIRE LINE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Sire line code:'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A            D1SICD    R        O  4 18REFFLD(@@SICD)
     A                                      COLOR(PNK)
     A                                  6  2'Sire line company ..'
     A            D1SICOMP  R        B  6 23REFFLD(HSRREF/@@SICOMP *LIBL/HSPREF)
     A N60N62N33                            DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A                                  7  2'Prime line .........'
     A            D1PRIME   R        B  7 23REFFLD(HSRREF/@@PRIME *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N60N62N34                            DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A                                  9  2'Cognos report grouping .......'
     A            D1GG0     R        B  9 35REFFLD(@@GG5A)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N37                               DSPATR(HI)
     A                                 11  2'Average daily gain genetic adjustm-
     A                                      ent pounds ..'
     A            D1AADGLB  R        B 11 52REFFLD(HSRREF/@@AADGLB *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62                                  DSPATR(UL)
     A N62                                  DSPATR(HI)
     A                                 12  2'Mortality rate genetic adjustment -
     A                                      .............'
     A            D1MGART   R        B 12 52REFFLD(HSRREF/@@MGART *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62                                  DSPATR(UL)
     A N62                                  DSPATR(HI)
     A                                 14  2'Alternate report grouping 1 ..'
     A            D1GG1     R        B 14 35REFFLD(@@GG5A)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62                                  DSPATR(HI)
     A                                 15  2'Alternate report grouping 2 ..'
     A            D1GG2     R        B 15 35REFFLD(@@GG5A)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62                                  DSPATR(HI)
     A                                 17  2'Description ..................'
     A            D1SIDS    R        B 17 35REFFLD(HSRREF/@@SIDS *LIBL/HSPREF)
     A                                      CHECK(LC)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N35                               DSPATR(HI)
     A                                 19  2'Active/inactive status .......'
     A N62                             19 33'?'
     A            D1AIST    R        B 19 35REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N36                               DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1009K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A    N62                          23 11'F4=Prompt'
     A N62N61                          23 23'F5=Refresh'
     A N62N61                          23 35'F11=Delete'
     A                                 23 45'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1009ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1009EC                  SFLCTL(HP1009ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
