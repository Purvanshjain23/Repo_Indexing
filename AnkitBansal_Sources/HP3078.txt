      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3078
      * TITLE:       Build Workfile: Room Availability Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/03/02
      *
      * FUNCTION:  Batch program to list room availability.
      *
      *            This listing is submitted from HP4078-Specify Options for
      *            Room Availability Report which is called from a menu.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/16/06  LeAnne Fedor
      *           Recompile only.
      *           We have removed "Bin Set" from HSP020-Building/Rooms and put it in our new
      *           HSP113-Rooms/Bin Sets file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site                (records selected/keyed in open query)
      *
      *
     Fhsp020    if   e           k disk
      *   Building/rooms           (records selected in open query)
      *
      *
     Fhsl034o   if   e           k disk
      *    Hog group (logical omits VD,CL,DS)
      *
      *
     Fhsj075i   if   e           k disk    rename(mdrec:mdreci)
      *  Transfer movement detail + transfer movement header
      *
      *
     Fhsj075g   if   e           k disk    rename(mdrec:mdrecg)
      *  Transfer movement detail + transfer movement header
      *
      *
     Fhsp3078   o    e           k disk
      *  Workfile: Room availability
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields
      *
     D wktotdays       s              7  0
     D wkdays          s              7  0
     D wkavgage        s                   like(wravgage)
     D count           s              3  0
      *
      *
      * Work fields for date manipulation
      *
     D wkisoudate      s               D   datfmt(*iso)
     D wkisoopdt       s               D   datfmt(*iso)
      *
      *
      * Define parms for the generic movement program
      *
     D zzcymd          s              8  0
      *
     D @@nu50          s              5  0
     D gpihd           s                   like(@@nu50)
     D pinhd           s                   like(@@nu50)
     D tinhd           s                   like(@@nu50)
     D tisphd          s                   like(@@nu50)
     D tidphd          s                   like(@@nu50)
     D pouhd           s                   like(@@nu50)
     D touhd           s                   like(@@nu50)
     D tosphd          s                   like(@@nu50)
     D todphd          s                   like(@@nu50)
     D rinhd           s                   like(@@nu50)
     D rouhd           s                   like(@@nu50)
     D qinhd           s                   like(@@nu50)
     D qouhd           s                   like(@@nu50)
     D morhd           s                   like(@@nu50)
     D inahd           s                   like(@@nu50)
      *
     D @@nu92          s              9  2
     D pinlb           s                   like(@@nu92)
     D tinlb           s                   like(@@nu92)
     D tisplb          s                   like(@@nu92)
     D tidplb          s                   like(@@nu92)
     D poulb           s                   like(@@nu92)
     D toulb           s                   like(@@nu92)
     D tosplb          s                   like(@@nu92)
     D todplb          s                   like(@@nu92)
     D rinlb           s                   like(@@nu92)
     D roulb           s                   like(@@nu92)
     D qinlb           s                   like(@@nu92)
     D qoulb           s                   like(@@nu92)
     D morlb           s                   like(@@nu92)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldppcd                 1      5
     D  ldcell                 6      7  0
     D  ldfscd                 8     12  0
     D  ldmdy                 13     18  0
     D  ldcymd                19     26  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read each farm selected by the open query.
      *
     C     *loval        setll     hsp018
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * We are going to key our workfile by populating the 'average farm age'
      * field.  So, go calculate an 'average age in days' for the farm.
      *
     C                   exsr      $avgage
      *
      * Process each active bldg/room record for the farm.
      *
     C     fsfscd        setll     hsp020
     C                   dou       *in91 = *on                                  Do rooms
     C     fsfscd        reade     hsp020                                 91
     C                   if        *in91 = *off
     C                   exsr      $room
     C                   endif
     C                   enddo                                                  Do rooms
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------
      * Process a 'Room'
      *--------------------------------------------------------------------------
      *
     C     $room         begsr
      *
     C                   clear                   wrrec
      *
      * Set up fields in workfile record.
      *
     C                   z-add     fscell        wrcell
     C                   z-add     fsfscd        wrfscd
     C                   move      fsfsnm        wrfsnm
     C                   z-add     wkavgage      wravgage
     C                   z-add     ldmdy         wrasofmdy
     C                   z-add     ldcymd        wrasofcymd
     C                   move      brblcd        wrblcd
     C                   move      brrmcd        wrrmcd
      *
      * Determine the 'Room Capacity' as:
      *   total square feet in the room / target square feet per pig on the farm
      *
     C                   if        fshdsqft <> 0
     C     brrmsqft      div(h)    fshdsqft      wrcphd
     C                   endif
      *
      *
      * Process groups in the room
      *
     c                   exsr      $groups
      *
      * Calculate the 'Total Head' you think will be in this room on the
      * As Of date.
      *
     C     wrinhd        add       wrscinhd      wrtothd
     C                   sub       wrscouthd     wrtothd
      *
      *
      * Calculate the 'available space in head' for the room.
      *
     C     wrcphd        sub       wrtothd       wravailhd
      *
      *
      * Write the workfile record for this farm/bldg/room.
      *
     C                   write     wrrec
      *
     c                   endsr
      /EJECT
      *-------------------------------------------------------------------------------
      * Process all created and open groups in the room.
      *-------------------------------------------------------------------------------
      *
     C     $groups       begsr
      *
     C     key01         setll     hsl034o
      *
     C                   dou       *in93 = *on                                  Do groups
     C     key01         reade     hsl034o                                93
     C                   if        *in93 = *off                                 If group
      *
      * Current inventory for the group
      *
     C                   movel     wkisoudate    zzcymd
     C                   call      'HPMOVE'      plist01
     C                   add       gpihd         wrinhd
      *
      * Scheduled Movements into this group
      *
     C     hghgsn        setll     hsj075i
     C                   dou       *in94 = *on                                  Do SC in
     C     hghgsn        reade     hsj075i                                94
     C                   if        *in94 = *off and mhmscd = 'SC'               If not eof
     C                   add       mdschd        wrscinhd
     C                   endif                                                  If not eof
     C                   enddo                                                  Do SC in
      *
      *
      * Scheduled Movements out of this group prior on/before the As Of Date
      *
     C     hghgsn        setll     hsj075g
     C                   dou       *in94 = *on                                  Do SC out
     C     hghgsn        reade     hsj075g                                94
     C                   if        *in94 = *off and mhmscd = 'SC'               If in range
     C                             and mhscdt <= ldcymd
     C                   add       mdschd        wrscouthd
     C                   endif                                                  If in range
     C                   enddo                                                  Do SC out
      *
     C                   endif                                                  If group
     C                   enddo                                                  Do groups
      *
     c                   endsr
      /EJECT
      *--------------------------------------------------------------------------
      * Calculate an 'average age in days' for the farm
      *--------------------------------------------------------------------------
      *
      * We will calculate the 'average age' for a farm by:
      *  1) Finding the number of days between the system date and the open date for
      *     each open group on the farm.
      *  2) Summing these days and incrementing a counter for each open group.
      *  3) After all open groups are processed, divide the total days by the counter.
      *
     C     $avgage       begsr
      *
     C                   z-add     0             count
     C                   z-add     0             wktotdays
      *
     C     fsfscd        setll     hsl034o
      *
     C                   dou       *in91 = *on                                  Do age
     C     fsfscd        reade     hsl034o                                91
     C                   if        *in91 = *off and hgopdt <> 0                 If open
     C                   add       1             count
      *
     C     *iso          move      hgopdt        wkisoopdt
     C     wkisoudate    subdur    wkisoopdt     wkdays:*d
     C                   add       wkdays        wktotdays
      *
     C                   endif                                                  If open
     C                   enddo                                                  Do age
      *
      * Calculate 'average age' for farm
      *
     C                   if        count <> 0
     C     wktotdays     div(h)    count         wkavgage
     C                   else
     C                   z-add     0             wkavgage
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     plist01       plist
     C                   parm                    hghgsn
     C                   parm                    zzcymd
      * Head
     C                   parm                    gpihd                          GROUP INV
     C                   parm                    pinhd                          PIG IN
     C                   parm                    tinhd                          TRSF IN
     C                   parm                    tisphd                         SAME PHASE
     C                   parm                    tidphd                         DIFF PHASE
     C                   parm                    pouhd                          PIGS OUT
     C                   parm                    touhd                          TRSF OUT
     C                   parm                    tosphd                         SAME PHASE
     C                   parm                    tosphd                         DIFF PHASE
     C                   parm                    rinhd                          REJECT IN
     C                   parm                    rouhd                          REJECT OUT
     C                   parm                    qinhd                          QUALITY IN
     C                   parm                    qouhd                          QUALITY OUT
     C                   parm                    morhd                          MORTALITY
     C                   parm                    inahd                          INV ADJ
      * Pounds
     C                   parm                    pinlb                          PIG IN
     C                   parm                    tinlb                          TRSF IN
     C                   parm                    tisplb                         SAME PHASE
     C                   parm                    tidplb                         DIFF PHASE
     C                   parm                    poulb                          PIG OUT
     C                   parm                    toulb                          TRSF OUT
     C                   parm                    tosplb                         SAME PHASE
     C                   parm                    todplb                         DIFF PHASE
     C                   parm                    rinlb                          REJECT IN
     C                   parm                    roulb                          REJECT OUT
     C                   parm                    qinlb                          QUALITY IN
     C                   parm                    qoulb                          QUALITY OUT
     C                   parm                    morlb                          MORTALITY
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    brfscd
     C                   kfld                    brblcd
     C                   kfld                    brrmcd
      *
      * Put the system date into a date format field
      *
     C     *mdy          move      udate         wkisoudate
      *
     C                   endsr
      /EJECT
