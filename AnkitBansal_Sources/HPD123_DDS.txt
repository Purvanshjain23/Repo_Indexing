     A*%%TS  SD  19950302  111435  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD123
     A*  TITLE:         COPY FUNCTIONS FROM USER TO USER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/14/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A  44                                  CF09(09 'COPY')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP123R1
     A*%%TS  SD  19950302  111435  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  3'HP123'
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 16'COPY AUTHORIZED FUNCTIONS FROM USE-
     A                                      R TO USER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Enter the user to copy from:'
     A                                  5  2'User code ..'
     A                                  5 15'?'
     A            C1USCD    R        B  5 18REFFLD(AUUSCD)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30
     AO 50                                  DSPATR(PC)
     A            C1USNM    R        O  5 30REFFLD(AUUSNM)
     A                                  6 30'User authorizations:'
     A                                  7 35'Restricted user ..................-
     A                                      ....:'
     A            C1RUFL    R        O  7 76REFFLD(AURUFL)
     A                                  8 35'Modify data ......................-
     A                                      ....:'
     A            C1MDFL    R        O  8 76REFFLD(AUMDFL)
     A                                  9 35'Restricted maintenance ...........-
     A                                      ....:'
     A            C1RMFL    R        O  9 76REFFLD(AURMFL)
     A                                 10 35'Restricted view ..................-
     A                                      ....:'
     A            C1RVFL    R        O 10 76REFFLD(AURVFL)
     A                                 11 35'Requires authorization to function-
     A                                      s ..:'
     A            C1AFFL    R        O 11 76REFFLD(AUAFFL)
     A                                 13  2'Copy all authorized functions for -
     A                                      the above user to the user below:'
     A                                 15  2'User code ..'
     A                                 15 15'?'
     A            DFUSCD    R        B 15 18REFFLD(AUUSCD)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A            DFUSNM    R        O 15 30REFFLD(AUUSNM)
     A                                 16 30'User authorizations:'
     A                                 17 35'Restricted user ..................-
     A                                      ....:'
     A            DFRUFL    R        O 17 76REFFLD(AURUFL)
     A                                 18 35'Modify data ......................-
     A                                      ....:'
     A            DFMDFL    R        O 18 76REFFLD(AUMDFL)
     A                                 19 35'Restricted maintenance ...........-
     A                                      ....:'
     A            DFRMFL    R        O 19 76REFFLD(AURMFL)
     A                                 20 35'Restricted view ..................-
     A                                      ....:'
     A            DFRVFL    R        O 20 76REFFLD(AURVFL)
     A                                 21 35'Requires authorization to function-
     A                                      s ..:'
     A            DFAFFL    R        O 21 76REFFLD(AUAFFL)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP123K1
     A*%%TS  SD  19950214  112134  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A  44                             23 22'F9=Copy'
     A                                 23 31'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP123ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP123EC                   SFLCTL(HP123ES)
     A*%%TS  SD  19940705  110806  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
