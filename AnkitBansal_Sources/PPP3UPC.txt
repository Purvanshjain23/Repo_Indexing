/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT/ EMAIL INVENTORY WAREHOUSE PRICES             */
/*                                                                  */
/*  SYSTEM    - INVENTORY MANAGEMENT SYSTEM                         */
/*  DATE      - 06/15/05                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     B    &COPYN      -  NO. OF COPIES (PASSED)                   */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &WHSE       -  WAREHOUSE                                */
/*     B    &COMP       -  COMPANY                                  */
/*                                                                  */
/********************************************************************/
/* MODIFICATION HISTORY                                             */
/********************************************************************/
/* DATE     INT PROJECT DESCRIPTION                                 */
/* -------- --- ------- --------------------------------------------*/
/* 02/13/17 DN  R9083   Added ISC Valuation Grouping & Grouping     */
/*                      description as input parm.                  */
/********************************************************************/

 PPPZUPC:    PGM        PARM(&RETURN &OUTQ &HOLD &SAVE &COMPL &COPYN +
                          &WHSE &WEEKA &EMAIL &ENDINV &ADDR &IVG +
                          &IVGDSC)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &WEEKA      *CHAR   (8)           /* WEEK ENDING CHAR          */
        DCL    &WEEKN      *DEC    (8 0)         /* WEEK ENDING DEC           */
        DCL    &WHSE       *CHAR   (3)           /* WAREHOUSE CODE           */
        DCL    &COMPL      *DEC    (15 5)        /* WAREHOUSE COMPANY        */
        DCL    &EMAIL      *CHAR   (1)           /* EMAIL STS                */
        DCL    &ADDR       *CHAR   (50)          /* EMAIL ADDR               */
        DCL    &ENDINV     *DEC    (15 5)        /* END INV MINIMUM         */
        DCL    &IVG        *CHAR   (1)           /* INV VALUATION GROUP      */
        DCL    &IVGDSC     *CHAR   (30)          /* INV VALUATION GROUP DESC */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPN      *DEC    (3)        /* WAREHOUSE COMPANY        */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)
        DCL    &COMMAND    *CHAR   (512)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&WEEKN) VALUE(&WEEKA)
             CHGVAR     VAR(&COMPN) VALUE(&COMPL)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CREATE QTEMP FILE IF EMAIL IS SELECTED                        */
/********************************************************************/
             CRTDUPOBJL OBJ(PPBVCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(PPBVCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(PPBVCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
/********************************************************************/
/*   CALL PRINT                                                   */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PPP0PFR$) +
                          OUTQ(&OUTQ) COPIES(&COPY) HOLD(&HOLD) +
                          SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

    /* R9083-ADDED INV VALUATION GROUPING & DESC AS PARM. */
          /* CALL       PGM(PPP0PFR) PARM(' ' &WEEKN &COMPN &WHSE &EMAIL +
                                           &ENDINV) */
             CALL       PGM(PPP0PFR) PARM(' ' &WEEKN &COMPN &WHSE &IVG +
                                           &IVGDSC &EMAIL &ENDINV)
/********************************************************************/
/*   EMAIL IF SELECTED                                            */
/********************************************************************/
          IF  COND(&EMAIL *EQ 'Y') THEN(DO)
             EXECUTE    SQL('SELECT CHAR(CVTDATE(BVAIDX,YMD1), MDY)  +
   COLHDG("WEEK" "END DATE"), BVB2C3 COLHDG("WHSE" "CO"),   +
                          BVGRAA COLHDG("WHSE" "CODE"), BVEJC7 +
                          COLHDG("ITEM" "CODE") EDTCDE(4),   BVM5WG +
                          COLHDG("END INV" "FINAL LBS"), BVF4PR +
                          COLHDG("WHSE" "PRICE"),    BVWBSX +
                          COLHDG("WHSE" "PRICE OVRRD") FROM       +
                          QTEMP/PPBVCPP') PCFMT(*XLS) +
                          RECIPIENT(&ADDR) EMLMSG('Warehouse Prices')
             ENDDO
             GOTO       CMDLBL(END)
/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

