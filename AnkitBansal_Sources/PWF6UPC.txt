 /***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PWASUPR                                       */
/*  PROGRAM NAME:    Email Delivery Schedule Reports               */
/*  PROGRAMMER:      Julius High                                   */
/*  CREATE DATE:     01/13/2017                                    */
/*                                                                 */
/*  FUNCTION:        This CLP function is used to Email a PDF      */
/*                   copy of the Delivery Schedule Report to each  */
/*                   Producer.  The Producer Distribution WRK file */
/*                   is built in QTEMP to list each report that is */
/*                   to sent.  Input may come from the Prt         */
/*                   Delivery Schedule PV or from the Web Applica- */
/*                   tion that generates the Delivery Sched Detail */
/*                   UPL file.                                     */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  E14585 JJH 03/04/19 - Add Parm &COMPA to PWFHXFR               */
/*  E12120 JJH 02/27/18 - Add Producer Location Logic              */
/*         - Change Parameters to use Prt Delv Sched CL Parms      */
/*         - Add new input parm: PDW Producer Loc Code             */
/*  E12120 JJH 11/28/17 - Email with PDF or XLS                    */
/*******************************************************************/
             PGM        PARM(&PDWINPUT &COMPA &PRODUCER &PRODLOC +
                          &KILLDTFMA &KILLDTTOA &SRTYPE &SOURCE +
                          &FROMLIB &EMAILPROD)
/*                                                                 */
/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* Function Specific                                               */
             DCL        VAR(&PDWINPUT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PRODUCER) TYPE(*CHAR) LEN(6)
             DCL        VAR(&PRODLOC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&KILLDTFMA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&KILLDTTOA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SRTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SOURCE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&FROMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMAILPROD) TYPE(*CHAR) LEN(1)

/*-----------------------------------------------------------------*/
/* Program Variables                                               */
             DCL        VAR(&COMPN) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&KILLDTFM) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&KILLDTTO) TYPE(*DEC) LEN(7 0)
/*                                                                 */
/*-----------------------------------------------------------------*/
/* Initialization                                                  */
             CHGVAR     VAR(&COMPN) VALUE(&COMPA)
             CHGVAR     VAR(&KILLDTFM) VALUE(&KILLDTFMA)
             CHGVAR     VAR(&KILLDTTO) VALUE(&KILLDTTOA)
/*                                                                 */
/*-----------------------------------------------------------------*/
/*                      MAIN                                       */
/*-----------------------------------------------------------------*/
/* Create Produce Distribution WRK (Work File) in QTEMP            */
             CHKOBJ     OBJ(QTEMP/PWABCPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
/*                         object not found                        */
/*                                                                 */
             CRTDUPOBJ  OBJ(PWABCPP) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWABCPL0) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWABCPL1) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             ENDDO

             CLRPFM     FILE(QTEMP/PWABCPP)
/*-----------------------------------------------------------------*/
             OVRDBF     FILE(PWABCPP) TOFILE(QTEMP/PWABCPP)
             OVRDBF     FILE(PWABCPL0) TOFILE(QTEMP/PWABCPL0)
             OVRDBF     FILE(PWABCPL1) TOFILE(QTEMP/PWABCPL1)
/*-----------------------------------------------------------------*/
/* Build Production Distribution WRK workfile in QTEMP which is    */
/* used to Print Delivery Schedule Report and sent via Email.      */
/*                                                                 */
             IF         COND(&PDWINPUT = 'P') THEN(DO)
             CALL       PGM(PWAOXFR) PARM('' &PDWINPUT &COMPN +
                          &PRODUCER &KILLDTFM &KILLDTTO &SRTYPE +
                          &SOURCE)
             ENDDO

/*-----------------------------------------------------------------*/
/* Print PKARPFR - Delivery Schedule Report for all Producers      */
             IF         COND(&EMAILPROD = 'P') THEN(DO)
             CALL       PGM(PWA7XFR) PARM('')
             ENDDO

/*-----------------------------------------------------------------*/
/* Create Delivery Schedule Rpt Wk (Work File) in QTEMP            */
             IF         COND(&EMAILPROD = 'X') THEN(DO)
             CHKOBJ     OBJ(QTEMP/PWAJCPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
/*                         object not found                        */
/*                                                                 */
             CRTDUPOBJ  OBJ(PWAJCPP) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWAJCPL0) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWAJCPL1) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             ENDDO
/*-----------------------------------------------------------------*/
             OVRDBF     FILE(PWAJCPP) TOFILE(QTEMP/PWAJCPP)
             OVRDBF     FILE(PWAJCPL0) TOFILE(QTEMP/PWAJCPL0)
             OVRDBF     FILE(PWAJCPL1) TOFILE(QTEMP/PWAJCPL1)

             CLRPFM     FILE(QTEMP/PWAJCPP)

             CALL       PGM(PWFHXFR) PARM('' '&COMPA')

             ENDDO

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
  ENDPGM:
             DLTOVR     FILE(*ALL)
             ENDPGM
