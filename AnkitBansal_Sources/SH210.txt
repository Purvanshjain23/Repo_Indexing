      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH210 - Datamart Sales: Build Market/Item
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     10/20/08
      *
      * FUNCTION: This program builds a file of Market/Items showing where
      *           each combination is "commissionable" or not.
      *
      *           This file in the Data Mart will be re-created with
      *           each load of the Data Mart.
      *
      *           Please note that "Company Organization" is not part of this file.
      *           Records are unique by: Market and Item
      *
      ****************************************************************
      * Date      Programmer
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpmddrel1  if   e           k disk
      *  Sales market
      *
      *
     Fpmeccpl1  if   e           k disk
      *  Workfile: Broker commission rate matrix
      *
      *
     Fshp210    uf a e           k disk
      *  Datamart: Market/Item
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Control fields
      *
     D writefl         s              1
      *
      * Parms
      *
     D xxcorg          s              1
      *
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read each record in the OMS Sales Market file.
      * If Commission Schedule is NOT blank,
      *   read all Workfile records for this Commission Schedule
      *   for Seaboard, write a datamart record for each Workfile record
      *   for Dailys, write a datamart record if one does not already exist
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      pmddrel1                               90
     C                   if        *in90 = *off and ddkbaa <> *blank            If not EOF
      *
     C                   exsr      $workfile
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Process all Workfile records for the Commission Schedule
      *---------------------------------------------------------------
      *
     C     $workfile     begsr
      *
      * Read all Workfile records for this Commission Schedule
      *
     C     ddkbaa        setll     pmeccpl1
      *
     C                   dou       *in91 = *on                                  Do loop
     C     ddkbaa        reade     pmeccpl1                               91
     C                   if        *in91 = *off                                 If not EOF
      *
      * For Seaboard, we will do a 1-to-1 read/write to the Datamart file.
      * For Dailys, only write a Datamart record if no record already
      * exists for the Market/Item.
      *
     C                   move      yes           writefl
      *
     C                   if        xxcorg = 'D'
     C     key01         chain(n)  shp210                             92
     C                   if        *in92 = *off
     C                   move      no            writefl
     C                   endif
     C                   endif
      *
      * Conditionally, write a Datamart record
      *
     C                   if        writefl = yes                                If writing
      *
     C                   movel     ddisaa        mimkcd
     C                   z-add     echgcd        miitcd
     C                   movel     ecavna        miitds
     C                   z-add     ecbkpr        micomrt
      *
     C                   if        micomrt = 0
     C                   move      no            micomfl
     C                   else
     C                   move      yes           micomfl
     C                   endif
      *
     C                   write     mirec
     C                   endif                                                  If writing
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      *  Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm list
      *
     C     *entry        plist
     C                   parm                    xxcorg
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ddisaa
     C                   kfld                    echgcd
      *
     C                   endsr
      /EJECT
