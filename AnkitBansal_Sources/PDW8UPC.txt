/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES COMPANY, DATE OF ENTRY AND     */
/*              USER ID ORIGINAL ENTRY. THEN PRINT COPY ORDERS      */
/*              CREATED REPORT.                                     */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 02/07/97                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &DATORGN    - ORIGINAL ENTRY DATE (PASSED)              */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &USRORG     - ORIGINAL USER  (PASSED)                   */
/*                                                                  */
/********************************************************************/

 PDW8UPC:    PGM        PARM(&RETURN &APPLIC &DESC &OUTQ &HOLD +
                          &SAVE &NQSTAT &NIGHTQ &COMPNYN &DATORGN +
                          &COPYN &USRORG)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (6)           /* Return Code               */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &DATORGN    *DEC                  /* ORIGINAL DATE  (Passed)   */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &USRORG     *CHAR   (10)          /* Market Code    (Passed)   */
/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMP       *DEC    (3 0)         /* COMPANY                   */
        DCL    &DATORG     *DEC    (7 0)         /* ORIGINAL DATE             */
        DCL    &COMMAND    *CHAR   (512)

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/* CONVERT PARAMETERS TO REQUIRED SIZE FOR PRINT FUNCTION                  */

             CHGVAR     VAR(&COMP)  VALUE(&COMPNYN)
             CHGVAR     VAR(&DATORG) VALUE(&DATORGN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDW6PFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

 CALL:       CALL       PGM(PDW6PFR) PARM(' ' &COMP &USRORG &DATORG)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

