/*  PROGRAM NUMBER:  HP4415CL                                      */
/*  PROGRAM NAME:    LISTING OF BGF TARGETS                        */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     09/27/07                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4415-SPECIFY BGF TARGET LISTING OPTIONS.    */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(80)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&LDCDYR) TYPE(*DEC) LEN(4)

             CHGVAR     VAR(&LDCDYR) VALUE(%SST(*LDA 8 4))

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* SELECT THE "YEAR" RECORDS TO PROCESS FROM BGF YEARLY TARGETS     */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('BYCDYR *NE 0')
/* YEAR                                                           */
             IF         COND(&LDCDYR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND BYCDYR +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 30 4)) VALUE(&LDCDYR)
             ENDDO

             OVRDBF     FILE(HSP096) SHARE(*YES)
             OPNQRYF    FILE((HSP096)) QRYSLT(&QDATA) KEYFLD(*FILE)  +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  CALL REPORT PROGRAM                                              */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

             CALL       PGM(HP6415)

             CLOF       OPNID(HSP096)
             DLTOVR     FILE(*ALL)
             ENDPGM

