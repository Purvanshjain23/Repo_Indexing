/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          ORDER MANAGEMENT SYSTEM                       */
/*  PROGRAM NUMBER:  PBKACLP                                       */
/*  PROGRAM NAME:    PRINT WAREHOUSE INVENTORY--AGED ON PRODUCTION */
/*                         DATE                                    */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     09/18/13                                      */
/*                                                                 */
/*  FUNCTION:        Print Aged Inventory Report by Warehouse      */
/*                                                                 */
/*                   NOTE: Parms are passed in the LDA             */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(81)
             DCL        VAR(&QDATA1) TYPE(*CHAR) LEN(101)
             DCL        VAR(&QDATA2) TYPE(*CHAR) LEN(101)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDFITCD)  TYPE(*DEC) LEN(7)
             DCL        VAR(&LDTITCD)  TYPE(*DEC) LEN(7)

             CHGVAR     VAR(&LDFCYMD)  VALUE(%SST(*LDA 15 7))
             CHGVAR     VAR(&LDTCYMD)  VALUE(%SST(*LDA 22 7))
             CHGVAR     VAR(&LDFITCD)   VALUE(%SST(*LDA 32 7))
             CHGVAR     VAR(&LDTITCD)   VALUE(%SST(*LDA 268 7))

/*---------------------------------------------------------------------*/
/* SET UP THE PRINT FILE                                               */
/*---------------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*************************************************************************/
/* EXTRACT THE DATA FROM THE ITEM BALANCE FILE (This is our driver file) */
/*************************************************************************/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* Only Active records where On-Hand Quantity is greater than zero           */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('B0DDQT +
                          *GT 0')
             CHGVAR     VAR(%SST(&QDATA 14 19)) VALUE('*AND B0VSST +
                          *EQ "A"')

/*------------------------------------------------------------------------*/
/* If users selected a From/To Item Code range                            */
/*------------------------------------------------------------------------*/

             IF         COND(&LDFITCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 34 15)) VALUE('*AND B0HGCD +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 50 7)) VALUE(&LDFITCD)
             CHGVAR     VAR(%SST(&QDATA 58 15)) VALUE('*AND B0HGCD +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 74 7)) VALUE(&LDTITCD)
             ENDDO

/* Extract data from the Item Balance file                             */

             OVRDBF     FILE(CAB0REL6) SHARE(*YES)
             OPNQRYF    FILE((CAB0REL6)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/***********************************************************************/
/* EXTRACT THE DATA FROM THE ITEM BALANCE DETAIL FILE                  */
/***********************************************************************/
/* Note: We will use this file when the Warehouse Lot Tracking Flag=NO */

             CHGVAR     VAR(&QDATA1) VALUE(' ')

/* Only Active records where the On-Hand Quantity is greater than zero */

             CHGVAR     VAR(%SST(&QDATA1 1 12)) VALUE('B1ARQT +
                          *GT 0')
             CHGVAR     VAR(%SST(&QDATA1 14 19)) VALUE('*AND B1VSST +
                          *EQ "A"')

/* From/To Production Date                                                */
/* Note: Since bulk items do not have Production Dates, I make sure I     */
/*       get that data by including records where the date is zero.       */

             CHGVAR     VAR(%SST(&QDATA1 34 34)) VALUE('*AND (B1ELDT +
                          *EQ 0 *OR (B1ELDT *GE')
             CHGVAR     VAR(%SST(&QDATA1 69 7)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA1 77 15)) VALUE('*AND +
                          B1ELDT *LE')
             CHGVAR     VAR(%SST(&QDATA1 93 7)) VALUE(&LDTCYMD)
             CHGVAR     VAR(%SST(&QDATA1 100 2)) VALUE('))')


/* Extract data from the Item Balance Detail file                      */

             OVRDBF     FILE(CAB1REL1) SHARE(*YES)
             OPNQRYF    FILE((CAB1REL1)) QRYSLT(&QDATA1) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/***********************************************************************/
/* EXTRACT THE DATA FROM THE ITEM LOT LOCATION FILE                    */
/***********************************************************************/
/* Note: We will use this file when the Warehouse Lot Tracking Flag=YES*/

             CHGVAR     VAR(&QDATA2) VALUE(' ')

/* Only Active records where the On-Hand Quantity is greater than zero     */

             CHGVAR     VAR(%SST(&QDATA2 1 12)) VALUE('L5J2QT +
                          *GT 0')
             CHGVAR     VAR(%SST(&QDATA2 14 19)) VALUE('*AND L5VSST +
                          *EQ "A"')

/* From/To Production Date                                                */
/* Note: Since bulk items do not have Production Dates, I make sure I     */
/*       get their data by including records where the date is zero.      */

             CHGVAR     VAR(%SST(&QDATA2 34 34)) VALUE('*AND (L5ELDT +
                          *EQ 0 *OR (L5ELDT *GE')
             CHGVAR     VAR(%SST(&QDATA2 69 7)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA2 77 15)) VALUE('*AND +
                          L5ELDT *LE')
             CHGVAR     VAR(%SST(&QDATA2 93 7)) VALUE(&LDTCYMD)
             CHGVAR     VAR(%SST(&QDATA2 100 2)) VALUE('))')

/* Extract data from the Item Lot Location file                        */

             OVRDBF     FILE(PDL5CPL1) SHARE(*YES)
             OPNQRYF    FILE((PDL5CPL1)) QRYSLT(&QDATA2) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*---------------------------------------------------------------------*/
/* CREATE A WORKFILE FOR THE SUMMARY PAGE                              */
/*---------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(PBBGCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(PBBGCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*---------------------------------------------------------------------*/
/* PRINT REPORT                                                        */
/*---------------------------------------------------------------------*/
             CALL       PGM(PBJ9PFR)

/* EOF Processing                                                      */

             CLOF       OPNID(CAB0REL6) /* Item Balance */
             CLOF       OPNID(CAB1REL1) /* Item Balance Detail */
             CLOF       OPNID(PDL5CPL1) /* Item Lot Location */
             DLTOVR     FILE(*ALL)

             ENDPGM

