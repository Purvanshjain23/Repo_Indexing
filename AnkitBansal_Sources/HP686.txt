      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP686
      * TITLE:       HOG GROUP REVIEW
      * PROGRAMMER:  MARY PALMER
      * CREATED:     8/25/94
      *
      * FUNCTION: THIS PROGRAM IS SUBMITTED FROM THE 'WORK WITH GROUP'
      *           PROGRAMS (HP440 AND HP441). THE REPORT HAS THE
      *           FOLLOWING FOUR SECTIONS:
      *                   1) MOVEMENTS
      *                   2) CHECK DETAIL
      *                   4) FEED
      *                   5) EXPENSES
      *
      *           THE REPORT MAY BE GENERATED 'WITH' OR 'WITHOUT'
      *           EXPENSES. USER SELECTIONS ARE PASSED IN THE LDA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/01/00  LeAnne Fedor
      *           Removed 'genetic line description' from heading.
      *
      * 02/21/01  LeAnne Fedor
      *           Added the group's production type code and description to headings.
      *
      * 07/18/01  LeAnne Fedor
      *           New field 'feed group' added to Hog Group file.
      *           Expense ticket amount expanded from 7,2 to 9,2.
      *
      * 10/24/02  LeAnne Fedor
      *           Added 3 new columns of Costed Transfer info:
      *               per head rate
      *               per pound rate
      *               amount
      *           Columns only show/print when user has elected the 'with costs' version.
      *
      * 10/29/02  LeAnne Fedor
      *           Added totals for 'transfer in' and 'transfer out' costs.
      *           Added additional hog group fields to headings.
      *
      * 04/10/03  LeAnne Fedor
      *           Replaced HSP086-Costed Transfers file with new file HSP118.
      *
      * 06/03/03  LeAnne Fedor
      *           Removed Ration Total Line in Feed Section.
      *
      * 11/29/04  LeAnne Fedor
      *           To facilitate base-line testing, we added the following total fields to
      *           the Total Section of the Movement Section:
      *               Head: daily dead, DOA, adjustment, transfer out, transfer in, cull sales,
      *                     market sales
      *               Amount: cull sale
      *           Also, we added a subtotal on Expense Class in the Expense Section.
      *
      * 12/30/05  LeAnne Fedor
      *           Recompile only; added Hog Group Code to workfile.
      *
      * 01/04/06  LeAnne Fedor
      *           We are now allowing the user to "download" the Movement Section of this
      *           report. So, we removed the "movement" logic from this program and put
      *           it into a new program (HP381) which builds a movement workfile. In this
      *           program, we now read/process our new Movement Workfile to build the
      *           Movement Section of this report.
      *
      * 04/10/08  LeAnne Ramsey
      *           Recompile only. We were getting level checks on the Workfiles.
      *
      * 07/06/09  LeAnne Ramsey
      *           Added new field 'Continuous Flow' flag (Hog Group file) to Headings lines.
      *
      * 07/27/11  LeAnne Ramsey  E1664
      *           Recompile only.
      *           Sami Wilson requested that we add 2 fields to the "Download Movements'
      *           function (file HSP381) on HP4440. The fields are:
      *              1) Leave Farm Time
      *              2) Arrive at Plant Time
      *
      * 08/09/11  LeAnne Ramsey  E1664
      *           Recompile only.
      *           Sami Wilson requested that we add 1 more field to the "Download Movements'
      *           function on HP4440 (file HSP381). The field is:
      *              1) Unload Time at Plant
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
      *
     FHSP011    IF   E           K DISK
      *    PRODUCITON PHASE
      *
      *
     FHSP025    IF   E           K DISK
      *    Group type
      *
      *
     FHSP031    IF   E           K DISK
      *    BREED STOCK
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP045    IF   E           K DISK
      *    PREMIUM/DISCOUNT CODES
      *
      *
     FHSL048C   IF   E           K DISK
      *    EXPENSE TICKET
      *
      *
     FHSP056    IF   E           K DISK
      *    Sex
      *
      *
     FHSL064I   IF   E           K DISK
      *    CHECK DETAIL
      *
      *
     FHSL065C   IF   E           K DISK
      *    CHECK ADJUSTMENTS
      *
      *
     FHSP076    IF   E           K DISK
      *  MOVEMENT TYPE CODE
      *
      *
     FHSP084    IF   E           K DISK
      *  SALE HEADER
      *
      *
     FHSP381    IF   E           K DISK
      *    WORKFILE FOR MOVEMENTS
      *
      *
     FHSP386    IF   E           K DISK
      *    WORKFILE FOR FEED TICKETS
      *
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
     D MGW             S              1    DIM(50)
      *
      *---------------------------------------------------------------
      *  ARRAYS FOR TOTALS
      *---------------------------------------------------------------
      *
     D FDLB            S              9  0 DIM(2)                               FEED POUNDS
     D FDAM            S              9  2 DIM(2)                               FEED AMOUNT
      *
      *
     D LVLB            S              7  0 DIM(2)                               LIVE POUNDS
     D CRLB            S              7  0 DIM(2)                               CARCASS LBS
      *
     D LVHD            S              5  0 DIM(2)                               LIVE HEAD
     D DOHD            S              5  0 DIM(2)                               DEAD HEAD
      *
     D BAAM            S              9  2 DIM(2)                               BASE AMOUNT $
     D GRAM            S              9  2 DIM(2)                               GROSS $
     D PDAM            S              9  2 DIM(2)                               PREM/DISCOUNT
     D DAAM            S              9  2 DIM(2)                               DEDUC/ADD-ON
      *
     D PYAM            S             11  4 DIM(2)                               PERF YIELD
     D PYHD            S              5  0 DIM(2)                               YIELD HEAD
      *
     D LNAM            S              9  2 DIM(2)                               LEAN
     D LNLB            S              7  0 DIM(2)                               LEAN LBS
      *
     D BFAM            S              9  2 DIM(2)                               BACKFAT
     D BFLB            S              7  0 DIM(2)                               BACKFAT LBS
      /EJECT
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *--------------------------------------------------------------------
      * DATA STRUCTURE FOR BUILDING THE TOTAL DESCRIPTION TO PRINT FOR FEED
      *--------------------------------------------------------------------
      *
     D                 DS
     D  TFDESC                 1     31
     D  XXDESC                 1     21
     D  XXRNCD                24     28
     D  XXACYR                24     27
     D  XXACPR                30     31
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('Unknown')
      *
     D TOTAC           C                   CONST('Total for Year/Peri-
     D                                     od')
      *
     D TOTFD           C                   CONST('       Total for Fe-
     D                                     ed')
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDHGSN                 6     12  0
     D  LDCOST                20     20
     D  LDBODS                21     50
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PRINT REPORT HEADINGS
      *
     C                   EXCEPT    HRHDR
      *
      * PROCESS THE FOLLOWING FOR THE HOG GROUP:
      *    MOVEMENTS
      *    CHECKS ASSOCIATED WITH SALES MOVEMENTS
      *    FEED TICKETS
      *
     C                   EXSR      $MOVE
     C                   EXSR      $CHECK
     C                   EXSR      $FEED
      *
      * IF THE USER HAS REQUESTED THIS REPORT 'WITH COSTS',
      * PROCESS EXPENSE TICKETS
      *
     C     LDCOST        IFEQ      YES
     C                   EXSR      $EXP
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * Movement Section
      *----------------------------------------------------------------
      *
     C     $move         begsr
      *
      * Process/print each record in the Movement workfile
      *
     C     *loval        setll     hsp381
      *
     C                   dou       *in90 = *on                                  Do moves
     C                   read      hsp381                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        firstm = yes
     C                   except    hmhdr
     C                   move      no            firstm
     C                   endif
      *
      * Event date
     C                   z-add     w1medtmdy     dmmdy
      * Description
     C                   movel(p)  w1desc        dmds
      *
      *
      * Process appropriate Movement Type
      *
      * Transfers
     C                   select
     C                   when      w1mtcd = 'T'
     C                   exsr      $tran
      * Sales
     C                   when      w1mtcd = 'S'
     C                   exsr      $sale
      *
      * Inventory adjustments
     C                   when      w1mtcd = 'A'
     C                   exsr      $adj
      * Daily deads
     C                   when      w1mtcd = 'D'
     C                   exsr      $dead
     C                   endsl
      *
      * Print
     C                   if        *inof = *on
     C                   except    hrhdr
     C                   except    hmhdr
     C                   endif
     C                   except    dmdtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do moves
      *
      * If there were no Movement transactions for this group, then print
      * the standard "no data..." message; otherwise, print totals.
      *
     C                   if        prtfl = no
     C                   except    nomove
     C                   else
     C                   exsr      $tmtot
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Print Movement Totals for the Group
      *----------------------------------------------------------------
      *
     C     $tmtot        begsr
      *
      * Print a blank line before starting the Totals Section
      *
     C                   except    blank
      *
      * Move workfields to print fields.
      *
     C                   z-add     wkkdhd        tmkdhd
     C                   z-add     wkdohd        tmdohd
     C                   z-add     wkiahd        tmiahd
      *
      * Mortality =    Daily Dead
      *             +  DOAS from transfers out
      *             +  DOAS from sales
      *             -  Inventory adjustments
      *
     C                   eval      tmmorthd = wkkdhd + wkdohd - wkiahd
      *
      *
      * Inventory  =  Head received into the group
      *             - Sold head that arrived live at the customer from
      *                  movements that are Paid In Full or Posted
      *             - Head shipped to the customer on sales movements that
      *                   are not Paid in Full or Posted
      *             - Transferred out head that were received at the
      *                  Destiination Farm (Quality + Reject Head)
      *             - Daily dead
      *             - DOA from transfers
      *             - DOA from sales
      *             + Inventory adjustments
      *
     C                   z-add     wkinhd        tminhd
      *
      * If you are running the 'with costs' version.
      *
     C                   if        *in72 = *on
     C                   z-add     wktiam        tmtiam
     C                   z-add     wktoam        tmtoam
     C                   z-add     wkmrktsam     tmmrktsam
     C                   z-add     wkcullsam     tmcullsam
     C                   endif
      *
     C                   except    tmtot
      *
     C                   z-add     0             wktiam
     C                   z-add     0             wktoam
     C                   z-add     0             wkmrktsam
     C                   z-add     0             wkcullsam
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Process a Transfer Movement
      *---------------------------------------------------------------
      *
     C     $tran         begsr
      *
      * Move "opposing" group fields to print fields
      *
     C                   movel     w1ohgcd       dmhgcd
     C                   z-add     w1ofscd       dmfscd
      *
      * Origin/destination flag
      * Movement Status
     C                   move      w1odfl        dmodfl
     C                   move      w1mscd        dmmscd
      *
      * Scheduled head
     C                   z-add     w1schd        dmschd
      * Shipped values
     C                   z-add     w1shhd        dmshhd
     C                   z-add     w1shlb        dmshlb
      *
      * Arrived values
     C                   z-add     w1arhd        dmarhd
     C                   z-add     w1arlb        dmarlb
      *
      * Transfer cost
     C                   z-add     w1hdrt        dmhdrt
      *
      * Conditionally perform "origin" or "destination" group logic depending
      * on whether the user's selected group is the origin or the destination
      * group on the movement.
      *
     C                   select
     C                   when      w1odfl = 'O'
     C                   exsr      $orig
      *
     C                   when      w1odfl = 'D'
     C                   exsr      $dest
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Logic for the Origin group on a Transfer
      *---------------------------------------------------------------
      *
     C     $orig         begsr
      *
      * Accumulate the 'transfer cost amount' field
      *
     C                   z-add     w1tram        dmtram
     C                   add       dmtram        wktoam
      * DOA head
     C                   z-add     w1dohd        dmdohd
     C                   add       dmdohd        wkdohd
      *
     C                   z-add     w1dolb        dmdolb
      *
      * Generic head/lbs
     C                   z-add     w1hd          dmhd
     C                   add       dmhd          tmtohd
     C                   add       dmhd          wkinhd
      *
     C                   z-add     w1lb          dmlb
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Logic for the Destination Group on a Transfer
      *---------------------------------------------------------------
      *
     C     $dest         begsr
      *
      * Transfer Cost Amount
      *
     C                   z-add     w1tram        dmtram
     C                   add       w1tram        wktiam
      *
     C                   z-add     w1hd          dmhd
     C                   add       dmhd          tmtihd
     C                   add       dmhd          wkinhd
      *
     C                   z-add     w1lb          dmlb
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Sales Movement logic
      *---------------------------------------------------------------
      *
     C     $sale         begsr
      *
      * Origin/Destination flag
      * Movement Status
     C                   move      w1odfl        dmodfl
      *
     C                   movel     w1mscd        dmmscd
     C                   movel     w1desc        dmds
      * Scheduled head
     C                   z-add     w1schd        dmschd
      *
      * Shipped values
     C                   z-add     w1shhd        dmshhd
     C                   z-add     w1shlb        dmshlb
      *
      * Arrived values
     C                   z-add     w1arhd        dmarhd
     C                   z-add     w1arlb        dmarlb
      *
      * DOA values
     C                   z-add     w1dohd        dmdohd
     C                   add       dmdohd        wkdohd
     C                   z-add     w1dolb        dmdolb
      *
      * Generic head/lbs
     C                   z-add     w1hd          dmhd
     C                   add       dmhd          wkinhd
      *
     C                   z-add     w1lb          dmlb
      *
      * Accumulate Sale head for total line
      *
     C                   if        w1stcd = 'CULLS'
     C                   add       dmhd          tmcullshd
     C                   else
     C                   add       dmhd          tmmrktshd
     C                   endif
      *
      * Cost of Sales
      *
     C                   select
     C                   when      w1stcd = 'CULLS'                             If hit
     C                   z-add     w1cullsam     wkcullsam
     C                   other
      *
     C                   z-add     w1hdrt        dmhdrt
     C                   z-add     w1tram        dmtram
     C                   add       dmtram        wkmrktsam
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Daily Dead Movements
      *---------------------------------------------------------------
      *
     C     $dead         begsr
      *
     C                   move      w1mscd        dmmscd
      *
     C                   z-add     w1hd          dmhd
     C                   add       dmhd          wkinhd
      *
     C                   z-add     w1lb          dmlb
      *
      * Accumulate the Daily Dead values
      *
     C                   eval      wkkdhd = wkkdhd + (dmhd * -1)
     C                   eval      amkdlb = amkdlb + (dmlb * -1)
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Adjustment Movements
      *---------------------------------------------------------------
      *
     C     $adj          begsr
      *
     C                   movel     w1mscd        dmmscd
     C                   z-add     w1hd          dmhd
     C                   add       w1hd          wkinhd
     C                   add       w1hd          wkiahd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $CHECK - PROCESS CHECK DETAIL FOR THE GROUP
      *---------------------------------------------------------------
      *
     C     $CHECK        BEGSR
      *
      * RESET PRINT FLAG TO NO
      *
     C                   MOVEL     NO            PRTFL
     C     LDHGSN        SETLL     HSL064I
      *
     C     *IN93         DOUEQ     *ON                                          DO CHECKS
     C     LDHGSN        READE     HSL064I                                93
     C     *IN93         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRSTC        IFEQ      YES                                          IF FIRST CHK
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    HCHDR
     C                   Z-ADD     CDMVSN        SVMVSN
     C                   Z-ADD     CDCVNO        SVCVNO
     C                   Z-ADD     CDCHNO        SVCHNO
     C                   Z-ADD     CDCHDT        SVCHDT
     C                   MOVEL     CDTPCD        SVTPCD
     C                   MOVEL     NO            FIRSTC
     C                   ENDIF                                                  IF FIRST CHK
      *
      * PRINT CHECK DETAIL LINE WHEN MOVEMENT, CUSTOMER,
      * CHECK NUMBER, CHECK DATE OR PAYMENT TYPE CHANGES
      *
     C     CDMVSN        IFNE      SVMVSN
     C     CDCVNO        ORNE      SVCVNO
     C     CDCHNO        ORNE      SVCHNO
     C     CDCHDT        ORNE      SVCHDT
     C     CDTPCD        ORNE      SVTPCD
     C                   EXSR      $DCDTL
     C                   ENDIF
      *
      * RETRIEVE DEDUCTION/ADD-ONS AND PREMIUM/DISCOUNTS
      *
     C                   EXSR      $TOTDA
      *
      * ACCUMULATE VALUES INTO TOTAL ARRAYS
      *
     C                   ADD       CDDOHD        DOHD
     C                   ADD       CDLVHD        LVHD
     C                   ADD       CDCRLB        CRLB
     C                   ADD       CDLVLB        LVLB
     C                   ADD       CDBAAM        BAAM
     C                   ADD       CDGRAM        GRAM
      *
      * SAVE PERFORMANCE VALUES SO THAT WEIGHTED AVERAGES
      * CAN BE CALCULATED IF MULTIPLE CHECK DETAIL LINES ARE
      * ULTIMATELY ROLLED INTO ONE CHECK LINE FOR PRINTING.
      *
      * PERFORMANCE YIELD (CARCASS PAYMENT ONLY)
      *
     C     CDTPCD        IFEQ      'C'
     C     CDLVLB        ANDNE     0
     C     CDCRLB        DIV       CDLVLB        WKF154
     C     WKF154        MULT      CDLVHD        WKF154
     C                   ADD       WKF154        PYAM
     C                   ADD       CDLVHD        PYHD
     C                   ENDIF
      *
      * BACKFAT INCHES
      *
     C     CDBFAM        MULT      CDCRLB        WKF152
     C     WKF152        IFNE      0
     C                   ADD       WKF152        BFAM
     C                   ADD       CDCRLB        BFLB
     C                   ENDIF
      *
      * LEAN PERCENT
      *
     C     CDLNPR        MULT      CDCRLB        WKF152
     C     WKF152        IFNE      0
     C                   ADD       WKF152        LNAM
     C                   ADD       CDCRLB        LNLB
     C                   ENDIF
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO CHECKS
      *
      * IF THERE WERE NO CHECKS, THEN PRINT THE STANDARD
      * 'NO DATA...' ERROR MESSAGE; OTHERWISE, PRINT THE
      * LAST CHECK AND THEN THE TOTALS FOR THE CHECK SECTION.
      *
     C     FIRSTC        IFEQ      YES
     C                   EXCEPT    NOCHEK
     C                   ELSE
     C                   EXSR      $DCDTL
     C                   EXSR      $TCTOT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOTDA - RETRIEVE DEDUCTIONS/ADD-ONS AND PREMIUMS/DISCOUNTS
      *----------------------------------------------------------------
      *
     C     $TOTDA        BEGSR
      *
     C     key02         SETLL     HSL065C
     C                   SETOFF                                       70
      *
     C     *IN70         DOWEQ     *OFF                                         DO LOOP
     C     key02         READE     HSL065C                                70
     C     *IN70         IFEQ      *OFF                                         IF NOT EOF
      *
      * DEDUCTION/ADD-ON
      *
     C     CJCJFL        IFEQ      'D'                                          IF DED
     C                   ADD       CJCJAM        DAAM
     C                   ELSE
      *
      * PREMIUM/DISCOUNT
      *
     C     CJCJCD        CHAIN     HSP045                             71
     C     *IN71         IFEQ      *OFF                                         IF FOUND
     C                   ADD       CJCJAM        PDAM
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF DED
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO LOOP
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DCDTL - PRINT DETAIL LINE IN CHECK SECTION
      *----------------------------------------------------------------
      *
     C     $DCDTL        BEGSR
      *
      * RETRIEVE RECEIVED DATE OF MOVEMENT
      *
     C     SVMVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF
     C                   movel     shstcd        dcstcd
     C                   z-add     shrcdt        wkcymd
     C                   exsr      $tomdy
     C                   z-add     wkmdy         dcmdy
     C                   ENDIF
      *
      * RETRIEVE CUSTOMER NAME
      *
     C                   Z-ADD     SVCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
     C     JDEFL         IFEQ      YES
     C                   MOVEL(P)  'UNKNOWN'     DCCNAM
     C                   ELSE
     C                   MOVEL     OUALPH        DCCNAM
     C                   ENDIF
      *
      * CALCULATE WEIGHTED AVERAGES FROM ACCUMULATED VALUES
      *
      * PERFORMANCE YIELD
      *
     C     PYHD(1)       IFNE      0
     C     PYAM(1)       DIV(H)    PYHD(1)       WKF54
     C     WKF54         MULT      100           DCPYLD
     C                   ENDIF
      *
      * AVERAGE LIVE POUNDS
      *
     C     LVHD(1)       IFNE      0
     C     LVLB(1)       DIV(H)    LVHD(1)       DCAVLB
     C                   ENDIF
      *
      * BACKFAT INCHES
      *
     C     BFLB(1)       IFNE      0
     C     BFAM(1)       DIV(H)    BFLB(1)       DCBFIN
     C                   ENDIF
      *
      * LEAN PERCENT
      *
     C     LNLB(1)       IFNE      0
     C     LNAM(1)       DIV(H)    LNLB(1)       DCLNPR
     C                   ENDIF
      *
      * NET CHECK AMOUNT = GROSS AMOUNT + DEDUCTIONS ADDONS
      *
     C     GRAM(1)       ADD       DAAM(1)       DCNTAM
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HCHDR
     C                   ENDIF
      *
     C                   EXCEPT    DCDTL
     C                   MOVEL     YES           PRTFL
      *
      * CLEAR TABLES
     C                   Z-ADD     1             X
     C                   EXSR      $CLEAR
      *
      * SAVE CONTROL FIELDS
     C                   Z-ADD     CDMVSN        SVMVSN
     C                   Z-ADD     CDCVNO        SVCVNO
     C                   Z-ADD     CDCHNO        SVCHNO
     C                   Z-ADD     CDCHDT        SVCHDT
     C                   MOVEL     CDTPCD        SVTPCD
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TCTOT- CALCULATE TOTALS FOR THE CHECK SECTION AND PRINT
      *----------------------------------------------------------------
      *
     C     $TCTOT        BEGSR
      *
      * CALCULATE WEIGHTED AVERAGES FROM ACCUMULATED VALUES
      *
      * PERFORMANCE YIELD
      *
     C     PYHD(2)       IFNE      0
     C     PYAM(2)       DIV(H)    PYHD(2)       WKF54
     C     WKF54         MULT      100           TCPYLD
     C                   ENDIF
      *
      * AVERAGE LIVE POUNDS
      *
     C     LVHD(2)       IFNE      0
     C     LVLB(2)       DIV(H)    LVHD(2)       TCAVLB
     C                   ENDIF
      *
      * BACKFAT INCHES
      *
     C     BFLB(2)       IFNE      0
     C     BFAM(2)       DIV(H)    BFLB(2)       TCBFIN
     C                   ENDIF
      *
      * LEAN PERCENT
      *
     C     LNLB(2)       IFNE      0
     C     LNAM(2)       DIV(H)    LNLB(2)       TCLNPR
     C                   ENDIF
      *
      * NET CHECK AMOUNT = GROSS AMOUNT + DEDUCTIONS ADDONS
      *
     C     GRAM(2)       ADD       DAAM(2)       TCNTAM
      *
     C                   EXCEPT    TCTOT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO RETRIEVE
      *         THE CUSTOMER NAME.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $FEED - PROCESS FEED TICKETS
      *----------------------------------------------------------------
      *
     C     $FEED         BEGSR
      *
      * RESET PRINT FLAG TO NO
      *
     C                   MOVEL     NO            PRTFL
      *
      * PRINT THE FEED TICKET HEADING LINE
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    HFHDR
      *
      * READ AND PRINT ALL FEED TICKET WORKFILE RECORDS
      *
     C     *LOVAL        SETLL     HSP386
      *
     C     *IN90         DOUEQ     *ON                                          DO FEED
     C                   READ      HSP386                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      *
     C     FIRSTF        IFEQ      YES
     C                   MOVEL     NO            FIRSTF
     C                   MOVEL     WTRNCD        SVRNCD
     C                   Z-ADD     WTACYR        SVACYR
     C                   Z-ADD     WTACPR        SVACPR
     C                   ENDIF
      *
      * CONTROL BREAK PROCESSING
      *
     C                   SELECT
     C     WTACYR        WHENNE    SVACYR
     C     WTACPR        ORNE      SVACPR
     C                   EXSR      $BRKRN
     C                   EXSR      $BRKAC
      *
     C     WTRNCD        WHENNE    SVRNCD
     C                   EXSR      $BRKRN
     C                   ENDSL
      *
      * DETAIL PROCESSING:
      * IF THIS TICKET IS AN ADJUSTMENT OR PICKUP TICKET, SET
      * THE INDICATOR TO PRINT THE REFERENCE TICKET NUMBER.
      *
     C     WTTRCD        IFEQ      'A'                                          IF ADJ/PICK
     C     WTTRCD        OREQ      'P'
     C                   SETON                                        85
     C                   ELSE
     C                   SETOFF                                       85
     C                   ENDIF                                                  IF ADJ/PICK
      *
      *
      * ACCUMULATE TOTALS INTO ARRAYS
      *
     C                   ADD       WTFDAM        FDAM
     C                   ADD       WTFDLB        FDLB
      *
      *
      * INCREMENT COUNTERS FOR NUMBER OF RECORDS PER ACCOUNTING PERIOD.
      *
     C                   ADD       1             COUNTP
      *
      *
      * PRINT FEED TICKET DETAIL LINE AND SET FLAG
      * THAT RECORD WAS PRINTED.
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HFHDR
     C                   ENDIF
      *
     C                   EXCEPT    DFDTL
     C                   MOVEL     YES           PRTFL
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO FEED
      *
      *--------------------------------------------------------------
      * EITHER PRINT LAST FEED TOTAL LINES OR THE 'NO DATA..' MESSAGE
      *
     C     PRTFL         IFEQ      NO                                           IF NO RECS
     C                   EXCEPT    NOFEED
     C                   ELSE
      *
     C                   EXSR      $BRKRN
     C                   EXSR      $BRKAC
      *
     C                   Z-ADD     2             X
     C                   EXSR      $TNRT
     C                   MOVE      *BLANK        TFDESC
     C                   MOVEL     TOTFD         XXDESC
     C                   EXCEPT    TFTOT
      *
     C                   ENDIF                                                  IF NO RECS
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * BREAK ON RATION IN THE FEED SECTION
      *----------------------------------------------------------------
      *
     C     $BRKRN        BEGSR
      *
      * IF THERE HAS NOT ALSO BEEN A BREAK IN ACCOUNTING
      * YEAR/PERIOD, PRINT A BLANK LINE TO CONTROL SPACING
      *
     C     WTACYR        IFEQ      SVACYR
     C     WTACPR        ANDEQ     SVACPR
     C                   EXCEPT    BLANK
     C                   ENDIF
      *
     C                   MOVEL     WTRNCD        SVRNCD
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * BREAK ON ACCOUNTING YEAR/PERIOD IN THE FEED SECTION
      *----------------------------------------------------------------
      *
     C     $BRKAC        BEGSR
      *
      * SET INDEX FOR FEED TOTAL ARRAYS TO ACCESS THE
      * ACCOUNTING YEAR/PERIOD TOTALS.
      *
     C                   Z-ADD     1             X
      *
      * IF YOU HAVE PRINTED MORE THAN ONE RECORD FOR THIS YEAR/PERIOD,
      *   1) CALCULATE THE PER TON COST FOR THE TOTAL LINE
      *   2) SET UP THE DESCRIPTION TO PRINT ON THE TOTAL LINE
      *   3) PRINT A YEAR/PERIOD TOTAL LINE
      *
      *
     C     COUNTP        IFGT      1
     C                   EXSR      $TNRT
      *
     C                   MOVE      *BLANK        TFDESC
     C                   MOVEL     TOTAC         XXDESC
     C                   MOVEL     SVACYR        XXACYR
     C                   MOVEL     SVACPR        XXACPR
      *
     C                   EXCEPT    TFTOT
     C                   ENDIF
     C                   EXCEPT    BLANK
      *
      *
      * ALWAYS CLEAR THE FEED TOTAL ARRAYS FOR 'YEAR/PERIOD', RESET
      * THE RECORD COUNTER FOR YEAR/PERIOD AND SAVE THE CONTROL VALUES.
      *
     C                   Z-ADD     0             FDLB(X)
     C                   Z-ADD     0             FDAM(X)
     C                   Z-ADD     0             COUNTP
     C                   Z-ADD     WTACYR        SVACYR
     C                   Z-ADD     WTACPR        SVACPR
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * CALCULATE FEED COST PER TON
      *----------------------------------------------------------------
      *
     C     $TNRT         BEGSR
      *
     C     FDLB(X)       DIV       2000          WKF152
     C     WKF152        IFNE      0
     C     FDAM(X)       DIV(H)    WKF152        TFTNRT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EXP - PROCESS EXPENSE TICKETS FOR HOG GROUP
      *----------------------------------------------------------------
      *
     C     $EXP          BEGSR
      *
      * RESET PRINT FLAG TO NO
     C                   MOVEL     NO            PRTFL
      *
      * PRINT THE EXPENSE TICKET HEADING LINE
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    EXPHDR
      *
      * READ AND PRINT ALL EXPENSE TICKETS FOR HOG GROUP
      *
     C     LDHGSN        SETLL     HSL048C
      *
     C     *IN91         DOUEQ     *ON                                          DO EXPENSES
     C     LDHGSN        READE     HSL048C                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
      * IF BREAK AND NOT THE FIRST TIME, PRINT SUBTOTAL LINE
      *
     C     FIRSTE        IFEQ      YES                                          IF FIRST
     C                   MOVEL     NO            FIRSTE
     C                   ELSE
     C     XTECCD        IFNE      SVECCD
     C     XTESCD        ORNE      SVESCD
     C     XTEXCD        ORNE      SVEXCD
     C     XTRPCD        ORNE      SVRPCD
     C                   EXCEPT    EXPSUB
      *
     C                   if        xteccd <> sveccd
     C                   except    expclass
     C                   endif
      *
     C                   ENDIF
     C                   ENDIF                                                  IF FIRST
      *
      * FLIP TICKET BEGIN AND END DATES TO MMDDYY
      *
     C                   Z-ADD     XTXBDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DXBMDY
      *
     C                   Z-ADD     XTXEDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DXEMDY
      *
      * ACCUMULATE EXPENSE DOLLARS FOR TOTAL LINES
      *
     C                   ADD       XTXTAM        TXSAM
     C                   ADD       XTXTAM        TXCAM
     C                   ADD       XTXTAM        TXTAM
      *
      * PRINT EXPENSE DETAIL LINE AND SET FLAG THAT RECORD WAS READ
      *
     C     *INOF         IFEQ      *ON                                          IF OVERFLOW
     C                   EXCEPT    HRHDR
     C                   EXCEPT    EXPHDR
     C                   SETON                                        818283
     C                   SETON                                        84
     C                   ELSE
      *
      * TEST FOR CHANGE IN EXPENSE CLASS, SUB-CLASS, OR CODE FOR PRINT
      *
     C                   SETOFF                                       818283
     C                   SETOFF                                       84
      *
     C     XTECCD        IFNE      SVECCD
     C                   SETON                                        81
     C                   ENDIF
      *
     C     XTESCD        IFNE      SVESCD
     C                   SETON                                        82
     C                   ENDIF
      *
     C     XTEXCD        IFNE      SVEXCD
     C                   SETON                                        83
     C                   ENDIF
      *
     C     XTRPCD        IFNE      SVRPCD
     C                   SETON                                        84
     C                   ENDIF
     C                   ENDIF                                                  IF OVERFLOW
      *
     C                   EXCEPT    EXPDTL
     C                   MOVEL     YES           PRTFL
      *
      * SAVE FIELDS
     C                   MOVEL     XTECCD        SVECCD
     C                   MOVEL     XTESCD        SVESCD
     C                   MOVEL     XTEXCD        SVEXCD
     C                   MOVEL     XTRPCD        SVRPCD
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO EXPENSES
      *
      *---------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFL         IFEQ      NO                                           IF NO RECS
     C                   EXCEPT    NOEXP
     C                   ELSE
     C                   EXCEPT    EXPSUB
     C                   EXCEPT    EXPCLASS
     C                   EXCEPT    EXPTOT
     C                   ENDIF                                                  IF NO RECS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLEAR - CLEAR TABLE ELEMENTS USED IN CHECK SECTION
      *---------------------------------------------------------------
      *
     C     $CLEAR        BEGSR
      *
     C                   Z-ADD     0             LVLB(X)
     C                   Z-ADD     0             CRLB(X)
      *
     C                   Z-ADD     0             LVHD(X)
     C                   Z-ADD     0             DOHD(X)
      *
     C                   Z-ADD     0             BAAM(X)
     C                   Z-ADD     0             GRAM(X)
     C                   Z-ADD     0             PDAM(X)
     C                   Z-ADD     0             DAAM(X)
     C                   Z-ADD     0             PYAM(X)
     C                   Z-ADD     0             PYHD(X)
      *
     C                   Z-ADD     0             LNAM(X)
     C                   Z-ADD     0             LNLB(X)
      *
     C                   Z-ADD     0             BFAM(X)
     C                   Z-ADD     0             BFLB(X)
      *
     C                   ENDSR
      /EJECT
      *--------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *--------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * TRANSFERS
      *
      *
     C     key02         KLIST
     C                   KFLD                    CDCHDT
     C                   KFLD                    CDCVNO
     C                   KFLD                    CDCHNO
     C                   KFLD                    CDLNNO
      *
      *
      *
      *
      * INDEX FOR TABLES
     C                   Z-ADD     0             X                 1 0
      *
      *
      * FIELDS FOR COUNTING THE NUMBER OF DETAIL LINES PRINTED PER
      * RATION AND PER ACCOUNTING PERIOD. THESE COUNTERS ARE USED
      * TO DETERMINE WHETHER TO PRINT TOTALS OR NOT IN THE FEED
      * SECTION.  IF THERE IS ONLY 1 DETAIL LINE PER BREAK, THEN
      * THERE IS NO NEED TO PRINT A TOTAL--IT JUST CLUTTERS UP THE
      * REPORT.
      *
     C                   Z-ADD     0             COUNTP            5 0
      *
      * FLAG FIELDS FOR CONTROLLING PROCESSING
      *
     C                   MOVEL     NO            PRTFL             1
     C                   MOVEL     YES           FIRSTE            1
     C                   MOVEL     YES           FIRSTM            1
     C                   MOVEL     YES           FIRSTC            1
     C                   MOVEL     YES           FIRSTF            1
      *
      * SAVE FIELDS
      *
     C     *LIKE         DEFINE    XTECCD        SVECCD
     C     *LIKE         DEFINE    XTESCD        SVESCD
     C     *LIKE         DEFINE    XTEXCD        SVEXCD
     C     *LIKE         DEFINE    XTRPCD        SVRPCD
      *
     C     *LIKE         DEFINE    CDMVSN        SVMVSN
     C     *LIKE         DEFINE    CDCVNO        SVCVNO
     C     *LIKE         DEFINE    CDCHNO        SVCHNO
     C     *LIKE         DEFINE    CDCHDT        SVCHDT
     C     *LIKE         DEFINE    CDTPCD        SVTPCD
      *
     C     *LIKE         DEFINE    WTACYR        SVACYR
     C     *LIKE         DEFINE    WTACPR        SVACPR
     C     *LIKE         DEFINE    WTRNCD        SVRNCD
      *
      * WORKFIELDS
     C                   Z-ADD     0             WKF54             5 4
     C                   Z-ADD     0             WKF154           15 4
     C                   Z-ADD     0             WKF152           15 2
      *
      * J.D. EDWARDS PARMS
      *
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C     *LIKE         DEFINE    CJCVNO        INAJD                          JDE NUMBER
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
      *
      *----------------------------------------------------------------
      * ACCUMULATION FIELDS FOR TOTAL LINE CALCULATIONS
      *----------------------------------------------------------------
      *
      * Transfer in/out amount
     C                   Z-ADD     0             WKTIAM            9 2
     C                   Z-ADD     0             WKTOAM            9 2
      *
      * Sales amounts
     C                   Z-ADD     0             WKMRKTSAM         9 2
     C                   Z-ADD     0             WKCULLSAM         9 2
      * DAILY DEAD
     C                   Z-ADD     0             WKKDHD            5 0
     C                   Z-ADD     0             AMKDLB            7 0
      *
      * INVENTORY ADJUSTMENT
     C                   Z-ADD     0             WKIAHD            5 0
      *
      * SHIPPED OUT OF GROUP AND ARRIVED DEAD
      *
     C                   Z-ADD     0             WKDOHD            5 0
      *
      * INVENTORY HEAD
     C                   Z-ADD     0             WKINHD            5 0
      *
      *----------------------------------------------------------------
      * DEFINE PRINT FIELDS FOR REPORT HEADINGS
      *----------------------------------------------------------------
      *
     C     *LIKE         DEFINE    HGFSCD        HRFSCD
     C     *LIKE         DEFINE    HGBLCD        HRBLCD
     C     *LIKE         DEFINE    HGRMCD        HRRMCD
     C     *LIKE         DEFINE    HGHGCD        HRHGCD
     C     *LIKE         DEFINE    HGHGSN        HRHGSN
     C     *LIKE         DEFINE    HGGSCD        HRGSCD
     C     *LIKE         DEFINE    BSBSDS        HRBSDS
     C     *LIKE         DEFINE    PTPTCD        HRPTCD
     C     *LIKE         DEFINE    PTPTDS        HRPTDS
     C     *LIKE         DEFINE    PPPPCD        HRPPCD
     C     *LIKE         DEFINE    PPPPDS        HRPPDS
     C     *LIKE         DEFINE    GTGTCD        HRGTCD
     C     *LIKE         DEFINE    GTGTDS        HRGTDS
     C     *LIKE         DEFINE    SXSXCD        HRSXCD
     C     *LIKE         DEFINE    SXSXDS        HRSXDS
     C     *LIKE         DEFINE    WKMDY         HRCMDY
     C     *LIKE         DEFINE    WKMDY         HROMDY
     C     *LIKE         DEFINE    WKMDY         HRDMDY
     C     *LIKE         DEFINE    HGADOI        HRADOI
     C     *LIKE         DEFINE    HGADOO        HRADOO
     C     *LIKE         DEFINE    HGCFFL        HRCFFL
      *
      *----------------------------------------------------------------
      * DEFINE PRINT FIELDS FOR THE MOVEMENT DETAIL AND TOTAL LINES
      *----------------------------------------------------------------
      *
     C     *LIKE         DEFINE    w1hdrt        dmhdrt
     C     *LIKE         DEFINE    w1tram        dmtram
     C     *LIKE         DEFINE    MTSMDS        DMDS
     C     *LIKE         DEFINE    w1ODFL        DMODFL
     C     *LIKE         DEFINE    WKMDY         DMMDY
     C     *LIKE         DEFINE    SHMSCD        DMMSCD
     C     *LIKE         DEFINE    w1shlb        DMLB
     C     *LIKE         DEFINE    w1shhd        DMHD
     C     *LIKE         DEFINE    w1shhd        DMDOHD
     C     *LIKE         DEFINE    w1shlb        DMDOLB
     C     *LIKE         DEFINE    w1shhd        DMSCHD
     C     *LIKE         DEFINE    w1shhd        DMSHHD
     C     *LIKE         DEFINE    w1shlb        DMSHLB
     C     *LIKE         DEFINE    w1shhd        DMARHD
     C     *LIKE         DEFINE    w1shlb        DMARLB
     C     *LIKE         DEFINE    HGFSCD        DMFSCD
     C     *LIKE         DEFINE    HGHGCD        DMHGCD
      *
      * DEFINE PRINT FIELDS FOR THE MOVEMENT TOTAL LINES
      *
     C                   Z-ADD     0             tmmorthd          5 0
     C                   Z-ADD     0             tmkdhd            5 0
     C                   Z-ADD     0             tmdohd            5 0
     C                   Z-ADD     0             tmiahd            5 0
     C                   Z-ADD     0             tminhd            5 0
      *
     C                   Z-ADD     0             tmtihd            5 0
     C                   Z-ADD     0             tmtiam            9 2
      *
     C                   Z-ADD     0             tmtohd            5 0
     C                   Z-ADD     0             tmtoam            9 2
      *
     C                   Z-ADD     0             tmmrktshd         5 0
     C                   Z-ADD     0             tmmrktsam         9 2
      *
     C                   Z-ADD     0             tmcullshd         5 0
     C                   Z-ADD     0             tmcullsam         9 2
      *
      *
      *----------------------------------------------------------------
      * DEFINE PRINT FIELDS FOR FEED TOTAL LINES
      *----------------------------------------------------------------
      *
     C                   Z-ADD     0             TFTNRT            5 2
      *
      *----------------------------------------------------------------
      * DEFINE PRINT FIELDS FOR THE CHECK DETAIL AND TOTAL LINES
      *----------------------------------------------------------------
      *
     C     *LIKE         DEFINE    WKMDY         DCMDY
     C                   MOVE      *BLANK        DCCNAM           11
     C     *LIKE         DEFINE    SHSTCD        DCSTCD
     C                   Z-ADD     0             DCNTAM            9 2
     C                   Z-ADD     0             DCPYLD            5 2
     C                   Z-ADD     0             DCBFIN            5 2
     C                   Z-ADD     0             DCLNPR            5 2
     C                   Z-ADD     0             DCAVLB            4 1
      *
      * DEFINE PRINT FIELDS FOR THE CHECK TOTAL LINE
      *
     C                   Z-ADD     0             TCNTAM            9 2
     C                   Z-ADD     0             TCPYLD            5 2
     C                   Z-ADD     0             TCBFIN            5 2
     C                   Z-ADD     0             TCLNPR            5 2
     C                   Z-ADD     0             TCAVLB            4 1
      *
      *----------------------------------------------------------------
      * DEFINE PRINT FIELDS FOR EXPENSE TICKET DETAIL AND TOTAL LINES
      *----------------------------------------------------------------
      *
     C                   Z-ADD     0             DXBMDY            6 0
     C                   Z-ADD     0             DXEMDY            6 0
      *
      * DEFINE PRINT EXPENSE TICKET SUBTOTAL AND TOTAL LINE
      *
     C                   Z-ADD     0             TXSAM            11 2
     C                   Z-ADD     0             TXCAM            11 2
     C                   Z-ADD     0             TXTAM            11 2
      *
      *----------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *----------------------------------------------------------------
      * RETRIEVE HOG GROUP INFO FOR HEADINGS.
      *
     C     LDHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        HRHGCD
     C                   Z-ADD     HGHGSN        HRHGSN
     C                   MOVEL     HGGSCD        HRGSCD
     C                   Z-ADD     HGFSCD        HRFSCD
     C                   MOVEL     HGPTCD        HRPTCD
     C                   MOVEL     HGPPCD        HRPPCD
     C                   MOVEL     HGGTCD        HRGTCD
     C                   MOVEL     HGCFFL        HRCFFL
     C                   MOVEL     HGSXCD        HRSXCD
     C                   MOVEL     HGBLCD        HRBLCD
     C                   MOVEL     HGRMCD        HRRMCD
     C                   Z-ADD     HGADOI        HRADOI
     C                   Z-ADD     HGADOO        HRADOO
     C                   ENDIF
      *
      * RETRIEVE SEX DESCRIPTION
      *
     C     HGSXCD        CHAIN     HSP056                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     SXSXDS        HRSXDS
     C                   ELSE
     C                   MOVEL     UNKNWN        HRSXDS
     C                   ENDIF                                                  IF FOUND
      *
      * RETRIEVE GROUP TYPE DESCRIPTION
      *
     C     HGGTCD        CHAIN     HSP025                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     GTGTDS        HRGTDS
     C                   ELSE
     C                   MOVEL     UNKNWN        HRGTDS
     C                   ENDIF                                                  IF FOUND
      *
      * RETRIEVE PRODUCTION TYPE DESCRIPTION
      *
     C     HGPTCD        CHAIN     HSP010                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HRPTDS
     C                   ELSE
     C                   MOVEL     UNKNWN        HRPTDS
     C                   ENDIF                                                  IF FOUND
      *
      * RETRIEVE PRODUCTION PHASE DESCRIPTION
      *
     C     HGPPCD        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HRPPDS
     C                   ELSE
     C                   MOVEL     UNKNWN        HRPPDS
     C                   ENDIF                                                  IF FOUND
      *
      * RETRIEVE BREED STOCK DESCRIPTION
      *
     C     HGBSCD        CHAIN     HSP031                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     BSBSDS        HRBSDS
     C                   ELSE
     C                   MOVEL     UNKNWN        HRBSDS
     C                   ENDIF                                                  IF FOUND
      *
      *
      * FLIP GROUP DATES TO MMDDY FOR PRINTING IN HEADING
      *
     C                   Z-ADD     HGCRDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HRCMDY
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HROMDY
      *
     C                   Z-ADD     HGDSDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HRDMDY
      *
      * SET UP HEADING UNDERLINE FIELDS FOR PRINTING
      *
     C                   MOVEL     *ALL'-'       DASH            117
     C                   MOVEL     *ALL'-'       DASH1           198
     C                   MOVEL     *ALL'-'       DASH2           129
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
      * IF THE USER ELECTED TO SEE 'COSTS', SET ON THE
      * INDICATOR THAT CONTROLS PRINTING OF THE COST COLUMNS
      *
     C     LDCOST        IFEQ      YES
     C                   SETON                                        72
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          103 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          100 'HOG GROUP REVIEW'
     O                                          188 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          188 'PAGE'
     O                       PAGE          Z    198
      *
      * Heading line 1
      *
     O          E            HRHDR          1
     O                                           17 'BUSINESS OFFICE:'
     O                       LDFSBO              24
     O                       LDBODS              56
      *
     O                                           75 'PRODUCTION TYPE:'
     O                       HRPTCD              82
     O                       HRPTDS              92
      *
     O                                          148 'FARM SITE/BUILDING/ROOM:'
     O                       HRFSCD        z    155
     O                                          157 '/'
     O                       HRBLCD             163
     O                                          165 '/'
     O                       HRRMCD             171
      *
      * Heading line 2
      *
     O          E            HRHDR          1
     O                                           17 'HOG GROUP:'
     O                       HRHGCD              26
      *
     O                                           75 'PRODUCTION PHASE:'
     O                       HRPPCD              82
     O                       HRPPDS             112
      *
     O                                          139 'DATE CREATED/OPENED/'
     O                                          148 'DISPOSED:'
     O                       HRCMDY             158 '  -  -  '
     O                                          160 '/'
     O                       HROMDY             169 '  -  -  '
     O                                          171 '/'
     O                       HRDMDY             180 '  -  -  '
      *
      * Heading line 3
      *
     O          E            HRHDR          1
      *
     O                                           17 'HOG GROUP NUMBER:'
     O                       HRHGSN        z     26
      *
     O                                           75 'GROUP TYPE:'
     O                       HRGTCD              78
     O                       HRGTDS              97
      *
     O                                          148 'BREED STOCK:'
     O                       HRBSDS             170
      *
      *
      * Heading line 4
      *
     O          E            HRHDR          1
     O                                           17 'GROUP STATUS:'
     O                       HRGSCD              21
      *
     O                                           75 'SEX:'
     O                       HRSXCD              78
     O                       HRSXDS              92
      *
     O                                          135 'AVG DAYS OLD IN/AVG '
     O                                          148 'DAYS OLD OUT:'
     O                       HRADOI        Z    153
     O                                          155 '/'
     O                       HRADOO        Z    158
      *
      * Heading line 5
      *
     O          E            HRHDR          2
     O                                          148 'CONTINUOUS FLOW:'
     O                       HRCFFL             151
      *
      *-------------------------------------------------------------
      * MOVEMENT HEADING
      *-------------------------------------------------------------
      *
     O          E            HMHDR          1
     O                       DASH1              198
      *
     O          E            HMHDR          1
     O                                           18 'MOVEMENT'
     O                                           40 'RUNNING'
     O               72                          76 'MARKET SALE/TR'
     O               72                          87 'ANSFER COST'
     O                                           96 'SCHED'
     O                                          102 'ORIG'
     O                                          120 '--- SHIPPED  ---'
     O                                          141 '--- ARRIVED ----'
     O                                          165 'DOA AT DESTINATION'
     O                                          186 '--- OPPOSING ---'
     O                                          198 'CUSTOMER'
      *
     O          E            HMHDR          1
     O                                            6 'DATE'
     O                                           18 'NUMBER'
     O                                           22 'ST'
     O                                           28 'TYPE'
     O                                           40 'TOTAL HEAD'
     O                                           50 'HEAD'
     O                                           60 'POUNDS'
      *
     O               72                          72 'PER HEAD'
     O               72                          87 'AMOUNT'
      *
     O                                           96 'HEAD'
     O                                          102 'DEST'
     O                                          110 'HEAD'
     O                                          120 'POUNDS'
     O                                          131 'HEAD'
     O                                          141 'POUNDS'
     O                                          153 'HEAD'
     O                                          165 'POUNDS'
     O                                          175 'FARM'
     O                                          184 'GROUP'
     O                                          198 'NUMBER'
      *
     O          E            HMHDR          1
     O                       DASH1              198
      *
      *-------------------------------------------------------------
      * DETAIL MOVEMENT LINE
      *-------------------------------------------------------------
      *
     O          E            DMDTL          1
     O                       DMMDY          B     8 '  /  /  '
     O                       W1MVSN        ZB    18
     O                       DMMSCD         B    22
     O                       DMDS           B    30
     O                       WKINHD        K     40
     O                       DMHD          KB    51
     O                       DMLB          KB    61
      *
     O               72      dmhdrt        kb    73
     O               72      dmtram        kb    88
      *
     O                       DMSCHD        KB    97
     O                       DMODFL         B   101
     O                       DMSHHD        KB   111
     O                       DMSHLB        KB   121
     O                       DMARHD        KB   132
     O                       DMARLB        KB   142
     O                       DMDOHD        KB   154
     O                       DMDOLB        KB   166
     O                       DMFSCD        MB   176
     O                       DMHGCD         B   186
     O                       w1cvno        MB   198
      *
      *-------------------------------------------------------------
      * MOVEMENT TOTAL LINES
      *-------------------------------------------------------------
      *
     O          e            tmtot          1
     O                                           20 'Daily dead head'
     O                       tmkdhd        JB    30
     O               72                          67 'Head'
     O                                           82 'Dollars'
      *
     O          e            tmtot          1
     O                                           20 'DOA head'
     O                       tmdohd        JB    30
     O                                           59 'Transfer in'
     O                       tmtihd        kb    68
     O               72      tmtiam        kb    83
     O                                          102 'Mortality head'
     O                       tmmorthd      JB   110
      *
     O          e            tmtot          1
     O                                           20 'Adjustment head'
     O                       tmiahd        JB    30
     O                                           59 'Transfer out'
     O                       tmtohd        kb    68
     O               72      tmtoam        kb    83
      *
     O          e            tmtot          1
     O                                           59 'Market sales'
     O                       tmmrktshd     kb    68
     O               72      tmmrktsam     kb    83
      *
     O          e            tmtot          1
     O                                           20 'Inventory head'
     O                       tminhd        JB    30
     O                                           59 'Cull sales'
     O                       tmcullshd     kb    68
     O               72      tmcullsam     kb    83
      *
      *
      *----------------------------------------------------------------
      * CHECK HEADING LINE
      *----------------------------------------------------------------
      *
     O          E            HCHDR       1  1
     O                       DASH1              198
      *
     O          E            HCHDR          1
     O                                            8 'MOVEMENT'
     O                                           18 'RECEIVED'
     O                                           24 'SALE'
     O                                           30 'PAY'
     O                                           39 'CHECK'
     O                                           59 'LIVE'
     O                                           67 'DOA'
     O                                           78 'LIVE'
     O                                           89 'CARCASS'
     O               72                         103 'BASE'
     O               72                         117 'PREMIUM'
     O               72                         131 'GROSS'
     O               72                         145 'DEDUCTION'
     O               72                         160 'NET'
     O                                          170 'CARCASS'
     O                                          179 'BACKFAT'
     O                                          188 'PERCENT'
     O                                          198 'AVERAGE'
      *
     O          E            HCHDR          1
     O                                            7 'NUMBER'
     O                                           16 'DATE'
     O                                           24 'TYPE'
     O                                           30 'TYPE'
     O                                           39 'NUMBER'
     O                                           49 'CUSTOMER'
     O                                           59 'HEAD'
     O                                           67 'HEAD'
     O                                           78 'POUNDS'
     O                                           89 'POUNDS'
     O               72                         103 'AMOUNT'
     O               72                         117 'DISCOUNT'
     O               72                         131 'AMOUNT'
     O               72                         145 'ADD-ON'
     O               72                         160 'AMOUNT'
     O                                          170 'YIELD'
     O                                          178 'INCHES'
     O                                          188 'LEAN'
     O                                          198 'LIVE LBS'
      *
     O          E            HCHDR          1
     O                       DASH1              198
      *
      *----------------------------------------------------------------
      * DETAIL LINE FOR EACH CHECK
      *----------------------------------------------------------------
      *
     O          E            DCDTL          1
     O                       SVMVSN        Z      7
     O                       DCMDY          B    18 '  /  /  '
     O                       DCSTCD         B    25
     O                       SVTPCD         B    29
     O                       SVCHNO        ZB    39
     O                       DCCNAM         B    52
     O                       LVHD(1)       ZB    59
     O                       DOHD(1)       ZB    67
     O                       LVLB(1)       KB    79
     O                       CRLB(1)       KB    90
     O               72      BAAM(1)       KB   104
     O               72      PDAM(1)       KB   118
     O               72      GRAM(1)       KB   132
     O               72      DAAM(1)       KB   146
     O               72      DCNTAM        KB   161
     O                       DCPYLD        KB   171
     O                       DCBFIN        KB   179
     O                       DCLNPR        KB   189
     O                       DCAVLB        KB   198
      *
      *----------------------------------------------------------------
      * CHECK TOTAL
      *----------------------------------------------------------------
      *
     O          E            TCTOT       1  1
     O                                           12 'CHECK TOTALS'
     O                       LVHD(2)       ZB    59
     O                       DOHD(2)       ZB    67
     O                       LVLB(2)       KB    79
     O                       CRLB(2)       KB    90
     O               72      BAAM(2)       KB   104
     O               72      PDAM(2)       KB   118
     O               72      GRAM(2)       KB   132
     O               72      DAAM(2)       KB   146
     O               72      TCNTAM        KB   161
     O                       TCPYLD        KB   171
     O                       TCBFIN        KB   179
     O                       TCLNPR        KB   189
     O                       TCAVLB        KB   198
      *
      *----------------------------------------------------------------
      * FEED TICKET HEADING
      *----------------------------------------------------------------
      *
     O          E            HFHDR       1  1
     O                       DASH2              129
      *
     O          E            HFHDR          1
     O                                           12 '-ACCOUNTING-'
     O          E            HFHDR          1
     O                                            4 'YEAR'
     O                                           12 'PERIOD'
     O                                           29 'RATION/ADDITIVE'
     O                                           39 'TICKET'
     O                                           46 'DATE'
     O                                           55 'TRANS'
     O                                           61 'PROD'
     O                                           67 'COST'
     O                                           71 'ST'
     O                                           84 'POUNDS'
     O               72                         100 'FEED AMOUNT'
     O               72                         111 'COST/TON'
     O                                          129 'REFERENCE TICKET'
     O          E            HFHDR          1
     O                       DASH2              129
      *
      *----------------------------------------------------------------
      * FEED DETAIL
      *----------------------------------------------------------------
      *
     O          E            DFDTL          1
      *
     O                       WTACYR        ZB     4
     O                       WTACPR        ZB     9
     O                       WTRNCD         B    19
     O                       WTADCD         B    24
     O                       WTTKNO        ZB    39
     O                       WTMDY          B    48 '  /  /  '
     O                       WTTRCD         B    53
     O                       WTPRFL         B    59
     O                       WTCSFL         B    65
     O                       WTTSCD         B    71
     O                       WTFDLB        KB    85
     O               72      WTFDAM        KB   101
     O               72      WTTNRT        KB   111
     O               85      WTRTNO        ZB   127
      *
      *----------------------------------------------------------------
      * FEED TOTAL
      *----------------------------------------------------------------
      *
     O          E            TFTOT       0  1
     O                                           85 '------------'
     O               72                         101 '-------------'
     O               72                         111 '-------'
      *
     O          E            TFTOT       0  1
     O                       TFDESC              71
     O                       FDLB(X)       K     85
     O               72      FDAM(X)       K    101
     O               72      TFTNRT        K    111
      *
      *----------------------------------------------------------------
      * EXPENSE TICKET HEADING LINE
      *----------------------------------------------------------------
      *
     O          E            EXPHDR      1  1
     O                       DASH               117
      *
     O          E            EXPHDR         1
     O                                           20 '------EXPENSE-------'
     O                                           29 'REPORT'
     O                                           39 'EXPENSE'
     O                                           50 'BEGIN'
     O                                           60 'END'
     O                                           72 'ALLOC'
     O          E            EXPHDR         1
     O                                           20 'CLASS SUBCLASS  CODE'
     O                                           28 'CODE'
     O                                           39 'TICKET'
     O                                           49 'DATE'
     O                                           61 'DATE'
     O                                           73 'METHOD'
     O                                           81 'STATUS'
     O                                           94 'AMOUNT'
     O                                          109 'SUB-TOTAL'
     O          E            EXPHDR         1
     O                       DASH               117
      *
      *----------------------------------------------------------------
      * EXPENSE TICKET DETAIL
      *----------------------------------------------------------------
      *
     O          E            EXPDTL         1
     O               81      XTECCD               5
     O               82      XTESCD              13
     O               83      XTEXCD              20
     O               84      XTRPCD              29
     O                       XTXTSN        Z     39
     O                       DXBMDY              51 '  /  /  '
     O                       DXEMDY              63 '  /  /  '
     O                       XTAMCD              72
     O                       XTXSCD              79
     O                       XTXTAM        KB    95
      *
      *----------------------------------------------------------------
      * EXPENSE SUB-TOTAL
      *----------------------------------------------------------------
      *
     O          E            EXPSUB         1
     O                       TXSAM         KB   110
      *
      *----------------------------------------------------------------
      * EXPENSE CLASS TOTAL
      *----------------------------------------------------------------
      *
     O          E            EXPCLASS       1
     O                       TXCAM         KB   110
     O                                          124 'Class Total'
      *
      *----------------------------------------------------------------
      * EXPENSE TOTAL
      *----------------------------------------------------------------
      *
     O          E            EXPTOT      1  1
     O                                           69 'EXPENSE TOTAL'
     O                       TXTAM         K     95
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE FOR MOVEMENTS
      *-------------------------------------------------------------
      *
     O          E            NOMOVE      1  1
     O                                           22 'NO MOVEMENT DATA MEETS'
     O                                         +  1 'YOUR SELECTION CRITERIA'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE FOR CHECKS
      *-------------------------------------------------------------
      *
     O          E            NOCHEK      1  1
     O                                           22 'NO CHECK DETAIL MEETS'
     O                                         +  1 'YOUR SELECTION CRITERIA'
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE FOR FEED TICKETS
      *-------------------------------------------------------------
      *
     O          E            NOFEED      1  1
     O                                           21 'NO FEED TICKETS MEET'
     O                                         +  1 'YOUR SELECTION CRITERIA'
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE FOR EXPENSE TICKETS
      *-------------------------------------------------------------
      *
     O          E            NOEXP       1  1
     O                                           23 'NO EXPENSE TICKETS MEET'
     O                                         +  1 'YOUR SELECTION CRITERIA'
      *-------------------------------------------------------------
      * BLANK LINE
      *-------------------------------------------------------------
      *
     O          E            BLANK       1
      /EJECT
