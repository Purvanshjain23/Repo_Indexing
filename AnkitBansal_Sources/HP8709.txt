      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP8709-Update Esend "Rules" file for Managers' Emails
      *              (Rules are for Report HP6709 only)
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/23/05
      *
      *
      * NOTE: You have to add ESEND to you library list to get this to compile.
      *
      *
      * Function:    This function:
      *                 Deletes Managers from the "Rules" file
      *                 Writes Managers into the "Rules" file
      *
      * We are using a command/file from the ESEND software to "burst" reports on
      * the spool file into individual pages to E-mail to Managers.
      *
      * This program will access the ESEND "Rules" file records for report HP6709. It will
      * first delete out all the records for report HP6709. Then, it will read the
      * HPS Managers file. If the manager has an e-mail address, it will write a record
      * to the ESEND "Rules" file.
      *
      * The e-mailing of HP6709 happens from 3 different places. We initially were going
      * to crtdupobj on the ESEND "Rules" physical/logical file into QTEMP. But, since
      * the report can be generated from an option off of a Work With, this meant that
      * we would have to create/build the file over-and-over--once for each time the
      * users took the option. So, we went with this approach of using the actual file
      * in the ESEND library.
      *
      * The above approach could run into timing issues--if multiple users were
      * submitting e-mails from the Work With or at the same time as someone else was
      * submitting the report from the Report Submission function---but, we thought
      * we would try this approach first. If there are too many problems, we can
      * go back and explore the crtdupobj approach.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp016    if   e           k disk
      *    Manager
      *
      *
     Frptrules  uf a e           k disk
      *    Esend E-Mail Rules
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * First, delete Manager records for HP6709.
      *
     C                   exsr      $delete
      *
      * Next, write Manager records for HP6709.
      *
     C                   exsr      $write
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Delete
      *---------------------------------------------------------------
      *
     C     $delete       begsr
      *
     C     'HP6709    '  setll     rptrules
      *
     C                   dou       *in90 = *on                                  Do deletes
     C     'HP6709    '  reade     rptrules                               90
     C                   if        *in90 = *off                                 If not eof
     C                   delete    rulfmt
     C                   endif                                                  If not eof
     C                   enddo                                                  Do deletes
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write
      *---------------------------------------------------------------
      *
     C     $write        begsr
      *
      * Set up default fields for "Rules" record.
      *
     C                   eval      ruleset = 'HP6709    '
     C                   eval      compare = '*EQ'
     C                   z-add     17            pageline
     C                   z-add     1             pagepos
      *
      * Read all records in the Manager file. If the record has an e-mail
      * address entry, write a 'Rules' record.
      *
593  C*    *loval        setll     hsp016
593  C     *start        setll     hsp016
     C                   dou       *in91 = *on                                  Do managers
     C                   read      hsp016                                 91
     C                   if        *in91 = *off and mgemail <> *blank           If not EOF
     C                   eval      cmptext = mgmgcd
     C                   eval      emailto = mgemail
     C                   write     rulfmt
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do managers
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /eject
