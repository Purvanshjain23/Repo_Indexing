      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6048
      * TITLE:       Listing of Reported Weekly Activity
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/26/02
      *
      * FUNCTION:  Batch program to list the records in the workfile.
      *            This listing is submitted from HP4048-Work With Reported Weekly
      *            Activity.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fhsp3048   if   e           k disk
      *    Workfile: Reported weekly activity listing
      *
      *
     Fqprint    o    f  110        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            110
     d rtime           s              6  0
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Save fields
      *
     D svfscd          s                   like(w1fscd)
     D svhgcd          s                   like(w1hgcd)
      *
      *
      * Print fields
      *
     D h1fsnm          s                   like(fsfsnm)
     D h1group         s                   like(w1hgcd)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfscd                 1      5  0
     D  ldhgsn                 6     12  0
     D  ldhgcd                13     19
     D  ldcdyr                20     23  0
     D  ldcdwk                24     25  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     *loval        setll     hsp3048
     C                   except    h1hdr
      *
      * Read all records in the workfile
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3048                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Print 1) report headings or 2) a blank line between farms/groups
      *
     C                   select
     C                   when      first = yes
     C                   z-add     w1fscd        svfscd
     C                   move      w1hgcd        svhgcd
     C                   move      no            first
      *
     C                   when      *inof = *on
     C                   except    h1hdr
      *
     C                   when      w1fscd <> svfscd or
     C                             w1hgcd <> svhgcd
     C                   except    blank
     C                   z-add     w1fscd        svfscd
     C                   move      w1hgcd        svhgcd
     C                   endsl
      *
      * Print detail line
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message.
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      *
      *  Setup the user selections in the headings.
      *
      * Farm site
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   else
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel(p)  fsfsnm        h1fsnm
     C                   else
     C                   movel(p)  'UNKNOWN'     h1fsnm
     C                   endif
     C                   endif
      *
      * Group
     C                   if        ldhgcd = *blank
     C                   movel     'All'         h1group
     C                   else
     C                   movel     ldhgcd        h1group
     C                   endif
      *
      * Calendar year
     C                   if        ldcdyr = 0
     C                   seton                                        88
     C                   endif
      *
      * Calendar week
     C                   if        ldcdwk = 0
     C                   seton                                        87
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           59 'HOG PRODUCTION SYSTEM'
     O                                          100 'DATE'
     O                       udate         y    110
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           57 'LISTING OF REPORTED WEEKLY'
     O                                           66 'ACTIVITY'
     O                                          100 'TIME'
     O                       rtime              110 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          100 'PAGE'
     O                       page          z    110
      *
     O          e            h1hdr          1
     O                                           18 'Farm site ......:'
     O              N89      ldfscd        z     25
     O              N89      h1fsnm              55
     O               89                          23 'All'
      *
      *
     O          e            h1hdr          1
     O                                           18 'Hog group ......:'
     O                       h1group             27
      *
      *
     O          e            h1hdr          1
     O                                           18 'Calendar year ..:'
     O              N88      ldcdyr        z     24
     O               88                          23 'All'
      *
      *
     O          e            h1hdr          2
     O                                           18 'Calendar week ..:'
     O              N87      ldcdwk        z     24
     O               87                          23 'All'
      *
      *
     O          e            h1hdr          1
     O                                           37 'BEGINNING'
     O                                           63 'CULL'
     O                                           76 'MARKET/FPS'
     O                                           97 'ENDING'
      *
     O          e            h1hdr          1
     O                                            5 'FARM'
     O                                           10 'HOG'
     O                                           26 '-CALENDAR-'
     O                                           37 'INVENTORY'
     O                                           55 'TRANSFERRED HEAD'
     O                                           63 'SALE'
     O                                           73 'SALE'
     O                                           85 'DEAD'
     O                                           97 'INVENTORY'
     O                                          110 'WEEK-ENDING'
     O          e            h1hdr          1
     O                                            5 'SITE'
     O                                           12 'GROUP'
     O                                           20 'YEAR'
     O                                           26 'WEEK'
     O                                           37 'HEAD'
     O                                           53 'IN'
     O                                           45 'OUT'
     O                                           63 'HEAD'
     O                                           73 'HEAD'
     O                                           85 'HEAD'
     O                                           97 'HEAD'
     O                                          107 'DATE'
      *
     O          e            h1hdr          1
     O                       dash               110
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       w1fscd        z      5
     O                       w1hgcd              14
     O                       w1cdyr        z     20
     O                       w1cdwk        z     25
     O                       w1bginhd      k     36
     O                       w1tihd        k     46
     O                       w1tohd        k     54
     O                       w1cuhd        k     64
     O                       w1mkhd        k     74
     O                       w1kdhd        k     86
     O                       w1eninhd      k     98
     O                       w1wemdy            109 '  /  /  '
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
