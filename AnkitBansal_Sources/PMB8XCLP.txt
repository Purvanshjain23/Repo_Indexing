/*  SYNOPSIS - BUILED FILES FOR THE LOAD SCHEDULING PROCESSING     */
/*  BLD LDSCHPST,WFITEM POST,LDSCHPOSITION & UPD GRADE             */
/*                                                                 */
/*                                                                 */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                            */
/*  PROGRAM   - BUILE LOAD SCHEDULING POSITION FILES               */
/*  DATE      - 01/30/09                                           */
/*                                                                 */
/*  FILES THAT ARE USED IN THIS PROCESS:                           */
/*  CALENDAR                    CAAFREP                            */
/*  COMPANY ITEM                CABZREP                            */
/*  ITEM BALANCE DETAIL         CAB1REP                            */
/*  LOAD DETAIL                 OMFLCPP                            */
/*  LOAD HEADER                 OMFJCPP                            */
/*  LOAD SCHDLNG POSITION       PDMHCPP                            */
/*  LOAD SCHDLNG PSTN KEY       PDMJCPP                            */
/*  ORDER DETAIL                OPBGWKP                            */
/*  ORDER HEADER TRG            OPBFCPP                            */
/*  PFS INVENTORY ADJUSTMENT    PPBGCPP                            */
/*  PROCESS REQUEST PARMS       PME2REP                            */
/*  WAREHOUSE                   CAADREP                            */
/*  WF ITEM POSITION BALANCE    PME0CPP                            */
/*                                                                 */
/*  FILES THAT ARE CREATED DURING THIS PROCESS.                    */
/*  WF ITEM POSITION BALANCE    PME0CPP                            */
/*  LOAD SCHDLNG PSTN KEY       PDMJCPP                            */
/*  LOAD SCHDLNG POSITION       PDMHCPP                            */
/*                                                                 */
/*  UPDATE THE ORDER GRADE                                         */
/*  LOAD HEADER                 OMFJCPP                            */
/*                                                                 */
/*  PARAMETERS RECEIVED:        NONE                               */
/*                                                                 */
/*  SPECIAL PROCESSING....                                         */
/*  SEE IF FILES ARE IN USE IN THE PRKFHST                         */
/*  NOT IN USE THEN COPY FILE FROM PRKFLIB TO PRKFHST              */
/*  CURRENTLY ONLY FILE IN NEED IS THE PDMHCPP - LOAD SCHEDULE     */
/*  ALCOBJ FOR 10 SECONDS TRY 3 TIMES                              */
/*  POSITION DATA BASE.                                            */
/*******************************************************************/
/*  MODIFICATION HISTORY                                           */
/*---------------------------------------------------------------- */
/*  04/15/2009 SUSAN MASON  - ERROR ON BB, ON THE CLRPFM           */
/*                            COULD MONMSG FOR THE ERROR           */
/*                            BUT MUST NOT CLEAR ANY OF THE FILES  */
/*                            UNTIL ALL 3 OF THE FILES BEING       */
/*                            CLEARED IN THE PRODUCTION CAN BE     */
/*                            ALLOCATED.                           */
/*                                                                 */
/*           CLRPFM     FILE(*LIBL/PDMJCPP)    KEY FILE            */
/*           CLRPFM     FILE(*LIBL/PME0CPP)    SUMMARY             */
/*           CLRPFM     FILE(*LIBL/PDMHCPP)    LD POSITION         */
/*                                                                 */
/*  05/19/2009 SUSAN MASON  - DUE TO PDMHCPP NOT GETTING BUILT     */
/*                            WE ARE ADDING A PRINT ON THE         */
/*                            WRKOBJLCK PDMHCPP IN PRKFHST         */
/*                            SO WE CAN SEE MORE INFORMATION.      */
/*                                                                 */
/*  05/19/2009 SUSAN MASON  - DUE TO PDMHCPP NOT GETTING BUILT     */
/*                            WE ARE TAKING THE WAIT ON THE        */
/*                            ALLOCATION FROM 10 SECONDS TO 20     */
/*                            SECONDS                              */
/*                                                                 */
/*  01/06/2014 LARA BUSER   - ADDED CPF1085 AND CPF9999 TO THE     */
/*                            MONMSG ON ALL ALCOBJ COMMANDS TO     */
/*                            PREVENT AN ERROR MSG TO QSYSOPR      */
/*                                                                 */
/*  10/11/2018 BRAD BADEN   - Added a call to program PUJSXFR at   */
/*                            the very beginning to determine if   */
/*                            this program running on a day and    */
/*                            time that may interfere with other   */
/*                            jobs.                                */
/*  09/28/2021 R CENTONZE   - DONT RUN WHEN THE MORNING ALLOCATION IS */
/*                            SO CHECK COMP VALUE 360 PPSBUILD     */
/*******************************************************************/
/*                                                                 */
/*******************************************************************/
 PMB8XCLP:   PGM

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)

/* Parameters for program PUJSXFR                                    */
             DCL        VAR(&COMPANY) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&CMPVLDAY) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMPVLFTM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMPVLTTM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TIMEFLAG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PPSBLD) TYPE(*CHAR) LEN(1)

/*******************************************************************/
/* Create Duplicate verion of file to QTEMP.                       */
/*  Pull the data from the physical only.                          */
/*******************************************************************/

             DCL        VAR(&DATA) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATAH) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATAS) TYPE(*CHAR) LEN(10)

             MONMSG     MSGID(CPF9999)

/*********************************************************************/
/* Call program to check the day and time range that this progran    */
/* is not allowed to run.  The parameters for this function are:     */
/*   1. Return Code              7A  blank                           */
/*   2. Company Number           3.0 If not for a specific Company,  */
/*                                   use Company Number 360.         */
/*   3. Company Value-Day       10A  This is the Company Value       */
/*                                   name for the Day of the Week    */
/*                                   that the From and To times      */
/*                                   that the From and To times      */
/*   4. Company Value-From Time 10A  This is the Company Value       */
/*                                   for the Beginning Time that     */
/*                                   this program is not allowed     */
/*                                   to execute.                     */
/*   5. Company Value-To Time   10A  This is the Company Value       */
/*                                   for the Ending Time that this   */
/*                                   program is not allowed to       */
/*                                   execute.                        */
/*   6. Within Day and Time      1A  This value is retuned from the  */
/*                                   program.  a value of 'Y'        */
/*                                   indicates that the current day  */
/*                                   and time falls on the day and   */
/*                                   and within the time range of    */
/*                                   the Company Values.             */
/*********************************************************************/

/* Set Company to 360 since this program is not company specific     */
             CHGVAR     VAR(&COMPANY) VALUE(360)

/* Set Day Company Values specific to this job                       */
             CHGVAR     VAR(&CMPVLDAY) VALUE('PFSLDSCHDY')
             CHGVAR     VAR(&CMPVLFTM) VALUE('PFSLDSCHFT')
             CHGVAR     VAR(&CMPVLTTM) VALUE('PFSLDSCHTT')

             CALL       PGM(PUJSXFR) PARM(' ' &COMPANY &CMPVLDAY +
                          &CMPVLFTM &CMPVLTTM &TIMEFLAG)
             IF         COND(&TIMEFLAG *EQ 'Y') THEN(GOTO +
                          CMDLBL(END))
/*********************************************************************/
/* DONT RUN WHEN MORNING ALLOCATION IS RUNNING  9/28/21                  */
             CALL       PGM(POMTXFR) PARM(' ' X'360F' 'PPSBUILD ' &PPSBLD)
             IF         COND(&PPSBLD   *EQ 'Y') THEN(GOTO +
                          CMDLBL(END))
/*********************************************************************/

/*           GET THE DATA LIBRARY                                  */
             CALL       PGM(CAQXXFR) PARM(' ' &DATA &DATAH)
/*           GET THE USER WORK LIBRARY                             */
             CALL       PGM(PMD9XFR) PARM(' ' &DATAS)

             RTVJOBA    USER(&USER)

CLRFLS1:
             ALCOBJ     OBJ((*LIBL/PDMJCPP *FILE *EXCL)) WAIT(10)
             MONMSG     MSGID(CPF1002 CPF1085 CPF9999) EXEC(DO)
             SNDMSG     MSG('# 1 PDMJCPP file in use in ' *cat +
                          &DATA *cat '. Cannot clear the +
                          file') TOUSR(&USER)
             GOTO       CMDLBL(ENDCLRFLS1)
             ENDDO

CLRFLS2:
             ALCOBJ     OBJ((*LIBL/PME0CPP *FILE *EXCL)) WAIT(10)
             MONMSG     MSGID(CPF1002 CPF1085 CPF9999) EXEC(DO)
             SNDMSG     MSG('# 2 PME0CPP file in use in ' *cat +
                          &DATA *cat '. Cannot clear the +
                          file') TOUSR(&USER)
             GOTO       CMDLBL(ENDCLRFLS2)
             ENDDO

CLRFLS3:
             ALCOBJ     OBJ((*LIBL/PDMHCPP *FILE *EXCL)) WAIT(10)
             MONMSG     MSGID(CPF1002 CPF1085 CPF9999) EXEC(DO)
             SNDMSG     MSG('# 3 PDMHCPP file in use in ' *cat +
                          &DATA *cat '. Cannot clear the +
                          file') TOUSR(&USER)
             GOTO       CMDLBL(ENDCLRFLS3)
             ENDDO

CLRFLS4:
             CLRPFM     FILE(*LIBL/PDMJCPP) /* KEY FILE  */
             CLRPFM     FILE(*LIBL/PME0CPP) /* SUMMARY   */
             CLRPFM     FILE(*LIBL/PDMHCPP) /* LD POSITION */

             CALL       PGM(PMCVXFR) PARM(' ' '0000000')

DATAFL1:
             ALCOBJ     OBJ((&DATAS/PDMHCPP *FILE *EXCL)) WAIT(20)
             MONMSG     MSGID(CPF1002 CPF1085 CPF9999) EXEC(DO)
             WRKOBJLCK  OBJ(&DATAS/PDMHCPP) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)
             SNDMSG     MSG('# 1 PDMHCPP file is in use in ' *BCAT +
                          &DATAS *BCAT 'Library') TOUSR(&USER)
             GOTO       CMDLBL(DATAFL2)
             ENDDO

             GOTO       CMDLBL(CPYFFLEA)
DATAFL2:
             ALCOBJ     OBJ((&DATAS/PDMHCPP *FILE *EXCL)) WAIT(20)
             MONMSG     MSGID(CPF1002 CPF1085 CPF9999) EXEC(DO)
             WRKOBJLCK  OBJ(&DATAS/PDMHCPP) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)
             SNDMSG     MSG('# 2 PDMHCPP file is in use in ' *BCAT +
                          &DATAS *BCAT 'Library') TOUSR(ISSMASO)
             GOTO       CMDLBL(DATAFL3)
             ENDDO

             GOTO       CMDLBL(CPYFFLEA)
DATAFL3:
             ALCOBJ     OBJ((&DATAS/PDMHCPP *FILE *EXCL)) WAIT(20)
             MONMSG     MSGID(CPF1002 CPF1085 CPF9999) EXEC(DO)
             WRKOBJLCK  OBJ(&DATAS/PDMHCPP) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)
             SNDMSG     MSG('# 3 PDMHCPP file is in use in ' *BCAT +
                          &DATAS *BCAT 'Library') TOUSR(ISSMASO)
             GOTO       CMDLBL(END)
             ENDDO

CPYFFLEA:
             CPYF       FROMFILE(&DATA/PDMHCPP) +
                          TOFILE(&DATAS/PDMHCPP) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             SNDMSG     MSG('PDMHCPP file is in use in ' *BCAT +
                          &DATAS *BCAT 'Library') TOUSR(ISSMASO)
             GOTO       CMDLBL(END)
             ENDDO

/*   PRODUCTION LIBRARY */
             DLCOBJ     OBJ((*LIBL/PDMJCPP *FILE *EXCL))
             DLCOBJ     OBJ((*LIBL/PME0CPP *FILE *EXCL))
             DLCOBJ     OBJ((*LIBL/PDMHCPP *FILE *EXCL))

/*   PRKFHST            */
             DLCOBJ     OBJ((&DATAS/PDMHCPP *FILE *EXCL))

             /* UPDATE DATE IN PME2REP LAST CPYF DATE   */
             CALL       PGM(PMEBXFR) PARM(' ')
             GOTO       CMDLBL(END)

 TRYLATER:

 ENDCLRFLS3:
             DLCOBJ     OBJ((*LIBL/PME0CPP *FILE *EXCL))
 ENDCLRFLS2:
             DLCOBJ     OBJ((*LIBL/PDMJCPP *FILE *EXCL))

 ENDCLRFLS1:

 END:        ENDPGM
