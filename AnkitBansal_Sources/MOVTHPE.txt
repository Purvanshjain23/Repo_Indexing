/***************************************************************************/
/*                                                                         */
/* THIS PROGRAM WILL CREATE A LIBRARY & LOAD IT WITH ALL CHANGED OBJECTS   */
/* SINCE THIS WAS LAST RAN                                                 */
/*                                                                         */
/* PARAMETERS:                                                             */
/*             &ACTION = Y=PRINT LISTING & BUILD LIBRARY    (REQUIRED)     */
/*                       N=PRINT LISTING ONLY               (REQUIRED)     */
/*             HPEMDL    = SYNON MODEL LIBRARY              (REQUIRED)     */
/*             HPEGEN    = SYNON OBJECT GENERATION LIBRARY  (REQUIRED)     */
/*                                                                         */
/* OBJECT LISTS USED =  HPEGNLOBJL = LIST OF CURRENT OBJECTS IN LIBRARY    */
/*                      HPELSSTAT   = LAST RUN OBJECT LIST                  */
/*                      HPELSSTATB  = COPY OF LAST RUN PRIOR TO NEW RUN     */
/*                      HPETAKE       = FINAL LIST CREATED OF TAKE OBJECTS    */
/*                                                                         */
/* MEMBER LISTS USED =  HPEGNLOBJL = LIST OF CURRENT OBJECT MBRS           */
/*                      HPETAKE       = FINAL LIST CREATED OBJ MBRS           */
/*                                                                         */
/***************************************************************************/
/*                                                                         */
             PGM        PARM(&ACTION &SOURCE &MENU)
/*                                                                         */
             DCL        VAR(&ACTION) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SOURCE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MENU) TYPE(*CHAR) LEN(1)
             MONMSG     MSGID(CPF0000)

             IF         COND(&ACTION *NE 'N' *AND &ACTION *NE 'Y') +
                          THEN(CHGVAR VAR(&ACTION) VALUE('N'))
             IF         COND(&SOURCE *NE 'N' *AND &SOURCE *NE 'Y') +
                          THEN(CHGVAR VAR(&SOURCE) VALUE('N'))
             IF         COND(&MENU *NE 'N' *AND &MENU *NE 'Y') +
                          THEN(CHGVAR VAR(&MENU) VALUE('N'))

                        ADDLIBLE   LIB(Y2SY)
                        ADDLIBLE   LIB(Y1SY)

             R          LIBLST(HPEMDL)

             IF         COND(&ACTION *EQ 'Y') THEN(DO)
                  CHKOBJ     OBJ(QSYS/INSTALLHPE) OBJTYPE(*LIB)
                  MONMSG     MSGID(CPF0000) EXEC(DO)
                  ENDDO
/*---------------------------------------------------------------------    */
/*  Clear Library for moving objects                                       */
/*---------------------------------------------------------------------    */
 /* 01 */    CLRLIB     LIB(INSTALLHPE) /* */
             MONMSG     MSGID(CPF0000) /* */
 PASS1:      ENDDO
/*---------------------------------------------------------------------    */
/*  Update Status Field Valid Values DBF                                   */
/*---------------------------------------------------------------------    */
             IF         COND(&ACTION *EQ 'Y') THEN(DO)
 /* 02 */    YCVTCNDVAL MDLLIB(HPEMDL) GENLIB(HPEGEN)
 /* 03 */ /* YCVTMDLMSG MDLLIB(HPEMDL) GENLIB(HPEGEN) */
             ENDDO

/*---------------------------------------------------------------------    */
/*  Build Current List                                                     */
/*---------------------------------------------------------------------    */
 /* 04 */    YBLDOBJLST OBJ(HPEGEN/*ALL) OBJLST(HPEGEN/HPEGNLOBJL)
/*---------------------------------------------------------------------    */
/*  Make Backup Copy of List from Last Run                                 */
/*---------------------------------------------------------------------    */
 /* 05 */    YCPYLST    LSTTYPE(*OBJ) FROMLST(HPEGEN/HPELSSTAT) +
                          TOLST(HPEGEN/HPELSSTATB) /* */
/*---------------------------------------------------------------------    */
/*  Make Working Copy of List from Last Run                                */
/*---------------------------------------------------------------------    */
 /* 06 */    YCPYLST    LSTTYPE(*OBJ) FROMLST(HPEGEN/HPELSSTAT) +
                          TOLST(QTEMP/TEMPLST) /* */
/*---------------------------------------------------------------------    */
/*  Remove CAVLLSP to Force New Condition List to go                       */
/*---------------------------------------------------------------------    */
 /* 07 */    YFLTOBJLST FILTER(*OMIT) OBJ((CAVLLSP)) /* */
/*---------------------------------------------------------------------    */
/*  Remove QUSRMSG to Force New Message File to go                         */
/*---------------------------------------------------------------------    */
             YFLTOBJLST FILTER(*OMIT) OBJ((QUSRMSG)) /* */
/*---------------------------------------------------------------------    */
/*  Generate HPETAKE List from Changes                                        */
/*  This operation will compare the new list (HPEGNLOBJL) to the old       */
/*  old list (TEMPLST) and create list HPETAKE which will contain all the     */
/*  the objects which have been changed since the old list was built       */
/*---------------------------------------------------------------------    */
 /* 08 */    YOPRLST    LSTTYPE(*OBJ) LSTA(HPEGEN/HPEGNLOBJL) +
                          LSTOPR(*SUB) LSTB(QTEMP/TEMPLST) +
                          TOLST(HPEGEN/HPETAKE) IGNLIB(*YES) +
                          IGNCRTDTE(*NO) IGNCHGDTE(*NO)

/* DHM THIS IS TEMPORARY...REMOVE BELOW...                         */

/*           YFLTOBJLST FILTER(*SELECT) DATE(*CRT *GT 090995) +
                   OBJLST(HPEGEN/HPEGNLOBJL) OUTLST(HPEGEN/HPETAKE)   */

/* DHM THIS IS TEMPORARY...REMOVE ABOVE...                         */


/*---------------------------------------------------------------------    */
/*  Remove all Q* *PHY *FILES from HPETAKE List                               */
/*---------------------------------------------------------------------    */
 /* 09 */    YFLTOBJLST FILTER(*OMIT) OBJ((Q*)) OBJTYPE(*FILE) +
                          OBJATR(*PHY) OBJLST(HPEGEN/HPETAKE)
/*---------------------------------------------------------------------    */
/*  Remove all OTHER DESIGNATED FILES                                      */
/*---------------------------------------------------------------------    */
 /* 10 */    YFLTOBJLST FILTER(*OMIT) OBJ((CARVCNR)) OBJTYPE(*PGM) +
                          OBJATR(*RPG) OBJLST(HPEGEN/HPETAKE)
             YFLTOBJLST FILTER(*OMIT) OBJ((CAVLLSR)) OBJTYPE(*PGM) +
                          OBJATR(*RPG) OBJLST(HPEGEN/HPETAKE)
             YFLTOBJLST FILTER(*OMIT) OBJ((CAVLLWR)) OBJTYPE(*PGM) +
                          OBJATR(*RPG) OBJLST(HPEGEN/HPETAKE)
             YFLTOBJLST FILTER(*OMIT) OBJ((CAVLLSR#)) OBJTYPE(*FILE) +
                          OBJATR(*DSPF) OBJLST(HPEGEN/HPETAKE)
             YFLTOBJLST FILTER(*OMIT) OBJ((CAVLLWR#)) OBJTYPE(*FILE) +
                          OBJATR(*DSPF) OBJLST(HPEGEN/HPETAKE)
             YFLTOBJLST FILTER(*OMIT) OBJ((Y2CFPMR)) OBJTYPE(*ANY) +
                          OBJATR(*ANY) OBJLST(HPEGEN/HPETAKE)
/* ADD */
             YFLTOBJLST FILTER(*OMIT) OBJ((AP*)) OBJTYPE(*ANY) +
                          OBJATR(*ANY) OBJLST(HPEGEN/HPETAKE)
/*---------------------------------------------------------------------    */
/*  Build Member List for Source from HPETAKE List                            */
/*---------------------------------------------------------------------    */
      IF (&ACTION *EQ 'Y' *AND &SOURCE *EQ 'Y') DO
 /* 11 */    YCVTOBJLST OBJLST(HPEGEN/HPETAKE) MBRLST(HPEGEN/HPETAKE) +
                          SRCFILE(*OBJSRC)
/*---------------------------------------------------------------------    */
/*  Build Current Member List from Source Files                            */
/*---------------------------------------------------------------------    */
 /* 12 */    YBLDMBRLST FILE(HPEGEN/Q*) MBRLST(HPEGEN/HPEGNLOBJL)
/*---------------------------------------------------------------------    */
/*  Limit HPETAKE Member List to Items for Which Source can be Found          */
/*---------------------------------------------------------------------    */
 /* 13 */    YOPRLST    LSTTYPE(*MBR) LSTA(HPEGEN/HPETAKE) +
                          LSTOPR(*INTERSECT) LSTB(HPEGEN/HPEGNLOBJL) +
                          IGNLIB(*YES)
      ENDDO

/*---------------------------------------------------------------------    */
/*  Build Source Files in Distribution Library                             */
/*---------------------------------------------------------------------    */
      IF (&ACTION *EQ 'Y' *AND &SOURCE *EQ 'Y') DO
 /* 14 */    YCRTSRCPF  LIB(INSTALLHPE)
             DLTF       FILE(INSTALLHPE/QCBLSRC)
             DLTF       FILE(INSTALLHPE/QLBLSRC)
             DLTF       FILE(INSTALLHPE/QQMFORMSRC)
             DLTF       FILE(INSTALLHPE/QQMQRYSRC)
             DLTF       FILE(INSTALLHPE/QREXSRC)
             DLTF       FILE(INSTALLHPE/QUDSSRC)
             DLTF       FILE(INSTALLHPE/YCB2SRC)
             DLTF       FILE(INSTALLHPE/YEXPSRC)
             DLTF       FILE(INSTALLHPE/Y1PFSRC)
             DLTF       FILE(INSTALLHPE/YRCSRC)
      ENDDO
/*---------------------------------------------------------------------    */
/*  Copy Source to Distribution Library                                    */
/*---------------------------------------------------------------------    */
      IF (&ACTION *EQ 'Y' *AND &SOURCE *EQ 'Y') DO

 /* 15 */    YEXCMBRLST RQSDTA('CPYF &L/&F INSTALLHPE/&F FROMMBR(&M) +
                          TOMBR(*FROMMBR) MBROPT(*REPLACE) FMTOPT(*MAP  )') +
                          MBRLST(HPEGEN/HPETAKE) /* */
      ENDDO
/*---------------------------------------------------------------------    */
/*  HPETAKE Help Text if Found                                                */
/*---------------------------------------------------------------------    */
 /* 16 */    YFLTMBRLST SEUTYPE(DSPF DSPF38) MBRLST(HPEGEN/HPETAKE) +
                          OUTLST(QTEMP/VWORK)
      IF (&ACTION *EQ 'Y') DO
             YEXCMBRLST RQSDTA('CPYF &L/QPNLSRC INSTALLHPE/QPNLSRC +
 /* 17 */                 FROMMBR(&M) TOMBR(*FROMMBR) +
                          MBROPT(*REPLACE) FMTOPT(*MAP  )') MBRLST(VWORK) +
                          ERRLVL(*NOMAX) /* */
      ENDDO

/*---------------------------------------------------------------------    */
/*  Build Menu Source File if &MENU = Y                                    */
/*---------------------------------------------------------------------    */
/*---------------------------------------------------------------------    */
/*  Remove *PHY and *LGL U* *File entries from HPETAKE list                   */
/*---------------------------------------------------------------------    */

 /* 19 */    YFLTOBJLST FILTER(*OMIT) OBJ((U*)) OBJATR(*PHY *LGL) +
                          OBJLST(HPEGEN/HPETAKE)
/*---------------------------------------------------------------------    */
/*  Print Finished HPETAKE List                                               */
/*---------------------------------------------------------------------    */

 /* 20 */    YDOCOBJLST OBJLST(HPEGEN/HPETAKE) DETAIL(*FULL) /* */
/*---------------------------------------------------------------------    */
/*  CALL OBJECT LIST PRINT PROGRAMS                                        */
/*---------------------------------------------------------------------    */
             OVRPRTF    FILE(XXA0PFR$) OUTQ(PRT01) SAVE(*YES)
             OVRPRTF    FILE(XXA1PFR$) OUTQ(PRT01) SAVE(*YES)
             CALL       PGM(HPEOLSLD) PARM(' ')
/*---------------------------------------------------------------------    */
/*  Duplicate All New Objects Except U* *PHY and *LGL Files                */
/*---------------------------------------------------------------------    */
      IF (&ACTION *EQ 'Y') DO

 /* 21 */    YCRTDUPOBJ OBJ(*O) FROMLIB(*O) OBJTYPE(*ALL) +
                          TOLIB(INSTALLHPE) CRTOPT(*ALL) +
                          NEWOWN(*LST) OBJLST(HPEGEN/HPETAKE) /* */
             YBLDOBJLST OBJ(INSTALLHPE/*ALL) OBJTYPE(*MENU)
 /* 22 */    YEXCOBJLST RQSDTA('CHGMNU MENU(INSTALLHPE/&O) +
                          DSPF(*LIBL/&O) MSGF(*LIBL/&O)')
 /* 23 */    YCPYLST    LSTTYPE(*OBJ) FROMLST(HPEGEN/HPEGNLOBJL) +
                          TOLST(HPEGEN/HPELSSTAT) /* */
/*                                                                         */
/*---------------------------------------------------------------------    */
/*  Save Current List for Next Run                                         */
/*---------------------------------------------------------------------    */
 END:        ENDDO

             ENDPGM
