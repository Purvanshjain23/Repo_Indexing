/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4048CL                                      */
/*  PROGRAM NAME:    LISTING OF REPORTED WEEKLY ACTIVITY           */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     09/26/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4048-WORK WITH REPORTED WEEKLY ACTIVITY.    */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(76)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDCDYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDCDWK) TYPE(*DEC) LEN(2)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 6 7))
             CHGVAR     VAR(&LDCDYR) VALUE(%SST(*LDA 20 4))
             CHGVAR     VAR(&LDCDWK) VALUE(%SST(*LDA 24 2))


/*------------------------------------------------------------------*/
/* BUILD QUERY OVER REPORTED WEEKLY ACTIVITY                        */
/*------------------------------------------------------------------*/

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('RWHGSN *NE 0')

/*  IF GROUP                                                     */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND RWHGSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 30 7)) VALUE(&LDHGSN)
             ENDDO

/*  IF CALENDAR YEAR                                             */

             IF         COND(&LDCDYR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 38 15)) VALUE('*AND RWCDYR +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 54 4)) VALUE(&LDCDYR)
             ENDDO

/*  IF CALENDAR WEEK                                             */

             IF         COND(&LDCDWK *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 59 15)) VALUE('*AND RWCDWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 75 2)) VALUE(&LDCDWK)
             ENDDO

/*  RUN QUERY OVER REPORTED WEEKLY ACTIVITY FILE                 */

             OVRDBF     FILE(HSP102)  SHARE(*YES)
             OPNQRYF    FILE((HSP102))  QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  CREATE/BUILD WORKFILE                                       */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3048) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP3048)

             CLOF       OPNID(HSP102)
             DLTOVR     FILE(*ALL)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD PRINT AT 10 CPI:  SO, ON A LASER PRINTER */
/* IT CAN PRINT 'PORTRAIT' ON LETTER PAPER                          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(110) LANDSCAPE(*NO) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  CALL LISTING PROGRAM                                             */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6048)

             DLTOVR     FILE(*ALL)

             ENDPGM

