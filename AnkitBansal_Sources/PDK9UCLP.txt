/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR LOAD SCHEDULING   */
/*             POSITION REPORT. PARMS CO,DATES, PRINT Y/N      */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT LOAD SCHEDULING POSITION REPORT             */
/*  DATE      - 03/12/98                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FRMDATN    - FROM SHIP DATE                            */
/*          &THRDATN    - THROUGH SHIP DATE                         */
/*          &STSYN      - PRINT REPORT Y/N                         */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/********************************************************************/
/* MODIFIED BY:  R CENTONZE  7/27/98 ADDED A SUMMARY REPORT OVRPRTF */
/*                                                                  */
/********************************************************************/

 PDK9UCLP:   PGM        PARM(&COMPNYN  &THRDATN +
                          &STSYN &OUTQ &HOLD &SAVE &COPYN)

/* PARMS                                                                      */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &THRDATN    *DEC                  /* THRU SHIP DATE (PASSED)   */
        DCL    &STSYN      *CHAR   (1)           /* PRINT REPORT Y/N          */
        DCL    &DESC       *CHAR   (25)          /* DESCRIPTION               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &COMMAND    *CHAR   (512)

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &THRDAT     *DEC    (7 0)         /* THRU SHIP DATE            */
        DCL    &USER       *CHAR   (10 )         /* USER                      */

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/

             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&THRDAT) VALUE(&THRDATN)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)


/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/

             IF COND(&STSYN = 'Y') THEN(DO)
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDK7PFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDSEPFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
             ENDDO

/*********************************************************************/
/*   CALL XF TO BUILD THE FILE AND PRINT THE REPORT                  */
/*********************************************************************/

             CLRPFM     FILE(*LIBL/PDMJCPP) /* KEY FILE  */

             ALCOBJ     OBJ((PDMHCPP *FILE *EXCL)) WAIT(15)
INUSE:       MONMSG     MSGID(CPF1002) EXEC(DO)
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('Cannot refresh data. The file is in +
                          use.') TOMSGQ(&USER)
             GOTO       CMDLBL(END)
             ENDDO
             CLRPFM     FILE(*LIBL/PDMHCPP) /* FILE WITH CORRECT SEQ +
                          NBRS */
             CALL       PGM(PDLAXFR) PARM(' ' &COMPANY +
                          &THRDAT &STSYN)
             DLCOBJ     OBJ((PDMHCPP *FILE *EXCL))

 END:        ENDPGM
