      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3049
      * TITLE:       Build Workfile: Crt PLCICPP from HSP068DL
      * PROGRAMMER:  Susan Blakey
      * CREATED:     02/02/16
      *
      * FUNCTION:  Batch program to build workfile for WK transactions
      *            from HSP068 to HPEDEVGEN - PLCICPP
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE
      *
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
     Fhsl068m   if   e           k disk
      *    Killed/dead hogs by business office, and dead date.
      *
     Fplcicpl2  if   e           k disk
      *    Wf to report CF Killed/Dead Hogs (HPEDEVGEN)
      *    only specified here to get the flds to pass to another
      *    program that will create the record for this file
      *
     Fhsl034d   If   E           K disk
      *    Hog group
      *
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      * End of File Specifications
      ****************************************************************
      * Declared stand-alone variables
      ****************************************************************
     D  P0RTN          S              7
     D  xxreturn       S              7
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
      ****************************************************************
      * FILE: hsp068 SAVE or work fields
      ****************************************************************
      *
     D XDMVSN          s                   like(KDMVSN)                         MOVEMENT NUMBER
     D XDMSCD          s                   like(KDMSCD)                         MOVEMENT STATUS CODE
     D XDEPDT          s                   like(KDEPDT)                         EOP POSTED DATE
     D XDFSBO          s                   like(KDFSBO)                         FARM SITE BUS OFF
     D XSFSCD          s                   like(KDFSCD)                         FARM SITE CODE
     D XDHGSN          s                   like(KDHGSN)                         SYSTEM HOG GROUP NO
     D XDKDDT          s                   like(KDKDDT)                         DEAD DATE
     D XDKDHD          s                   like(KDKDHD)                         DEAD HEAD
     D XDKDLB          s                   like(KDKDLB)                         DEAD POUNDS
     D XDDRCD          s                   like(KDDRCD)                         DEATH/DISEASE REASON
     D XDDSFL          s                   like(KDDSFL)                         DESTROYED FLAG
      ****************************************************************
      * FILE: plcicpp save or use as fields into next program
      ****************************************************************
     D ZIXXA1          s                   like(CIXXA1)                         WFS Killed/Dead Hogs
     D ZIRVCD          s                   like(CIRVCD)                         WFS Group Number
     D ZIBST1          s                   like(CIBST1)                         WFS Site Number
     D ZIBTT1          s                   like(CIBTT1)                         WFS Barn
     D ZIBUT1          s                   like(CIBUT1)                         WFS Transaction Date
     D ZIXZA1          s                   like(CIXZA1)                         WFS Head Quantity
     D ZIBQT1          s                   like(CIBQT1)                         WFS Reason
     D ZIBRT1          s                   like(CIBRT1)                         WFS Ref Number
     D ZIHJDT          s                   like(CIHJDT)                         WFS Call Date
     D ZIB3TM          s                   like(CIB3TM)                         WFS Call Time
     D ZIBVT1          s                   like(CIBVT1)                         WFS Business Office
     D ZIX1A1          s                   like(CIX1A1)                         WFS system hog grp#
     D ZIBWT1          s                   like(CIBWT1)                         WFS Year
     D ZIX2A1          s                   like(CIX2A1)                         WFS Week
     D ZIHKDT          s                   like(CIHKDT)                         WFS Week Beginning
     D ZIBYT1          s                   like(CIBYT1)                         WFS Week Ending
      *
      ****************************************************************
      * input parms
      ****************************************************************
      *
     D ibuso           s              5
     D icono           s              3  0
     D iyear4          s              4  0
     D iyearwk         s              2  0
      *
     D xxbuso          s                   like(ibuso)
     D xxcono          s                   like(icono)
     D xxYEAR4         s                   like(iyear4)
     D xxYEARWK        s                   like(iyearwk)
      ****************************************************************
      * MAINLINE
      ****************************************************************
      * Entry parameters
      * Input the Business Office and the Yesr like 2016
      * and the week like 49.
      *
     C     *ENTRY        PLIST
     C                   PARM                    P0RTN
     C                   PARM                    ibuso
     C                   PARM                    iyear4
     C                   PARM                    iyearwk
     C                   PARM                    wkbeging          8 0
     C                   PARM                    wkending          8 0
      *
     C                   z-add     *ZERO         ZIXXA1                         WFS Week Ending
      *
      * Use company Guymon so we can get general files using company 360
      * all of the data that we will be using for CF WILL use company 360
      * for other standard files - this one is the calendar.
      *
     C                   MOVE      '360'         icono
      *
      * Check for existance "a" record(this will be in qtemp but if run)
      * without qtemp then remove the records ) also using to get the flds)
      * which will be to move the fields or rules on the field to the
      * work file PLCICPP
      *
      * Read WF and verify if there are any records is so then get rid of them
      *
      * Define keylist
     C     WFSSA         KLIST
     C                   KFLD                    xxseqn            5 0           Sequence Number
      * Setup key
     C                   Z-ADD     0             xxseqn                         Company number
     C     WFSSA         setll     plcicpl2
      *
     C                   read      plcicpl2                               89
     C                   if        *in89 = *off                                 Not end of file
     C                   exsr      $dltwrk
     C                   endif
      *
      * calculate the from date and the to date based on the first day of the
      * week and the 7th day of the week for the ending date.
      * Pass in Business Office and then byh Date - From and to Date
      * EQUAL TO business office and the date is within
      *
      * Setup key
     C                   Z-ADD     icono         xxcono                         Company number
     C                   Z-ADD     iyear4        xxyear4                        HPS Calendar Ye
     C                   Z-ADD     iyearwk       xxyearwk                       HPS Calendar We
     C****               Z-ADD     bccyymmdd     wkbeging          8 0          Pass Out WK Beg
     C                   Z-ADD     wkbeging      bccyymmdd         8 0          Pass Out WK Beg
      * EQUAL TO business office and the date is within
      * Begin Week Date and the Ending Week Date
      *
     C****               EXSR      $Dtrang
     C****               Z-ADD     eccyymmdd     wkending          8 0          Pass Out WK End
     C                   Z-ADD     wkending      eccyymmdd         8 0          Pass Out WK End
      *
      * EQUAL TO business office and the date is within
      * Begin Week Date
     C     KRSSA         KLIST
     C                   KFLD                    kdfsbo                          Business Office
     C                   KFLD                    kdkddt                          dead date
      * Setup key
     C                   movel     ibuso         kdfsbo                         Business Office
     C                   Z-ADD     bccyymmdd     kdkddt                          dead date
      *
      *
      *
      * Read the detail information from HSl068m
      * Select on Business Office and then by range of Beg Week and End Week.
      *
     C     KRSSA         setll     hsl068m
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl068m                                90
     C                   if        *in90 = *off                                 If not EOF
      *     Select on business office and beg range and ending range dates
     C                   if        ibuso  =  kdfsbo                             Business Off
     C                   if        kdkddt   >=  bccyymmdd                       Range of dates
     C                   if        kdkddt   <=  eccyymmdd                       Range of dates
      *
     C                   exsr      $bldwrk

     C                   endif
     C                   endif                                                  If not EOF
     C                   endif
      *
     C                   endif
      *
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      *---------------------------------------------------------------
      *  Use SYNON program to validate date
      *  Call synon program from OMSDEVGEN
      *  pass in the year and week, and get the beg and end day of the
      *  week, the 1st day of the week is sunday and the 7th day of
      *  week will be a saturday week ending date.
      *---------------------------------------------------------------
      *
     C     $Dtrang       begsr
      *
     C                   call      'PBV3XFR'
     C                   parm      *blank        xxreturn
     C                   parm                    xxcono
     C                   parm                    xxyear4
     C                   parm                    xxyearwk
     C                   parm                    hpswkbeg          7 0          HPS Beg Week
     C                   parm                    hpswkend          7 0          HPS End Week
     C                   parm                    bccyymmdd         8 0          HPS Beg Week
     C                   parm                    eccyymmdd         8 0          HPS End Week
      *
     C                   endsr
      *---------------------------------------------------------------
      *  Use SYNON program to create data into the SYNON FILE PLCICPP
      *  Call synon program from OMSDEVGEN
      *---------------------------------------------------------------
      *
     C     $bldwrk       begsr
      *
     C                   exsr      $movwrk
      *
     C                   endsr
      *---------------------------------------------------------------
      *  Move data fields from HSP068 into the plcicpp
      *---------------------------------------------------------------
      *
     C     $movwrk       begsr
      *
     C     KDHGSN        chain(n)  hsl034d                            92
     C                   if        *in92 = *off                                 If already exists
     C                   movel     HGHGCD        ZIRVCD                         Group number
     C                   else
     C                   movel     *blanks       ZIRVCD                         Group number
     C                   endif
      *
     C     KDFSCD        CHAIN(N)  HSP018                             92
     C                   if        *in92 = *off                                 If already exists
     C                   movel     FSMTECHREF    ZIBST1                         WFS Site Number
     C                   else                                                   get the mtechref no
     C                   movel     *blanks       ZIBST1                         WFS Site Number
     C                   endif
      *
     C*** move each individaul fld frm HSP068
     C
     C                   move      *blanks       ZIBTT1                         WFS Barn
     C                   movel     eccyymmdd     ZIBUT1                         WFS Transaction Date
     C                   z-add     kdkdhd        ZIXZA1                         WFS Head Quantity
     C                   move      kddrcd        ZIBQT1                         WFS Reason
     C                   move      *blanks       ZIBRT1                         WFS Ref Number
     C                   z-add     kdkddt        ZIHJDT                         WFS Call Date
     C                   z-add     *zeros        ZIB3TM                         WFS Call Time
     C                   movel     ibuso         ZIBVT1                         WFS Business Office
     C                   z-add     kdhgsn        ZIX1A1                         WFS System hog Group
     C                   z-add     iyear4        ZIBWT1                         WFS Year
     C                   z-add     iyearwk       ZIX2A1                         WFS Week
     C                   z-add     bccyymmdd     ZIHKDT                         WFS Week Beginning
     C                   z-add     eccyymmdd     ZIBYT1                         WFS Week Ending
     C     ZIXXA1        ADD       1             ZIXXA1                         WFS Week Ending
     C***
     C                   call      'PLYBXFR'
     C                   parm      *blank        xxreturn
     C                   parm                    ZIXXA1
     C                   parm                    ZIRVCD
     C                   parm                    ZIBST1
     C                   parm                    ZIBTT1
     C                   parm                    ZIBUT1
     C                   parm                    ZIXZA1
     C                   parm                    ZIBQT1
     C                   parm                    ZIBRT1
     C                   parm                    ZIHJDT
     C                   parm                    ZIB3TM
     C                   parm                    ZIBVT1
     C                   parm                    ZIX1A1
     C                   parm                    ZIBWT1
     C                   parm                    ZIX2A1
     C                   parm                    ZIHKDT
     C                   parm                    ZIBYT1
      *
     C                   endsr
      *---------------------------------------------------------------
      *  Use SYNON program to delete data into the SYNON FILE PLCICPP
      *  Call synon program from OMSDEVGEN
      *---------------------------------------------------------------
      *
     C     $dltwrk       begsr
      *
     C                   call      'PLYDXFR'
     C                   parm      *blank        xxreturn
      *
     C                   endsr
