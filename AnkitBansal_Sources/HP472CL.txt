/***************************************************************** */
/*  PROGRAM NUMBER --> HP472CL                                     */
/*  PROGRAM NAME ----> HOG MOVEMENT OUT SUMMARY REPORT             */
/*  PROGRAMMER ------> MARY PALMER                                 */
/*  DATE ------------> 06/27/94                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT THE      */
/*                     HOG MOVEMENT OUT SUMMARY REPORT.            */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/
/*‚06/11/24  Jagdish Mistry (JM-WI628),                            */
/*‚Project 628-RabApp Bypass to Agview                             */
/*‚a. Added parameter to get count for HP370                       */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(204)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDTRANFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSALEFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSTCD) TYPE(*CHAR) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
/*‚JM-WI628-START                                                        */
             DCL        VAR(&TOTALCNT) TYPE(*DEC) LEN(8 0)
/*‚JM-WI628-END                                                          */

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDSTCD) VALUE(%SST(*LDA 37 5))
             CHGVAR     VAR(&LDTRANFL) VALUE(%SST(*LDA 42 1))
             CHGVAR     VAR(&LDSALEFL) VALUE(%SST(*LDA 43 1))

/*-------------------------------------------------------------------*/
/* CREATE WORKFILE                                                   */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP370) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE MOVEMENT EVENT FILE                         */
/*-------------------------------------------------------------------*/

/*  SELECT ALL SALES AND TRANSFER EVENT RECORDS IN THE DATE RANGE    */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MEMEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MEMEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)
             CHGVAR     VAR(%SST(&QDATA 46 40)) VALUE('*AND (MEMTCD +
                          *EQ "T" *OR MEMTCD *EQ "S")')

             OVRDBF     FILE(HSP058) SHARE(*YES)
             OPNQRYF    FILE((HSP058)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  BUILD THE WORKFILE                                               */
/*-------------------------------------------------------------------*/

/*‚JM-WI628-START                                                        */
/*           CALL PGM(HP370)                                             */
             CALL       PGM(HP370) PARM((&TOTALCNT))
/*‚JM-WI628-END                                                          */

             CLOF       OPNID(HSP058)
             DLTOVR     FILE(HSP058)

/*-------------------------------------------------------------------*/
/* SELECT THE APPROPRIATE RECORDS FROM THE WORKFILE                  */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  RECEIVED DATE MUST BE WITHIN USER'S SELECTED DATE RANGE     */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('WFRCDT +
                          *GE ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 16)) VALUE('*AND WFRCDT +
                          *LE ')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE                                               */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND WFORBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO

/*  IF ORIGIN PRODUCTION PHASE                                       */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 70 17)) VALUE('*AND WFORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 87 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 92 1)) VALUE('"')
             ENDDO

/*  IF ORIGIN FARM SITE                                              */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 94 16)) VALUE('*AND WFORFS +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 110 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF ORIGIN PRODUCTION TYPE                                        */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 17)) VALUE('*AND WFORPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 135 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 140 1)) VALUE('"')
             ENDDO

/*  IF SALES TYPE                                                    */

             IF         COND(&LDSTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 142 17)) VALUE('*AND WFCODE +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 159 5)) VALUE(&LDSTCD)
             CHGVAR     VAR(%SST(&QDATA 164 1)) VALUE('"')
             ENDDO

/*  IF OMIT TRANSFERS                                               */

             IF         COND(&LDTRANFL *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 166 19)) VALUE('*AND WFMTCD +
                          *NE "T"')
             ENDDO

/*  IF OMIT SALES                                                   */

             IF         COND(&LDSALEFL *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 186 19)) VALUE('*AND WFMTCD +
                          *NE "S"')
             ENDDO

/*  RUN QUERY                                                    */

             OVRDBF     FILE(HSP370) SHARE(*YES)
             OPNQRYF    FILE((HSP370)) QRYSLT(&QDATA) +
                          KEYFLD((WFORPP) (WFMTCD *DESCEND) (WFCODE) +
                          (WFORFS) (WFDNNM)) SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD 10 CPI ON STANDARD 132 CHARACTER PAPER   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(155) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*-------------------------------------------------------------------*/
/* PRINT THE REPORT                                                  */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP672)

             CLOF       OPNID(HSP370)
             DLTOVR     FILE(*ALL)

             ENDPGM

