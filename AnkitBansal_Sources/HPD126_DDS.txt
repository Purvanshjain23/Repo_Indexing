     A*%%TS  SD  20060824  163010  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD126
     A*  TITLE:         REVISE USER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/13/95
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 63 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 90 IS ON IF THE RECORD SELECTED BY THE USER HAS BEEN
     A*               DELETED BY ANOTHER USER SINCE SELECTED.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90N63                               CF04(04 'PROMPT')
     A N90N63                               CF05(05 'REFRESH')
     A N90N61                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP126R1
     A*%%TS  SD  20060824  163010  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(50)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP126'
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 34'REVISE USER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'User code ..'
     A            DFUSCD    R        B  4 15REFFLD(AUUSCD)
     A  60
     AO 63
     AO 90                                  DSPATR(PR)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N60N63
     A N90                                  DSPATR(HI)
     A N31N60N63
     A N90                                  DSPATR(UL)
     A                                  5  2'User name ..'
     A            DFUSNM    R        B  5 15REFFLD(AUUSNM)
     A  63
     AO 90                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N63N90                            DSPATR(HI)
     A N32N63N90                            DSPATR(UL)
     A                                  6  2'Security location ..'
     A                                  6 23'?'
     A            DFSLCD    R        B  6 25REFFLD(AUSLCD)
     A  63
     AO 90                                  DSPATR(PR)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35N63N90                            DSPATR(HI)
     A N35N63N90                            DSPATR(UL)
     A                                  7  2'Output queue .......'
     A            DFOUTQ    R        B  7 25REFFLD(AUREC/AUOUTQ *LIBL/HSP001)
     A  63
     AO 90                                  DSPATR(PR)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N63N90                            DSPATR(HI)
     A N36N63N90                            DSPATR(UL)
     A                                  9  2'Are there any restrictions on this-
     A                                       user ...'
     A                                  9 46'?'
     A            DFRUFL    R        B  9 48REFFLD(AURUFL)
     A  63
     AO 90                                  DSPATR(PR)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N63N90                            DSPATR(HI)
     A N33N63N90                            DSPATR(UL)
     A                                 10  2'Is this user allowed to delete mov-
     A                                      ements ..'
     A                                 10 46'?'
     A            DFDMFL    R        B 10 48REFFLD(AUREC/AUDMFL *LIBL/HSP001)
     A  63
     AO 90                                  DSPATR(PR)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N63N90                            DSPATR(HI)
     A N30N63N90                            DSPATR(UL)
     A                                 11  2'If the user can delete movements, -
     A                                      you can restrict him to Scheduled o-
     A                                      r'
     A                                 12  2'Shipped movements only. Enter SC o-
     A                                      r SH to restrict; otherwise, leave -
     A                                      blank'
     A            DFMSCD    R        B 12 78REFFLD(HSRREF/@@MSCD *LIBL/HSPREF)
     A  63
     AO 90                                  DSPATR(PR)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N40N63N90                            DSPATR(HI)
     A N40N63N90                            DSPATR(UL)
     A                                 14  2'If there are no restrictions on th-
     A                                      is user, the remaining fields must -
     A                                      be blank.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 16  2'Is this user allowed to modify dat-
     A                                      a ..'
     A                                 16 41'?'
     A            DFMDFL    R        B 16 43REFFLD(AUMDFL)
     A  63
     AO 90                                  DSPATR(PR)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N63N90                            DSPATR(HI)
     A N38N63N90                            DSPATR(UL)
     A                                 17  2'For modifying, is he restricted to-
     A                                       the security location above .'
     A                                 17 67'?'
     A            DFRMFL    R        B 17 69REFFLD(AURMFL)
     A  63
     AO 90                                  DSPATR(PR)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N63N90                            DSPATR(HI)
     A N34N63N90                            DSPATR(UL)
     A                                 18  2'For viewing, is he restricted to t-
     A                                      he security location above ...'
     A                                 18 67'?'
     A            DFRVFL    R        B 18 69REFFLD(AURVFL)
     A  63
     AO 90                                  DSPATR(PR)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N63N90                            DSPATR(HI)
     A N37N63N90                            DSPATR(UL)
     A                                 20  2'This user has extremely limited ac-
     A                                      cess to the system.  He'
     A                                 21  2'must be specifically authorized to-
     A                                       individual functions ..'
     A                                 21 61'?'
     A            DFAFFL    R        B 21 63REFFLD(AUAFFL)
     A  63
     AO 90                                  DSPATR(PR)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39N63N90                            DSPATR(HI)
     A N39N63N90                            DSPATR(UL)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP126K1
     A*%%TS  SD  19940330  133000  ISMPALM     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90N63                          23 11'F4=Prompt'
     A N90N63                          23 22'F5=Refresh'
     A N90N61                          23 34'F11=Delete'
     A                                 23 46'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP126ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP126EC                   SFLCTL(HP126ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
