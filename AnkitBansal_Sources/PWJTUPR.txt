      *****************  COMPILER OVERRIDES   ************************
     H DATFMT(*YMD) DATEDIT(*YMD) DEBUG(*YES)
     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) DBGVIEW(*SOURCE) CVTOPT(*DATETIME)
     Z* ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *****************  RPG PROGRAM HEADING  ************************
      *
      * PROGRAMMER:  Brad Baden
      * CREATED:      8/23/2019
      *
      * FUNCTION: This program reads a copy of the Producer Site file
      *           that was created from a spreadsheet.  The file is a
      *           copy of the Producer Site file for Triumpg Foods.
      *           The first column of the file has a new producer code
      *           in the first column.  Create new Producer Site code
      *           records with the new Producer Code and keep the existing
      *           values in the rest of the fields.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * xx/xx/xxxx xxxxxxxxxxxxxxxxxxx Exxxx
      *            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      *            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
     fpkacrel1  if   e           k disk
      *  Producer - Retrieval index
      *
      *
     fplasrepn  if   e             disk    prefix(n_)
      *  Spreadsheet of Producer Sites
      *
      *
     fplasrel0  uf a e           k disk
      *   Producer Site Mileage - Update index
      *
      *
     fplasrel1  if   e           k disk
      *   Producer Site Mileage - Retrieval index
      *
      *
     fplatrel0  uf a e           k disk
      *   Producer Site - Update index
      *
      *
     fplatrel1  if   e           k disk
      *   Producer Site - Retrieval index
      *
      *
      /eject
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
      *
      *---------------------------------------------------------------
      *  Stand alone fields
      *---------------------------------------------------------------
      *
     d jobdate         s                   like(asaadt)
     d wkasabcd        s                   like(asabcd)
     d wkasnycd        s                   like(asnycd)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     d pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     c                   if        sdpgm = *blank
     c                   endif
      /eject
      **********************************************************************************************
      * Mainline
      **********************************************************************************************
      *
     c                   dou       %eof(plasrepn)                               dou %eof(plasrepn)
     c                   read      plasrepn
     c                   if        not %eof(plasrepn)                           if not %eof(plasrepn
      *
      ** If new producer field is not blank, continue processing
     c                   if        n_asabcdn <> *blanks                         if n_asabcdn<>asabcd
      *
      ** Check if existing producer code exists and skip if not found
     c     n_asabcdn     chain     pkacrel1
     c                   if        %found(pkacrel1)                             if %found(pkacrel1)
      *
      **  write all new producer site and Producer site miles
     c                   exsr      $wrtsite
     c                   exsr      $wrtmiles
      *
     c                   endif                                                  fi %found(pkacrel1)
     c                   endif                                                  fi n_asabcdn<>asabcd
     c                   endif                                                  fi not %eof(plasrepn
     c                   enddo                                                  uod %eof(plasrepn)
      *
     c                   eval      *inlr = *on
      /eject
      ****************************************************************
      * $wrtsite - Load and write new producer site record           *
      ****************************************************************
      *
     c     $wrtsite      begsr
      *
      * Check if a record with the new Producer Code exists
     c                   eval      wkasabcd = n_asabcdn
     c                   eval      wkasnycd = n_asnycd
     c     key01         setll     platrel1
     c                   if        not %equal(platrel1)                         if not %found(platre
      *
      * Retrieve values from current record
     c                   eval      wkasabcd = n_asabcd
     c                   eval      wkasnycd = n_asnycd
     c     key01         chain     platrel1
     c                   if        %found(platrel1)                             if %found(platrel1)
      *
      ** Load new producer code and audit stamp values
     c                   eval      atabcd = n_asabcdn                           new producer
      *
     c                   eval      ataavn = *blanks                             User changed
     c                   eval      ataadt = *zero                               Date changed
     c                   eval      atamtm = *zero                               Time changed
     c                   eval      atahvn = sdjob                               Job name
     c                   eval      atagvn = sdpgm                               Program name
     c                   eval      atabvn = sdusr                               User added
     c                   eval      atabdt = jobdate                             Date added
     c                   eval      atabtm = sdstm                               Time added
      *
      ** write new Producer Site record
     c                   write     @atrell
      *
     c                   endif                                                  fi not %found(platre
     c                   endif                                                  fi %equal(platrel1)
      *
     c                   endsr
      /eject
      ****************************************************************
      * $wrtmiles - Load and write new producer mileage record       *
      ****************************************************************
      *
     c     $wrtmiles     begsr
      *
      * Check if a record with the new Producer Code exists
     c                   eval      wkasabcd = n_asabcdn
     c                   eval      wkasnycd = n_asnycd
     c     key01         setll     plasrel1
     c                   if        not %equal(plasrel1)                         if not %equal(plasre
      *
      ** Create a new Producer Site Mileage record for each existing
      ** record in the Producer Site Mileage file
     c                   eval      wkasabcd = n_asabcd
     c                   eval      wkasnycd = n_asnycd
     c     key02         setll     plasrel1
     c                   if        %equal(plasrel1)                             if %equal(plasrel1)
      *
     c                   dou       %eof(plasrel1)                               dou %eof(plasrel1)
     c     key02         reade     plasrel1
     c                   if        not %eof(plasrel1)                           if not %eof(plasrel1
      *
      ** load new producer code and audit stamp values
     c                   eval      asabcd = n_asabcdn                           new producer
      *
     c                   eval      asaavn = *blanks                             User changed
     c                   eval      asaadt = *zero                               Date changed
     c                   eval      asamtm = *zero                               Time changed
     c                   eval      asahvn = sdjob                               Job name
     c                   eval      asagvn = sdpgm                               Program name
     c                   eval      asabvn = sdusr                               User added
     c                   eval      asabdt = jobdate                             Date added
     c                   eval      asabtm = sdstm                               Time added
      *
      ** Write a Producer Site Mileage record
     c                   write     @asrek8
      *
     c                   endif                                                  fi not %eof(plasrel1
     c                   enddo                                                  uod %eof(plasrel1)
     c                   endif                                                  fi %equal(plasrel1)
     c                   endif                                                  fi not% found(plasre
      *
     c                   endsr
      /eject
      ****************************************************************
      * *INZSR - Initialization                                      *
      ****************************************************************
      *
     c     *inzsr        begsr
      *
      ** Key list for Producer Site
     c     key01         klist
     c                   kfld                    wkasabcd                       Producer code
     c                   kfld                    wkasnycd                       Site ID
      ** Key list for Producer Site Mileage
     c     key02         klist
     c                   kfld                    wkasabcd                       Producer code
     c                   kfld                    wkasnycd                       Site ID
      *
      ** Convert job date to cyymmdd (Synon date)
     C                   eval      jobdate = %dec(%char(%date( +                Job date
     C                                sdsdt:*mdy):*cymd0):7:0)
      *
     c                   endsr
