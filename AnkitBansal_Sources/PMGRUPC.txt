/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              PASSES PARAMETERS TO THE SUBMITTED PROGRAM          */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PMT TF MEAT COST GROUP CL     PMGRUPC               */
/*  DATE      - 05/07/2007 LJB                                      */
/*                                                                  */
/* PARAMETERS:                                                      */
/*                                                                  */
/* &RETURN     - RETURN CODE                                        */
/* &APPLIC     - APPLICATION CODE                                   */
/* &PGM        - CL PROGRAM TO SUBMIT - PMGNUPC                     */
/* &TFMC       - MEAT COST GROUP                                    */
/* &STATUS     - RECORD STATUS   (PASSED)                           */
/* &COPYN      - NO. OF COPIES (PASSED)                             */
/* &DESC       - PRINT PROGRAM DESCRIPTION                          */
/* &OUTQ       - OUTPUT QUEUE                                       */
/* &HOLD       - HOLD STATUS                                        */
/* &SAVE       - SAVE STATUS                                        */
/* &NQSTAT     - NIGHT QUEUE STATUS INDICATOR                       */
/* &NIGHTQ     - NIGHT JOB QUEUE                                    */
/*                                                                  */
/********************************************************************/
 PMGRUPC:    PGM        PARM(&RETURN &DESC &OUTQ &HOLD &SAVE &NQSTAT +
                          &NIGHTQ &COPYN &APPLIC &PGM &TFMC &STATUS)

/* PARMS                                                             */
        DCL    &TFMC       *CHAR   (4)   /* MEAT COST GROUP          */
        DCL    &RETURN     *CHAR   (7)   /* RETURN CODE              */
        DCL    &APPLIC     *CHAR   (06)  /* APPLICATION CODE         */
        DCL    &PGM        *CHAR   (10)  /* CL TO SUBMIT             */
        DCL    &STATUS     *CHAR   (01)  /* RECORD STATUS   (PASSED) */
        DCL    &DESC       *CHAR   (25)  /* PRT PGM DESCRIPTION      */
        DCL    &COPYN      *DEC          /* NO. OF COPIES            */
        DCL    &OUTQ       *CHAR   (10)  /* OUTPUT QUEUE             */
        DCL    &HOLD       *CHAR   (4)   /* HOLD STATUS              */
        DCL    &SAVE       *CHAR   (4)   /* SAVE STATUS              */
        DCL    &NQSTAT     *CHAR   (01)  /* IF MUST RUN IN NIGHT Q   */
        DCL    &NIGHTQ     *CHAR   (10)  /* NIGHT JOB QUEUE          */

/* INTERNAL FIELDS                                                   */
        DCL    &COPY       *DEC    (2 0) /* NUMBER OF COPIES         */

/* ERROR MESSAGE FIELDS                                              */
        DCL    &MSG        *CHAR   (512) /* MESSAGE                  */
        DCL    &MSGID      *CHAR   (7)   /* MESSAGE ID               */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM - CALLS PMGNUPC                        */
/********************************************************************/
 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&TFMC &STATUS +
                          &APPLIC &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ)) JOB(&PGM) JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

