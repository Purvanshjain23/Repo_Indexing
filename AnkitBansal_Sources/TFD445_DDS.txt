     A*%%TS  SD  20061011  125923  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD445
     A*  TITLE:         WORK WITH PRODUCT SPLIT FOR MARGIN ADJUSTMENT
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          10/09/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF445S1                   SFL
     A*%%TS  SD  20061010  085210  ISLRAMS     REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDEFDT    R        H      REFFLD(@@EFDT)
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1EFDTMDY R        O 10  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1PRCD    R        O 10 18REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1SPLTPC  R        O 10 28REFFLD(TFREF/@@SPLTPC *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TFCGCD  R        O 10 41REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A            D1TFCLCD  R        O 10 51REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A            D1ISTYCD  R        O 10 63REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1AIST    R        O 10 76REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A            D1PRDS    R        O 11 44REFFLD(@@PRDS)
     A                                      COLOR(PNK)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF445C1                   SFLCTL(TF445S1)
     A*%%TS  SD  20061010  083314  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'WORK WITH PRODUCT SPLITS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 28'FOR MARGIN ADJUSTMENT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  5 21'?'
     A                                      DSPATR(HI)
     A                                  5 42'?'
     A                                      DSPATR(HI)
     A                                  5 52'?'
     A                                      DSPATR(HI)
     A                                  5 64'?'
     A                                      DSPATR(HI)
     A                                  5 76'?'
     A                                      DSPATR(HI)
     A                                  6 38'TF Class'
     A                                      DSPATR(HI)
     A                                  6 62'Item'
     A                                      DSPATR(HI)
     A                                  6 73'Active/'
     A                                      DSPATR(HI)
     A                                  7  7'Effective'
     A                                      DSPATR(HI)
     A                                  7 18'Product'
     A                                      DSPATR(HI)
     A                                  7 30'Split'
     A                                      DSPATR(HI)
     A                                  7 40'Group'
     A                                      DSPATR(HI)
     A                                  7 49'TF Class'
     A                                      DSPATR(HI)
     A                                  7 60'Structure'
     A                                      DSPATR(HI)
     A                                  7 72'Inactive'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  9'Date'
     A                                      DSPATR(HI)
     A                                  8 20'Code'
     A                                      DSPATR(HI)
     A                                  8 29'Percent'
     A                                      DSPATR(HI)
     A                                  8 40'Code'
     A                                      DSPATR(HI)
     A                                  8 51'Code'
     A                                      DSPATR(HI)
     A                                  8 60'Type Code'
     A                                      DSPATR(HI)
     A                                  8 73'Status'
     A                                      DSPATR(HI)
     A            C1EFDTMDY R        B  9  7REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1PRCD    R        B  9 18REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1TFCGCD  R        B  9 41REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1TFCLCD  R        B  9 51REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1ISTYCD  R        B  9 63REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A  54                                  DSPATR(PC)
     A            C1AIST    R        B  9 76REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF445K1
     A*%%TS  SD  20061011  125923  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F13=Fold/Drop'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF445ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF445EC                   SFLCTL(TF445ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
