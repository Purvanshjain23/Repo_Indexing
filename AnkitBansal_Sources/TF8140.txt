      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF814 - Calculate Weekly Yields STF
      * PROGRAMMER:  Danny Nguyen
      * CREATED:     02/23/23
      *
      * Function:    This function uses incoming data to calc/return Weekly Yield values
      *              for STF only.
      *
      *             ***This program was clone from TF814 & will get STF 440 weekly
      *                yield values. It will be called in TF6790
      *                Consider changing TF814 if program logic changes.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 02/23/23  Danny Nguyen    - W121675 - Clone from TF814. Initial creation to
      *                             get STF weekly yield values.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl050a   if   e           k disk
      *    Kill/Cut data
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Workfields
      *
     D wkpc            s             15  9
      *
      *
      * Parm fields
      *
     D xxwedt          s              8  0
     D xxistycd        s              3  0
     D xxisgrcd        s              3  0
     D xxtfclcd        s              3
     D xxtfcgcd        s              2
      * W121675 - Added STF Parms.
     D xxxpulb         s             11  2
     D xxxpuslb        s             11  2
     D xxxpc           s              7  4
     D xxxkllb         s              7  4
     D xxxctlb         s              7  4
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Retrieve Kill/Cut record for the WeekEnding Date
      *
     C     xxwedt        chain     tfl050a                            92
     C                   if        *in92 = *off                                 If hit
      *
     C                   select
     C                   when      xxtfcgcd = 'OT'
      *
      * Item Structures: 700-Misc and 720-Trim, calc:
      *    1) Produced Pounds per Head Cut
      *    2) Yield % (calc: Produced Pounds / Cold Carcass Pounds)
      *
     C                   when      xxistycd = 700 or xxistycd = 720
      *
      *              STF
     C                   if        kcxcthd <> 0
     C                   eval(h)   xxxctlb = xxxpulb / kcxcthd
     C                   endif
      *
     C                   if        kcxcclb <> 0
     C                   eval      wkpc = (xxxpulb / kcxcclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxxpc
     C                   endif
     C                   endif
      *
      * Item Structures: 740-Kill and 750-Render, calc:
      *    1) Produced Pounds per Head Killed
      *    2) Yield % (calc: Produced Pounds / Hot Carcass Pounds)
      *
     C                   when      xxistycd = 740 or xxistycd = 750
      *
      *              STF
     C                   if        kcxklhd <> 0
     C                   eval(h)   xxxkllb = xxxpulb / kcxklhd
     C                   endif
      *
     C                   if        kcxhclb <> 0
     C                   eval      wkpc = (xxxpulb / kcxhclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxxpc
     C                   endif
     C                   endif
      *
      * For Non-ByProducts and Cushion ByProducts, calc:
      *   1) Yield Percent (calc: Start Weight / Cold Carcass Pounds)
      *
     C                   when      xxtfcgcd <> 'BP' or
     C                             (xxtfcgcd = 'BP' and xxtfclcd = 'BP5'
     C                              and xxistycd = 600 and xxisgrcd = 630)
      *
      *              STF
     C                   if        kcxcclb <> 0
     C                   eval      wkpc = (xxxpuslb / kcxcclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxxpc
     C                   endif
     C                   endif
      *
     C                   endsl
      *
     C                   endif                                                  If hit
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxwedt
     C                   parm                    xxistycd
     C                   parm                    xxisgrcd
     C                   parm                    xxtfclcd
     C                   parm                    xxtfcgcd
      * W121675 - Added STF Parms.
     C                   parm                    xxxpulb
     C                   parm                    xxxpuslb
     C                   parm                    xxxpc
     C                   parm                    xxxctlb
     C                   parm                    xxxkllb
      *
     C                   endsr
      /eject
