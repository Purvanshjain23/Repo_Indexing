     A*%%TS  SD  20060424  155940  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD421
     A*  TITLE:         SPECIFY VERIFICATION FOR FREIGHT VERIFICATION REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/24/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF421R1
     A*%%TS  SD  20060424  155940  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'SPECIFY OPTIONS FOR THE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 20'VERIFICATION FOR FREIGHT VARIANCE -
     A                                      REPORT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Period/Year .........'
     A            D1PE      R        B  4 26REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1YR      R        B  4 31REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                  6  2'Report or download ..'
     A                                  6 24'?'
     A*
     A            D1RDFL    R        B  6 26REFFLD(@@FLAG)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1RDDS        10A  O  6 31
     A                                  7  2'E-mail address ......'
     A            D1EMAIL   R        B  7 26REFFLD(TFREF/@@EMAIL *LIBL/TFPREF)
     A                                      CHGINPDFT(UL LC FE)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF421K1
     A*%%TS  SD  20060424  144913  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF421ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF421EC                   SFLCTL(TF421ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
