     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *****************  RPG PROGRAM HEADING  ************************
      *                                                              *
      * PROGRAM NUMBER..... -  ARACTEDTW                             *
      *          DATE....... -  3/13/2020                            *
      *          AUTHOR..... - Brad Baden   SDN465   TFS# 16169      *
      *          DESCRIPTION - THIS PROGRAM IS CALLED FROM THE A/R   *
      *                        HOG SYSTEM.  IT IS USED TO EDIT A     *
      *                        COMPANY, COST CENTER, OBJECT AND      *
      *                        SUB CODE THAT IS PASSED IN.  IT WILL  *
      *                        RETURN THE ACCOUNT ID, DESCRIPTION    *
      *                        AND ERROR STATUS.                     *
      *                                                              *
      *                        This is the JDE World version.        *
      *                                                              *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      *  Date      Programmer
      *
      *  06/29/96  Barb Gutierrez
      *            Recompiled for JDE Release A7.1 only
      *                                                              *
      *  CHANGES MADE -        12/28/05     Leanne Fedor             *
      *                        rewrote in ILE                        *
      *                                                              *
      *  CHANGES MADE -        12/29/05     ALICE BROWNFIELD         *
      *                        CHANGED TO NOT INCLUDE COMPANY # AS   *
      *                        PART OF KEY. CHANGED LOGICAL FROM     *
      *                        F0901LE TO F0901LB.                   *
      *                                                              *
      *  CHANGES MADE -        02/18/10     Barb Gutierrez           *
      *                        Need to pass out the posting edit     *
      *                        code associated with the account #.   *
      *                                                              *
      *   3/11/20  Brad Baden   SDN465   TFS# 16169                  *
      *            This source was copied from source ARACTEDT in    *
      *            library JFOODCUST and renamed to ARACTEDTW.       *
      *                                                              *
      /eject
      *****************************************************************
      * File specifications
      ****************************************************************
      *
     Ff0901lb   if   e           k disk
      * JDE Account master
      *
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Standard fields
      *
     d return          s              7
      *
      *
      * Workfields
      *
      * Parm fields
      *
     d xxmcu           s                   like(gmmcu)
     d xxobj           s                   like(gmobj)
     d xxsub           s                   like(gmsub)
     d xxco            s              3
     d xxactid         s              8
     d xxpec           s              1
     d xxdesc          s             30
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      *
      /eject
      *----------------------------------------------------------------------------------
      * Mainline
      *----------------------------------------------------------------------------------
      *
      *  Retrieve Account info from the Account Master
      *
     C     key01         chain     f0901lb                            92
      *
      * If you did not find the record or it has an "inactive" or
      * "non-posting" status, set the Return Code to indicate an error.
      *
     C                   if        *in92 = *on or                               If no good
     C                             gmpec = 'I' or
     C                             gmpec = 'N'
      *
     C                   movel     'ERR'         return
     C                   move      *blank        xxdesc
     C                   move      *blank        xxactid
     C                   move      *blank        xxpec
     C                   else
      *
     C                   move      *blank        return
     C                   movel     gmaid         xxactid
     C                   movel     gmdl01        xxdesc
     C                   move      gmpec         xxpec
     C                   endif                                                  If no good
      *
     C                   seton                                        lr
      *
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxco
     C                   parm                    xxmcu
     C                   parm                    xxobj
     C                   parm                    xxsub
     C                   parm                    xxactid
     C                   parm                    xxpec
     C                   parm                    xxdesc
     C                   parm                    return
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxmcu
     C                   kfld                    xxobj
     C                   kfld                    xxsub
      *
     C                   endsr
      /eject
