/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES PROD DATE AND SHIFT            */
/*                                                                  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 06/24/02                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &SHIFT      - SHIFT          (PASSED)                   */
/*     B    &FDATE      - FROM DATE      (PASSED)                   */
/*     B    &TDATE      - TO DATE        (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*                                                                  */
/********************************************************************/

 POQBUPC:    PGM        PARM(&RETURN  &OUTQ &HOLD +
              &SAVE  &COPYN &FDATE &TDATE &SHIFT &CO)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
             DCL        VAR(&PAPER) TYPE(*CHAR) LEN(1) +
                          VALUE('L') /* LEGAL, +
                          LETTER             */
        DCL    &FDATE      *DEC
        DCL    &TDATE      *DEC
        DCL    &SHIFT      *CHAR   (1)
        DCL    &CO         *DEC    (3 0)
        DCL    &COPYN      *DEC                  /* No. of Copies             */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &FDATEN     *DEC    (7 0)
        DCL    &TDATEN     *DEC    (7 0)
        DCL    &FDATEA     *CHAR   (7)
        DCL    &TDATEA     *CHAR   (7)
        DCL    &FYYMMA     *CHAR   (8)
        DCL    &TYYMMA     *CHAR   (8)
        DCL    &FYYMM      *DEC    (8 0)
        DCL    &TYYMM      *DEC    (8 0)
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

        DCL    &COMMAND    *CHAR   (512)
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7) /*  RETURN +
/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&FDATEN) VALUE(&FDATE)
             CHGVAR     VAR(&TDATEN) VALUE(&TDATE)
             CHGVAR     VAR(&FDATEA) VALUE(&FDATEN)
             CHGVAR     VAR(&TDATEA) VALUE(&TDATEN)
/********************************************************************/
/* Retrieve Data Library name and Data History library name         */
/********************************************************************/

             CALL       PGM(CAQXXFR) PARM(' ' &DATA &DATAH)


/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
             CRTDUPOBJL OBJ(POB2CPP) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(POB2CPL0) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJL OBJ(POB2CPL1) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJL OBJ(POB2CPL2) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJL OBJ(POB2CPL3) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CVTDAT     DATE(&FDATEA) TOVAR(&FYYMMA) +
                          FROMFMT(*CYMD) TOFMT(*YYMD) TOSEP(*NONE)
             CVTDAT     DATE(&TDATEA) TOVAR(&TYYMMA) +
                          FROMFMT(*CYMD) TOFMT(*YYMD) TOSEP(*NONE)
             CHGVAR     VAR(&FYYMM) VALUE(&FYYMMA)
             CHGVAR     VAR(&TYYMM) VALUE(&TYYMMA)

             CPYF       FROMFILE(&DATA/POB2CPP) +
                          TOFILE(QTEMP/POB2CPP) MBROPT(*ADD) +
                          INCREL((*IF B2AADX *GE &FYYMMA) (*AND +
                          B2AADX *LE &TYYMMA))
/********************************************************************/
/*   OVERRIDE PRINT FILE                                          */
/********************************************************************/
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(POP9PFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/********************************************************************/
/*   ADD DUMMY RECORDS TO FILE FOR MISSING ITEMS/SHIFT/DAY        */
/********************************************************************/
             CALL       PGM(POQEXFR) PARM(&RTN &CO &FDATEN &TDATEN +
                          &SHIFT)
/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/
             CALL       PGM(POP9PFR) PARM(&RTN &CO &FDATEN &TDATEN +
                          &SHIFT)

             GOTO       CMDLBL(END)


/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

