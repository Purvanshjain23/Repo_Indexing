      *‚***************  RPG PROGRAM HEADING  ************************
      *‚
      *‚SYSTEM:      Hog Production
      *‚PROGRAM:     HP60262
      *‚TITLE:       Work with Regions
      *‚PROGRAMMER:  Jagdish Mistry
      *‚CREATED:     07/22/25
      *
      *‚FUNCTION: This is a Work With that displays the records in the Regions file.
      *
      *‚          Positioners:
      *‚              Region code
      *
      *‚          Selectors:
      *‚              Description
      *
      *‚ NOTE: When the user takes F7 to print a listing, the listing will use
      *‚       the positioner/selector values in the control record as the listing
      *‚       selections.
      *
      ****************************************************************
      *‚MODIFICATIONS:
      ****************************************************************
      *‚DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      *‚FILE SPECIFICATIONS
      ****************************************************************
      *
      *‚Region code
     FHSP008    IF   E           K DISK
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      *‚INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *‚STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *‚Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldrgcd                 1      5
     D  ldrgds                 6     26
      *
      *---------------------------------------------------------------
      *‚ NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      *‚STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Print fields
      *
     D dash            s             80    inz(*all'-')
      *
     D h1region        s             75
     D*h1status        s             75
     D*h1type          s             75
     D*h1ttds          s             25
      *
      /EJECT
      ****************************************************************
      *‚MAIN LINE
      ****************************************************************
      *
      *‚PREPARE TO READ THE region FILE AND PRINT HEADINGS THE FIRST TIME THROUGH.
      *
     C     ldrgcd        setll     hsp008
     C                   EXCEPT    h1hdr
      *
      *‚READ AND PRINT ALL RECORDS IN THE FILE THAT MATCH THE USERS SELECTIONS.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp008                                 90
      *
     C                   if        *in90 = *off                                 If not EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    h1hdr
     C                   ENDIF
      *
      *‚Print detail line
     C                   EXCEPT    d1dtl
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *‚End of File
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      *‚INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *‚SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
      *‚RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
     C                   endsr
      *
      /EJECT
      *-------------------------------------------------------------
      *‚REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            h1hdr          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           66 'DATE'
     O                       UDATE         Y     76
      *
     O          E            h1hdr          1
     O                       SDUSR               10
     O                                           47 ' LISTING OF REGIONS'
     O                                           66 'TIME'
     O                       RTIME               76 '  :  :  '
      *
     O          E            h1hdr          2
     O                                           66 'PAGE'
     O                       PAGE          Z     71
      *
      *
     O          e            h1hdr          1
     O                       h1region            75
      *
      *
     O          E            h1hdr          1
     O                                            7 'REGION'
      *
     O          E            h1hdr          1
     O                                            5 'CODE'
     O                                           27 'DESCRIPTION'
      *
     O          E            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      *‚ DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            d1dtl       1
     O                       rgrgcd               6
     O                       rgrgds              36
      *
      *-------------------------------------------------------------
      *‚NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
