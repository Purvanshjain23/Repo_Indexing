/***************************************************************** */
/*  PROGRAM NUMBER --> HP325CL                                     */
/*  PROGRAM NAME ----> WORKFILE: BUILD GROUP SOURCE/SIRE LINE INFO */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 11/14/01                                    */
/*                                                                 */
/*  FUNCTION: THIS FUNCTION IS MEANT TO BE RUN FROM THE JOB        */
/*            SCHEDULER TO EXTRACT/BUILD SIRE LINE INFO FOR        */
/*            GROUPS. BUT, WE ALSO HAVE IT AS A MENU OPTION.       */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/*  04/08/03  LEANNE FEDOR                                         */
/*            SINCE WE HAVE THIS JOB AS A MENU OPTION AS WELL AS   */
/*            OFF THE JOB SCHEDULER, WE SET UP A DATA AREA TO KEEP */
/*            USERS FROM INADVERTANTLY SUBMITTING THE JOB MULTIPLE */
/*            TIMES FROM THE MENU.  WE DON'T WANT THE JOB SUBMITTED*/
/*            MULTIPLE TIMES BECAUSE IT IS NOT CREATING TEMP FILES */
/*            IN QTEMP; IT IS CLEARING REAL FILES.                 */
/*                                                                 */
/*            SO, WE JUST ADDED CODE IN THIS CL TO POPULATE AND    */
/*            CLEAR THIS NEW DATA AREA. (THE SUBMISSION SCREEN     */
/*            WILL POPULATE IT FOR THE MENU SUBMISSIONS.)          */
/*                                                                 */
/*            SO, IF THIS CL ABENDS ABNORMALLY, YOU WILL PROBABLY  */
/*            MANUALLY NEED TO RESET THE DATA AREA:                */
/*                                                                 */
/*                  CHGDTAARA  DTAARA(DASIRE (1 1)) VALUE(' ')     */
/*                                                                 */
/*                                                                 */
/***************************************************************** */

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(89)


/*  SET THE DATA AREA THAT KEEPS THE DERIVATION FUNCTION FROM BEING    */
/*  SUBMITTED TWICE SIMULTANEOUSLY. IF THE USERS SUBMITTED THIS JOB    */
/*  FROM THE MENU, IT WILL ALREADY HAVE BEEN SET. BUT, THE CODE HERE   */
/*  WILL HANDLE THE SITUATION WHEN IT IS SUBMITTED FROM THE JOB        */
/*  SCHEDULER.                                                         */

             CHGDTAARA  DTAARA(DASIRE (1 1)) VALUE('Y')

/*-------------------------------------------------------------------*/
/* CLEAR WORKFILES                                                   */
/*-------------------------------------------------------------------*/

             CLRPFM     FILE(HSP325) /* Weekly sire line dose %        */

             CLRPFM     FILE(HSP327)  /* Nursery-to-nursery transfers     */
             CLRPFM     FILE(HSP3310) /* Nursery groups--sow/sire detail  */
             CLRPFM     FILE(HSP328)  /* Nursery groups--sow/sire summary */
             CLRPFM     FILE(HSP326)  /* Nursery groups--BGF sources      */
             CLRPFM     FILE(HSP336)  /* Nurs groups--Total BGF sources   */
             CLRPFM     FILE(HSP340)  /* Nurs groups--Total sire lines    */

             CLRPFM     FILE(HSP351)  /* BGF-to-GF transfers                 */
             CLRPFM     FILE(HSP3311) /* BGF-to-GF transfers-sow/sire detail */
             CLRPFM     FILE(HSP353)  /* BGF-to-GF transfers-sow/sire summary*/
             CLRPFM     FILE(HSP330)  /* GF-to-GF transfers                  */
             CLRPFM     FILE(HSP329)  /* GF groups--nursery sources          */
             CLRPFM     FILE(HSP3312) /* GF groups--sow/sire detail          */
             CLRPFM     FILE(HSP331)  /* GF groups--sow/sire summary         */
             CLRPFM     FILE(HSP354)  /* BGF-GF-GF transfers--head           */
             CLRPFM     FILE(HSP324)  /* GF groups--BGF sources              */
             CLRPFM     FILE(HSP356)  /* GF groups--Total BGF sources        */
             CLRPFM     FILE(HSP350)  /* GF groups--Total sire lines         */

/*-------------------------------------------------------------------*/
/* POPULATE WORKFILE: WEEKLY SIRE LINE DOSE PERCENTS                 */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP325)

/*********************************************************************/
/* NURSERY PROCESSING                                                */
/*********************************************************************/

/*-------------------------------------------------------------------*/
/* PROCESS BGF-TO-NURSERY TRANSFERS                                  */
/*-------------------------------------------------------------------*/

/*  ALWAYS OMIT SCHEDULED TRANSFER MOVEMENTS                     */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('MHMSCD +
                          *NE "SC"')

/*  ALWAYS SELECT ONLY TRANSFERS WITH A RECEIVED DATE IN 1999 OR  */
/*  LATER                                                         */

             CHGVAR     VAR(%SST(&QDATA 17 24)) VALUE('*AND MHRCDT +
                          *GE 19990512')

/*  ALWAYS SELECT ONLY BGF-TO-NURSERY TRANSFERS                   */

             CHGVAR     VAR(%SST(&QDATA 42 47)) VALUE('*AND MHORPP +
                          *EQ "BGF  " *AND MHDNPP *EQ "NUR  "')

             OVRDBF     FILE(HSJ075G) SHARE(*YES)
             OPNQRYF    FILE((HSJ075G)) QRYSLT(&QDATA) +
                          KEYFLD((MHRCDT)) SEQONLY(*YES)

             CALL       PGM(HP326)

             CLOF       OPNID(HSJ075G)
             DLTOVR     FILE(HSJ075G)

/*-------------------------------------------------------------------*/
/* PROCESS NURSERY-TO-NURSERY TRANSFERS                              */
/*-------------------------------------------------------------------*/

/*  ALWAYS OMIT SCHEDULED TRANSFER MOVEMENTS                     */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('MHMSCD +
                          *NE "SC"')

/*  ALWAYS SELECT ONLY TRANSFERS WITH A RECEIVED DATE IN 1999 OR  */
/*  LATER                                                         */

             CHGVAR     VAR(%SST(&QDATA 17 24)) VALUE('*AND MHRCDT +
                          *GE 19990512')

/*  ALWAYS SELECT ONLY NURSERY-TO-NURSERY TRANSFERS               */

             CHGVAR     VAR(%SST(&QDATA 42 47)) VALUE('*AND MHORPP +
                          *EQ "NUR  " *AND MHDNPP *EQ "NUR  "')

             OVRDBF     FILE(HSJ075G) SHARE(*YES)
             OPNQRYF    FILE((HSJ075G)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP327)

             CLOF       OPNID(HSJ075G)
             DLTOVR     FILE(HSJ075G)

/*-------------------------------------------------------------------*/
/* UPDATE THE NURSERY GROUP BGF SOURCES WORKFILE WITH THE            */
/* NURSERY-TO-NURSERY TRANSFER INFO                                  */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP326A)


/*-------------------------------------------------------------------*/
/* BUILD THE NURSERY GROUP: SOW/SIRE DETAIL FILE                     */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP3310)

/*-------------------------------------------------------------------*/
/* BUILD THE NURSERY GROUP: SOW/SIRE SUMMARY FILE                    */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP328)


/*--------------------------------------------------------------------*/
/* BUILD THE FILES THAT ARE BASED ON 'TOTAL HEAD RECEIVED' INTO GROUP */
/*--------------------------------------------------------------------*/

             CALL       PGM(HP336) /* Nursery-Total BGF Sources */
             CALL       PGM(HP340) /* Nursery-Total Sire Lines  */


/*********************************************************************/
/* GROW FINISH PROCESSING                                            */
/*********************************************************************/

/*-------------------------------------------------------------------------*/
/* PROCESS BGF-TO-GROW FINISH TRANSFERS                                    */
/*-------------------------------------------------------------------------*/

/*  ALWAYS OMIT SCHEDULED TRANSFER MOVEMENTS                     */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('MHMSCD +
                          *NE "SC"')

/*  ALWAYS SELECT ONLY TRANSFERS WITH A RECEIVED DATE IN 1999 OR  */
/*  LATER                                                         */

             CHGVAR     VAR(%SST(&QDATA 17 24)) VALUE('*AND MHRCDT +
                          *GE 19990512')

/*  ALWAYS SELECT ONLY BGF-TO-GROW FINISH TRANSFERS                  */

             CHGVAR     VAR(%SST(&QDATA 42 47)) VALUE('*AND MHORPP +
                          *EQ "BGF  " *AND MHDNPP *EQ "GF   "')

             OVRDBF     FILE(HSJ075G) SHARE(*YES)
             OPNQRYF    FILE((HSJ075G)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP351) /* Process Transfers */

             CLOF       OPNID(HSJ075G)
             DLTOVR     FILE(HSJ075G)

/*-------------------------------------------------------------------*/
/* PROCESS NURSERY-TO-GROW FINISH TRANSFERS                          */
/*-------------------------------------------------------------------*/

/*  ALWAYS OMIT SCHEDULED TRANSFER MOVEMENTS                     */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('MHMSCD +
                          *NE "SC"')

/*  ALWAYS SELECT ONLY TRANSFERS WITH A RECEIVED DATE IN 1999 OR  */
/*  LATER                                                         */

             CHGVAR     VAR(%SST(&QDATA 17 24)) VALUE('*AND MHRCDT +
                          *GE 19990512')

/*  ALWAYS SELECT ONLY NURSERY-TO-GROW FINISH TRANSFERS           */

             CHGVAR     VAR(%SST(&QDATA 42 47)) VALUE('*AND MHORPP +
                          *EQ "NUR  " *AND MHDNPP *EQ "GF   "')

             OVRDBF     FILE(HSJ075G) SHARE(*YES)
             OPNQRYF    FILE((HSJ075G)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP329)

             CLOF       OPNID(HSJ075G)
             DLTOVR     FILE(HSJ075G)

/*-------------------------------------------------------------------*/
/* PROCESS TRANSFERS THAT WENT FROM GROW FINISH TO GROW FINISH       */
/*-------------------------------------------------------------------*/

/*  ALWAYS OMIT SCHEDULED TRANSFER MOVEMENTS                     */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('MHMSCD +
                          *NE "SC"')

/*  ALWAYS SELECT ONLY TRANSFERS WITH A RECEIVED DATE IN 1999 OR  */
/*  LATER                                                         */

             CHGVAR     VAR(%SST(&QDATA 17 24)) VALUE('*AND MHRCDT +
                          *GE 19990512')

/*  ALWAYS SELECT ONLY GROW FINISH-TO-GROW FINISH TRANSFERS       */

             CHGVAR     VAR(%SST(&QDATA 42 47)) VALUE('*AND MHORPP +
                          *EQ "GF   " *AND MHDNPP *EQ "GF   "')

             OVRDBF     FILE(HSJ075G) SHARE(*YES)
             OPNQRYF    FILE((HSJ075G)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP330)

             CLOF       OPNID(HSJ075G)
             DLTOVR     FILE(HSJ075G)

/*-------------------------------------------------------------------*/
/* UPDATE FILES WITH THE GF-TO-GF TRANSFER DATA                      */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP333)


/*-------------------------------------------------------------------------*/
/* Build Sow/Sire Detail/Summary files                                     */
/*-------------------------------------------------------------------------*/

             CALL       PGM(HP354)  /* BGF-GF-GF Transfers: Head             */
             CALL       PGM(HP3311) /* BGF-to-GF Transfers: sow/sire detail  */
             CALL       PGM(HP353)  /* BGF-to-GF Transfers: sow/sire summary */
             CALL       PGM(HP3312) /* GF groups: sow/sire detail            */
             CALL       PGM(HP331)  /* GF groups: sow/sire summary           */

/*-------------------------------------------------------------------*/
/* BUILD THE BGF SOURCES FOR GROW FINISH GROUPS                      */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP324)

/*--------------------------------------------------------------------*/
/* BUILD THE FILES THAT ARE BASED ON 'TOTAL HEAD RECEIVED' INTO GROUP */
/*--------------------------------------------------------------------*/

             CALL       PGM(HP356) /* Grow Finish-Total BGF Sources */
             CALL       PGM(HP350) /* Grow Finish-Total Sire Lines  */


/*********************************************************************/
/* UPDATE HOG GROUP MASTER WITH SIRE LINE DATA                       */
/*********************************************************************/

             CALL       PGM(HP332)


/*  SET THE DATA AREA THAT KEEPS THE DERIVATION FUNCTION FROM BEING    */
/*  SUBMITTED TWICE SIMULTANEOUSLY BACK TO BLANK                       */

             CHGDTAARA  DTAARA(DASIRE (1 1)) VALUE(' ')

             ENDPGM

