      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3708-Update a Request as Completed
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/17/04
      *
      * Function:    This program runs weekly off the Job Scheduler. It processes
      *              all Requests that are 30+ days old and marks them as Completed.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl192c   uf   e           k disk
      *    Request header (logical selects only R=Requested orders)
      *
      *
     Fhsp193    uf   e           k disk
      *    Request detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               d   datfmt(*iso)
     D wksysiso        s               d   datfmt(*iso)
     D wksysdt         s              8  0
     D wkdays          s              5  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdusr = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read each Request Header record with a status of R=Requested. If it
      * is 30 or more days old, mark it as completed.
      *
     C     *loval        setll     hsl192c
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl192c                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Is this Request 30+ days old?
      *
     C     *iso          move      qhrqdt        wkcymdiso
     C     wksysiso      subdur    wkcymdiso     wkdays:*d
      *
     C                   if        wkdays >= 30                                 If process
      *
      * Process detail records
      *
     C                   exsr      $detail
      *
      * Update header
     C                   move      'C'           qhrqscd
     C                   z-add     wksysdt       qhupdt
     C                   time                    qhuptm
     C                   move      sdpgm         qhuscd
     C                   update    qhrec
     C                   endif                                                  If process
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Process detail records for this work order
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C     qhrqsn        setll     hsp193
      *
     C                   dou       *in91 = *on                                  Do detail
     C     qhrqsn        reade     hsp193                                 91
     C                   if        *in91 = *off and                             If not EOF
     C                             qdrqscd <> 'D' and
     C                             qdrqscd <> 'E'
     C                   move      'C'           qdrqscd
     C                   update    qdrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do Detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve system date.
      *
     C     *mdy          move      udate         wkcymdiso
     C                   move      wkcymdiso     wksysiso
     C                   move      wkcymdiso     wksysdt
      *
     C                   endsr
      /eject
