     A*%%TS  SD  20081210  094812  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1023
     A*  TITLE:         REVISE TARGETS FOR NUR/FIN TARGET TABLE/YEAR
     A*  PROGRAMMER:    SHARON ZURN
     A*  DATE:          01/04/07
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF09(09 'ACCEPT')
     A                                      CF03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R HP1023R1
     A*%%TS  SD  20081210  094812  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(70)
     A                                      SETOF(71)
     A                                      SETOF(72)
     A                                      SETOF(73)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      SETOF(76)
     A                                      SETOF(77)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 17'REVISE TARGETS FOR NUR/FIN TARGET -
     A                                      TABLE/YEAR'
     A                                      DSPATR(HI)
     A                                  4  2'Target Table    '
     A                                      DSPATR(UL)
     A                                  5  2'Origin BGF cell:'
     A                                      DSPATR(HI)
     A            D1OBCELL  R        O  5 20REFFLD(NTREC/NTOBCELL *LIBL/HSP122)
     A                                      EDTCDE(M)
     A                                  6  2'Origin BGF pod.:'
     A                                      DSPATR(HI)
     A            D1OBPOD   R        O  6 19REFFLD(NTREC/NTOBPOD *LIBL/HSP122)
     A                                      EDTCDE(M)
     A  61                              8 10'Year ...'
     A                                      DSPATR(HI)
     A N61                              8 13'Year:'
     A                                      DSPATR(HI)
     A            D1CDYR    R        B  8 19REFFLD(NYREC/NYCDYR *LIBL/HSP123)
     A                                      EDTCDE(M)
     A N62N60N31                            DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A                                 10 19'----------- Grow Finish -----------
     A                                      ---'
     A                                 11  9'Nursery'
     A                                 11 19'      Normal     '
     A                                      DSPATR(UL)
     A                                 11 40'     GDU        '
     A                                      DSPATR(UL)
     A                                 12 10'Total'
     A                                 12 20'Total'
     A                                 12 41'Total'
     A                                 13  9'Loss %'
     A                                 13 19'Loss %'
     A                                 13 29'Cull %'
     A                                 13 40'Loss %'
     A                                 13 50'Cull %'
     A                                 14  2'QTR 1'
     A            D1NTLSPR1 R        B 14  9REFFLD(HSRREF/@@GTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N32                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GTLSPR1 R        B 14 19REFFLD(HSRREF/@@NTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N33                               DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GCUPR1  R        B 14 29REFFLD(HSRREF/@@GCUPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N34                               DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GGTLSPR1R        B 14 40REFFLD(HSRREF/@@NTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N70                               DSPATR(HI)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GGCUPR1 R        B 14 50REFFLD(HSRREF/@@GCUPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N71                               DSPATR(HI)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A                                 15  2'QTR 2'
     A            D1NTLSPR2 R        B 15  9REFFLD(HSRREF/@@GTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N35                               DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GTLSPR2 R        B 15 19REFFLD(HSRREF/@@NTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N36                               DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GCUPR2  R        B 15 29REFFLD(HSRREF/@@GCUPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N37                               DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GGTLSPR2R        B 15 40REFFLD(HSRREF/@@NTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N72                               DSPATR(HI)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GGCUPR2 R        B 15 50REFFLD(HSRREF/@@GCUPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N73                               DSPATR(HI)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A                                 16  2'QTR 3'
     A            D1NTLSPR3 R        B 16  9REFFLD(HSRREF/@@GTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N30                               DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GTLSPR3 R        B 16 19REFFLD(HSRREF/@@NTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N38                               DSPATR(HI)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GCUPR3  R        B 16 29REFFLD(HSRREF/@@GCUPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N39                               DSPATR(HI)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GGTLSPR3R        B 16 40REFFLD(HSRREF/@@NTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N74                               DSPATR(HI)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GGCUPR3 R        B 16 50REFFLD(HSRREF/@@GCUPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N75                               DSPATR(HI)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A                                 17  2'QTR 4'
     A            D1NTLSPR4 R        B 17  9REFFLD(HSRREF/@@GTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N40                               DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GTLSPR4 R        B 17 19REFFLD(HSRREF/@@NTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N41                               DSPATR(HI)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GCUPR4  R        B 17 29REFFLD(HSRREF/@@GCUPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N42                               DSPATR(HI)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GGTLSPR4R        B 17 40REFFLD(HSRREF/@@NTLSPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N76                               DSPATR(HI)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1GGCUPR4 R        B 17 50REFFLD(HSRREF/@@GCUPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N62N77                               DSPATR(HI)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R HP1023K1
     A*%%TS  SD  20070611  163740  ISSZURN     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 12'F5=Refresh'
     A N62N61                          23 36'F11=Delete'
     A                                 23 25'F9=Accept'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R HP1023ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R HP1023EC                  SFLCTL(HP1023ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
