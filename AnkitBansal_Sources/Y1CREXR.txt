      **  FIXED MOVE SERIAL TO WRK ARRAY STATEMENT - 10/28/92-DLH
     FY1CREXFMCF  E                    WORKSTN
     E                    MS1    37  37  1               ORGINAL VALUES
     E                    MS2    37  37  1               ENCRIPTED VALUES
     E                    NUMB    9   9  1               NUMBERS 1 - 9
     E                    S0          8  1               SERIAL-NO OFFSET
     E                    S1          8  1               SERIAL-OFFSET 1
     E                    S2          8  1               SERIAL-OFFSET 2
     E                    D1          6  1               DATE  -OFFSET 1
     E                    M1          6  1               MODEL -OFFSET 2
     E                    SER         8  1               SERIAL-ORGINAL
     E                    WRK         8  1               WORK
     E                    DTE         6  1               DATE  -ORGINAL
     E                    MOD         6  1               MODEL -ORGINAL
     E                    AUT        36  1               AUTCD -ORGINAL
     IDS1         DS
     I                                        2   9 SERIAL
     I                                        2   9 SER
     I                                       10  150EXDATE
     I                                       10  15 DTE
     I                                       16  19 MACTYP
     I                                       20  21 MACMOD
     I                                       16  21 MOD
     C                     SETOF                     5090
     C                     EXSR PASS1
     C                     EXFMTY1CREX01
     C           *INKC     DOWNE'1'
     C                     EXSR DETAUT
     C                     EXFMTY1CREX01
     C                     END
     C                     SETON                     LR
     CSR         PASS1     BEGSR
     CSR                   SETON                     505101
     CSR         UDATE     COMP 120000               20
     CSR         UDATE     MULT 10000.01  EXDATE
     CSR 20                ADD  008900    EXDATE
     CSRN20                ADD  000100    EXDATE
     CSR                   ENDSR
     C*****************************************************************
     CSR         DETAUT    BEGSR
     CSR                   MOVEL*BLANK    AUT
     CSR                   MOVEASERIAL    WRK              WAS 'MOVE'
     C*
     C* - - - SET OFFSET1 TO BE LAST NON-ZERO DIGIT OF SERIAL NUMBER    - - - *
     C* - - - SET OFFSET2 TO BE POSITION 5 OF MODEL NUMBER + OFFSET1    - - - *
     C* - - - IF NO DIGITS - SET OFFSET1  TO 1 AND OFFSET2  TO 2  - - - - - - *
     C* - - - IF ONLY ONE DIGIT SET OFFSET2  TO BE ONE MORE THAN OFFSET1  - - *
     CSR                   Z-ADD0         OFSET1  30
     CSR                   Z-ADD0         OFSET2  30
     CSR                   Z-ADD9         X1      30
     CSR                   SETOF                     202122
     CSR         LOOP1     TAG
     CSR                   SUB  1         X1           2020
     CSR 20                GOTO LOOP1E
     CSR         WRK,X1    LOKUPNUMB                     20
     CSRN20                GOTO LOOP1
     CSR                   MOVE WRK,X1    OFSET1
     CSR                   SETON                     21
     CSR         LOOP1E    TAG
     CSRN21                ADD  1         OFSET1
     C**                   MOVE MOD,5     OFSET2
      **  ADD MODEL ELEMENTS 5 + 6 TO ACCOUNT FOR DIFFERENT MODEL
      **  ENDINGS (I.E. MODEL 20 AND 25)
     C                     MOVE MOD,5     MOD5    10
     C                     MOVE MOD,6     MOD6    10
     C           MOD5      ADD  MOD6      OFSET2
     CSR                   ADD  OFSET1    OFSET2
     C*
     C* - - - SET UP ENCRYPTED SERIAL NUMBERS - - - - - - - - - - - - - - - - *
     C*
     CSR                   Z-ADD0         OFSET   30
     CSR                   EXSR DETSER
     CSR                   MOVEAS2        S0
     CSR                   Z-ADDOFSET1    OFSET
     CSR                   EXSR DETSER
     CSR                   MOVEAS2        S1
     CSR                   Z-ADDOFSET2    OFSET
     CSR                   EXSR DETSER
     C*
     C* - - - SET UP ENCRYPTED DATE AND MODEL - - - - - - - - - - - - - - - - *
     C*
     CSR                   Z-ADDOFSET1    OFSET
     CSR                   EXSR DETDTE
     CSR                   Z-ADDOFSET2    OFSET
     CSR                   EXSR DETMOD
     C*
     C* - - - MOVE ENCRYPTED SERIAL NUMBERS AND DATES TO AUTHORIZATION CODE - *
     C*
     CSR                   EXSR MOVAUT
     CSR                   MOVEAAUT       AUTCDE
     CSR                   ENDSR
     C****************************************************************
     CSR         DETSER    BEGSR
     CSR                   MOVE *BLANK    S2
     CSR                   Z-ADD9         X1
     CSR         DETSLP    TAG
     CSR                   SUB  1         X1           2020
     CSR 20                GOTO DETSEN
     CSR                   Z-ADD1         X2      30
     CSR         SER,X1    LOKUPMS1,X2                   20
     CSRN20                SETON                     90
     CSR                   ADD  OFSET     X2
     CSR         X2        COMP 37                   20
     CSR 20                SUB  37        X2
     CSR                   MOVE MS2,X2    S2,X1
     CSR                   GOTO DETSLP
     CSR         DETSEN    ENDSR
     C** ENCRYPT EXPIRATION DATE
     CSR         DETDTE    BEGSR
     CSR                   MOVE *BLANK    D1
     CSR                   Z-ADD7         X1
     CSR         DETDLP    TAG
     CSR                   SUB  1         X1           2020
     CSR 20                GOTO DETDEN
     CSR                   Z-ADD1         X2      30
     CSR         DTE,X1    LOKUPMS1,X2                   20
     CSRN20                SETON                     90
     CSR                   ADD  OFSET     X2
     CSR         X2        COMP 37                   20
     CSR 20                SUB  37        X2
     CSR                   MOVE MS2,X2    D1,X1
     CSR                   GOTO DETDLP
     CSR         DETDEN    ENDSR
     C** ENCRYPT MODEL NUMBER
     CSR         DETMOD    BEGSR
     CSR                   MOVE *BLANK    M1
     CSR                   Z-ADD7         X1
     CSR         DETMLP    TAG
     CSR                   SUB  1         X1           2020
     CSR 20                GOTO DETMEN
     CSR                   Z-ADD1         X2      30
     CSR         MOD,X1    LOKUPMS1,X2                   20
     CSRN20                SETON                     90
     CSR                   ADD  OFSET     X2
     CSR         X2        COMP 37                   20
     CSR 20                SUB  37        X2
     CSR                   MOVE MS2,X2    M1,X1
     CSR                   GOTO DETMLP
     CSR         DETMEN    ENDSR
     C** SCRAMBLE POSITIONS OF COMPONENTS INTO AUTHORIZATION CODE
     CSR         MOVAUT    BEGSR
     CSR                   MOVE S0,8      AUT,01
     CSR                   MOVE S0,6      AUT,02
     CSR                   MOVE S0,4      AUT,03
     CSR                   MOVE S0,2      AUT,04
     CSR                   MOVE S2,7      AUT,05
     CSR                   MOVE D1,4      AUT,06
     CSR                   MOVE S2,4      AUT,07
     CSR                   MOVE S1,6      AUT,08
     CSR                   MOVE S1,3      AUT,09
     CSR                   MOVE S1,5      AUT,10
     CSR                   MOVE M1,1      AUT,11
     CSR                   MOVE D1,2      AUT,12
     CSR                   MOVE S2,1      AUT,13
     CSR                   MOVE M1,2      AUT,14
     CSR                   MOVE M1,4      AUT,15
     CSR                   MOVE D1,5      AUT,16
     CSR                   MOVE S1,4      AUT,17
     CSR                   MOVE S1,7      AUT,18
     CSR                   MOVE M1,6      AUT,19
     CSR                   MOVE S1,2      AUT,20
     CSR                   MOVE D1,1      AUT,21
     CSR                   MOVE M1,5      AUT,22
     CSR                   MOVE S2,8      AUT,23
     CSR                   MOVE S1,1      AUT,24
     CSR                   MOVE M1,3      AUT,25
     CSR                   MOVE S2,3      AUT,26
     CSR                   MOVE S2,5      AUT,27
     CSR                   MOVE D1,6      AUT,28
     CSR                   MOVE S2,6      AUT,29
     CSR                   MOVE S1,8      AUT,30
     CSR                   MOVE S2,2      AUT,31
     CSR                   MOVE D1,3      AUT,32
     CSR                   MOVE S0,1      AUT,33
     CSR                   MOVE S0,3      AUT,34
     CSR                   MOVE S0,5      AUT,35
     CSR                   MOVE S0,7      AUT,36
     CSR                   ENDSR
** MS1 - MASTER ORGINAL SERIAL NUMBER AND DATE ARRAY
 1234567890ABCDEFGHIJKLMNOPQRSTUVWXYZ
** MS2 - MASTER ENCRYPTION KEY (MEK)
4CA2BZ9QP1WO7EI3RG5HF0TVLX6DJ8MYUSKN-
** NUMB - THE NUMBERS BETWEEN ONE AND NINE
123456789
