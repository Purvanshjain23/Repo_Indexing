      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Workfile: Grow Finish Groups--BGF Sources
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/04/01
      *
      * FUNCTION:    This function builds a file that contains 1 record for each
      *              unique combination of origin BGF farm/destination GF group.
      *
      ****************************************************************
      * MODIFICATIONS:
      ************************************************************************************
      * DATE      PROGRAMMER
      *
      * 12/16/02  LeAnne Fedor
      *           Added logic to get the new BGF-to-GF transfer sources into the file.
      /EJECT
      ************************************************************************************
      * FILE SPECIFICATIONS
      ************************************************************************************
      *
     Fhsp336    if   e           k disk
      *  Workfile: Nursery groups--BGF source total
      *
      *
     Fhsp329    if   e           k disk
      *  Workfile: Grow finish groups--nursery sources
      *
      *
     Fhsp351    if   e           k disk
      *  Workfile: BGF-to-Grow Finish transfers
      *
      *
     Fhsp324    uf a e           k disk
      *  Workfile: Grow finish groups--BGF sources
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkhd            s                   like(w5rchd)
     D wkremhd         s                   like(w5rchd)
     D wkrchd          s                   like(w5rchd)
     D wkdisthd        s                   like(w5rchd)
     D wkpr            s              5  4
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Process each destination group in the Workfile: Grow Finish Groups--Nursery Sources.
      * (Note: In this workfile, the destination groups are all Grow Finish Groups.)
      *
     C     *loval        setll     hsp329
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsp329                                 90
     C                   if        *in90 = *off and w5orhgsn <> 0               If not EOF
     C                   z-add     w5rchd        wkrchd
     C                   z-add     w5rchd        wkremhd
      *
     C                   exsr      $nursery
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      * Now, process the BGF-to-GF transfers.
      *
     C                   exsr      $bgf
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------
      * Read all records for this origin nursery group
      *----------------------------------------------------------------
      *
     C     $nursery      begsr
      *
      * Clear workfields
     C                   z-add     0             wkdisthd
     C                   z-add     0             wkhd
      *
      *
      * Read each BGF farm that contributed pigs to this nursery group.
      *
     C     w5orhgsn      setll     hsp336
      *
     C                   dou       *in91 = *on                                  Do nursery
     C     w5orhgsn      reade     hsp336                                 91
     C                   if        *in91 = *off                                 If more BGFs
      *
      * If you have any Received Head left to distribute for the Grow Finish
      * group, calculate the number of head to associate with this BGF farm.
      *
     C                   if        wkremhd > 0                                  If any left
     C                   exsr      $dist
     C                   endif                                                  If any left
      *
     C                   if        wkhd <> 0
     C                   exsr      $nur324
     C                   endif
      *
     C                   endif                                                  If more BGFs
     C                   enddo                                                  Do nursery
      *
      * If you had any leftover head, add them into the last record you
      * wrote/updated for this group/BGF farm.
      *
     C                   if        wkdisthd < wkrchd                            If leftover head
     C     wkrchd        sub       wkdisthd      wkhd
     C                   exsr      $nur324
     C                   endif                                                  If leftover head
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Calculate head to distribute to this BGF farm
      *----------------------------------------------------------------
      *
     C     $dist         begsr
      *
     C     .01           mult      w9hdpr        wkpr
     C     wkpr          mult      wkrchd        wkhd
      *
     C                   select
     C                   when      wkhd <= 0
      *
     C                   when      wkhd > wkremhd
     C                   z-add     wkremhd       wkhd
     C                   z-add     0             wkremhd
     C                   other
      *
     C                   sub       wkhd          wkremhd
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------
      * Write/update a record in Workfile: Grow Finish Groups-BGF Sources
      *-------------------------------------------------------------------
      *
     C     $nur324       begsr
      *
     C                   add       wkhd          wkdisthd
      *
      * If you already have a record for this Grow Finish group/BGF farm, just
      * update head; otherwise, write a record.
      *
      *
     C     key01         chain     hsp324                             92
     C                   if        *in92 = *off
     C                   add       wkhd          w8rchd
     C                   update    w8rec
     C                   else
      *
     C                   z-add     wkhd          w8rchd
     C                   z-add     w5dnhgsn      w8dnhgsn
     C                   move      w5dnhgcd      w8dnhgcd
     C                   move      w5dnppcd      w8dnppcd
     C                   z-add     w5dnfscd      w8dnfscd
     C                   z-add     w5dncell      w8dncell
     C                   z-add     w9orfscd      w8orfscd
     C                   z-add     w9orcell      w8orcell
     C                   write     w8rec
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------
      * Write/update BGF source records with BGF-to-GF transfer data
      *-------------------------------------------------------------------
      *
     C     $bgf          begsr
      *
     C     *loval        setll     hsp351
      *
     C                   dou       *in91 = *on                                  Do BGF/GF loop
     C                   read      hsp351                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * If you already have a record for this Grow Finish group/BGF farm, just
      * update head; otherwise, write a record.
      *
     C     key02         chain     hsp324                             92
     C                   if        *in92 = *off
     C                   add       wcrchd        w8rchd
     C                   update    w8rec
     C                   else
      *
     C                   z-add     wcrchd        w8rchd
     C                   z-add     wcdnhgsn      w8dnhgsn
     C                   move      wcdnhgcd      w8dnhgcd
     C                   move      wcdnppcd      w8dnppcd
     C                   z-add     wcdnfscd      w8dnfscd
     C                   z-add     wcdncell      w8dncell
     C                   z-add     wcorfscd      w8orfscd
     C                   z-add     wcorcell      w8orcell
     C                   write     w8rec
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do BGF/GF loop
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    w5dnhgsn
     C                   kfld                    w9orfscd
      *
     C     key02         klist
     C                   kfld                    wcdnhgsn
     C                   kfld                    wcorfscd
      *
     C                   endsr
      /EJECT
