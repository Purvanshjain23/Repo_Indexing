/******************************************************************* */
/*                                                                   */
/*  PROGRAM NUMBER:  HP204CLFD                                       */
/*  PROGRAM NAME:    BUILD JDE UPLOAD FILE: HPS FEED DELIVERY DOLLARS*/
/*  PROGRAMMER:      LEANNE FEDOR                                    */
/*  CREATE DATE:     10/12/01                                        */
/*                                                                   */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE DRIVER CL         */
/*                   HP404CL.                                        */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.             */
/*                   AN OPNQRY STATEMENT IS USED TO SELECT           */
/*                   RECORDS.                                        */
/*                                                                   */
/*                                                                   */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/* 10/16/18  Danny Nguyen  (S13682)                                */
/*           Retrieve Company Code from LDA. Change OPNQRYF logic  */
/*           for HSL038F to select by Company. Increase &QDATA     */
/*           from 65 to 90.                                        */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(90)

             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 25 3))


/*********************************************************************/
/*  SETUP OPNQRY STATEMENTS                                          */
/*********************************************************************/

/*  ALWAYS SELECT FEED TICKET DETAIL RECORDS FOR DATE RANGE OF THE       */
/*  CURRENTLY OPEN PERIOD                                                */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FDTKDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND FDTKDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

/*  ALWAYS SELECT ONLY DELIVERY TICKETS                                  */

             CHGVAR     VAR(%SST(&QDATA 46 19)) VALUE('*AND FDTRCD +
                          *EQ "D"')

/*  S13682-ALWAYS SELECT BY COMPANY CODE - JOIN BY BUSINESS OFFICE */

             CHGVAR     VAR(%SST(&QDATA 66 17)) VALUE('*AND BOCOCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 83 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA 86 1)) VALUE('"')


/*********************************************************************/
/*  SET UP FILE OVERRIDES AND PERFORM QUERIES                        */
/*********************************************************************/

/*  FEED DETAIL RECORDS                                          */

             OVRDBF     FILE(HSL038F) SHARE(*YES)
/*  S13682   OPNQRYF    FILE((HSL038F)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)            */
/*  S13682-JOIN TO BUSINESS OFFICE (HSP009) FILE TO GET COMPANY */
             OPNQRYF    FILE((HSL038F) (HSP009)) FORMAT(HSL038F) +
                          QRYSLT(&QDATA) KEYFLD(*FILE) +
                          JFLD((HSL038F/FDFMBO HSP009/BOBOBO)) +
                          SEQONLY(*YES)


/*********************************************************************/
/*  CALL PROGRAM TO BUILD UPLOAD FILE OF FEED DELIVERIES             */
/*********************************************************************/

             CALL       PGM(HP204FD)

/*******************************************************************/
/*  DELETE FILE OVERRIDES                                          */
/*******************************************************************/

             CLOF       OPNID(HSL038F)
             DLTOVR     FILE(HSL038F)

             ENDPGM

