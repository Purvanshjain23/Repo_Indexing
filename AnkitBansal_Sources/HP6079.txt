      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6079
      * TITLE:       Room Inventory Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     0416/02
      *
      * FUNCTION:  Batch program to list room inventory...data comes from Datamart files!
      *
      *            This listing is submitted from HP4079-Specify Options for
      *            Room Inventory Listing which is called from a menu.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/27/04  LeAnne Fedor
      *           Recompile only. New field "3-week average dead" added to file
      *           Datamart FIN: Weekly Group Detail.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart Group Header file.
      *
      * 08/25/05  LeAnne Fedor
      *           Recompile only.
      *           Added a new field "Man Hours" to Weekly Group Detail file.
      *
      * 01/16/06  LeAnne Fedor
      *           Recompile only.
      *           We have removed "Bin Set" from HSP020-Building/Rooms and put it in our new
      *           HSP113-Rooms/Bin Sets file.
      *
      * 08/02/06  LeAnne Fedor
      *           Recompile only. Two new fields (ration 8 feed pounds, ration paylean flag)
      *           added to Datamart Group Header file.
      *
      * 08/10/07  LeAnne Ramsey
      *           Recompile only. New "target" fields renamed in the Datamart Group
      *           Header file.
      *
      * 09/21/07  LeAnne Ramsey
      *           Recompile only. New "target" fields added to the Datamart FIN files:
      *              1) Weekly Group Detail
      *              2) Group Header
      *
      * 05/24/10  LeAnne Ramsey
      *           Recompile only. Added 2 fields to HPPF075-Weekly Group Detail:
      *              WBWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      *              WBPAJTIHD -Placement Adjusted Transfer In Head
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/14/16  Barb Gutierrez
      *           Recompile only. Added fields Sold days in phase, Avg Sold days in phase,
      *           Wgt open date, and company number to HPPF034.         E6136
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp011    if   e           k disk
      *   Production phase
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site                (records selected/keyed in open query)
      *
      *
     Fhsp020    if   e           k disk
      *   Building/rooms           (records selected in open query)
      *
      *
     Fhppf034   if   e           k disk
      *    Datamart: Hog group
      *
      *
     Fhplf075c  if   e           k disk
      *  Datamart: Weekly group information
      *
      *
     Fqprint    o    f   85        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D only            c                   'O'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             85
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D h1ppcd          s                   like(ldppcd)
     D h2ppds          s                   like(ppppds)
     D h1empds         s              4
      *
     D l1emphd         s              7  0
     D l1notemphd      s              7  0
     D l1mdy           s              6  0
      *
      *
      * Work fields
      *
     D wkhd            s              7  0
     D wkmdy           s               D   datfmt(*mdy)
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D printfl         s              1    inz('N')
     D svfscd          s                   like(fsfscd)
     D svppcd          s                   like(fsppcd)
     D svhgcd          s                   like(hghgcd)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldppcd                 1      5
     D  ldcell                 6      7  0
     D  ldfscd                 8     12  0
     D  ldempfl               13     13
      *
     D  ldppds                14     43
     D  ldfsnm                44     68
     D  ldempds               69     98
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the farm file and print headings the first time through.
      *
593  C*    *loval        setll     hsp018
593  C     *start        setll     hsp018
     C                   except    h1hdr
      *
      * Read each farm that was selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        first = yes
     C                   move      no            first
     C                   z-add     fsfscd        svfscd
     C                   move      fsppcd        svppcd
     C                   exsr      $ppds
     C                   endif
      *
      * Read each active bldg/room record for the farm and go accumulate
      * its current inventory from the Datamart file.
      *
     C     fsfscd        setll     hsp020
     C                   dou       *in91 = *on                                  Do rooms
     C     fsfscd        reade     hsp020                                 91
     C                   if        *in91 = *off
     C                   exsr      $inventory
     C                   endif
     C                   enddo                                                  Do rooms
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------
      * Retrieve Production Phase description and print
      *--------------------------------------------------------------------------
      *
     C     $ppds         begsr
      *
     C     svppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   move      ppppds        h2ppds
     C                   else
     C                   move      *blank        h2ppds
     C                   endif
      *
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------
      * Retrieve inventory from Datamart FIN Weekly file for the building/room
      *--------------------------------------------------------------------------
      *
     C     $inventory    begsr
      *
     C                   z-add     0             wkhd
     C                   move      *blank        svhgcd
      *
     C     key01         setll     hplf075c
     C                   dou       *in93 = *on                                  Do weekly
     C     key01         reade     hplf075c                               93
     C                   if        *in93 = *off                                 If record
      *
      * Determine if group is 'open'. We only want the 'open' groups...
      * all other groups will have zero inventory.
      *
     C     wbhgcd        chain     hppf034                            92
     C                   if        *in92 = *off and hggscd = 'OP'               If open
      *
      * You will have multiple weekly records for a group. Take the ending
      * inventory head from the first record only...(file is keyed descending
      * by date.)
     C                   if        svhgcd <> wbhgcd                             If new group
     C                   add       wbeihd        wkhd
     C                   move      wbhgcd        svhgcd
     C                   movel     wbwedt        wkmdy
     C                   movel     wkmdy         l1mdy
     C                   endif                                                  If new group
      *
     C                   endif                                                  If open
      *
     C                   endif                                                  If record
     C                   enddo                                                  Do weekly
      *
      * Now, determine whether you are to print this building/room..it depends
      * on the user's report selections.
      *
     C                   move      yes           printfl
      *
     C                   select
     C                   when      ldempfl = no and wkhd < 50
     C                   move      no            printfl
      *
     C                   when      ldempfl = only and wkhd >= 50
     C                   move      no            printfl
     C                   endsl
      *
      *
      *  If you are printing......
      *
     C                   if        printfl = yes                                 If print
      *
      * Determine whether the accumulated head goes into:
      *  1) The 'Empty column'---less than fifty head, or
      *  1) The 'Not Empty column'---50 head or more
      *
     C                   if        wkhd < 50
     C                   z-add     wkhd          l1emphd
     C                   else
     C                   z-add     wkhd          l1notemphd
     C                   endif
      *
      * Insert a blank line before each farm..except when you are already
      * going to a new page.
      *
     C                   select
     C                   when      fsppcd <> svppcd
     C                   except    h1hdr
     C                   move      fsppcd        svppcd
     C                   exsr      $ppds
      *
     C                   when      *inof = *on
     C                   except    h1hdr
     C                   except    h2hdr
      *
     C                   when      fsfscd <> svfscd
     C                   except    blank
     C                   z-add     fsfscd        svfscd
     C                   endsl
      *
     C                   except    l1dtl
     C                   move      yes           prtfl
     C                   endif                                                  If print
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    brfscd
     C                   kfld                    brblcd
     C                   kfld                    brrmcd
      *
      *
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up headings
      *
     C                   if        ldppcd = *blank
     C                   movel     'All'         h1ppcd
     C                   else
     C                   move      ldppcd        h1ppcd
     C                   endif
      *
     C                   if        ldcell = 0
     C                   seton                                        88
     C                   endif
      *
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   endif
      *
     C                   select
     C                   when      ldempfl = no
     C                   move      'No  '        h1empds
     C                   when      ldempfl = yes
     C                   move      'Yes '        h1empds
     C                   when      ldempfl = only
     C                   move      'Only'        h1empds
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           75 'DATE'
     O                       udate         y     85
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           52 'ROOM INVENTORY LISTING'
     O                                           75 'TIME'
     O                       rtime               85 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           75 'PAGE'
     O                       page          z     85
      *
      *
     O          e            h1hdr          1
     O                                           20 'Production phase ..:'
     O                       h1ppcd              27
     O                       ldppds              59
      *
      *
     O          e            h1hdr          1
     O                                           20 'Cell ..............:'
     O              N88      ldcell        z     24
     O               88                          25 'All'
      *
      *
     O          e            h1hdr          1
     O                                           20 'Farm site .........:'
     O              N89      ldfscd        z     27
     O              N89      ldfsnm              54
     O               89                          25 'All'
      *
     O          e            h1hdr          2
     O                                           20 'Print empty rooms .:'
     O                       h1empds             26
      *
      *
      *-------------------------------------------------------------
      * Phase heading
      *-------------------------------------------------------------
      *
     O          e            h2hdr          1
     O                                           17 'Production phase:'
     O                       h2ppds              50
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           57 'WEEK-'
      *
     O          e            h1hdr          1
     O                                           11 'FARM'
     O                                           46 '---INVENTORY HEAD---'
     O                                           57 'ENDING'
     O          e            h1hdr          1
     O                                            4 'CELL'
     O                                           11 'SITE'
     O                                           17 'BLDG'
     O                                           24 'ROOM'
     O                                           34 'EMPTY'
     O                                           46 'NOT EMPTY'
     O                                           57 'DATE'
     O                                           69 'FARM NAME'
      *
     O          e            h1hdr          1
     O                       dash                85
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       fscell        z      3
     O                       fsfscd        z     11
     O                       brblcd              18
     O                       brrmcd              25
     O                       l1emphd       kb    35
     O                       l1notemphd    kb    47
     O                       l1mdy          b    57 '  /  /  '
     O                       fsfsnm              85
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
