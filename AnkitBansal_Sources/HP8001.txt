      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP8001 - Update Control File: HPS to JDE Upload
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/15/01
      *
      * Function:    Update control file of HPS to JDE Upload.
      *
      *              This program is called from both the Upload Function and
      *              the Reset Function.
      *
      *************************************************************************************
      * MODIFICATIONS:
      *************************************************************************************
      * DATE      PROGRAMMER
      *
      * 11/11/08  LeAnne Ramsey
      *           We are giving the users the functionality to reset a control record.
      *           As part of this change, we are adding a new file (HSP116) to track
      *           the status of each control record as it changes.
      *
      * 04/27/09  LeAnne Ramsey
      *           Added Period Begin/End Dates as entry parms and as new fields in HSP087.
      *
      * 09/26/18  Danny Nguyen - S13682
      *           Added Company to entry parm list & to Key01 list.
      *           Set Company Code from Company passed in from *entry parm before creating
      *           HSP087 & HSP116 record.
      /eject
      *************************************************************************************
      * FILE SPECIFICATIONS
      *************************************************************************************
      *
     Fhsp087    uf a e           k disk
      *    Control file: HPS Upload to JDE
      *
      *
     Fhsp116    o    e           k disk
      *    HPS-to-JDE Uploads: Status changes
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkcrdt          s              8  0
      *
      *
      * Parm fields
      *
13682D xxcocd          s                   like(hscocd)
     D xxuplscd        s                   like(hsuplscd)
     D xxhucd          s                   like(hshucd)
     D xxacyr          s                   like(hsacyr)
     D xxacpe          s                   like(hsacpe)
     D xxbpdt          s                   like(hsbpdt)
     D xxepdt          s                   like(hsepdt)
     D xxpgm           s                   like(scpgm)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Write/update the Control Record for this:
      *   0) company code
      *   1) upload code
      *   2) accounting year
      *   3) accounting period
      *
     C     key01         chain     hsp087                             92
     C                   if        *in92 = *off                                 If found
     C                   move      xxuplscd      hsuplscd
     C                   update    hsrec
     C                   else
      *
13682C                   move      xxcocd        hscocd
     C                   move      xxhucd        hshucd
     C                   z-add     xxacyr        hsacyr
     C                   z-add     xxacpe        hsacpe
     C                   z-add     xxbpdt        hsbpdt
     C                   z-add     xxepdt        hsepdt
     C                   move      xxuplscd      hsuplscd
     C                   write     hsrec
     C                   endif                                                  If found
      *
      * Write a Status Change record for this:
      *
     C                   exsr      $wrt116
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Write a Status Change record
      *---------------------------------------------------------------
      *
     C     $wrt116       begsr
      *
13682C                   move      xxcocd        sccocd
     C                   move      xxhucd        schucd
     C                   z-add     xxacyr        scacyr
     C                   z-add     xxacpe        scacpe
     C                   move      xxuplscd      scuplscd
      *
     C                   move      sdusr         sccrusr
     C                   move      xxpgm         scpgm
     C                   z-add     wkcrdt        sccrdt
     C                   time                    sccrtm
      *
     C                   write     screc
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
13682C                   parm                    xxcocd
     C                   parm                    xxuplscd
     C                   parm                    xxhucd
     C                   parm                    xxacyr
     C                   parm                    xxacpe
     C                   parm                    xxbpdt
     C                   parm                    xxepdt
     C                   parm                    xxpgm
      *
      *
     C     key01         klist
13682C                   kfld                    xxcocd
     C                   kfld                    xxhucd
     C                   kfld                    xxacyr
     C                   kfld                    xxacpe
      *
      * Save date for record stamp
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wkcrdt
      *
     C                   endsr
      /eject
