      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP388
      * TITLE:       BUILD WORKFILES--CLOSED GROUP REPORTS
      * PROGRAMMER:  MARY PALMER
      * CREATED:     10/19/94
      *
      *  FUNCTION:   THIS PROGRAM BUILDS THE DATA FOR CLOSED GROUPS.
      *
      *              THIS WORKFILE PROGRAM IS USED IN DIFFERENT CLOSED
      *              GROUP REPORTS:
      *                HP482/HP482CL - CLOSED GROUP PERFORMANCE REPORT
      *                HP498/HP498CL - CLOSED GROUP RATION REPORT
      *
      *           THE REPORT SELECTIONS ARE IN THE LDA AND QCMDEXC IS
      *           USED TO SUBMIT THE JOBS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/12/00  LeAnne Fedor
      *           Recompile only. The 'closed group pounds' field in the
      *           Closed Hog Group file was increased to 11,2.
      *
      * 08/17/00  LeAnne Fedor
      *           Added logic to populate the new workfile field 'adjusted
      *           transfer in head'.  Also, changed cull % and mortality %
      *           calculations to use this 'adjusted' value instead of the
      *           normal transfer in head.
      *
      * 05/02/01  LeAnne Fedor
      *           Changed logic that calculates 'average pounds placed' to used
      *           the 'adjusted' transferred in head and pounds.
      *
      * 04/04/02  LeAnne Fedor
      *           Per Jeff Sherboundy, removed all special logic for FHAUL/CHAUL.
      *
      * 07/22/02  LeAnne Fedor
      *           In preparation for allowing groups on a farm to have different phases, we
      *           added phase and phase logic to the Farm Totals workfile.
      *
      * 01/29/03  LeAnne Fedor
      *           Removed all hardcoded special logic for SHAUL.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL006B   IF   E           K DISK
      *    REPORTING CODES
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
     FHSP033    IF   E           K DISK
      *    CLOSED GROUP SUMMARY
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP
      *
     FHSP044    IF   E           K DISK
      *    PREMUIM/DISCOUNT
      *
     FHSP045    IF   E           K DISK
      *    DEDUCTIONS/ADDONS
      *
     FHSP387    O  A E             DISK
      *    WORKFILE OF GROUP DETAIL
      *
     FHSP388    O  A E             DISK
      *    WORKFILE OF FARM TOTALS
      *
     FHSP389    O  A E             DISK
      *    WORKFILE OF PHASE TOTALS
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFROM                 6     13  0
     D  LDTO                  14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDFSCD                32     36  0
     D  LDOPTN                37     37
     D  LDCELL                50     51  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D GF              C                   CONST('GF   ')
      *
      * REPORT CODES
      *
     D FPS             C                   CONST('FPS  ')
     D COS             C                   CONST('COS  ')
     D BTCAR           C                   CONST('BTCAR')
     D BTLIV           C                   CONST('BTLIV')
     D WGAIN           C                   CONST('WGAIN')
     D DDEAD           C                   CONST('DDEAD')
     D TRANI           C                   CONST('TRANI')
     D TRANA           C                   CONST('TRANA')
     D TRANO           C                   CONST('TRANO')
     D FCONS           C                   CONST('FCONS')
     D DEAD            C                   CONST('DEAD ')
     D INADJ           C                   CONST('INADJ')
     D CULLS           C                   CONST('CULLS')
     D CYIEL           C                   CONST('CYIEL')
     D BFIN            C                   CONST('BFIN ')
     D LEAN            C                   CONST('LEAN ')
     D PDAYS           C                   CONST('PDAYS')
     D FCAL            C                   CONST('FCAL ')
      *
      * REPORT CATEGORIES
      *
     D OPCST           C                   CONST('OPCST')
     D OTCST           C                   CONST('OTCST')
     D SALES           C                   CONST('SALES')
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ EACH HOG GROUP RECORD SELECTED BY THE OPEN QUERY. IF
      * THE USER SELECT A SPECIFIC 'CELL', CHECK TO SEE THAT THIS
      * GROUP IS ON A FARM IN THAT CELL.
      *
     C     *IN99         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP034                                 99
     C     *IN99         IFEQ      *OFF                                         IF NOT EOF
     C                   MOVEL     YES           PROCFL
      *
      * DETERMINE IF THE GROUP IS ON A FARM FOR THE SELECTED:
      *   1) CELL
      *
     C     LDCELL        IFGT      0                                            IF SPECIFIC
     C     HGFSCD        CHAIN     HSP018                             92
     C                   IF        *IN92 = *ON OR
     C                             (FSCELL <> LDCELL AND LDCELL > 0)
     C                   MOVEL     NO            PROCFL
     C                   ENDIF
     C                   ENDIF                                                  IF SPECIFIC
      *
     C     PROCFL        IFEQ      YES                                          IF PROCESS
      *
     C     FIRST         IFEQ      YES
     C                   MOVEL     NO            FIRST
     C                   MOVEL     HGPPCD        SVPPCD
     C                   Z-ADD     HGFSCD        SVFSCD
     C                   ENDIF
      *
      * IF THERE IS A CHANGE IN THE PRODUCTION PHASE
      *    WRITE FARM TOTAL RECORD TO WORKFILE
      *    WRITE PRODUCTION PHASE TOTAL RECORD TO WORKFILE
      *    SAVE CONTROL FIELDS
      *    CLEAR WORKFIELDS AND ACCUMULATORS
      *
     C     HGPPCD        IFNE      SVPPCD                                       IF PP BREAK
     C                   EXSR      $WFTOT
     C                   EXSR      $WPTOT
     C                   MOVEL     HGPPCD        SVPPCD
     C                   Z-ADD     HGFSCD        SVFSCD
     C                   EXSR      $CLRFS
     C                   EXSR      $CLRPP
     C                   ENDIF                                                  IF PP BREAK
      *
      * IF THERE IS A CHANGE IN THE FARM SITE,
      *    WRITE FARM TOTAL RECORD TO WORKFILE
      *    SAVE CONTROL VALUE
      *    CLEAR WORKFIELDS AND ACCUMULATORS FOR FARM SITE
      *
     C     HGFSCD        IFNE      SVFSCD                                       IF FS BREAK
     C                   EXSR      $WFTOT
     C                   Z-ADD     HGFSCD        SVFSCD
     C                   EXSR      $CLRFS
     C                   ENDIF                                                  IF FS BREAK
      *
      * SETUP AND FORMAT FIELDS FOR THE HOG GROUP RECORD
      *
     C                   EXSR      $SETUP
      *
      * RETRIEVE THE FOLLOWING INFORMATION FOR THE HOG GROUP.
      *    MOVEMENTS
      *    PERFORMANCE
      *    SALES
      *    COSTS (INCLUDING CULL SALES AS COST)
      *    OTHER COSTS
      *
     C                   EXSR      $MOVE
     C                   EXSR      $PERF
     C                   EXSR      $SALE
     C                   EXSR      $COST
     C                   EXSR      $OTCST
      *
      * WRITE A GROUP RECORD TO THE WORKFILE AND THEN
      * CLEAR WORKFIELDS AND ALSO FIELDS IN THE GROUP WORKFILE
      *
     C                   WRITE     WGREC
     C                   EXSR      $CLRHG
      *
     C                   ENDIF                                                  IF PROCESS
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *
     C     FIRST         IFEQ      NO
     C                   EXSR      $WFTOT
     C                   EXSR      $WPTOT
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * SETUP AND FORMAT FIELDS FOR THE HOG GROUP DETAIL RECORD
      *----------------------------------------------------------------
      *
     C     $SETUP        BEGSR
      *
      * MOVE GROUPS FIELDS TO THE GROUP WORKFILE RECORD
      * AND, SET UP A KEY FIELD FOR THE HOG GROUP
      *
     C                   MOVEL     HGPPCD        WGPPCD
     C                   MOVEL     HGPTCD        WGPTCD
     C                   MOVEL     HGFSBO        WGFSBO
     C                   Z-ADD     HGFSCD        WGFSCD
     C                   MOVEL     HGHGCD        WGHGCD
     C                   Z-ADD     HGHGSN        WGHGSN
     C                   MOVEL     HGSXCD        WGSXCD
     C                   MOVEL     HGGNCD        WGGNCD
     C                   Z-ADD     HGCLDT        WGCLDT
     C                   Z-ADD     HGDSDT        WGDSDT
      *
     C                   Z-ADD     HGHGSN        KYHGSN
      *
      * CALCULATE THE DAYS IN THE PHASE & CONVERT TO WEEKS - DAYS
      *
     C                   Z-ADD     HGOPDT        PFRM8
     C                   Z-ADD     HGDSDT        PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAY
     C                   MOVEL     'W'           PCODE
     C                   MOVE      *BLANKS       PRTRN
      *
     C                   EXSR      $DATE
      *
      * CONVERT ELASPED DAYS TO WEEKS AND DAYS
      *
     C                   Z-ADD     PDAY          WGPDAY
     C                   Z-ADD     0             WGWK
     C                   Z-ADD     0             WGDAY
      *
     C     PDAY          IFGT      0
     C     PDAY          DIV       7             WGWK
     C                   MVR                     WGDAY
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * MOVEMENTS INFORMATION
      *----------------------------------------------------------------
      *
     C     $MOVE         BEGSR
      *
      *--------------------------------------
      * GET THE ADJUSTED TRANSFERRED IN HEAD
      *--------------------------------------
      *
     C                   MOVEL     TRANA         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         If adjusted in
     C                   Z-ADD     CGCGHD        WGAJTIHD
      *
      * CALCULATE THE AVERAGE POUNDS PLACED
      *
     C     CGCGHD        IFNE      0
     C     CGCGLB        DIV(H)    CGCGHD        WGAPLB
     C                   ENDIF
      *
      * ACCUMULATE TOTALS
     C                   ADD       CGCGLB        AFAJTILB
     C                   ADD       CGCGLB        APAJTILB
     C                   ADD       CGCGHD        WFAJTIHD
     C                   ADD       CGCGHD        WPAJTIHD
     C                   ENDIF                                                  If adjusted in
      *
      *-------------------------------
      * GET THE TRANSFERRED IN HEAD
      *-------------------------------
      *
     C                   MOVEL     TRANI         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF TRAN IN
     C                   Z-ADD     CGCGHD        WGTIHD
     C                   Z-ADD     CGCGAM        WKTIAM
      *
      * ACCUMULATE TOTALS
     C                   ADD       CGCGHD        WFTIHD
     C                   ADD       CGCGAM        AFTIAM
      *
     C                   ADD       CGCGHD        WPTIHD
     C                   ADD       CGCGAM        APTIAM
     C                   ENDIF                                                  IF TRAN IN
      *
      *---------------------------------
      * GET THE TRANSFERRED OUT HEAD
      *----------------------------------
      *
     C                   MOVEL     TRANO         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF TRAN OUT
      *
     C                   Z-ADD     CGCGHD        WGTOHD
     C                   Z-ADD     CGCGLB        WKTOLB
      * ACCUMULATE TOTALS
     C                   ADD       CGCGHD        WFTOHD
     C                   ADD       CGCGHD        WPTOHD
     C                   ENDIF                                                  IF TRAN OUT
      *
      *------------------------------------------------------
      * GET THE DEAD FOR USE IN CALCULATING MORTALITY PERCENT
      *------------------------------------------------------
      *
     C                   MOVEL     DEAD          KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF DEAD
     C                   ADD       CGCGHD        WGDDHD
      * ACCUMULATE TOTALS
     C                   ADD       CGCGHD        WFDDHD
     C                   ADD       CGCGHD        WPDDHD
     C                   ENDIF                                                  IF DEAD
      *
      *------------------------------------------------------
      * GET THE ADJUSTMENTS AND SUBTRACT FROM THE DEAD COUNT
      *------------------------------------------------------
      *
     C                   MOVEL     INADJ         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF ADJUST
     C                   SUB       CGCGHD        WGDDHD
      * ACCUMULATE TOTALS
     C                   SUB       CGCGHD        WFDDHD
     C                   SUB       CGCGHD        WPDDHD
     C                   ENDIF                                                  IF ADJUST
      *
      *--------------------------------------------------------
      * MORTALITY % = DEAD HEAD / ADJUSTED TRANSFERRED IN HEAD
      *--------------------------------------------------------
      * MORTALITY PERCENT IS ALSO CALLED 'DEATH LOSS %'.
      * IT IS CALCULATED USING 'DEAD' HEAD THAT HAVE BEEN
      * ADJUSTED WITH THE 'INVENTORY ADJUSTMENT' HEAD. THUS,
      * THE 'DEAD' HEAD HERE INCLUDE DAILY DEADS, DOA HEAD
      * AND INVENTORY ADJUSTMENT HEAD.
      *
     C     WGAJTIHD      IFNE      0
     C     WGDDHD        DIV(H)    WGAJTIHD      WKFLD3
     C     WKFLD3        MULT(H)   100           WGDDPR
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * PERFORMANCE STATISTICS
      *----------------------------------------------------------------
      *
     C     $PERF         BEGSR
      *
      *--------------------------------------------------------------
      * RETRIEVE POUNDS ASSOCIATED WITH DAILY DEAD HEAD
      *--------------------------------------------------------------
      *
     C                   MOVEL     DDEAD         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF DAILY DEAD
     C                   Z-ADD     CGCGLB        WKDDLB
      * ACCUMULATE TOTALS
     C                   ADD       CGCGLB        AFDDLB
     C                   ADD       CGCGLB        APDDLB
     C                   ENDIF                                                  IF DAILY DEAD
      *
      *--------------------------------------------------------------
      * RETRIEVE WEIGHT GAINED
      *--------------------------------------------------------------
      *
     C                   MOVEL     WGAIN         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF GAIN
     C                   Z-ADD     CGCGLB        WKWGLB
      * ACCUMULATE TOTALS
     C                   ADD       CGCGLB        AFWGLB
     C                   ADD       CGCGLB        AFWXLB
      *
     C                   ADD       CGCGLB        APWGLB
     C                   ADD       CGCGLB        APWXLB
     C                   ENDIF                                                  IF GAIN
      *
      *----------------------------------------------------
      * RETRIEVE TOTAL COST
      *----------------------------------------------------
      *
      *  ACCUMULATE TOTAL COST INTO THE PHASE COST DOLLARS
      *
     C                   MOVEL     COS           KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF
     C                   ADD       CGCGAM        WKP$AM
     C                   ENDIF
      *
      * DEDUCT THE COST OF TRANSFERS IN FROM THE PHASE COST
      * THEN, CALCULATE THE PHASE COST PER POUND OF GAIN
      *
     C     WKP$AM        SUB       WKTIAM        WKP$AM
     C     WKWGLB        IFNE      0
     C     WKP$AM        DIV(H)    WKWGLB        WGP$GN
      *
      * ACCUMULATE TOTALS
     C                   ADD       WKP$AM        AFP$AM
     C                   ADD       WKP$AM        APP$AM
     C                   ENDIF
      *
      *----------------------------------------------
      * RETRIEVE PIG DAYS
      *----------------------------------------------
      *
     C                   MOVEL     PDAYS         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF PIG DAYS
      *
      * ACCUMULATE TOTALS
     C                   ADD       CGCGQT        AFPDAY
     C                   ADD       CGCGQT        APPDAY
      *
      * CALCULATE:
      *  1) AVERAGE DAILY GAIN = WEIGHT GAIN / TOTAL PIG DAYS
      *  2) ADJUSTED AVERAGE DAILY GAIN =
      *          (WEIGHT GAIN + DAILY DEAD POUNDS) / TOTAL PIG DAYS
      *
     C     CGCGQT        IFNE      0                                            IF DAYS
      *
      * AVERAGE DAILY GAIN
      *
     C     WKWGLB        DIV(H)    CGCGQT        WGAGLB
      *
      * ADJUSTED AVERAGE DAILY GAIN
      *
     C     WKWGLB        ADD       WKDDLB        WKFLD2
     C     WKFLD2        DIV(H)    CGCGQT        WGJGLB
     C                   ENDIF                                                  IF DAYS
     C                   ENDIF                                                  IF PIG DAYS
      *
      *----------------------------------------------------------------
      * THE REMAINING CODE IN THIS SUBROUTINE IS ONLY EXECUTED
      * FOR GROW FINISH GROUPS
      *----------------------------------------------------------------
      *
     C     HGPPCD        IFEQ      GF                                           IF G/F
      *
      *--------------------------
      * RETRIEVE BACKFAT INCHES
      *---------------------------
      *
     C                   MOVEL     BFIN          KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF BACKFAT
     C                   Z-ADD     CGCGQT        WGBFIN
      *
      * CALCULATE BACKFAT QTY =  BACKFAT INCHES * CARCASS POUNDS
      *
     C     CGCGQT        MULT      CGCRLB        WKFLD3
      *
      * ACCUMULATE TOTALS
     C                   ADD       WKFLD3        AFTBFI
     C                   ADD       WKFLD3        APTBFI
      *
     C                   ADD       CGCRLB        AFCRLB
     C                   ADD       CGCRLB        APCRLB
     C                   ENDIF                                                  IF BACKFAT
      *
      *------------------------
      * RETRIEVE CARCASS YIELD
      *------------------------
      *
     C                   MOVEL     CYIEL         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF YIELD
     C                   Z-ADD     CGCGQT        WGCRYD
      * ACCUMULATE TOTALS
     C                   ADD       CGCGLB        AFCYLB
     C                   ADD       CGCRLB        AFCYCB
      *
     C                   ADD       CGCGLB        APCYLB
     C                   ADD       CGCRLB        APCYCB
     C                   ENDIF                                                  IF YIELD
      *
      *-----------------------------------------------
      * RETRIEVE LEAN PERCENT
      *-----------------------------------------------
      *
     C                   MOVEL     LEAN          KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF LEAN
     C                   Z-ADD     CGCGQT        WGLEAN
      *
      * CALCULATE LEAN QTY = MULTIPLY LEAN PERCENT * CARCASS POUNDS
      *
     C     CGCGQT        MULT      CGCRLB        WKFLD3
      *
      * ACCUMULATE TOTALS
     C                   ADD       WKFLD3        AFLNQT
     C                   ADD       WKFLD3        APLNQT
      *
     C                   ADD       CGCRLB        AFLNCB
     C                   ADD       CGCRLB        APLNCB
     C                   ENDIF                                                  IF LEAN
     C                   ENDIF                                                  IF G/F
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SALE
      *----------------------------------------------------------------
      *
     C     $SALE         BEGSR
      *
      *-----------------------------------------------------
      * RETRIEVE BUTCHER HOG, LIVE AND FEEDER PIG SALES
      *-----------------------------------------------------
      *
     C     SALES         SETLL     HSL006B
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO SALES
     C     SALES         READE     HSL006B                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
     C     RPRPCD        IFEQ      BTCAR                                        IF RIGHT
     C     RPRPCD        OREQ      BTLIV
     C     RPRPCD        OREQ      FPS
      *
     C                   MOVEL     RPRPCD        KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF HIT
      *
     C                   ADD       CGCGHD        WGSLHD
     C                   ADD       CGCGLB        WKSLLB
     C                   ADD       CGCGAM        WKSLAM
      *
      * ACCUMULATE TOTALS
     C                   ADD       CGCGHD        WFSLHD
     C                   ADD       CGCGLB        AFSLLB
     C                   ADD       CGCGAM        AFSLAM
      *
     C                   ADD       CGCGHD        WPSLHD
     C                   ADD       CGCGLB        APSLLB
     C                   ADD       CGCGAM        APSLAM
     C                   ENDIF                                                  IF HIT
     C                   ENDIF                                                  IF RIGHT
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO SALES
      *
      * ADD SOLD AND TRANSFERRED OUT VALUES TO GET MOVED OUT
      *
     C     WKSLLB        ADD       WKTOLB        WKMOLB
     C     WGSLHD        ADD       WGTOHD        WKMOHD
      *
      * ACCUMULATE TOTALS
     C                   ADD       WKMOHD        AFMOHD
     C                   ADD       WKMOHD        AFMXHD
     C                   ADD       WKMOLB        AFMOLB
     C                   ADD       WKMOLB        AFMXLB
      *
     C                   ADD       WKMOHD        APMOHD
     C                   ADD       WKMOHD        APMXHD
     C                   ADD       WKMOLB        APMOLB
     C                   ADD       WKMOLB        APMXLB
      *
      * CONVERT SOLD POUNDS AND MOVED OUT POUNDS TO CWT
      *
     C     WKSLLB        DIV       100           WKSCWT
     C     WKMOLB        DIV       100           WKMCWT
      *
      *-------------------------------
      * RETRIEVE CALORIES
      *-------------------------------
      *
     C                   MOVEL     FCAL          KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     CGCGQT        WKCGQT
      * ACCUMULATE TOTALS
     C                   ADD       CGCGQT        AFCGQT
     C                   ADD       CGCGQT        APCGQT
     C                   ENDIF
      *
      *-------------------------------
      * RETRIEVE FEED CONSUMED
      *-------------------------------
      *
     C                   MOVEL     FCONS         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     CGCGLB        WKFCLB
     C                   Z-ADD     CGCGAM        WKFCAM
      * ACCUMULATE TOTALS
     C                   ADD       CGCGLB        AFFCLB
     C                   ADD       CGCGAM        AFFCAM
      *
     C                   ADD       CGCGLB        APFCLB
     C                   ADD       CGCGAM        APFCAM
     C                   ENDIF
      *
      * IF FEED POUNDS AND FEED AMOUNT ARE ZERO, BACK OUT
      * WEIGHT GAIN, HEAD MOVED OUT AND POUNDS MOVED OUT FROM THE
      * ACCUMULATORS WITH AN 'X'
      *
     C     *IN92         IFEQ      *ON                                          IF NO FEED
     C     WKFCLB        OREQ      0
     C     WKFCAM        ANDEQ     0
      *
     C     AFWXLB        SUB       WKWGLB        AFWXLB
     C     AFMXHD        SUB       WKMOHD        AFMXHD
     C     AFMXLB        SUB       WKMOLB        AFMXLB
      *
     C     APWXLB        SUB       WKWGLB        APWXLB
     C     APMXHD        SUB       WKMOHD        APMXHD
     C     APMXLB        SUB       WKMOLB        APMXLB
     C                   ENDIF                                                  IF NO FEED
      *
      *
      * CALCULATE AVERAGE POUNDS OF FEED PER HEAD MOVED
      *
     C                   Z-ADD     WKMOHD        WGMVHD
     C     WKMOHD        IFNE      0
     C     WKFCLB        DIV(H)    WKMOHD        WGAFLB
     C                   ENDIF
      *
      * IF POUNDS OF WEIGHT GAINED IS NOT ZERO,
      *     CALCULATE FEED CONVERSION
      *     CALCULATE FEED COST PER POUND OF GAIN
      *
     C     WKWGLB        IFNE      0
     C     WKFCLB        DIV(H)    WKWGLB        WGFDCV
     C     WKFCAM        DIV(H)    WKWGLB        WGF$GN
     C                   ENDIF
      *
      * CALCULATE ADJUSTED FEED CONVERSION =
      *    FEED POUNDS / (WEIGHT GAIN + DAILY DEAD POUNDS)
      *
     C     WKWGLB        ADD       WKDDLB        WKFLD2
     C     WKFLD2        IFNE      0
     C     WKFCLB        DIV(H)    WKFLD2        WGAFCV
     C                   ENDIF
      *
      * CALCULATE AVERAGE CALORIES PER POUND OF FEED =
      *    CALORIES / LBS OF FEED
      *
     C     WKFCLB        IFNE      0
     C     WKCGQT        DIV(H)    WKFCLB        WGACAL
     C                   ENDIF
      *
      * CALCULATE AVERAGE FEED COST PER TON
      *
     C     WKFCLB        DIV(H)    2000          WKTONS
     C     WKTONS        IFNE      0
     C     WKFCAM        DIV(H)    WKTONS        WGFC$T
     C                   ENDIF
      *
      * CALCULATE THE AVERAGE POUNDS PER HEAD MOVED OUT
      *
     C     WKMOHD        IFNE      0
     C     WKMOLB        DIV(H)    WKMOHD        WGAMLB
     C                   ENDIF
      *
      *-----------------------------------------------------------
      * RETRIEVE PREMIUM/DISCOUNT DOLLARS
      *-----------------------------------------------------------
      *
593  C*    *LOVAL        SETLL     HSP045
593  C     *START        SETLL     HSP045
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO PREMIUMS
     C                   READ      HSP045                                 91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
     C                   MOVEL     PDRPCD        KYRPCD
      *
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       CGCGAM        WKPDAM
      * ACCUMULATE TOTALS
     C                   ADD       CGCGAM        AFPDAM
     C                   ADD       CGCGAM        APPDAM
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO PREMIUMS
      *
      *---------------------------------------------------
      * RETRIEVE DEDUCTION/ADD-ON DOLLARS
      *---------------------------------------------------
      *
593  C*    *LOVAL        SETLL     HSP044
593  C     *START        SETLL     HSP044
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO DISCOUNTS
     C                   READ      HSP044                                 91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
     C                   MOVEL     DARPCD        KYRPCD
      *
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF DIS FOUND
     C                   ADD       CGCGAM        WKDAAM
      * ACCUMULATE TOTALS
     C                   ADD       CGCGAM        AFDAAM
     C                   ADD       CGCGAM        APDAAM
     C                   ENDIF                                                  IF DIS FOUND
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO DISCOUNTS
      *
      *
      * CALCULATE NET SALES DOLLARS =
      *     SALES + DEDUCTION ADDONS + PREMIUM DISCOUNTS
      *
     C     WKSLAM        ADD       WKDAAM        WGN$AM
     C                   ADD       WKPDAM        WGN$AM
      *
      * CALCULATE NET SALES PER CWT SOLD
      *
     C     WKSCWT        IFNE      0
     C     WGN$AM        DIV(H)    WKSCWT        WGN$CW
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * COST INFORMATION
      *----------------------------------------------------------------
      *
     C     $COST         BEGSR
      *
     C     WKMCWT        IFNE      0                                            IF MOVED CWT
      *
      * CALCULATE TRANSFER IN DOLLARS PER CWT LIVE MOVED OUT =
      *      TRANSFER IN DOLLARS / HUNDRED WEIGHT MOVED OUT
      *
     C     WKTIAM        DIV(H)    WKMCWT        WGTI$M
      *
      * ADD TRANSFER IN DOLLARS TO TOTAL COST
      *
     C                   ADD       WKTIAM        WKTCAM
      *
      * CALCULATE FEED COST PER CWT LIVE MOVED OUT =
      *     FEED CONSUMED DOLLARS / MOVED OUT LIVE POUNDS CWT
      *
     C     WKFCAM        DIV(H)    WKMCWT        WGFC$M
     C                   ENDIF                                                  IF MOVED CWT
      *
      * ADD FEED DOLLARS INTO TOTAL COST
      *
     C                   ADD       WKFCAM        WKTCAM
      *
      *-------------------------------------
      * RETRIEVE OPERATING COSTS
      *-------------------------------------
      *
     C     OPCST         SETLL     HSL006B
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO OPER COST
     C     OPCST         READE     HSL006B                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   MOVEL     RPRPCD        KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       CGCGAM        WKOCAM
     C                   ADD       CGCGAM        WKTCAM
      * ACCUMULATE TOTALS
     C                   ADD       CGCGAM        AFOCAM
     C                   ADD       CGCGAM        APOCAM
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO OPER COST
      *
      *-------------------------------
      * RETREIVE CULL SALES
      *-------------------------------
      *
     C                   MOVEL     CULLS         KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF CULLS
     C                   Z-ADD     CGCGHD        WGCUHD
      *
      * CULL % = CULLED HEAD / ADJUSTED TRANSFERRED IN HEAD
      *
     C     WGAJTIHD      IFNE      0                                            If adjusted in
     C     WGCUHD        DIV(H)    WGAJTIHD      WKFLD3
     C     WKFLD3        MULT(H)   100           WGCUPR
     C                   ENDIF                                                  If adjusted in
      *
      * SUBTRACT THE CULL SALES DOLLARS FROM THE COSTS
      *
     C                   SUB       CGCGAM        WKTCAM
     C                   SUB       CGCGAM        WKOCAM
      * ACCUMULATE TOTALS
     C                   ADD       CGCGHD        WFCUHD
     C                   ADD       CGCGHD        WPCUHD
     C                   SUB       CGCGAM        AFOCAM
     C                   SUB       CGCGAM        APOCAM
     C                   ENDIF                                                  IF CULLS
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $OTCST - OTHER COSTS
      *----------------------------------------------------------------
      *
      * DO NOT INCLUDE THE TOTAL COST RECORD IN THIS PROCESSING
      *
     C     $OTCST        BEGSR
      *
     C     OTCST         SETLL     HSL006B
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO OTHER
     C     OTCST         READE     HSL006B                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
     C     RPRPCD        ANDNE     COS
      *
     C                   MOVEL     RPRPCD        KYRPCD
     C     KP033         CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF                                         IF HIT
     C                   ADD       CGCGAM        WKTCAM
     C                   ADD       CGCGAM        WKOCAM
      * ACCUMULATE TOTALS
     C                   ADD       CGCGAM        AFOCAM
     C                   ADD       CGCGAM        APOCAM
     C                   ENDIF                                                  IF HIT
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO OTHER
      *
      * ACCUMULATE TOTALS
     C                   ADD       WKTCAM        AFTCAM
     C                   ADD       WKTCAM        APTCAM
      *
      * CALCULATE THE TOTAL COST DOLLARS PER CWT MOVED OUT.
      * CALCULATE THE OTHER COST DOLLARS PER CWT MOVED OUT.
      *
     C     WKMCWT        IFNE      0
     C     WKTCAM        DIV(H)    WKMCWT        WGTC$M
     C     WKOCAM        DIV(H)    WKMCWT        WGOC$M
     C                   ENDIF
      *
      * CALCULATE THE MARGIN PER CWT =
      * NET DOLLARS PER CWT - TOTAL COST DOLLARS PER CWT MOVED OUT
      *
     C     WGN$AM        IFNE      0
     C     WGN$CW        SUB       WGTC$M        WGM$CW
     C                   ENDIF
      *
      * CALCULATE COST OF THE TRANSFER OUTS
      *
     C     WKTOLB        DIV(H)    100           WKFLD3
     C     WKFLD3        MULT(H)   WGTC$M        WK$TO
      *
      * IF NET SALES ARE NOT ZERO
      *
     C     WGN$AM        IFNE      0                                            IF NET NOT 0
      *
      * ACCUMULATE TOTAL COSTS ASSOCATED WITH NET SALES DOLLARS
      *
     C                   ADD       WKTCAM        AFSCAM
     C                   ADD       WKTCAM        APSCAM
      *
      * ACCUMULATE TRANSFER OUT COSTS
      *
     C                   ADD       WK$TO         AF$TO
     C                   ADD       WK$TO         AP$TO
      *
      * ACCUMULATE MOVED OUT POUNDS THAT HAD NET SALES DOLLARS
      *
     C                   ADD       WKMOLB        AFMSLB
     C                   ADD       WKMOLB        APMSLB
      *
      * FOR PRINTING, REDUCE TOTAL COST BY COST OF TRANSFER OUT
      *
     C     WKTCAM        SUB       WK$TO         WGC$AM
      *
      * CALCULATE MARGIN DOLLARS
      *
     C     WGN$AM        SUB       WGC$AM        WGM$AM
     C                   ENDIF                                                  IF NET NOT 0
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $WFTOT - CONTROL BREAK IN FARM SITE
      *----------------------------------------------------------------
      *
     C     $WFTOT        BEGSR
      *
      * CALCULATE MORTALITY % = DEAD HEAD / ADJUSTED TRANSFERRED IN HEAD
      *
     C                   IF        WFAJTIHD <> 0                                IF adjusted in
     C     WFDDHD        DIV(H)    WFAJTIHD      WKFLD3
     C     WKFLD3        MULT(H)   100           WFDDPR
      *
      * CALCULATE CULL PERCENT = CULL HEAD / ADJUSTED TRANSFERRED IN HEAD
      *
     C     WFCUHD        DIV(H)    WFAJTIHD      WKFLD3
     C     WKFLD3        MULT(H)   100           WFCUPR
      *
      * CALCULATE THE AVERAGE POUNDS PLACED:
      *  ADJUSTED TRANSFERRED IN LBS / ADJUSTED TRANSFERRED IN HEAD
      *
     C     AFAJTILB      DIV(H)    WFAJTIHD      WFAPLB
     C                   ENDIF                                                  If adjusted in
      *
      * CALCULATE THE AVERAGE POUNDS MOVED OUT
      *
     C     AFMOHD        IFNE      0
     C     AFMOLB        DIV(H)    AFMOHD        WFAMLB
     C                   ENDIF
      *
      * CALCULATE FEED POUNDS PER HEAD MOVED THAT ATE FEED
      *
     C                   Z-ADD     AFMXHD        WFMVHD
     C     AFMXHD        IFNE      0
     C     AFFCLB        DIV(H)    AFMXHD        WFAFLB
     C                   ENDIF
      *
      * CALCULATE FEED CONVERSION = FEED POUNDS / TOTAL WEIGHT GAIN
      * CALCULATE  FEED COST PER POUNDS OF GAIN =
      *                     TOTAL FEED COST DOLLARS / TOTAL WEIGHT GAIN
      *
     C     AFWXLB        IFNE      0
     C     AFFCLB        DIV(H)    AFWXLB        WFFDCV
     C     AFFCAM        DIV(H)    AFWXLB        WFF$GN
     C                   ENDIF
      *
      *
      * CALCULATE ADJUSTED FEED CONVERSION =
      *    FEED POUNDS / (WEIGHT GAIN + DAILY DEAD POUNDS)
      *
     C     AFWXLB        ADD       AFDDLB        WKFLD2
     C     WKFLD2        IFNE      0
     C     AFFCLB        DIV(H)    WKFLD2        WFAFCV
     C                   ENDIF
      *
      *
      * CALCULATE AVERAGE CALORIES PER POUND OF FEED =
      *    CALORIES / LBS OF FEED
      *
     C     AFFCLB        IFNE      0
     C     AFCGQT        DIV(H)    AFFCLB        WFACAL
     C                   ENDIF
      *
      * CALCULATE AVERAGE FEED COST PER TON
      *
     C     AFFCLB        DIV(H)    2000          WKTONS
     C     WKTONS        IFNE      0
     C     AFFCAM        DIV(H)    WKTONS        WFFC$T
     C                   ENDIF
      *
      * CALCULATE COST PER LB OF GAIN FOR THIS PHASE
      *
     C     AFWGLB        IFNE      0
     C     AFP$AM        DIV(H)    AFWGLB        WFP$GN
     C                   ENDIF
      *
      * CALCULATE:
      *  1) AVERAGE DAILY GAIN = WEIGHT GAIN / TOTAL PIG DAYS
      *  2) ADJUSTED AVERAGE DAILY GAIN =
      *          (WEIGHT GAIN + DAILY DEAD POUNDS) / TOTAL PIG DAYS
      *
     C     AFPDAY        IFNE      0                                            IF PIG DAYS
      *
      * AVERAGE DAILY GAIN
      *
     C     AFWGLB        DIV(H)    AFPDAY        WFAGLB
      *
      * ADJUSTED AVERAGE DAILY GAIN
      *
     C     AFWGLB        ADD       AFDDLB        WKFLD2
     C     WKFLD2        DIV(H)    AFPDAY        WFJGLB
     C                   ENDIF                                                  IF PIG DAYS
      *
      * CALCULATE HUNDERED WEIGHT VALUES FOR:
      *   SOLD POUNDS
      *   MOVED POUNDS
      *   MOVED POUNDS ASSOCIATED HOGS THAT ATE FEED
      *
     C     AFSLLB        DIV(H)    100           WKSCWT
     C     AFMOLB        DIV(H)    100           WKMCWT
     C     AFMXLB        DIV(H)    100           WKXCWT
      *
      * CALCULATE TRANSFER IN DOLLARS PER CWT WEIGHT LIVE MOVED OUT =
      *     TOTAL TRANI DOLLARS / MOVED OUT LIVE CWT
      *
     C     WKMCWT        IFNE      0
     C     AFTIAM        DIV(H)    WKMCWT        WFTI$M
     C                   ENDIF
      *
      * CALCULATE FEED COST PER CWT LIVE MOVED OUT
      *
     C     WKXCWT        IFNE      0
     C     AFFCAM        DIV(H)    WKXCWT        WFFC$M
     C                   ENDIF
      *
      * CALCULATE THE OTHER COST PER CWT LIVE MOVED OUT
      *
     C     WKMCWT        IFNE      0
     C     AFOCAM        DIV(H)    WKMCWT        WFOC$M
     C                   ENDIF
      *
      * CALCULATE TOTAL COST PER CWT LIVE MOVED OUT
      *
     C     WKMCWT        IFNE      0
     C     AFTCAM        DIV(H)    WKMCWT        WFTC$M
     C                   ENDIF
      *
      * CALCULATE CWT DOLLAR VALUES FOR:
      *     SALES
      *     DEDUCTIONS AND ADDONS
      *     PREMIUMS AND DISCOUNTS
      *
     C                   Z-ADD     0             WKFLD1
     C     WKSCWT        IFNE      0
     C     AFSLAM        DIV(H)    WKSCWT        WKFLD1
     C     AFDAAM        DIV(H)    WKSCWT        AFDACW
     C     AFPDAM        DIV(H)    WKSCWT        AFPDCW
     C                   ENDIF
      *
      * CALCULATE NET SALES PER CWT SOLD =
      *    SALES PER CWT + DISCOUNTS PER CWT + PREMIUMS PER CWT
      *
     C     WKFLD1        ADD       AFDACW        WFN$CW
     C                   ADD       AFPDCW        WFN$CW
      *
      * CALCULATE NET SALES =
      *    SALES + TOTAL DEDUCTIONS ADDONS + PREMIUMS DISCOUNTS
      *
     C     AFSLAM        ADD       AFDAAM        WFN$AM
     C     WFN$AM        ADD       AFPDAM        WFN$AM
      *
      *
      * CALCULATE TOTAL COSTS PER MOVED OUT CWT WHEN THERE WERE NET
      * SALES DOLLARS.
      * THE FIELDS AFMSLB AND AFSCAM WILL ONLY HAVE ACCUMULATED
      * VALUES WHEN THE NET SALES DOLLARS WERE NOT ZERO.
      *
     C     AFMSLB        DIV(H)    100           WKFLD3
     C                   Z-ADD     0             WKFLD1
     C     WKFLD3        IFNE      0
     C     AFSCAM        DIV(H)    WKFLD3        WKFLD1
     C                   ENDIF
      *
      * CALCULATE MARGIN DOLLARS PER CWT =
      *    NET DOLLARS PER CWT - TOTAL COSTS PER MOVED OUT CWT
      *
     C     WFN$CW        SUB       WKFLD1        WFM$CW
      *
      * SUBTRACT COST OF TRANSFER OUTS FROM THE TOTAL COST DOLLARS
      * ACCUMLATED WHEN NET SALES DOLLARS WERE NOT ZERO.
      *
     C     AFSCAM        SUB       AF$TO         WFC$AM
      *
      * CALCULATE THE MARGIN $ = NET DOLLARS - COST DOLLARS
      *
     C     WFN$AM        SUB       WFC$AM        WFM$AM
      *
      *-------------------------------------------------------------
      * THE FOLLOWING LOGIC IS ONLY PERFORMED FOR GROW FINISH GROUPS
      *-------------------------------------------------------------
      *
     C     SVPPCD        IFEQ      GF                                           IF G/F
      *
      * LEAN % = TOTAL LEAN % / TOTAL CARCASS LBS PER CWT
      *
     C     AFLNCB        IFNE      0
     C     AFLNQT        DIV(H)    AFLNCB        WFLEAN
     C                   ENDIF
      *
      * BACKFAT INCHES
      *
      * ((AVG BF INCHES * CARCASS LBS)ACCUMULATED) / TOTAL CARCASS LBS
      * ((CGCGQT        *  CGCRLB)ACCUMULATED)
      *
     C     AFCRLB        IFNE      0
     C     AFTBFI        DIV(H)    AFCRLB        WFBFIN
     C                   ENDIF
      *
      * CALCULATE CARCASS YIELD = CARCASS POUNDS / LIVE POUNDS
      *
     C     AFCYLB        IFNE      0
     C     AFCYCB        DIV(H)    AFCYLB        WKFLD3
     C     WKFLD3        MULT(H)   100           WFCRYD
     C                   ENDIF
     C                   ENDIF                                                   IF G/F
      *
      * WRITE FARM TOTAL RECORD
      *
     C                   move      svppcd        wfppcd
     C                   Z-ADD     SVFSCD        WFFSCD
     C                   WRITE     WFREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $WPTOT - CONTROL BREAK IN PRODUCTION PHASE
      *----------------------------------------------------------------
      *
     C     $WPTOT        BEGSR
      *
      * MORTALITY % = DEAD HEAD / ADJUSTED TRANSFERRED IN HEAD
      *
     C                   IF        WPAJTIHD <> 0                                If adjusted in
     C     WPDDHD        DIV(H)    WPAJTIHD      WKFLD3
     C     WKFLD3        MULT(H)   100           WPDDPR
      *
      * CULL % = CULL HEAD / ADJUSTED TRANSFERRED IN HEAD
      *
     C     WPCUHD        DIV(H)    WPAJTIHD      WKFLD3
     C     WKFLD3        MULT(H)   100           WPCUPR
      *
      * CALCULATE THE AVERAGE POUNDS PLACED:
      *  ADJUSTED TRANSFERRED IN LBS / ADJUSTED TRANSFERRED IN HEAD
      *
     C     APAJTILB      DIV(H)    WPAJTIHD      WPAPLB
     C                   ENDIF                                                  If adjusted in
      *
      * CALCULATE THE AVERAGE POUNDS MOVED OUT
      *
     C     APMOHD        IFNE      0
     C     APMOLB        DIV(H)    APMOHD        WPAMLB
     C                   ENDIF
      *
      * CALCULATE FEED CONSUMED PER MOVED HEAD
      *
     C     APMXHD        IFNE      0
     C     APFCLB        DIV(H)    APMXHD        WPAFLB
     C                   ENDIF
      *
      * CALCULATE FEED CONVERSION = FEED POUNDS / TOTAL WEIGHT GAIN
      * CALCULATE FEED COST PER POUND OF GAIN =
      *                  TOTAL FEED COST DOLLARS / TOTAL WEIGHT GAIN
      *
     C     APWXLB        IFNE      0
     C     APFCLB        DIV(H)    APWXLB        WPFDCV
     C     APFCAM        DIV(H)    APWXLB        WPF$GN
     C                   ENDIF
      *
      *
      * CALCULATE ADJUSTED FEED CONVERSION =
      *    FEED POUNDS / (WEIGHT GAIN + DAILY DEAD POUNDS)
      *
     C     APWXLB        ADD       APDDLB        WKFLD2
     C     WKFLD2        IFNE      0
     C     APFCLB        DIV(H)    WKFLD2        WPAFCV
     C                   ENDIF
      *
      *
      * CALCULATE AVERAGE CALORIES PER POUND OF FEED =
      *    CALORIES / LBS OF FEED
      *
     C     APFCLB        IFNE      0
     C     APCGQT        DIV(H)    APFCLB        WPACAL
     C                   ENDIF
      *
      *
      * CALCULATE AVERAGE FEED COST PER TON
      *
     C     APFCLB        DIV(H)    2000          WKTONS
     C     WKTONS        IFNE      0
     C     APFCAM        DIV(H)    WKTONS        WPFC$T
     C                   ENDIF
      *
      * CALCULATE COST PER LB OF GAIN FOR A PHASE
      *
     C     APWGLB        IFNE      0
     C     APP$AM        DIV(H)    APWGLB        WPP$GN
     C                   ENDIF
      *
      * CALCULATE:
      *  1) AVERAGE DAILY GAIN = WEIGHT GAIN / TOTAL PIG DAYS
      *  2) ADJUSTED AVERAGE DAILY GAIN =
      *          (WEIGHT GAIN + DAILY DEAD POUNDS) / TOTAL PIG DAYS
      *
     C     APPDAY        IFNE      0                                            IF PIG DAYS
      *
      * AVERAGE DAILY GAIN
      *
     C     APWGLB        DIV(H)    APPDAY        WPAGLB
      *
      * ADJUSTED AVERAGE DAILY GAIN
      *
     C     APWGLB        ADD       APDDLB        WKFLD2
     C     WKFLD2        DIV(H)    APPDAY        WPJGLB
     C                   ENDIF                                                  IF PIG DAYS
      *
      * CALCULATE HUNDRED WEIGHT VALUES
      *
     C     APSLLB        DIV(H)    100           WKSCWT
     C     APMOLB        DIV(H)    100           WKMCWT
     C     APMXLB        DIV(H)    100           WKXCWT
      *
      * CALCULATE TRANSFER IN DOLLARS PER MOVED CWT
      *
     C     WKMCWT        IFNE      0
     C     APTIAM        DIV(H)    WKMCWT        WPTI$M
     C                   ENDIF
      *
      * CALCULATE FEED COST PER CWT MOVED OUT
      *
     C     WKXCWT        IFNE      0
     C     APFCAM        DIV(H)    WKXCWT        WPFC$M
     C                   ENDIF
      *
      * CALCULATE THE OTHER COST PER CWT MOVED OUT
      *
     C     WKMCWT        IFNE      0
     C     APOCAM        DIV(H)    WKMCWT        WPOC$M
     C                   ENDIF
      *
      * CALCULATE TOTAL COST PER CWT MOVED OUT
      *
     C     WKMCWT        IFNE      0
     C     APTCAM        DIV(H)    WKMCWT        WPTC$M
     C                   ENDIF
      *
      * TOTAL SALES / TOTAL LIVE POUNDS CWT
      * TOTAL DED ADDONS / TOTAL LIVE POUNDS CWT
      * TOTAL PREMUIM DISCOUNTS/ TOTAL LIVE POUNDS CWT
      *
     C                   Z-ADD     0             WKFLD1
      *
     C     WKSCWT        IFNE      0
     C     APSLAM        DIV(H)    WKSCWT        WKFLD1
     C     APDAAM        DIV(H)    WKSCWT        APDACW
     C     APPDAM        DIV(H)    WKSCWT        APPDCW
     C                   ENDIF
      *
     C     WKFLD1        ADD       APDACW        WPN$CW
     C                   ADD       APPDCW        WPN$CW
      *
      * CALCULATE NET SALES =
      *    TOTAL SALES + TOTAL DED/ADDONS + PREMIUMS/DISCOUNTS
      *
     C     APSLAM        ADD       APDAAM        WPN$AM
     C     WPN$AM        ADD       APPDAM        WPN$AM
      *
      * CALCULATE TOTAL COSTS PER MOVED OUT CWT WHEN THERE WERE NET
      * SALES DOLLARS.
      * THE FIELDS APMSLB AND APSCAM WILL ONLY HAVE ACCUMULATED VALUES
      * WHEN THE NET SALES DOLLARS WERE NOT ZERO.
      *
     C     APMSLB        DIV(H)    100           WKFLD3
     C                   Z-ADD     0             WKFLD1
     C     WKFLD3        IFNE      0
     C     APSCAM        DIV(H)    WKFLD3        WKFLD1
     C                   ENDIF
      *
      * CALCULATE MARGIN PER CWT =
      *    NET DOLLARS PER CWT - TOTAL COSTS PER CWT MOVED OUT
      *
     C     WPN$CW        SUB       WKFLD1        WPM$CW
      *
      * SUBTRACT COST OF TRANSFER OUTS FROM THE TOTAL COST DOLLARS
      * ACCUMULATED WHEN THE NET SALES DOLLARS WERE NOT ZERO.
      *
     C     APSCAM        SUB       AP$TO         WPC$AM
      *
      * CALCULATE THE MARGIN DOLLARS
      *
     C     WPN$AM        SUB       WPC$AM        WPM$AM
      *
      *-------------------------------------------------------------
      * THE FOLLOWING CODE IS ONLY PERFORMED FOR GROW FINISH GROUPS
      *-------------------------------------------------------------
      *
     C     SVPPCD        IFEQ      GF                                           IF G/F
      *
      * LEAN % = TOTAL LEAN % / TOTAL CARCASS LBS PER CWT
      *
     C     APLNCB        IFNE      0
     C     APLNQT        DIV(H)    APLNCB        WPLEAN
     C                   ENDIF
      *
      * BACKFAT INCHES
      *
      * ((AVG BF INCHES * CARCASS LBS)ACCUMULATED) / TOTAL CARCASS LBS
      * ((CGCGQT        *  CGCRLB)ACCUMULATED)
      *
     C     APCRLB        IFNE      0
     C     APTBFI        DIV(H)    APCRLB        WPBFIN
     C                   ENDIF
      *
      * CARCASS YIELD
      *
     C     APCYLB        IFNE      0
     C     APCYCB        DIV       APCYLB        WKFLD3
     C     WKFLD3        MULT      100           WPCRYD
     C                   ENDIF
      *
     C                   ENDIF                                                   IF G/F
      *
      * WRITE PRODUCTION PHASE TOTAL RECORD TO WORKFILE
      *
     C                   MOVEL     SVPPCD        WPPPCD
     C                   WRITE     WPREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAY              4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CLRHG - CLEAR WORK FIELDS AND WORKFILE FIELDS FOR HOG GROUP
      *----------------------------------------------------------------
      *
     C     $CLRHG        BEGSR
      *
      * CLEAR WORK FIELDS
     C                   Z-ADD     0             WKTIAM                         T/I $
     C                   Z-ADD     0             WKTOLB                         T/O LBS
     C                   Z-ADD     0             WKP$AM                         PHASE COST
     C                   Z-ADD     0             WKWGLB                         WEIGHT GAIN
     C                   Z-ADD     0             WKDAAM                         DED/ADDON $
     C                   Z-ADD     0             WKPDAM                         PREM/DIS $
     C                   Z-ADD     0             WKSLAM                         SALES $
     C                   Z-ADD     0             WKTCAM                         TOTAL COST $
     C                   Z-ADD     0             WKOCAM                         OTHER COST $
     C                   Z-ADD     0             WKFCAM                         FEED COST $
     C                   Z-ADD     0             WKTONS                         FEED TONS
     C                   Z-ADD     0             WKFCLB                         FEED LBS
     C                   Z-ADD     0             WKSLLB                         SALES LBS
     C                   Z-ADD     0             WKMOLB                         MOVED LBS
     C                   Z-ADD     0             WKDDLB                         DAILY DEAD LBS
     C                   Z-ADD     0             WKCGQT                         CALORIES
     C                   Z-ADD     0             WKMOHD                         MOVED HD
      *
      * CLEAR WORKFILE FIELDS
      *
     C                   CLEAR                   WGREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CLRFS - CLEAR FIELDS FOR FARM SITE
      *----------------------------------------------------------------
      *
     C     $CLRFS        BEGSR
      *
      * CLEAR FARM SITE ACCUMULATORS
      *
     C                   Z-ADD     0             AFTBFI                         BACKFAT IN
     C                   Z-ADD     0             AFCRLB                         BF CARC LBS
     C                   Z-ADD     0             AFOCAM                         OTHER COST
     C                   Z-ADD     0             AFSCAM                         TOTAL COST
     C                   Z-ADD     0             AFTCAM                         TOTAL COST
     C                   Z-ADD     0             AFFCLB                         FEED LBS
     C                   Z-ADD     0             AFWGLB                         WEIGHT GAIN
     C                   Z-ADD     0             AFWXLB                         WEIGHT GAIN
     C                   Z-ADD     0             AFCYLB                         YIELD LBS
     C                   Z-ADD     0             AFCYCB                         YIELD CARC
     C                   Z-ADD     0             AFTIAM                         T/I $
     C                   Z-ADD     0             AFFCAM                         FEED COST $
     C                   Z-ADD     0             AFDAAM                         DED/ADDON $
     C                   Z-ADD     0             AFPDAM                         PREM/DISC $
     C                   Z-ADD     0             AFSLAM                         SALES $$
     C                   Z-ADD     0             AFAJTILB                       T/I LBS
     C                   Z-ADD     0             AFP$AM                         PHASE COST
     C                   Z-ADD     0             AFDACW                         DED/ADDON CW
     C                   Z-ADD     0             AFPDAY                         PIG DAYS
     C                   Z-ADD     0             AFPDCW                         PREM/DISC CW
     C                   Z-ADD     0             AFLNCB                         LEAN CARC LB
     C                   Z-ADD     0             AFLNQT                         LEAN %
     C                   Z-ADD     0             AF$TO                          TRAN/OUT $
     C                   Z-ADD     0             AFMOHD                         HEAD MOVED
     C                   Z-ADD     0             AFMXHD                         HEAD MOVED
     C                   Z-ADD     0             AFSLLB                         LBS SOLD
     C                   Z-ADD     0             AFMOLB                         LBS MOVED
     C                   Z-ADD     0             AFMXLB                         LBS MOVED
     C                   Z-ADD     0             AFMSLB                         LBS MOVED
     C                   Z-ADD     0             AFDDLB                         DAILY DEAD LBS
     C                   Z-ADD     0             AFCGQT                         CALORIES
      *
      * CLEAR WORKFILE FIELDS FOR FARM SITE
      *
     C                   CLEAR                   WFREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CLRPP - CLEAR WORK FIELDS FOR PRODUCTION PHASE
      *----------------------------------------------------------------
      *
     C     $CLRPP        BEGSR
      *
      * CLEAR ACCUMULATOR FIELDS FOR PRODUCTION PHASE
      *
     C                   Z-ADD     0             APTBFI                         BACKFAT IN
     C                   Z-ADD     0             APCRLB                         BACKFAT CB
     C                   Z-ADD     0             APOCAM                         OTHER COST
     C                   Z-ADD     0             APSCAM                         TOTAL COST
     C                   Z-ADD     0             APTCAM                         TOTAL COST
     C                   Z-ADD     0             APSLAM                         SALES $
     C                   Z-ADD     0             APFCLB                         FEED LBS
     C                   Z-ADD     0             APWGLB                         WEIGHT GAIN
     C                   Z-ADD     0             APWXLB                         WEIGHT GAIN
     C                   Z-ADD     0             APCYLB                         CAR/YLD LBS
     C                   Z-ADD     0             APCYCB                         CAR/YLD CBS
     C                   Z-ADD     0             APTIAM                         T/I $
     C                   Z-ADD     0             APFCAM                         DOLLARS
     C                   Z-ADD     0             APAJTILB                       T/I LBS
     C                   Z-ADD     0             APP$AM                         PHASE COST
     C                   Z-ADD     0             APDACW                         DED/ADD CWT
     C                   Z-ADD     0             APPDCW                         PREM/DIS CWT
     C                   Z-ADD     0             APPDAY                         PIG DAYS
     C                   Z-ADD     0             APLNQT                         LEAN %
     C                   Z-ADD     0             APLNCB                         CAR LBS/CWT
     C                   Z-ADD     0             APPDAM                         PREM/DISC $
     C                   Z-ADD     0             APDAAM                         DED/ADDON $
     C                   Z-ADD     0             AP$TO                          TRAN/OUT  $
     C                   Z-ADD     0             APSLLB                         SOLD LBS
     C                   Z-ADD     0             APMOHD                         MOVED HEAD
     C                   Z-ADD     0             APMXHD                         MOVED HEAD
     C                   Z-ADD     0             APMOLB                         MOVED LBS
     C                   Z-ADD     0             APMXLB                         MOVED LBS
     C                   Z-ADD     0             APMSLB                         MOVED LBS
     C                   Z-ADD     0             APDDLB                         DAILY DEAD LBS
     C                   Z-ADD     0             APCGQT                         CALORIES
      *
      * CLEAR WORKFILE FIELDS FOR PRODUCTION PHASE
      *
     C                   CLEAR                   WPREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FIELDS
      *
     C     KP033         KLIST
     C                   KFLD                    KYHGSN
     C                   KFLD                    KYRPCD
      *
     C     *LIKE         DEFINE    HGHGSN        KYHGSN
     C     *LIKE         DEFINE    RPRPCD        KYRPCD
      *
      * FLAG FIELD TO CONTROL PROCESSING
      *
     C                   MOVEL     YES           PROCFL            1
      * DEFINE SAVE FIELDS
      *
     C     *LIKE         DEFINE    HGPPCD        SVPPCD
     C     *LIKE         DEFINE    HGFSCD        SVFSCD
      *
      * WORKFIELDS FOR INTERIM CALCULATIONS AND ACCUMULATIONS
      *
     C                   Z-ADD     0             WKFLD1            5 2
     C                   Z-ADD     0             WKFLD2           15 2
      *
     C     *LIKE         DEFINE    WKFLD2        WKCGQT
      *
     C     *LIKE         DEFINE    WKFLD2        WKFCLB
     C     *LIKE         DEFINE    WKFLD2        WKWGLB
     C     *LIKE         DEFINE    WKFLD2        WKDDLB
     C     *LIKE         DEFINE    WKFLD2        WKSLLB
     C     *LIKE         DEFINE    WKFLD2        WKMOLB
     C     *LIKE         DEFINE    WKFLD2        WKTOLB
      *
     C     *LIKE         DEFINE    WKFLD3        WKSCWT
     C     *LIKE         DEFINE    WKFLD3        WKMCWT
     C     *LIKE         DEFINE    WKFLD3        WKXCWT
      *
     C     *LIKE         DEFINE    WKFLD2        WK$TO
     C     *LIKE         DEFINE    WKFLD2        WKFCAM
     C     *LIKE         DEFINE    WKFLD2        WKTCAM
     C     *LIKE         DEFINE    WKFLD2        WKOCAM
     C     *LIKE         DEFINE    WKFLD2        WKDAAM
     C     *LIKE         DEFINE    WKFLD2        WKSLAM
     C     *LIKE         DEFINE    WKFLD2        WKPDAM
     C     *LIKE         DEFINE    WKFLD2        WKTIAM
     C     *LIKE         DEFINE    WKFLD2        WKP$AM
      *
     C                   Z-ADD     0             WKMOHD           15 0
     C     *LIKE         DEFINE    WKFLD2        WKTONS
      *
     C                   Z-ADD     0             WKFLD3           15 4
     C                   Z-ADD     0             WKFLD4            3 2
      *
      * ACCUMULATORS FOR FARM SITE CALCULATIONS
      *
     C     *LIKE         DEFINE    WKFLD2        AFCGQT
      *
     C     *LIKE         DEFINE    WKFLD2        AFMXHD
     C     *LIKE         DEFINE    WKFLD2        AFMOHD
      *
     C     *LIKE         DEFINE    WKFLD2        AFAJTILB
     C     *LIKE         DEFINE    WKFLD2        AFMOLB
     C     *LIKE         DEFINE    WKFLD2        AFDDLB
     C     *LIKE         DEFINE    WKFLD2        AFMXLB
     C     *LIKE         DEFINE    WKFLD2        AFMSLB
     C     *LIKE         DEFINE    WKFLD2        AFSLLB
     C     *LIKE         DEFINE    WKFLD2        AFFCLB
     C     *LIKE         DEFINE    WKFLD2        AFWGLB
     C     *LIKE         DEFINE    WKFLD2        AFWXLB
     C     *LIKE         DEFINE    WKFLD2        AFCRLB
     C     *LIKE         DEFINE    WKFLD2        AFCYLB
     C     *LIKE         DEFINE    WKFLD2        AFCYCB
     C     *LIKE         DEFINE    WKFLD2        AFLNCB
      *
     C     *LIKE         DEFINE    WKFLD2        AFOCAM
     C     *LIKE         DEFINE    WKFLD2        AFSCAM
     C     *LIKE         DEFINE    WKFLD2        AFTCAM
     C     *LIKE         DEFINE    WKFLD2        AFFCAM
     C     *LIKE         DEFINE    WKFLD2        AFTIAM
     C     *LIKE         DEFINE    WKFLD2        AFP$AM
     C     *LIKE         DEFINE    WKFLD2        AFPDAM
     C     *LIKE         DEFINE    WKFLD2        AFDAAM
     C     *LIKE         DEFINE    WKFLD2        AFSLAM
     C     *LIKE         DEFINE    WKFLD2        AF$TO
      *
     C     *LIKE         DEFINE    WKFLD2        AFPDAY
     C     *LIKE         DEFINE    WKFLD3        AFLNQT
     C     *LIKE         DEFINE    WKFLD3        AFTBFI
     C     *LIKE         DEFINE    WKFLD1        AFDACW
     C     *LIKE         DEFINE    WKFLD1        AFPDCW
      *
      * ACCUMULATORS FOR PRODUCTION PHASE TOTAL CALCULATIONS
      *
     C     *LIKE         DEFINE    WKFLD2        APCGQT
      *
     C     *LIKE         DEFINE    WKFLD2        APDDLB
     C     *LIKE         DEFINE    WKFLD2        APWGLB
     C     *LIKE         DEFINE    WKFLD2        APWXLB
     C     *LIKE         DEFINE    WKFLD2        APAJTILB
     C     *LIKE         DEFINE    WKFLD2        APCRLB
     C     *LIKE         DEFINE    WKFLD2        APSLLB
     C     *LIKE         DEFINE    WKFLD2        APMOLB
     C     *LIKE         DEFINE    WKFLD2        APMXLB
     C     *LIKE         DEFINE    WKFLD2        APMSLB
     C     *LIKE         DEFINE    WKFLD2        APFCLB
     C     *LIKE         DEFINE    WKFLD2        APCYLB
     C     *LIKE         DEFINE    WKFLD2        APCYCB
     C     *LIKE         DEFINE    WKFLD2        APLNCB
      *
     C     *LIKE         DEFINE    WKFLD2        APMOHD
     C     *LIKE         DEFINE    WKFLD2        APMXHD
      *
     C     *LIKE         DEFINE    WKFLD2        APP$AM
     C     *LIKE         DEFINE    WKFLD2        APTIAM
     C     *LIKE         DEFINE    WKFLD2        APOCAM
     C     *LIKE         DEFINE    WKFLD2        APSCAM
     C     *LIKE         DEFINE    WKFLD2        APFCAM
     C     *LIKE         DEFINE    WKFLD2        APTCAM
     C     *LIKE         DEFINE    WKFLD2        APPDAM
     C     *LIKE         DEFINE    WKFLD2        APDAAM
     C     *LIKE         DEFINE    WKFLD2        APSLAM
     C     *LIKE         DEFINE    WKFLD2        AP$TO
      *
     C     *LIKE         DEFINE    WKFLD2        APPDAY
     C     *LIKE         DEFINE    WKFLD3        APTBFI
     C     *LIKE         DEFINE    WKFLD3        APLNQT
     C     *LIKE         DEFINE    WKFLD1        APDACW
     C     *LIKE         DEFINE    WKFLD1        APPDCW
      *
      * SET UP A FIRST TIME FLAG
      *
     C                   MOVEL     YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
