     A*%%TS  SD  20050322  174200  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4718
     A*  TITLE:         WORK WITH ORDER REQUESTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/08/04
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4718S1                  SFL
     A*%%TS  SD  20040308  160906  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            HDRQSN    R        H      REFFLD(@@RQSN)
     A            D1OPT     R        B 12  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WO      R        O 12  7REFFLD(HSRREF/@@WO *LIBL/HSPREF)
     A            D1MDY     R        O 12 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1FSCD    R        O 12 33REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O 12 42REFFLD(FSFSNM)
     A            D1RQSCD   R        O 12 73REFFLD(@@RQSCD)
     A                                 13 12'Comment:'
     A                                      COLOR(PNK)
     A            D1COM     R        O 13 21REFFLD(@@COM50)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4718C1                  SFLCTL(HP4718S1)
     A*%%TS  SD  20050322  174200  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0004)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH ORDER REQUESTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Include completed requests ..'
     A                                  4 32'?'
     A            C1YNFL    R        B  4 34REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  6  2'2=Revise'
     A                                  6 12'4=Delete'
     A                                  6 22'5=View'
     A                                  6 30'6=Print Pick List'
     A                                  6 49'8=Email Cell & Production Mgr'
     A                                  8 35'?'
     A                                      DSPATR(HI)
     A                                  8 73'?'
     A                                      DSPATR(HI)
     A                                  9  7'Work Order'
     A                                      DSPATR(HI)
     A                                  9 22'Request'
     A                                      DSPATR(HI)
     A                                  9 34'Farm'
     A                                      DSPATR(HI)
     A                                  9 70'Request'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  9'Number'
     A                                      DSPATR(HI)
     A                                 10 23'Date'
     A                                      DSPATR(HI)
     A                                 10 34'Site'
     A                                      DSPATR(HI)
     A                                 10 42'Name'
     A                                      DSPATR(HI)
     A                                 10 71'Status'
     A                                      DSPATR(HI)
     A            C1WO      R        B 11  7REFFLD(HSRREF/@@WO *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A N31N51N52
     A N56                                  DSPATR(PC)
     A            C1MDY     R        B 11 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1FSCD    R        B 11 33REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A  52                                  DSPATR(PC)
     A            C1FSNM    R        B 11 42REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1RQSCD   R        B 11 73REFFLD(HSRREF/@@RQSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP4718K1
     A*%%TS  SD  20040315  112131  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F13=Fold/Unfold'
     A                                 22 62'F17-Print Options'
     A                                 23 62'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4718ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4718EC                  SFLCTL(HP4718ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
