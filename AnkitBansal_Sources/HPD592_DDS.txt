     A*%%TS  SD  20131021  135323  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD592 - SELECT LOAD CREW
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          01/15/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP592S1                   SFL
     A*%%TS  SD  20131021  135323  ISLRAMS     REL-V6R1M0  5761-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1LCCD    R        O  8  2REFFLD(@@LCCD)
     A            D1LCDS    R        O  8  9REFFLD(HSRREF/@@LCDS *LIBL/HSPREF)
     A            D1TOCD    R        O  8 36REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A            D1SHIFT   R        O  8 46REFFLD(HSRREF/@@SHIFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1AIST    R        O  8 52REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A            D1MTECHREFR        O  8 56REFFLD(HSRREF/@@MTECHREF +
     A                                             *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP592C1                   SFLCTL(HP592S1)
     A*%%TS  SD  20131021  135323  ISLRAMS     REL-V6R1M0  5761-WDS
     A  *DS3                                SFLSIZ(0100)
     A  *DS4                                SFLSIZ(0100)
     A  *DS3                                SFLPAG(0009)
     A  *DS4                                SFLPAG(0009)
     A  *DS3                                WINDOW(HP592EC)
     A  *DS4                                WINDOW(HP592EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 32'SELECT LOAD CREW'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 26REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  4 38'?'
     A                                      DSPATR(HI)
     A N62                              4 52'?'
     A                                      DSPATR(HI)
     A                                  5 36'Trans'
     A                                      DSPATR(HI)
     A                                  5 51'A/I'
     A                                      DSPATR(HI)
     A                                  6  2'Code'
     A                                      DSPATR(HI)
     A                                  6  9'Description'
     A                                      DSPATR(HI)
     A                                  6 36'Office'
     A                                      DSPATR(HI)
     A                                  6 44'Shift'
     A                                      DSPATR(HI)
     A                                  6 51'Sta'
     A                                      DSPATR(HI)
     A                                  6 56'MTech Reference'
     A                                      DSPATR(HI)
     A            C1LCCD    R        B  7  2REFFLD(HSRREF/@@LCCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1LCDS    R        B  7  9REFFLD(HSRREF/@@LCDS *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1TOCD    R        B  7 36REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1SHIFT   R        B  7 46REFFLD(HSRREF/@@SHIFT *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A N62        C1AIST    R        B  7 52REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1MTECHREFR        B  7 56REFFLD(HSRREF/@@MTECHREF +
     A                                             *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP592K1
     A                                      WINDOW(HP592EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP592ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP592EC                   SFLCTL(HP592ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
