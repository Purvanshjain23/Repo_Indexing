      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP391
      * TITLE:       UPDATE THE CALORIC RECORD FOR CLOSED GROUPS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     08/15/96
      *
      *
      * FUNCTION: ALLOWS I.S. PERSONNEL TO RECALCULATE THE CALORIES
      *           FOR THE SPECIFIED CLOSED GROUPS AND UPDATE/WRITE
      *           THE 'FCAL' CALORIC RECORD FOR THE GROUP IN THE
      *           CLOSED GROUP FILE.
      *
      *           I.S. PERSONNEL SHOULD USE THIS FUNCTION WHEN THE
      *           USERS HAVE 1) FAILED TO MAINTAIN ACCURATE CALORIC
      *           VALUES ON RATIONS AND, 2) AFTER UPDATING THE CALORIC,
      *           VALUES, WANT TO RECALCULATE THE CALORIES FOR
      *           SPECIFIED CLOSED GROUPS.
      *
      /EJECT
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 03/04/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail, check detail and
      *           closed hog group files:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 03/09/98  LeAnne Fedor
      *           Recompile only. Feed pounds and feed amount fields
      *           increased in size per Nathan Malone.
      *
      * 04/13/00  LeAnne Fedor
      *           Recompile only. The 'closed group pounds' field in the
      *           Closed Hog Group file was increased to 11,2.
      *
      * 01/30/01  LeAnne Fedor
      *           Recompile only.  Production type was removed from feed files.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL034I   IF   E           K DISK
      *    HOG GROUP  (LF selects HGGSCD = CL; OPNQRY used to select/key file)
      *
      *
     FHSL038E   IF   E           K DISK
      *   FEED TICKET DETAIL
      *
      *
     FHSP073    IF   E           K DISK
      *    CALORIES PER RATION
      *
      *
     FHSP033    UF A E           K DISK
      *    CLOSED GROUP
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDBODS                 6     35
     D  LDFYMD                36     43  0
     D  LDFMDY                44     49  0
     D  LDTYMD                50     57  0
     D  LDTMDY                58     63  0
      *
     D  LDPTCD                64     68
     D  LDPTDS                69     78
     D  LDPPCD                79     83
     D  LDPPDS                84    113
      *
     D  LDFSCD               149    153  0
     D  LDFSNM               154    178
      *
     D  LDHGSN               217    223  0
     D  LDHGCD               224    230
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * PROCESS EACH HOG GROUP RECORD SELECTED BY THE OPEN QUERY
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL034I                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * PRINT HEADINGS FIRST TIME THROUGH
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   MOVEL     NO            FIRST
     C                   ENDIF
      *
     C                   EXSR      $FEED
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE; OTHERWISE
      *  PRINT THE TOTALS.
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      *
      /EJECT
      *---------------------------------------------------------------
      * $FEED - PROCESS ALL FEED DETAIL RECORDS FOR A CLOSED GROUP
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE FIRST READS ALL FEED TICKET DETAIL RECORDS FOR
      * THE GROUP ACCUMULATING VALUES.  AFTER ALL DETAIL RECORDS HAVE
      * BEEN PROCESSED FOR A GROUP, THE CLOSED GROUP FILE IS UPDATED.
      *
     C     $FEED         BEGSR
      *
      * INITIALIZE WORK FIELDS FOR ACCUMULATING FEED VALUES
      *
     C                   Z-ADD     0             WKKCQT
      *
     C     HGHGSN        SETLL     HSL038E
     C                   SETOFF                                       96
      *
     C     *IN96         DOUEQ     *ON                                          DO FEED
     C     HGHGSN        READE     HSL038E                                96
     C     *IN96         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE THE RATION'S CALORIC CONTENT. USE THE
      * REFERENCE TICKET DATE FOR RETRIEVING THE RECORD.
      *
     C     K1P073        CHAIN     HSP073                             92
     C     *IN92         IFEQ      *ON                                          IF NOT EXACT
     C     K1P073        SETLL     HSP073
     C     FDRNCD        READPE    HSP073                                 76
     C                   ENDIF                                                  IF NOT EXACT
      *
     C     *IN76         IFEQ      *OFF                                         IF ANY REC
     C     FDFDLB        MULT      CPCLRT        WKQT
     C                   ADD       WKQT          WKKCQT
     C                   ENDIF                                                  IF ANY REC
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO FEED
      *
      *----------------------------------------------------------------
      * ALL FEED DETAIL HAS BEEN PROCESSED FOR A HOG GROUP, UPDATE
      * THE CALORIC RECORD FOR THE CLOSED GROUP.
      *
      *
     C                   EXSR      $UP033
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $UP033 - UDPATE CALORIC RECORD IN CLOSED GROUP FILE
      *---------------------------------------------------------------
      *
     C     $UP033        BEGSR
      *
     C                   Z-ADD     0             WKCGQT
      *
     C     K2P033        CHAIN     HSP033                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     CGCGQT        WKCGQT
     C                   ENDIF
      *
      * PRINT BEFORE/AFTER IMAGE
      *
     C                   EXSR      $PRINT
      *
      * MAINTAIN CALORIE RECORD BY:
      *   1) WRITING
      *   2) DELETING OR
      *   3  UPDATING
      *
      * CALORIES EXIST NOW BUT DID NOT EXIST AT THE TIME
      * THE GROUP WAS CLOSED; SO, WRITE A RECORD.
      *
     C                   SELECT
     C     *IN92         WHENEQ    *ON                                          WH WRITE
     C     WKKCQT        ANDNE     0
     C                   Z-ADD     HGHGSN        CGHGSN
     C                   MOVEL     WKCACD        CGCACD
     C                   MOVEL     WKRPCD        CGRPCD
     C                   Z-ADD     WKKCQT        CGCGQT
     C                   Z-ADD     0             CGCGAM
     C                   Z-ADD     0             CGCGHD
     C                   Z-ADD     0             CGCGLB
     C                   Z-ADD     0             CGCRLB
     C                   Z-ADD     0             CGSHHD
     C                   Z-ADD     0             CGSHLB
     C                   Z-ADD     0             CGRCHD
     C                   Z-ADD     0             CGRCLB
     C                   Z-ADD     0             CGDOHD
     C                   Z-ADD     0             CGDOLB
     C                   WRITE     CGREC
      *
      * NO CALORIES EXIST NOW; BUT CALORIES DID EXIST AT THE
      * TIME THE GROUP WAS CLOSED; SO, DELETE THE CALORIE RECORD
      *
     C     *IN92         WHENEQ    *OFF                                         WH DELETE
     C     WKKCQT        ANDEQ     0
     C                   DELETE    CGREC
      *
      * CALORIES EXIST NOW; CALORIES EXISTED AT THE TIME THE
      * GROUP WAS CLOSED; SO, UPDATE THE CALORIE RECORD
      *
     C     *IN92         WHENEQ    *OFF                                         WH UPDATE
     C     WKKCQT        ANDNE     0
     C                   Z-ADD     WKKCQT        CGCGQT
     C                   UPDATE    CGREC
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PRINT - PRINT A LINE FOR EACH GROUP
      *---------------------------------------------------------------
      *
     C     $PRINT        BEGSR
      *
      * FLIP DATES FROM CCYYMMDD TO MMDDYY
      *
     C                   Z-ADD     HGCRDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTCRDT
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTOPDT
      *
     C                   Z-ADD     HGDSDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTDSDT
      *
     C                   Z-ADD     HGCLDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTCLDT
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *
     C     K1P073        KLIST
     C                   KFLD                    FDRNCD
     C                   KFLD                    FDRTDT
      *
     C     K2P033        KLIST
     C                   KFLD                    HGHGSN
     C                   KFLD                    WKRPCD
      *
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   Z-ADD     0             DTDSDT            6 0
     C     *LIKE         DEFINE    DTDSDT        DTCRDT
     C     *LIKE         DEFINE    DTDSDT        DTOPDT
     C     *LIKE         DEFINE    DTDSDT        DTCLDT
      *
      * DEFINE WORKFIELDS FOR FEED TICKET PROCESSING
      *
     C     *LIKE         DEFINE    CGCGQT        WKKCQT
     C     *LIKE         DEFINE    CGCGQT        WKQT
     C     *LIKE         DEFINE    CGCGQT        WKCGQT
     C     *LIKE         DEFINE    CGRPCD        WKRPCD
     C     *LIKE         DEFINE    CGCACD        WKCACD
      *
      * SET UP REPORT CATAGORY AND REPORT CODE
      *
     C                   MOVEL     'FEED '       WKCACD
     C                   MOVEL     'FCAL '       WKRPCD
      *
      *
      *----------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *----------------------------------------------------------------
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDFSBO
     C                   ENDIF
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDPTCD
     C                   ENDIF
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDPPCD
     C                   ENDIF
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
     C     LDHGCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDHGCD
     C                   ENDIF
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            132
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           75 'HOG PRODUCTION SYSTEM'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           67 'CLOSED GROUPS FOR'
     O                                         +  1 'CALORIC UPDATING'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          126 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR          1
     O                                           16 'BUSINESS OFFICE '
     O                                           22 '.....'
     O                       LDFSBO              29
     O                       LDBODS              63
      *
     O          E            HRHDR          1
     O                                           22 'FROM CLOSED DATE .....'
     O                       LDFMDY        Y     32
      *
     O          E            HRHDR          1
     O                                           22 'TO CLOSED DATE .......'
     O                       LDTMDY        Y     32
      *
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION TYPE ......'
     O                       LDPTCD              29
     O                       LDPTDS              43
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       LDPPCD              29
     O                       LDPPDS              63
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O              N89      LDFSCD        M     30
     O               89                          27 'ALL'
     O                       LDFSNM              58
      *
     O          E            HRHDR          2
     O                                           22 'GROUP ................'
     O                       LDHGCD              31
      *
     O          E            HRHDR          1
     O                                            5 'GROUP'
     O                                           17 'GROUP'
     O                                           26 'DATE'
     O                                           37 'DATE'
     O                                           48 'DATE'
     O                                           59 'DATE'
     O                                           72 'BUSINESS'
     O                                           80 'FARM'
      *
     O          E            HRHDR          1
     O                                            4 'CODE'
     O                                           17 'NUMBER'
     O                                           28 'CREATED'
     O                                           38 'OPENED'
     O                                           50 'DISPOSED'
     O                                           60 'CLOSED'
     O                                           70 'OFFICE'
     O                                           80 'SITE'
     O                                          108 'OLD CALORIES'
     O                                          131 'NEW CALORIES'
      *
     O          E            HRHDR          2
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       HGHGCD               7
     O                       HGHGSN        Z   +  3
     O                       DTCRDT        Y   +  3
     O                       DTOPDT        Y   +  3
     O                       DTDSDT        Y   +  3
     O                       DTCLDT        Y   +  3
     O                       HGFSBO              69
     O                       HGFSCD        K     81
     O                       WKCGQT        K    109
     O                       WKKCQT        K    132
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /EJECT
