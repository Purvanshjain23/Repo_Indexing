      *****************  RPG PROGRAM HEADING  ************************
      * ENVIRONMENT: PORK
      * SYSTEM:      ACCOUNTS RECEIVABLE
      * PROGRAM:     AR610
      * TITLE:       FOREIGN CORPORATE TAX SAVINGS
      *              YEARLY STATEMENT OF ACCOUNT REPORTS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/27/97
      *
      *  FUNCTION:   THIS PROGRAM PRINTS TWO REPORTS:
      *                 1) A DETAIL REPORT FOR EACH INDIVIDUAL CUSTOMER
      *                 2) A SUMMARY LISTING OF ALL CUSTOMERS
      *
      *              THE TWO WORKFILES USED IN THIS PROGRAM ARE
      *              BUILT IN AR310. AND, AN OPEN QUERY STATEMENT
      *              IS USED TO SELECT AND KEY DATA IN AR410CL
      *
      *              SELECTION CRITERIA IS PASSED THROUGH THE LDA.
      *
      *              THIS PROGRAM IS CALLED FROM AR410CL WHICH IS
      *              SUBMITTED FROM AR410 THROUGH QCMDEXC.
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FARP310  IF  E           K        DISK
      *  WORKFILE: CUSTOMER DETAIL
      *
      *
     FARP311  IF  E           K        DISK
      *  WORKFILE: CUSTOMER SUMMARY
      *
      *
     FQPRINT  O   F      82     OF     PRINTER
      *  INDIVIDUAL CUSTOMER REPORT
      *
     FQPRINT2 O   F      90     OV     PRINTER
      *  SUMMARY LISTING OF ALL CUSTOMERS WITH EXPORT SALES
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     I           UDS
     I                                        1   40LDCCYY
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     IPGMDS     ESDSUTPGFR
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     I              'Y'                   C         YES
     I              'N'                   C         NO
     I              'DECEMBER 31'         C         DEC31
      *
      *
     I              'TOTAL DOMESTIC -     C         DSALES
     I              'SALES:'
     I              'TOTAL EXPORT SALES:' C         ESALES
     I              'TOTAL SALES:'        C         TSALES
     I              'OUTSTANDING BALANCE:'C         OUTBAL
      *
     I                                        1   1 DUMMYD
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * USE THE SUMMARY FILE AS THE PRIMARY FILE FOR PROCESSING.
      *
     C           *LOVAL    SETLLARP311
      *
     C           *IN90     DOUEQ*ON                        MAIN DO
     C                     READ ARP311                   90
     C           *IN90     IFEQ *OFF                       IF NOT EOF
      *
      * IF FIRST TIME, PRINT HEADER FOR THE SUMMARY REPORT
      *
     C           FIRST     IFEQ YES
     C                     EXCPTSRHDR
     C                     MOVELNO        FIRST
     C                     ENDIF
      *
      * ACCUMULATE SALES AND OUTSTANDING BALANCE DOLLARS INTO
      * THE PRINT FIELDS USED TO PRINT THE TOTALS ON THE
      * SUMMARY REPORT.
      *
     C                     ADD  WSTOT     STSALE
     C                     ADD  WSBAL     STBAL
      *
      * PRINT A SINGLE LINE FOR CUSTOMER ON SUMMARY REPORT
      *
     C           *INOV     IFEQ *ON
     C                     EXCPTSRHDR
     C                     ENDIF
     C                     EXCPTSDLINE
      *
      * NOW, RETRIEVE AND PRINT ALL SALES DETAIL RECORDS FOR
      * THIS CUSTOMER ON THE INDIVIDUAL CUSTOMER REPORT.
      *
     C                     EXSR $CUST
      *
     C                     ENDIF                           IF NOT EOF
     C                     ENDDO                           MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C           FIRST     IFEQ YES                        IF NOTHING
     C                     EXCPTSRHDR
     C                     EXCPTNODATA
     C                     ELSE
      *
      * PRINT TOTALS FOR SUMMARY REPORT
      *
     C                     EXCPTSTTOT
     C                     ENDIF                           IF NOTHING
      *
      *
     C                     SETON                     LR
      /EJECT
      *---------------------------------------------------------------
      * $CUST - PRINT REPORT FOR INDIVIDUAL CUSTOMER
      *---------------------------------------------------------------
      *
     C           $CUST     BEGSR
      *
      * PRINT REPORT HEADINGS FOR INDIVIDUAL CUSTOMER
      *
     C                     EXCPTIRHDR
     C                     EXCPTICHDR
      *
      * PRINT EACH RECORD FROM THE DETAIL WORKFILE FOR THIS
      * A/R CUSTOMER.
      *
     C           WSANC7    SETLLARP310
      *
     C           *IN91     DOUEQ*ON                        DO DETAIL
     C           WSANC7    READEARP310                   91
     C           *IN91     IFEQ *OFF                       IF NOT EOF
      *
     C           *INOF     IFEQ *ON                        IF OVERFLOW
     C                     EXCPTTOP
     C                     EXCPTICHDR
     C                     ENDIF                           IF OVERFLOW
      *
     C                     EXCPTIDLINE
      *
     C                     ENDIF                           IF NOT EOF
     C                     ENDDO                           DO DETAIL
      *
      * PRINT CUSTOMER TOTALS
      *
      * TOTAL SALES
     C                     EXCPTIUNDER
     C                     MOVELTSALES    ITDESC    P
     C                     Z-ADDWSTOT     ITAMT
     C                     EXCPTITTOT
     C                     EXCPTBLANK
      *
      * TOTAL EXPORT SALES
     C                     MOVELESALES    ITDESC    P
     C                     Z-ADDWSTOTE    ITAMT
     C                     EXCPTITTOT
      *
      * TOTAL DOMESTIC SALES
      *
     C                     MOVELDSALES    ITDESC    P
     C                     Z-ADDWSTOTD    ITAMT
     C                     EXCPTITTOT
      *
      * OUTSTANDING BALANCE
     C                     EXCPTBLANK
     C                     MOVELOUTBAL    ITDESC    P
     C                     Z-ADDWSBAL     ITAMT
     C                     EXCPTITTOT
      *
     C                     ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
      *
      * DEFINE PRINT FIELDS FOR TOTAL LINES ON SUMMARY REPORT
      *
     C                     Z-ADD0         STSALE 152
     C                     Z-ADD0         STBAL  152
      *
      * DEFINE PRINT FIELDS FOR TOTAL LINES ON INDIVIDUAL CUSTOMER REPORT
      *
     C                     MOVE *BLANK    ITDESC 21
     C           *LIKE     DEFN WSTOT     ITAMT
      *
      * DEFINE PRINT FIELDS FOR HEADINGS
      *
     C                     MOVE *BLANK    HRYEAR 17
     C                     MOVE LDCCYY    WKYEAR  4
      *
     C           DEC31     CAT  ',':0     HRYEAR    P
     C                     CAT  WKYEAR:1  HRYEAR
      *
      * SET UP A FIRST TIME FLAG
      *
     C                     TIME           RTIME   60
     C                     MOVELYES       FIRST   1
     C                     MOVEL*ALL'-'   DASH   90
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING FOR INDIVIDUAL CUSTOMER REPORT
      *-------------------------------------------------------------
      *
     OQPRINT  E  101           IRHDR
     O                                   47 'SEABOARD INTERNATIONAL/'
     O                                      'FARMS'
      *
     O        E  1             IRHDR
     O                                   49 'C/O 9000 W. 67TH STREET'
      *
     O        E  2             IRHDR
     O                                   43 'SHAWNEE MISSION, '
     O                                      'KS 66201'
      *
     O        E  1             IRHDR
     O                                   45 'SUMMARY STATEMENT OF '
     O                                      'ACCOUNT'
      *
      *
     O        E  2             IRHDR
     O                                   38 'FOR THE YEAR ENDED'
     O                         HRYEAR    56
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES FOR CUSTOMER INDIVIDUAL REPORT
      *-------------------------------------------------------------
      *
     O        E 22             ICHDR
     O                                   16 'CUSTOMER NUMBER:'
     O                         WSANC7Z   25
      *
     O        E 1              ICHDR
     O                         WSAQTX    30
      *
     O        E 1              ICHDR
     O                         WSADD1    45
      *
     O        E 1              ICHDR
     O                         WSADD2    45
      *
     O        E 1              ICHDR
     O                         WSADD3    45
      *
     O        E 13             ICHDR
     O                         WSADD4    45
      *
     O        E  1             ICHDR
     O                                    9 'ACTUAL'
      *
     O        E  1             ICHDR
     O                                    8 'SHIP'
     O                                   20 'INVOICE'
     O                                   61 'EXPORT'
     O                                   81 'SALES'
      *
     O        E  2             ICHDR
     O                                    8 'DATE'
     O                                   20 'NUMBER'
     O                                   43 'PRODUCT DESCRIPTION'
     O                                   61 'STATUS'
     O                                   81 'AMOUNT'
      *
      *
      *-------------------------------------------------------------
      * DETAIL LINE FOR INDIVIDUAL REPORT
      *-------------------------------------------------------------
      *
     O        E  1             IDLINE
     O                         WFMDY     10 '  /  /  '
     O                         WFCONBZ   20
     O                         WFG7NA    54
     O                         WFU2ST    59
     O                         WFKDVAK   82
      *
      *-------------------------------------------------------------
      * CUSTOMER TOTALS FOR INDIVIDUAL REPORT
      *-------------------------------------------------------------
      *
     O        E 10             ITTOT
     O                         ITDESC    21
     O                         ITAMT K   82
      *
      *-------------------------------------------------------------
      * UNDERSCORE FOR INDIVIDUAL REPORT
      *-------------------------------------------------------------
      *
     O        E 10             IUNDER
     O                                   82 '---------------'
      *-------------------------------------------------------------
      * TOP OF PAGE
      *-------------------------------------------------------------
      *
     O        E    1           TOP
      *
      *-------------------------------------------------------------
      * BLANK LINE
      *-------------------------------------------------------------
      *
     O        E 1              BLANK
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING FOR SUMMARY REPORT
      *-------------------------------------------------------------
      *
     OQPRINT2 E  101           SRHDR
     O                         SDPGM     10
     O                                   52 'STATEMENT OF ACCOUNT '
     O                                      'LISTING'
     O                                   80 'DATE'
     O                         UDATE Y   90
      *
     O        E  1             SRHDR
     O                         SDUSR     10
     O                                   43 'FOR THE YEAR ENDED'
     O                         HRYEAR    61
     O                                   80 'TIME'
     O                         RTIME     90 '  :  :  '
      *
      *
     O        E  2             SRHDR
     O                                   80 'PAGE'
     O                         PAGE  Z   90
      *
      *
     O        E  1             SRHDR
     O                                    8 'CUSTOMER'
     O                                   66 'TOTAL'
     O                                   89 'OUTSTANDING'
      *
     O        E  1             SRHDR
     O                                    8 'NUMBER'
     O                                   16 'NAME'
     O                                   66 'SALES'
     O                                   89 'BALANCE'
      *
      *
     O        E  2             SRHDR
     O                         DASH      90
      *
      *-------------------------------------------------------------
      * DETAIL LINE FOR SUMMARY REPORT
      *-------------------------------------------------------------
      *
     O        E  1             SDLINE
     O                         WSANC7Z    8
     O                         WSAQTX    42
     O                         WSTOT K   67
     O                         WSBAL K   90
      *
      *-------------------------------------------------------------
      * TOTALS FOR SUMMARY REPORT
      *-------------------------------------------------------------
      *
     O        E 01             STTOT
      *
     O                                   67 '---------------------'
     O                                   90 '---------------------'
      *
     O        E 00             STTOT
     O                                   42 'TOTALS:'
     O                         STSALEK   67
     O                         STBAL K   90
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O        E  1             NODATA
     O                                   19 'NO DATA MEETS YOUR'
     O                                   38 'SELECTION CRITERIA'
