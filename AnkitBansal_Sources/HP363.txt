      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP363
      * TITLE:       Workfile: Expense Ticket Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/11/02
      *
      *  FUNCTION:   This program builds a workfile for the report.
      *
      *
      *           This program is submitted by HP466CL which is called from HP466 through
      *           QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp048    if   e           k disk
      *  Expense ticket
      *  (records selected by open query)
      *
      *
     fhsp363    o    e           k disk
      *    Workfile: Expense ticket report
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
     D wkppcd          s                   like(hgppcd)
     D wkptcd          s                   like(hgptcd)
     D wkhgcd          s                   like(hghgcd)
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldppcd                27     31
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C     *loval        setll     hsp048
      *
      * Process each expense ticket record selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp048                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      *blank        wkppcd
     C                   move      *blank        wkptcd
     C                   move      *blank        wkhgcd
      *
      * Conditionally get a Production phase/type.
      *
     C                   select
     C                   when      xthgsn = 0
     C                   move      xtppcd        wkppcd
     C                   move      xtptcd        wkptcd
     C                   other
      *
     C     xthgsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hgppcd        wkppcd
     C                   move      hgptcd        wkptcd
     C                   move      hghgcd        wkhgcd
     C                   endif
     C                   endsl
      *
      * Make check of Production Phase against user's selection.
      *
     C                   if        ldppcd = *blank or                           If continue
     C                             (ldppcd = wkppcd)
      *
     C                   exsr      $wrt363
      *
     C                   endif                                                  If continue
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *--------------------------------------------------------------
      * EOF processing
      *--------------------------------------------------------------
      *
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------
      * Write workfile record
      *--------------------------------------------------------------
      *
     C     $wrt363       begsr
      *
     C                   move      wkppcd        wtppcd
     C                   move      wkptcd        wtptcd
     C                   move      wkhgcd        wthgcd
      *
     C                   z-add     xtxtsn        wtxtsn
     C                   move      xtxscd        wtxscd
     C                   move      xtbobo        wtbobo
     C                   z-add     xtxbdt        wtxbdt
     C                   z-add     xtxedt        wtxedt
     C                   move      xteccd        wteccd
     C                   move      xtescd        wtescd
     C                   move      xtexcd        wtexcd
      *
     C                   z-add     xtxtam        wtxtam
     C                   z-add     xtxthd        wtxthd
     C                   z-add     xtxtlb        wtxtlb
     C                   z-add     xtxtqt        wtxtqt
      *
     C                   move      xtrpcd        wtrpcd
     C                   move      xtamcd        wtamcd
     C                   move      xtalfl        wtalfl
      *
     C                   move      xtcocd        wtcocd
     C                   move      xtcjd         wtcjd
      *
     C                   move      xtalbo        wtalbo
     C                   move      xtdvcd        wtdvcd
     C                   move      xtrgcd        wtrgcd
      *
     C                   z-add     xtfscd        wtfscd
     C                   move      xtblcd        wtblcd
     C                   move      xtrmcd        wtrmcd
     C                   z-add     xthgsn        wthgsn
      *
     C                   z-add     xtaldt        wtaldt
     C                   z-add     xtpodt        wtpodt
     C                   z-add     xtepdt        wtepdt
      *
     C                   write     wtrec
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------
      * Initialization subroutine
      *--------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C                   endsr
      /eject
