     A*%%TS  SD  20090707  132545  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD441
     A*  TITLE:         WORK WITH CLOSED/VOIDED HOG GROUPS
     A*  PROGRAMMER:    ROSE CENTONZE
     A*  DATE:          07/20/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP441S1                   SFL
     A*%%TS  SD  20090707  132545  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(@@HGSN)
     A            DFOPT          2A  B 13  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFDSDT    R        O 13  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFHGCD    R        O 13 17REFFLD(@@HGCD)
     A            DFFSCD    R        O 13 26REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            DFBLCD    R        O 13 33REFFLD(@@BLCD)
     A            DFRMCD    R        O 13 39REFFLD(@@RMCD)
     A            DFPTCD    R        O 13 46REFFLD(@@PTCD)
     A            DFPPCD    R        O 13 52REFFLD(@@PPCD)
     A            DFGTCD    R        O 13 60REFFLD(@@GTCD)
     A            DFSDCD    R        O 13 65REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A            DFSMCD    R        O 13 70REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A            DFCFFL    R        O 13 74REFFLD(HSRREF/@@CFFL *LIBL/HSPREF)
     A            DFGSCD    R        O 13 77REFFLD(@@GSCD)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP441C1                   SFLCTL(HP441S1)
     A*%%TS  SD  20090707  132545  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 31'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1VERSION R        O  2  2REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH CLOSED/VOIDED HOG GROUPS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .. ?'
     A            C1FSBO    R        B  4 23REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A  88                              6  2'2=Revise Closed Group'
     A                                  6 25'5=View Group'
     A                                  6 46'6=Print Reports/Downloads'
     A  88                              7  2'3=Reopen Voided Group'
     A                                  7 25'7=View Movements'
     A                                  8 24'12=View Adjustments'
     A                                  9 29'?'
     A                                      DSPATR(HI)
     A                                  9 48'?'
     A                                      DSPATR(HI)
     A                                  9 54'?'
     A                                      DSPATR(HI)
     A                                  9 60'?'
     A                                      DSPATR(HI)
     A                                  9 65'?'
     A                                      DSPATR(HI)
     A                                  9 70'?'
     A                                      DSPATR(HI)
     A                                  9 74'?'
     A                                      DSPATR(HI)
     A                                  9 78'?'
     A                                      DSPATR(HI)
     A                                 10  9'Date'
     A                                      DSPATR(HI)
     A                                 10 46'Production'
     A                                      DSPATR(HI)
     A                                 10 59'Grp'
     A                                      DSPATR(HI)
     A                                 10 64'Stk'
     A                                      DSPATR(HI)
     A                                 10 69'SM'
     A                                      DSPATR(HI)
     A                                 10 73'Con'
     A                                      DSPATR(HI)
     A                                 11  2'Opt'
     A                                      DSPATR(HI)
     A                                 11  7'Disposed'
     A                                      DSPATR(HI)
     A                                 11 17'Group'
     A                                      DSPATR(HI)
     A                                 11 27'Farm'
     A                                      DSPATR(HI)
     A                                 11 33'Bldg'
     A                                      DSPATR(HI)
     A                                 11 39'Room'
     A                                      DSPATR(HI)
     A                                 11 46'Type'
     A                                      DSPATR(HI)
     A                                 11 52'Phase'
     A                                      DSPATR(HI)
     A                                 11 59'Typ'
     A                                      DSPATR(HI)
     A                                 11 64'Den'
     A                                      DSPATR(HI)
     A                                 11 69'Cd'
     A                                      DSPATR(HI)
     A                                 11 73'Flo'
     A                                      DSPATR(HI)
     A                                 11 77'St'
     A                                      DSPATR(HI)
     A            C1HGCD    R        B 12 17REFFLD(@@HGCD)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A N31N51N52
     A N53N54N55
     A N58                                  DSPATR(PC)
     A            C1FSCD    R        B 12 26REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1BLCD    R        B 12 33REFFLD(@@BLCD)
     A                                      DSPATR(HI)
     A            C1RMCD    R        B 12 39REFFLD(@@RMCD)
     A                                      DSPATR(HI)
     A            C1PTCD    R        B 12 46REFFLD(@@PTCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1PPCD    R        B 12 52REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1GTCD    R        B 12 60REFFLD(@@GTCD)
     A                                      DSPATR(HI)
     A  58                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1SDCD    R        B 12 65REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  59                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1SMCD    R        B 12 70REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1CFFL    R        B 12 74REFFLD(HSRREF/@@CFFL *LIBL/HSPREF)
     A  56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1GSCD    R        B 12 77REFFLD(@@GSCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP441K1
     A*%%TS  SD  19970618  110718  ISLRAMS     REL-V3R6M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A N99                             23 22'F5=Refresh'
     A N99                             23 34'F17=Print Options'
     A N99                             23 53'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP441ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP441EC                   SFLCTL(HP441ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
