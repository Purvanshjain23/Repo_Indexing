      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF808
      * TITLE:       Retrieve Deposit Amounts for a Cash Header
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/18/05
      *
      * FUNCTION:  This function:
      *             1) Retrieves the Cash Disbursement Header record for the incoming
      *                batch number
      *             2) Flips the Daily Date from the Header into Synon format
      *             3) Retrieves the Deposit Amounts for this batch and accumulates
      *                Prior Day and Current Day amounts based on the Daily Date
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/29/06  LeAnne Ramsey
      *           Replaced the calls to the generic date conversion programs
      *           with ILE date logic.
      *
      * 10/14/09  LeAnne Ramsey
      *           Recompile only. Cash Distribution Header was changed.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp012    if   e           k disk
      *    Cash disbursement header
      *
     Fpdj2cpl6  if   e           k disk
      *   AR bank deposit
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     d wksyndt         s                   like(j2hxdt)
      *
      *
      * Parms
      *
     D xxbtsn          s                   like(chbtsn)
     D xxpdam          s                   like(chdpdccam)
     D xxcdam          s                   like(chdcdccam)
      *
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * First, retrieve the Cash Disbursement Header record for the incoming
      * Batch Number.
      *
     C     xxbtsn        chain     tfp012                             92
     C                   if        *in92 = *off                                 If found
      *
      * Next, convert the retrieved Daily Date into the Synon format.
      *
     C     *iso          move      chddt         wkisodate
     C     *cymd         move      wkisodate     wksyndt
      *
      * Last, retrieve/accumulate Deposit Amounts for this Batch Number.
      *
     C     xxbtsn        setll     pdj2cpl6
      *
     C                   dou       *in90 = *on                                  Do loop
     C     xxbtsn        reade     pdj2cpl6                               90
     C                   if        *in90 = *off                                 If read
      *
     C                   select
     C                   when      j2hxdt < wksyndt
     C                   add       j2uiva        xxpdam
      *
     C                   when      j2hxdt = wksyndt
     C                   add       j2uiva        xxcdam
     C                   endsl
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do loop
      *
     C                   endif                                                  If found
      *
      * EOF processing
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxbtsn
     C                   parm                    xxpdam
     C                   parm                    xxcdam
      *
     C                   endsr
      /EJECT
