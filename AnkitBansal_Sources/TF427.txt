     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF427
      * TITLE:       Eopfees: Specify Carrier Invoice Close
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/16/06
      *
      *
      * FUNCTION: This function uses an edit record format to allow the user
      *           to submit the function. The Traffic folks take this on-demand
      *           function to "freeze" the Load data for a Period. This must be
      *           done prior the the running of the Seaboard/Triumph EOP Close
      *           function.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/10/06  LeAnne Fedor
      *           Recompile only. New fields were added to Freight Variance Detail file:
      *              1) Difference amount
      *              2) Prior difference amount
      *
      * 11/22/06  LeAnne Fedor
      *           Recompile only. New fields were added to Freight Variance Detail file.
      *
      * 07/17/08  LeAnne Ramsey
      *           Recompile only.
      *           In file TFP027-Freight Variance Detail, we:
      *              Replaced: FDAACTFAM  Allocated Actual Freight Amount
      *                  With: FDALFAM    Allocated Load Total Freight Amount
      *
      * 03/10/09  LeAnne Ramsey
      *           Recompile only.
      *           The users want to revert back to Allocated Actual Freight Amount
      *           from Allocated Load Total Freight Amount.
      *           This will reverse the change of 07/17/08 above!
      *           We have decided to keep both of the fields in file TFP027-Freight
      *           Variance Detail:
      *              FDALFAM    Allocated Load Total Freight Amount
      *              FDAACTFAM  Allocated Actual Freight Amount
      *
      * 05/06/09  LeAnne Ramsey
      *           Changed the Print logic to match Meat Costing.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfd427    cf   e             workstn infds(iofeed)
      *
      *
     Ftfl027a   if   e           k disk
      *    Freight variance detail
      *
      *
     Ftfp099    if   e           k disk
      *    Function control
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Compile time array for SBMJOB used in QCMDEXC
      *---------------------------------------------------------------
     D cmd             s              1    dim(80) ctdata perrcd(80)
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D maxmsg          s              2  0  inz(20)
      *
      *  Parms for use with QCMDEXC.  'LENGTH' is the length of the
      *  command that will be issued with QCMDEXC.  QCMDEXC requires
      *  the field containing length to be defined as 15 with 5 decimal
      *  positions.  The field 'CMDSBM' will hold the values of the
      *  submit job command when passed as a parm on the call to
      *  QCMDEXC.
      *
     D length          s             15  5 inz(80)
     D cmdsbm          s             80
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldcyr                  2      5  0
     D  ldcpe                  6      7  0
      *
     D  ldcbdt                 8     15  0
     D  ldcbmdy               16     21  0
     D  ldcbsyn               22     28  0
      *
     D  ldcedt                29     36  0
     D  ldcemdy               37     42  0
     D  ldcesyn               43     49  0
      *
     D  ldoutq               401    410
     D  ldhold               411    414
     D  ldsave               415    418
     D  ldcopy               419    419  0
      *
      *---------------------------------------------------------------
      *  Definition for external data area 'DATFLOAD'
      *---------------------------------------------------------------
     D
     D datfload        ds                  dtaara(datfload)
     D  dafl                   1      1
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
     C
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the error
      * message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to CRT.
      *
     C                   write     tf427k1
     C                   write     tf427ec
     C                   exfmt     tf427r1
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      routne = exit
      *
     C                   when      *in17 = *on                                  F17-print options
     C                   call      'RO890'
     C                   eval      routne = scrn1
      *
     C                   when      *in20 = *on                                  F20-wrksplf
     C                   call      'QEZOUTPT'                                       API
     C                   eval      routne = scrn1
     C                   other
      *
     C                   eval      routne = edit1
     C                   endsl
      *
      * Bring in the LDA to pick up any printer changes the user may
      * have made from called programs
      *
     C                   in        lda
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit screen 1 input fields
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * Validate that there are not already "frozen" records for this Period.
      *
     C     key01         chain     tfl027a                            92
     C                   if        *in92 = *off
     C                   add       1             error
     C                   movel     'HS08079'     mgid(e)
     C                   endif
      *
      *
      * Validate that the EOP Fees function is not running.
      *
     C     'EOPFEES   '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If hit
      *
     C                   if        fnsbfl <> *blank
     C                   add       1             error
     C                   movel     'HS08076'     mgid(e)
     C                   endif
      *
     C                   if        fncyr <> d1yr or
     C                             fncpe <> d1pe
     C                   add       1             error
     C                   movel     'HS08014'     mgid(e)
     C                   endif
      *
     C                   endif                                                  If hit
      *
      * Validate that no "freeze" has already been submitted.
      *
     C                   in        datfload
      *
     C                   if        datfload <> *blank
     C                   add       1             error
     C                   movel     'HS08075'     mgid(e)
     C                   endif
      *
      *
      * If there are no errors, execute the subroutine to submit.
      *
     C                   if        error = 0 and *in08 = *on                    If no error
     C                   exsr      $proc1
     C                   endif                                                  If no error
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process user's request
      *---------------------------------------------------------------
      *
     C     $proc1        begsr
      *
      * Bring in the data area that keeps multiple users (or one spacey user) from
      * making multiple submissions of this "freeze". Update the area to indicate
      * that there has been submission and put the data area back out.
      *
     C     *lock         in        datfload
     C                   move      yes           dafl
     C                   out       datfload
      *
     C                   out       lda
      *
      * Move the 80-character compile time array 'CMD' to an
      * 80-character field for passing as a parm with QCMDEXC.
      *
     C                   movea     cmd           cmdsbm
     C                   call      'QCMDEXC'
     C                   parm                    cmdsbm
     C                   parm                    length
      *
      * Issue the standard request submission message
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09003'
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * End job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *   none
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    d1yr
     C                   kfld                    d1pe
      *
      * Retrieve/initialize "printer" fields in the LDA
      *
     C                   call      'RO891CL'
     C                   in        lda
      *
      * Initialize LDA fields
      *
     C                   z-add     0             ldcyr
     C                   z-add     0             ldcpe
     C                   z-add     0             ldcbdt
     C                   z-add     0             ldcbmdy
     C                   z-add     0             ldcbsyn
      *
     C                   z-add     0             ldcedt
     C                   z-add     0             ldcemdy
     C                   z-add     0             ldcesyn
     C                   out       lda
      *
      *
      * Set up screen values and set some LDA fields using the Control Record.
      *
     C     'EOPFEES   '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If ok
     C                   z-add     fncyr         d1yr
     C                   z-add     fncpe         d1pe
     C                   z-add     fncbmdy       d1pbmdy
     C                   z-add     fncemdy       d1pemdy
      *
      * Set up the lda with the 'current' fields from the Control record.
      *
     C                   z-add     fncyr         ldcyr
     C                   z-add     fncpe         ldcpe
      *
     C                   z-add     fncbdt        ldcbdt
     C                   z-add     fncbmdy       ldcbmdy
     C                   z-add     fncbsyn       ldcbsyn
      *
     C                   z-add     fncedt        ldcedt
     C                   z-add     fncemdy       ldcemdy
     C                   z-add     fncesyn       ldcesyn
      *
     C                   out       lda
     C                   endif                                                  If ok
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
      *****************************************************************
      * COMPILE TIME ARRAY 'CMD' USED IN QCMDEXC
      *****************************************************************
** CMD table 'CMD' submits listing when F8 selected
SBMJOB CMD(CALL PGM(TF427CL)) JOB(FRZLOADS) JOBQ(*JOBD)
