     A*%%TS  SD  20031230  145500  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD564 - SELECT EXPENSE CODE
     A*                          (USER CONTROLLED REPORT CODES ONLY)
     A*  RPG PROGRAM:   HP564  - SELECT EXPENSE CODE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          07/12/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP564S1                   SFL
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1EXCD    R        O  8  1REFFLD(@@EXCD)
     A            D1EXDS    R        O  8  8REFFLD(EXEXDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP564C1                   SFLCTL(HP564S1)
     A*%%TS  SD  20031230  100440  ISLRAMS     REL-V5R2M0  5722-WDS
     A  *DS3                                SFLSIZ(0100)
     A  *DS4                                SFLSIZ(0100)
     A  *DS3                                SFLPAG(0009)
     A  *DS4                                SFLPAG(0009)
     A  *DS3                                WINDOW(HP564EC)
     A  *DS4                                WINDOW(HP564EC)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 15'SELECT EXPENSE CODE'
     A                                      DSPATR(HI)
     A                                  2  1'(Only User-Controlled codes are di-
     A                                      splayed.)'
     A                                      DSPATR(HI)
     A                                  4  1'Position Cursor, Press Enter'
     A                                  6  1'Code'
     A                                      DSPATR(HI)
     A                                  6  8'Description'
     A                                      DSPATR(HI)
     A            C1EXCD    R        B  7  1REFFLD(HSRREF/@@CACD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1EXDS    R        B  7  8REFFLD(HSRREF/EXEXDS *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      CHGINPDFT(UL LC FE)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP564K1
     A*%%TS  SD  20031230  145500  ISLRAMS     REL-V5R2M0  5722-WDS
     A  *DS3                                WINDOW(HP564EC)
     A  *DS4                                WINDOW(HP564EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP564ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP564EC                   SFLCTL(HP564ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 43)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160564  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
