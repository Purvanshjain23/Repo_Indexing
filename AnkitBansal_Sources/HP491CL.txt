/*****************************************************************   */
/*  PROGRAM NUMBER --> HP491CL                                       */
/*                                                                   */
/*  PROGRAM NAME ----> SPECIFY CLOSED GROUPS FOR CALORIC UPDATING    */
/*                                                                   */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAMS TO UPDATE THE      */
/*                     'FCAL' CALORIC RECORD FOR THE SPECIFIED       */
/*                     CLOSED GROUPS AND PRINT A LISTING OF THE      */
/*                     BEFORE AND AFTER CALORIE VALUES FOR GROUPS.   */
/*                     SELECTIONS ARE PASSED IN THE LDA.             */
/*                                                                   */
/*                     THIS FUNCTION IS ONLY AVAILABLE FOR I.S.      */
/*                     PERSONNEL.                                    */
/*                                                                   */
/*                     REQUIRED SELECTIONS:                          */
/*                       FROM CLOSED DATE                            */
/*                       TO CLOSED DATE                              */
/*                                                                   */
/*                     OPTIONAL SELECTIONS:                          */
/*                       BUSINESS OFFICE                             */
/*                       PRODUCTION TYPE                             */
/*                       PRODUCTION PHASE                            */
/*                       FARM SITE                                   */
/*                       HOG GROUP                                   */
/*                                                                   */
/*  PROGRAMMER ------> LEANNE FEDOR                                  */
/*  DATE ------------> 08/15/96                                      */
/*                                                                   */
/*********************************************************************/
/* MODIFICATIONS                                                     */
/* DATE      PROGRAMMER                                              */
/*                                                                   */
/* 06/03/97 LEANNE FEDOR                                             */
/*          RECOMPILE ONLY.  NEW FIELDS 'PURGE FLAG' AND 'DATE OF    */
/*          FIRST PURGE' ADDED TO HOG GROUP FILE.                    */
/*                                                                   */
/* 06/19/97 LEANNE FEDOR                                             */
/*          CHANGED OUT QUEUE LOGIC.                                 */
/*********************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(240)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC)  LEN(8)

             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&LDHGCD) TYPE(*CHAR) LEN(7)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 36 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 50 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 79 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 64 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 149 5))
             CHGVAR     VAR(&LDHGCD) VALUE(%SST(*LDA 224 7))

/*  SETUP QUERY SELECTION                                            */

/*  ALWAYS SELECT ONLY GROUPS IN THE DATE RANGE SELECTED             */

             CHGVAR     VAR(%SST(&QDATA 1  10)) VALUE('HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/* IF THE USER SELECTED A SPECIFIC BUSINESS OFFICE                   */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO

/* IF THE USER SELECTED A SPECIFIC GROUP                             */

             IF         COND(&LDHGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 92 17)) VALUE('*AND HGHGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 109 7)) VALUE(&LDHGCD)
             CHGVAR     VAR(%SST(&QDATA 116 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A PRODUCTION TYPE                          */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 135 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 140 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A PRODUCTION PHASE                      */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 142 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 159 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 164 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A FARM SITE                             */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 166 4)) VALUE('*AND')
             CHGVAR     VAR(%SST(&QDATA 171 10)) VALUE('HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 182 5)) VALUE(&LDFSCD)
             ENDDO


/*  RUN QUERY OVER CLOSED GROUPS                                 */

             OVRDBF     FILE(HSL034I) SHARE(*YES)

             OPNQRYF    FILE((HSL034I)) QRYSLT(&QDATA) +
                          KEYFLD((HGHGCD)) SEQONLY(*YES)

/* SET PARAMETERS FROM LDA                         */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT CAN PRINT AT 10 CPI IN 132 CHARACTERS                */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*-------------------------------------------------------------------*/
/*  CALL PROGRAM TO RECALCULATE CALORIES FOR GROUPS AND PRINT REPORT */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP391)

/*-------------------------------------------------------------------*/
/* DELETE FILE OVERRIDES                                             */
/*-------------------------------------------------------------------*/

             CLOF       OPNID(HSL034I)
             DLTOVR     FILE(HSL034I QPRINT)


 EXIT:       ENDPGM

