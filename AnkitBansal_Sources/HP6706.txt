      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6706
      * TITLE:       Listing of Farm Budgets
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/25/04
      *
      *
      *            This listing is submitted from HP4702-Work With Farm Budgets.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp183    if   e           k disk
      *    Creation schedule
      *
      *
     Fhsp184    if   e           k disk
      *    Budget availability
      *
      *
     Fhsp185    if   e           k disk
      *    Budget template header
      *
      *
     Fhsl188h   if   e           k disk
      *    Farm budget header
      *
      *
     Fhsp191    if   e           k disk
      *    Budget status
      *
      *
     Fqprint    o    f  157        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            157    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('y')
     D procfl          s              1    inz('N')
     D svfscd          s                   like(jhfscd)
     D svfbscd         s                   like(jhfbscd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D h1btds          s                   like(thbtds)
     D h1fsnm          s                   like(fsfsnm)
     D h1bads          s                   like(babads)
     D h1csds          s                   like(cscsds)
     D h1fbsds         s                   like(sbfbsds)
     D h1fmdy          s              6  0
     D h1tmdy          s              6  0
      *
     D l1fmdy          s              6  0
     D l1tmdy          s              6  0
     D l1btds          s                   like(thbtds)
     D l1bads          s                   like(babads)
     D l1csds          s                   like(cscsds)
     D l1fbsds         s                   like(sbfbsds)
     D l1btwk          s                   like(thbtwk)
     D l1fsnm          s                   like(fsfsnm)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfscd                 9     13  0
     D  ldbtcd                14     19
     D  ldbacd                20     20
     D  ldcscd                21     21
     D  ldbtwk                22     26  0
     D  ldfcymd               27     34  0
     D  ldtcymd               35     42  0
     D  ldfbscd               43     43
     D  ldynfl                44     44
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     *loval        setll     hsl188h
     C                   except    h1hdr
      *
      * Process all records in the file (some selections were made with the
      * open query). Additional selections will be made in this program.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl188h                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      yes           procfl
     C                   exsr      $check
     C                   if        procfl = yes                                 If process
      *
      * Print a blank line between farms and budget statuses
      *
     C                   select
     C                   when      first = yes
     C                   z-add     jhfscd        svfscd
     C                   move      jhfbscd       svfbscd
     C                   move      no            first
      *
     C                   when      jhfscd <> svfscd or
     C                             jhfbscd <> svfbscd
     C                   z-add     jhfscd        svfscd
     C                   move      jhfbscd       svfbscd
     C                   except    blank
     C                   endsl
      *
      * Flip From Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        jhbfdt <> 0
     C     *iso          test(d)                 jhbfdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      jhbfdt        wkisodate
     C     *mdy          move      wkisodate     l1fmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip To Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        jhbtdt <> 0
     C     *iso          test(d)                 jhbtdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      jhbtdt        wkisodate
     C     *mdy          move      wkisodate     l1tmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Retrieve Farm Site name
      *
     C     jhfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If found
     C                   move      fsfsnm        l1fsnm
     C                   endif                                                  If found
      *
      * Retrieve Budget Status description
      *
     C     jhfbscd       chain     hsp191                             92
     C                   if        *in92 = *off
     C                   move      sbfbsds       l1fbsds
     C                   endif
      *
      * Print
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    l1dtl
     C                   movel     yes           prtfl
     C                   endif                                                  If process
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------------------------------------------------------------------
      * EOF processing
      *-----------------------------------------------------------------------------
      *
      * If no records met the user's selections, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Make additional checks for matches on user selections
      *---------------------------------------------------------------
      *
     C     $check        begsr
      *
     C     jhbtcd        chain     hsp185                             92
     C                   if        *in92 = *off                                 If template hit
      *
     C                   move      thbtds        l1btds
     C                   z-add     thbtwk        l1btwk
      *
      * Retrieve descriptions for printing
      *
     C     thcscd        chain     hsp183                             92
     C                   if        *in92 = *off
     C                   move      cscsds        l1csds
     C                   endif
      *
     C     thbacd        chain     hsp184                             92
     C                   if        *in92 = *off
     C                   move      babads        l1bads
     C                   endif
      *
      *
      * Creation schedule
     C                   if        ldcscd <> *blank and ldcscd <> thcscd
     C                   move      no            procfl
     C                   endif
      *
      * Budget availability
     C                   if        ldbacd <> *blank and ldbacd <> thbacd
     C                   move      no            procfl
     C                   endif
      *
      * Template weeks
     C                   if        ldbtwk <> 0 and ldbtwk <> thbtwk
     C                   move      no            procfl
     C                   endif
      *
     C                   endif                                                  If template hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *  None
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Farm site
     C                   select
     C                   when      ldfscd = 0
     C                   seton                                        83
     C                   other
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   eval      h1fsnm = fsfsnm
     C                   else
     C                   eval      h1fsnm = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Budget template
     C                   select
     C                   when      ldbtcd = *blank
     C                   seton                                        80
     C                   other
     C     ldbtcd        chain     hsp185                             92
     C                   if        *in92 = *off
     C                   eval      h1btds = thbtds
     C                   else
     C                   eval      h1btds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Budget availability code
      *
     C                   select
     C                   when      ldbacd = *blank
     C                   seton                                        81
     C                   other
     C     ldbacd        chain     hsp184                             92
     C                   if        *in92 = *off
     C                   eval      h1bads = babads
     C                   else
     C                   eval      h1bads = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Creation schedule code
      *
     C                   select
     C                   when      ldcscd = *blank
     C                   seton                                        82
     C                   other
     C     ldcscd        chain     hsp183                             92
     C                   if        *in92 = *off
     C                   eval      h1csds = cscsds
     C                   else
     C                   eval      h1csds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Budget status
     C                   if        ldfbscd = *blank
     C                   seton                                        85
     C                   else
     C     ldfbscd       chain     hsp191                             92
     C                   if        *in92 = *off
     C                   move      sbfbsds       h1fbsds
     C                   endif
     C                   endif
      *
      * Include closed budgets
      *
     C                   if        ldynfl = yes
     C                   seton                                        86
     C                   endif
      * Weeks
     C                   if        ldbtwk = 0
     C                   seton                                        87
     C                   endif
      *
      * From date
     C                   if        ldfcymd = 0
     C                   seton                                        88
     C                   else
     C     *iso          test(d)                 ldfcymd                92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ldfcymd       wkisodate
     C     *mdy          move      wkisodate     h1fmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * To date
     C                   if        ldtcymd = 0
     C                   seton                                        89
     C                   else
     C     *iso          test(d)                 ldtcymd                92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ldtcymd       wkisodate
     C     *mdy          move      wkisodate     h1tmdy
     C                   endif                                                  If OK date
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           92 'HOG PRODUCTION SYSTEM'
     O                                          147 'DATE'
     O                       udate         y    157
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           93 'LISTING OF FARM BUDGETS'
     O                                          147 'TIME'
     O                       rtime              157 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          147 'PAGE'
     O                       page          z    157
      *
      *
     O          e            h1hdr          1
     O                                           25 'Farm site ...............'
     O               83                          31 'All'
     O              n83      ldfscd        z     33
     O              n83      h1fsnm              60
      *
      *
     O          e            h1hdr          1
     O                                           25 'Budget template .........'
     O               80                          31 'All'
     O              n80      ldbtcd              33
     O              n80      h1btds              60
      *
      *
     O          e            h1hdr       0  1
     O                                           25 'Budget availability .....'
     O               81                          31 'All'
     O              n81      ldbacd              28
     O              n81      h1bads              55
      *
      *
     O          e            h1hdr       0  1
     O                                           25 'Creation schedule .......'
     O               82                          31 'All'
     O              n82      ldcscd              28
     O              n82      h1csds              55
      *
      *
     O          e            h1hdr       0  1
     O                                           25 'Budget template weeks ...'
     O               87                          31 'All'
     O              n87      ldbtwk        z     33
      *
      *
     O          e            h1hdr       0  1
     O                                           25 'From date ...............'
     O               88                          31 'All'
     O              n88      h1fmdy              35 '  /  /  '
      *
      *
      *
     O          e            h1hdr       0  1
     O                                           25 'To date .................'
     O               89                          31 'All'
     O              n89      h1tmdy              35 '  /  /  '
      *
      *
     O          e            h1hdr       0  1
     O                                           25 'Budget status ...........'
     O               85                          31 'All'
     O              n85      ldfbscd             28
     O              n85      h1fbsds             43
      *
      *
     O          e            h1hdr       0  2
     O                                           25 'Include closed budgets ..'
     O               86                          31 'Yes'
     O              n86                          31 'No '
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            5 'Farm'
     O                                           13 'Budget'
     O                                           22 'Budget'
      *
      *
     O          e            h1hdr       0  1
     O                                            5 'Site'
     O                                           13 'Status'
     O                                           23 'Template'
     O                                           31 'From'
     O                                           40 'To'
     O                                           52 'Budget'
     O                                           61 'Budget'
     O                                           85 'Creation'
      *
      *
     O          e            h1hdr       0  1
     O                                            5 'Code'
     O                                           12 'Code'
     O                                           21 'Code'
     O                                           31 'Date'
     O                                           41 'Date'
     O                                           52 'Number'
     O                                           67 'Availability'
     O                                           85 'Schedule'
     O                                          103 'Weeks'
     O                                          126 'Template Description'
     O                                          141 'Farm Name'
      *
     O          e            h1hdr       0  2
     O                       dash               157
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
      *
     O          e            l1dtl          1
     O                       jhfscd        z      5
     O                       l1fbsds        b    15
     O                       jhbtcd              23
     O                       l1fmdy         b    33 '  /  /  '
     O                       l1tmdy         b    43 '  /  /  '
     O                       jhfbsn        z     52
     O                       l1bads         b    75
     O                       l1csds         b    97
     O                       l1btwk        zb   103
     O                       l1btds         b   131
     O                       l1fsnm         b   157
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
