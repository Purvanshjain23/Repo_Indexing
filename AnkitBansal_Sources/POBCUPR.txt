/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              PROMPTING FUNCTION USED TO PROCESS SALES PRICE      */
/*              EDIT PRINT. PROVIDES  PRINTER OVERRIDE PROMPTING    */
/*              AND PASSES COMPANY, SCHED SHIP DATE, ACTUAL SHIP DT,*/
/*              SALES ROUTE AND CUSTOMER NUMBER.                    */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRT SALES PRICE EDIT                                */
/*  DATE      -  6/08/99                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &CUSTN      - CUSTOMER NUMBER (PASSED)                  */
/*     B    &SCHDATN    - SCHED SHIP DATE(PASSED)                   */
/*     B    &ACTDATN    - ACTUAL SHIPPED DATE (PASSED)              */
/*     B    &SLSRTE     - SALES ROUTE     (PASSED)                  */
/*                                                                  */
/********************************************************************/

 POBCUPR:    PGM        PARM(&RETURN &APPLIC &PGM &DESC &OUTQ &HOLD +
                          &SAVE &NQSTAT &NIGHTQ &COMPNYN &CUSTN +
                          &SCHDATN &ACTDATN &COPYN &SLSRTE)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &CUSTN      *DEC                  /* CUSTOMER NO.   (Passed)   */
        DCL    &SCHDATN    *DEC                  /* SCHED DELIV DT (Passed)   */
        DCL    &ACTDATN    *DEC                  /* ACTUAL    DATE (Passed)   */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &SLSRTE     *CHAR   (3)           /* SALES ROUTE               */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/


             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &CUSTN +
                          &SCHDATN &ACTDATN &SLSRTE &OUTQ +
                          &HOLD &SAVE &COPYN &NIGHTQ)) +
                          JOB(&PGM) JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

