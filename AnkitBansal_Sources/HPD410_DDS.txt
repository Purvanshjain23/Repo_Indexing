     A*%%TS  SD  20030624  081531  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD410
     A*  TITLE:         WORK WITH EXPENSE CLASSES
     A*  PROGRAMMER:    MARY PALMER
     A*  CREATED:       04/11/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF15(15 'W/W EXPENSE CODES')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP410S1                   SFL
     A*%%TS  SD  20030619  094733  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            D1ECSEQ   R        O 10  8REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      EDTCDE(L)
     A            D1ECCD    R        O 10 18REFFLD(ECECCD)
     A            D1ECDS    R        O 10 27REFFLD(ECECDS)
     A            D1AMCD    R        O 10 52REFFLD(HSRREF/@@AMCD *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP410C1                   SFLCTL(HP410S1)
     A*%%TS  SD  20030624  081531  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP410'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'WORK WITH EXPENSE CLASSES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  4 22'7=Work with Subclasses'
     A                                  4 46'8=View Subclasses'
     A                                  6 54'?'
     A                                      DSPATR(HI)
     A                                  7  7'Class'
     A                                      DSPATR(HI)
     A                                  7 18'Class'
     A                                      DSPATR(HI)
     A                                  7 27'Class'
     A                                      DSPATR(HI)
     A                                  7 50'Allocation'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'Sequence'
     A                                      DSPATR(HI)
     A                                  8 18'Code'
     A                                      DSPATR(HI)
     A                                  8 27'Description'
     A                                      DSPATR(HI)
     A                                  8 51'Method'
     A                                      DSPATR(HI)
     A            C1ECSEQ   R        B  9  8REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A            C1ECCD    R        B  9 18REFFLD(HSRREF/@@ECCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ECDS    R        B  9 27REFFLD(HSRREF/ECECDS *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
     A            C1AMCD    R        B  9 52REFFLD(HSRREF/@@AMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP410K1
     A*%%TS  SD  20030619  074652  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F5=Refresh'
     A N99                             22 23'F6=Create'
     A N99                             22 34'F7=Listing'
     A                                 22 46'F15=W/W Expense Codes'
     A N99                             23 46'F17=Print Options'
     A N99                             23 65'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP410ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP410EC                   SFLCTL(HP410ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
