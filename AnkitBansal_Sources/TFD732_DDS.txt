     A*%%TS  SD  20050316  135425  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD732
     A*  TITLE:         VIEW INVOICE CHARGE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/08/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R TF732R1
     A*%%TS  SD  20050316  135425  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'VIEW INVOICE CHARGE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Invoice Charge:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 45'System-generated flag:'
     A                                      COLOR(PNK)
     A            D1SYSFL   R        O  3 68REFFLD(TFREF/@@SYSFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                  4  2'Date ...................:'
     A            D1ICDTMDY R        O  4 29REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                      DSPATR(HI)
     A                                  4 45'Invoice charge number:'
     A                                      COLOR(PNK)
     A            D1ICSN    R        O  4 68REFFLD(TFREF/@@ICSN *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                  5  2'Amount .................:'
     A            D1ICAM    R        O  5 29REFFLD(TFREF/@@ICAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  5 45'Invoice charge status:'
     A                                      COLOR(PNK)
     A            D1STDS    R        O  5 68REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                  6  2'Units ..................:'
     A            D1UNIT    R        O  6 29REFFLD(TFREF/@@UNIT *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  7  2'Charge code ............:'
     A            D1CCCD    R        O  7 29REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                  8  2'Comment ................:'
     A            D1ICCOM   R        O  8 29REFFLD(TFREF/@@ICCOM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 10  2'Invoice Info:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 39'Invoice Date'
     A                                      DSPATR(UL)
     A                                 10 54'Due Date'
     A                                      DSPATR(UL)
     A                                 10 65'Posted Date'
     A                                      DSPATR(UL)
     A                                 11  2'Invoice number .........:'
     A            D1INSN    R        O 11 29REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            D1INDTMDY R        O 11 41REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                      DSPATR(HI)
     A            D1DUDTMDY R        O 11 54REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                      DSPATR(HI)
     A            D1PODTMDY R        O 11 66REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                      DSPATR(HI)
     A                                 13  2'Charge Code Info:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 45'User-controlled:'
     A                                      COLOR(PNK)
     A            D1UCFL    R        O 13 62REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                 14  2'Charge code ............:'
     A            D1CCCD2   R        O 14 29REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1CCDS    R        O 14 45REFFLD(TFREF/@@CCDS *LIBL/TFPREF)
     A                                 15  2'Company ................:'
     A            D1CONO    R        O 15 29REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            D1CONM    R        O 15 45REFFLD(TFREF/@@CONM *LIBL/TFPREF)
     A                                 16  2'G/L code - debit  ......:'
     A            D1GLDBCD  R        O 16 29REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1GLDBCDDSR        O 16 45REFFLD(TFREF/@@GLDBCDDS *LIBL/TFPRE-
     A                                      F)
     A                                 17  2'G/L code - credit ......:'
     A            D1GLCRCD  R        O 17 29REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1GLCRCDDSR        O 17 45REFFLD(TFREF/@@GLCRCDDS *LIBL/TFPRE-
     A                                      F)
     A                                 18  2'Active/inactive status .:'
     A            D1AIST    R        O 18 29REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 19  2'Charge code frequency ..:'
     A            D1CFQCD   R        O 19 29REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1CFQDS   R        O 19 45REFFLD(TFREF/@@CFQDS *LIBL/TFPREF)
     A                                 20  2'Include in invoice total:'
     A            D1INFL    R        O 20 29REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 21  2'System module ..........:'
     A            D1SMCD    R        O 21 29REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1SMDS    R        O 21 45REFFLD(TFREF/@@SMDS *LIBL/TFPREF)
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R TF732K1
     A*%%TS  SD  20040706  113301  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R TF732ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R TF732EC                   SFLCTL(TF732ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
