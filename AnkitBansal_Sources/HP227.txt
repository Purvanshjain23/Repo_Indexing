      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      HPS---Datamart
      * PROGRAM:     HP227 - Datamart BGF/FIN: Build BGF and NURFIN Period Group Hdr
      *
      * PROGRAMMER:  Barb Gutierrez
      * CREATED:     08/25/03
      *
      * FUNCTION:    Write a group record for each BGF farm for each period
      *              to be used in the Financial Analysis Cubes
      *              Simulate a Group # with xxxyyzz
      *              1. xxx - last 3 digits of the Farm #
      *              2. yy  - period #
      *              3. zz  - year
      *
      *              Also write a group record for each Nur/Fin farm for each
      *              period to be ussed in the Financial Anaylsis Cubes in Cognos.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/03/03  Barb Gutierrez
      *           Added the process to delete any records for the period
      *           being processed in the INZSR routine.
      *
      * 01/29/04  LeAnne Fedor
      *           Added 3 fields:
      *                opened accounting period
      *                disposed accounting period
      *                closed accounting period
      *
      * 12/28/04  LeAnne Fedor
      *           Recompile only. New field 'Farm Size' added to Datamart Farm Site
      *           file.
      *
      * 02/01/05  LeAnne Fedor
      *           Two new fields (Sire Line Company, Prime Line) were added to the
      *           Datamart Group Header files and Datamart BGF: Weekly Production file.
      *           Added logic to populate new fields in the Datamart All: Group Header file.
      *
      * 02/07/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *                    Added: Transferred In Open Gilts
      *                  Removed: Total Lactation Pounds
      *
      * 07/05/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *           Added 1 new field: Average Daily Beginning Piglet Inventory
      *
      * 08/23/05  LeAnne Fedor
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Man hours
      *
      * 03/23/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *
      * 04/24/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *                 Cognos Report Grouping
      *                 Weaning Cognos Report Grouping
      *                 Report PCP Cognos Report Grouping
      *                 Report Farrowed Cognos Report Grouping
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Total Gilt Weight @ 28 Weeks
      *
      * 08/02/06  LeAnne Fedor
      *           Recompile only. Two new fields (ration 8 feed pounds, ration paylean flag)
      *           added to Datamart Group Header file.
      *
      * 08/02/06  LeAnne Fedor
      *           Added 2 fields to HPPA034-Datamart Hog Group:
      *               1) Ration Paylean Flag
      *               2) Ration 8 Feed Pounds
      *
      * 08/10/07  LeAnne Ramsey
      *           Recompile only. New "target" fields renamed in the Datamart Group
      *           Header file.
      *
      * 09/21/07  LeAnne Ramsey
      *           Recompile only. New "target" fields added to the Datamart FIN files:
      *              1) Weekly Group Detail
      *              2) Group Header
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields deleted from Weekly Production file.
      *                           BPPSGLSV - Nbr Gilts Bred w/Passed Date
      *                           BPWSLB   - Weekly Ending Lbs Supplement Used
      *                           BPP2LT   - Total P2 Litters Farrowed
      *                           BPTDGB   - Total Days Gilt Breeding Age
      *                           BPIOGLHD - Transferred in Open Gilts
      *                           BPOOGLHD - Gilts Transferred Out Open
      *                           BP17WNLT - 17+ Day Weaned Sows
      *                           BPTGLLB  - Total Gilt Weight @ 28 Weeks
      *
      * 11/07/08  LeAnne Ramsey
      *           Recompile only:
      *           Added 3 new fields to HPPB092-Datamart BGF: Weekly Production
      *            1) Target open gilt pool eligible
      *            2) Target litters farrowed/mated female/year
      *            3) Target pigs weaned/mated female/year
      *
      * 05/28/09  LeAnne Ramsey
      *           Recompile only:
      *           Added new field "Farrowing Crates" to:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 09/01/09  LeAnne Ramsey
      *           Recompile only. Added new field "Report Total Litters Farrowed" to:
      *                 HPPB092-Datamart BGF: Weekly Production
      *
      * 01/14/10  LeAnne Ramsey
      *           Recompile only. Changed text on BPLVHD and BPLVLB in:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 02/01/10  LeAnne Ramsey
      *           We will now retrieve BGF "produced head" for the "period" from the
      *           HPS files. Prior to this we were summing the weekly records from
      *           the Datamart file HPPB092.....but, Week53 was causing a problem.
      *
      * 03/02/10  LeAnne Ramsey
      *           The users now want the existing FPS (Feeder Pig Sales) fields populated for
      *           the BGF farms.
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 09/14/11  LeAnne Ramsey  (C1732)
      *           Program bug. We were not clearing the output line at the end of
      *           $nurfin. So, once in a blue moon, we got bad data in the $bgf
      *           subroutine.
      *
      *  05/12/16 Barb Gutierrez       E005752
      *           - Added company control file HSP0071 and changed some logic to
      *             accomodate multiple companies being processed.
      *           - Changed hppa018 to hpla018a to process by company being passed in.
      *
      * 10/14/16  Barb Gutierrez
      *           Recompile only. Added fields sold days in phase, avg sold days in phase,
      *           wgt open date and company number to HPPF034. Added sold days in phase,
      *           avg sold days in phase and wgt open date to HPPA034.  E6136
      *
      * 10/20/16  Barb Gutierrez  E007666
      *           Update BGF group number structure to hard code a Z in the leading position
      *           of the farm number.  Dummy BGF farms were changed to be in a 2000 series number
      *           so they could reuse the original number (ie, farm 071 became 2071).
      *           The group number for BGF's are positions 1 - 3 = farm number, positions 4 - 5 =
      *           period number, and positions 6 - 7 = year. Because 2071 wont fit, it is writing
      *           the groups twice, once for farm 71 and once for 2071 which is causing problems
      *           in cognos.  Group must be unique.  Hard coding the Z will keep the groups unique.
      *
      * 01/12/23  Eric Louchez - 111690 - Increment HGCD from 7 A to 9A.
      *           The group code in is being incremented due to the business utilizing more than 3
      *           digits for a BGF farm number.  Nursery and Grow Finisher groups will still be 7.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     F*hppa018   if   e           k disk
     Fhpla018a  if   e           k disk
      *   Datamart ALL: Farm site
      *
      *
     Fhsl034d   if   e           k disk    rename(hgrec:hgrecd) prefix(p2)
      *   Hog group
      *
      *
     Fhplf034b  if   e           k disk    rename(hgrec:hgrecb)
      *   Datamart FIN: Group header
      *
      *
     Fhpla034a  uf a e           k disk
      *   Datamart All: Group header
      *
      *
     Fhsl064b   if   e           k disk
      *   Check detail
      *
      *
     Fhsl074h   if   e           k disk
      *   Transfer movement header  (key fields overridden in CLP)
      *
      *
     Fhsp075    if   e           k disk
      *   Transfer movement detail
      *
      *
     Fhsl084l   if   e           k disk
      *   Sales movement header   (LF selects on records where status = PP)
      *
      *
     Fhsp085    if   e           k disk
      *   Sales movement detail
      *
     Fhsp0071   if   e           k disk
      *   Company Control File
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D bgfz            c                   'Z'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkclacqr        s                   like(hgclacqr)
     D wkpe            s              2  0
     D wkhd            s              7  0
     D wkavlb          s              7  2
     d svccly          s                   like(ccccly)
     d svcclp          s                   like(cccclp)
     d svlepdt         s                   like(cclepdt)
     d svlbpdt         s                   like(cclbpdt)
      *
      *
      * Workfields for date manipulation
      *
     D wkbpiso         s               D   datfmt(*iso)
     D wkepiso         s               D   datfmt(*iso)
      *
      *
      * Data structure for the simulated Group Code
      *
E7666D                 ds
  |  ** 111690 *D  wkhgcd                 1      7
  |  ** 111690 *D  wkfscd3                1      3
  |  ** 111690 *D  wkgppe                 4      5
E7666** 111690 *D  wkgpyr                 6      7
      *
E7666D                 ds                                                        111690
  |  D  wkhgcd                 1      9                                          111690
  |  D  wkfscd5                1      5                                          111690
  |  D  wkgppe                 6      7                                          111690
E7666D  wkgpyr                 8      9                                          111690
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *-----------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  Definition for external data area 'DALPER' which holds the last
      *  period that was closed in HPS.
      *---------------------------------------------------------------
      *   Remove use of dalper.  e5752
     D*                DS
     D* DALPER                 1     24
     D* DACCYY                 1      4  0
     D* DAPER                  5      6  0
     D* DABPDT                 7     14  0
     D* DAEPDT                15     22  0
     D* DAPPFL                24     24
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
      ***************************************************************************************
     IHGREC
      * Renamed input format fields
     I              HGHGCD                      HGHGCD9
      ***************************************************************************************
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Read each record in the Datamart Farm file that was selected in open query.
      *
     C     xxcocd        setll     hpla018a
     C                   dou       *in90 = *on                                  Main do loop
     C     xxcocd        reade     hpla018a                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * We are using 3 "034" files. Clear some records before you start.
      *
     C                   clear                   hgrec
     C                   clear                   hgrecb
      *
      *
      * BGF or Nur/Fin Farms
     C                   select
     C                   when      fsppcd = 'BGF  '
     C                   exsr      $bgf
     C                   other
     C                   exsr      $nurfin
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      * EOF Processing
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Process BGF Farms
      *----------------------------------------------------------------
      *
     C     $bgf          begsr
      *
      * Populate fields
      *
E7666 * If farm number is one of the dummy farms (2000 - 2999), hard code a Z in the
  |   * 1st position of the farm number to be used in the group number to keep it unique.
  |   *
  |   *** 111690         if        fsfscd >= 2000 and        **********************
E7666 *** 111690                     fsfscd <= 2999          **********************
      *** 111690         move      fsfscd        wkfscd3     **********************
E7666 *** 111690         movel     bgfz          wkfscd3     **********************
  |   *** 111690         else                                **********************
  |  C                   move      fsfscd        wkfscd5
  |   *** 111690         endif                               **********************
E7666 *
     C                   move      fscocd        hgcocd
      *
      *  use year and period from company control file instead of lda.  e5752
      *
     C**                 z-add     daper         wkgppe
     C**                 move      daccyy        wkgpyr
E7666C                   move      svcclp        wkgppe
E7666C                   move      svccly        wkgpyr
      ****** 111690      move      wkhgcd        hghgcd
     C                   Eval      hghgcd9 =%Trim(WkHGCD:' ')                   111690
      *
     C                   move      fsfscd        hgfscd
     C                   move      fsppcd        hgppcd
     C                   move      fsgtcd        hggtcd
     C                   move      'CL'          hggscd
     C                   move      wkepiso       hgcldt
     C                   z-add     wkclacqr      hgclacqr
     C***                z-add     daccyy        hgclcdyr
     C***                z-add     daper         hgclcdpe
     C**                 z-add     daper         hgclacpe
     C                   z-add     svccly        hgclcdyr
     C                   z-add     svcclp        hgclcdpe
     C                   z-add     svcclp        hgclacpe
      *
      * Retrieve Feeder Pig Sales info for this BGF Farm for the Period being closed
      *
     C                   exsr      $fps
      *
      * Retrieve BGF Produced Head using the Transfer Movements OUT of the
      * Farm for the Period.
      *
     C                   exsr      $tranout
      *
     C                   write     hgrec
     C                   clear                   hgrec
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Retrieve Feeder Pig Sales for the Period for this BGF Farm
      *----------------------------------------------------------------
      *
      * To make sure that we only get the "weaner" pigs, we will ultimately
      * only include data where the "average pounds" is <= 20 pounds.
      *
     C     $fps          begsr
      *
     C     key03         setll     hsl084l
      *
     C                   dou       *in91 = *on                                  Do fps
     C     key03         reade     hsl084l                                91
     C                   if        *in91 = *off and shstcd = 'FPS  '            If FPS header
      *
     C     shmvsn        setll     hsp085
     C                   dou       *in93 = *on                                  Do detail
     C     shmvsn        reade     hsp085                                 93
     C                   if        *in93 = *off                                 If more detail
      *
      * Calculate average live pounds
      *
     C                   z-add     0             wkavlb
     C                   if        sglvhd <> 0
     C                   eval(h)   wkavlb = sglvlb / sglvhd
     C                   endif
      *
      * If this is "weaner" size pigs, then process
      *
     C                   if        wkavlb <= 20                                 If a weaner
     C                   add       sglvhd        hgfpshd
     C                   add       sglvlb        hgfpslb
      *
      *    Get Gross Amount from Check for this Movement/Hog Group
      *
     C     key04         setll     hsl064b
     C                   dou       *in94 = *on                                  Do check
     C     key04         reade     hsl064b                                94
     C                   if        *in94 = *off                                 If check
     C                   add       cdgram        hgfpsam
     C                   endif                                                  If check
     C                   enddo                                                  Do check
      *
     C                   endif                                                  If a weaner
      *
     C                   endif                                                  If more detail
     C                   enddo                                                  Do detail
      *
     C                   endif                                                  If FPS header
     C                   enddo                                                  Do fps
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Retrieve Produced Head for the Period for the BGF Farms
      *----------------------------------------------------------------
      *
     C     $tranout      begsr
      *
     C                   move      'GEN  '       hgptcd
      *
      * Read all transfer movement headers out of this BGF farm for the Period.
      * Read the detail records.    Accumulate values when:
      *       a) the origin group IS NOT a GDU group and
      *       b) the destination phase is 'nursery' or
      *       c) the destination phase is 'grow finish' or 'BGF' and
      *          the 'average received pounds' is 20 pounds or less
      *
      * NOTE: The header file is truly keyed with 'shipped date'; but, we override
      *       the key list with 'received date' in the CL.
      *     changed to use ending date from company control.  E5752
      *
     C     key01         setll     hsl074h
      *
     C                   dou       *in91 = *on or mhrcdt > svlepdt              Do header
     C     fsfscd        reade     hsl074h                                91
     C                   if        *in91 = *off and mhrcdt <= svlepdt           If not EOF
      *
     C     mhmvsn        setll     hsp075
     C                   dou       *in93 = *on                                  Do detail
     C     mhmvsn        reade     hsp075                                 93
     C                   if        *in93 = *off                                 If record
      *
      * Determine if origin group is GDU.
      *
     C     mdorsn        chain     hsl034d                            92
     C                   if        *in92 = *off and p2hggtcd <> 'G'             If not GDU
      *
      * Calculate average received pounds when the destination phase is
      * Grow Finish or BGF.
      *
     C                   z-add     0             wkavlb
     C                   if        mhdnpp = 'GF   ' or mhdnpp = 'BGF  '         If grow finish
     C     mdrjhd        add       mdqlhd        wkhd
     C                   if        wkhd <> 0
     C                   eval(h)   wkavlb = (mdqllb + mdrjlb) / wkhd
     C                   endif
     C                   endif                                                  If grow finish
      *
     C                   if        (mhdnpp = 'GF   ' and wkavlb <= 20) or       If OK
     C                             (mhdnpp = 'BGF  ' and wkavlb <= 20) or
     C                              mhdnpp = 'NUR  '
     C                   add       mdqlhd        hgdtohd
     C                   add       mdrjhd        hgdtohd
     C                   move      p2hgptcd      hgptcd
     C                   endif                                                  If OK
      *
     C                   endif                                                  If not GDU
     C                   endif                                                  If record
     C                   enddo                                                  Do detail
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do header
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Process NUR/FIN Farms
      *----------------------------------------------------------------
      *
     C     $nurfin       begsr
      *
     C     key02         setll     hplf034b                               92
      *
     C                   dou       *in92 = *on or hgcldt > wkepiso              Do nur/fin
     C     fsfscd        reade     hplf034b                               92
     C                   if        *in92 = *off and hgcldt <= wkepiso           If more
      *
      *     changed to use last period from company control.  E5752
      *
     C**                 z-add     daper         hgclcdpe
     C                   z-add     svcclp        hgclcdpe
     C                   movel     hghgcd        hghgcd9
     C                   write     hgrec
     C                   endif                                                  If more
     C                   enddo                                                  Do nur/fin
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *  Pass in company.  E5752
      *
     C     *entry        plist
     C                   parm                    xxcocd            3
      *
      * Bring in the data area that contains the last Period posted
      * in HPS.
      *    remove use of dalper E5752
      *
     C**   *dtaara       define                  dalper
     C**                 in        dalper
      *
      * Read the company control file for the company being passed in to retrieve
      * the last HPS closed year/period.      E5752
      *
     C     xxcocd        chain     hsp0071                            92
     C                   if        *in92 = *off                                 If exists
     C                   move      ccccly        svccly                         last close year
     C                   move      cccclp        svcclp                         last close period
     C                   move      cclbpdt       svlbpdt                        last close beg date
     C                   move      cclepdt       svlepdt                        last close end date
     C                   endif                                                  If exists
      *
      *  use the begin/end dates and period from company constants instead of lda. e5752
      *
     C**   *ymd          move      daepdt        wkepiso
     C**   *ymd          move      dabpdt        wkbpiso
     C**                 z-add     daper         wkpe
     C     *ymd          move      svlepdt       wkepiso
     C     *ymd          move      svlbpdt       wkbpiso
     C                   z-add     svcclp        wkpe
      *
      * Call the generic program to retrieve the quarter associated with the
      * accounting period....you will need this for the BGF records.
      *
     C                   call      'HP8006'
     C                   parm                    wkpe
     C                   parm      0             wkclacqr
      *
      *
      * Always delete any existing period records from the Datamart file. Should
      * only have the company selected in open query.
      *
      * Changed to use key05 to delete records instead of just wkepiso since we have
      *  multiple companies now.   E005752
      *
     C     key05         setll     hpla034a
     C                   dou       *in91 = *on                                  Do deletes
     C     key05         reade     hpla034a                               91
     C                   if        *in91 = *off
     C                   delete    hgrec
     C                   endif
     C                   enddo                                                  Do deletes
      *
      * Key lists
      *   changed keys to use date values retrieved from company control file. e5752
      *
     C     key01         klist
     C                   kfld                    fsfscd
     C                   kfld                    svlbpdt
     C*****              kfld                    dabpdt
      *
     C     key02         klist
     C                   kfld                    fsfscd
     C                   kfld                    wkbpiso
      *
     C     key03         klist
     C***                kfld                    daepdt
     C                   kfld                    svlepdt
     C                   kfld                    fsfscd
      *
     C     key04         klist
     C                   kfld                    sgmvsn
     C                   kfld                    sghgsn
      *
     C     key05         klist
     C                   kfld                    xxcocd
     C                   kfld                    wkepiso
      *
     C                   endsr
      *
