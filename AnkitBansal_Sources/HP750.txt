      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP750
      * TITLE:       VIEW FARM SITE
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     04/04/94
      *
      * FUNCTION: DISPLAYS FAMR SITE INFORMATION
      *
      *           THIS PROGRAM IS CALLED FROM HP450 - WORK WITH
      *           FARM SITES.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 02/28/95  LEANNE FEDOR
      *           REMOVED DATE/TIME STAMP.
      *
      * 04/19/95  LEANNE FEDOR
      *           ADDED 'CELL' TO SCREEN AND CHANGED FARM SITE CODE
      *           FROM ALPHA TO NUMERIC.
      *
      * 05/15/96  LEANNE FEDOR
      *           REMOVED J.D. EDWARDS ADDRESS FIELDS.
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 04/13/98  LeAnne Fedor
      *           Added new 'state' field to screen.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file
      *           to (production manager/cell manager/multisite manager).
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 07/01/02  LeAnne Fedor
      *           Added new 'county' and 'type of farm' fields to listing. Changed layout.
      *
      * 02/25/03  LeAnne Fedor
      *           Added new 'transportation office'; had to put Production Type/Phase together
      *           on 1 line and get rid of their descriptions to make room.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 02/10/16  Susan Blakey (E05050)
      *           Place the MTech Reference on screen for
      *           Farm Reference HSP018
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD750    CF   E             WORKSTN
     F                                     INFDS(IOFEED)
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
     FHSP012    IF   E           K DISK
      *    COMPANY RELATIONSHIP
      *
     FHSP013    IF   E           K DISK
      *    FEED DELIVERY ZONES
      *
     FHSP016    IF   E           K DISK
      *    MANAGER
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP025    IF   E           K DISK
      *    GROUP TYPE
      *
      *
     FHSP101    IF   E           K DISK
      *    TYPE OF FARM
      *
      *
     FHSP109    IF   E           K DISK
      *    Transportation office
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO, DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *
      * RETRIEVE FARM SITE INFO AND DESCRIPTION
      *
     C                   MOVEL     *blanks       DFMTEC
     C     DFFSCD        CHAIN     HSP018                             90
      *
     C     *IN90         IFEQ      *ON                                          IF NOT FOUND
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00132'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   eval      dfsacd = fssacd
     C                   MOVEL     FSTOCD        DFTOCD
     C                   MOVEL     FSPTCD        DFPTCD
     C                   MOVEL     FSPPCD        DFPPCD
     C                   Z-ADD     FSCELL        DFCELL
     C                   MOVEL     FSPMGCD       DFPMGCD
     C                   MOVEL     FSCMGCD       DFCMGCD
     C                   MOVEL     FSMMGCD       DFMMGCD
     C                   MOVEL     FSFMBO        DFFMBO
     C                   MOVEL     FSDZCD        DFDZCD
     C                   MOVEL     FSCRCD        DFCRCD
     C                   MOVEL     FSCNTY        DFCNTY
     C                   MOVEL     FSTFCD        DFTFCD
     C                   MOVEL     FSGTCD        DFGTCD
     C                   MOVEL     FSMTECHREF    DFMTEC
      *
     C                   Z-ADD     FSCNFSCD      DFCNFSCD
     C                   Z-ADD     FSPOD         DFPOD
     C                   Z-ADD     FSPAJD        DFPAJD
     C                   Z-ADD     FSFAJD        DFFAJD
     C                   MOVEL     FSCJD         DFCJD
      *
      * RETRIEVE DESCRIPTIONS AND NAMES FOR FIELDS
      *
     C                   if        dftocd <> *blank
     C     DFTOCD        CHAIN     HSP109                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     TOTONM        DFTONM
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFTONM
     C                   ENDIF
     C                   endif
      *
     C     DFPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     PTPTDS        DFPTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFPTDS
     C                   ENDIF
      *
     C     DFPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     PPPPDS        DFPPDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFPPDS
     C                   ENDIF
      *
     C     DFCRCD        CHAIN     HSP012                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     CRCRDS        DFCRDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFCRDS
     C                   ENDIF
      *
     C     DFPMGCD       CHAIN     HSP016                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     MGMGNM        DFPMGNM
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFPMGNM
     C                   ENDIF
      *
     C     DFCMGCD       CHAIN     HSP016                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     MGMGNM        DFCMGNM
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFCMGNM
     C                   ENDIF
      *
     C     DFMMGCD       CHAIN     HSP016                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     MGMGNM        DFMMGNM
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFMMGNM
     C                   ENDIF
      *
     C     DFFMBO        CHAIN     HSP009                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     BOBODS        DFFMDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFFMDS
     C                   ENDIF
      *
     C     K1P013        CHAIN     HSP013                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     DZDZDS        DFDZDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFDZDS
     C                   ENDIF
      *
      *
     C     DFTFCD        CHAIN     HSP101                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     TFTFDS        DFTFDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFTFDS
     C                   ENDIF
      *
      *
     C     DFGTCD        CHAIN     HSP025                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     GTGTDS        DFGTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFGTDS
     C                   ENDIF
      *
      * RETRIEVE FARM SITE JDE NUMBER ADDRESS FIELDS
      *
     C     DFFAJD        IFNE      0
     C                   MOVEL     DFFAJD        INAJD
     C                   MOVEL(P)  'F'           INAT1
     C                   EXSR      $JDEV
     C                   MOVEL     OUALPH        DFFJNM
     C                   ENDIF
      *
      * RETRIEVE PRODUCER JDE NUMBER NAME FIELD
      *
     C     DFPAJD        IFNE      0
     C                   MOVEL     DFPAJD        INAJD
     C                   MOVEL(P)  'P'           INAT1
     C                   EXSR      $JDEV
     C                   MOVEL     OUALPH        DFPJNM
     C                   ENDIF
      *
      * RETRIEVE COST CENTER JDE DESCRIPTION FIELD
      *
     C     DFCJD         IFNE      *BLANK
     C                   MOVEL     DFCJD         INCJD
     C                   EXSR      $JDEC
     C                   MOVEL     OUCCDS        DFCCDS
     C                   ENDIF
     C                   ENDIF                                                  IF NOT FOUND
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      *  WRITE MESSAGES FROM ARRAYS TO MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP750K1
     C                   WRITE     HP750EC
     C                   EXFMT     HP750R1
      *
      * CLEAR MESSAGES
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S INPUT AND SET ROUTINE
      *
     C                   SELECT
      *
     C     *IN03         WHENEQ    *ON                                          F3=EXIT
     C                   MOVEL     '03'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN12         WHENEQ    *ON                                          F12-PREVIOUS
     C                   MOVEL     '12'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C                   MOVEL     SET1          ROUTNE
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      *
     C                   MOVEL     EXIT          ROUTNE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEC - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         COST CENTERS.
      *----------------------------------------------------------------
      *
     C     $JDEC         BEGSR
      *
     C                   MOVE      *BLANK        OUCCDS
     C                   MOVEL     NO            JDEFL
      *
     C                   CALL      'HPJDEC'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INCJD                          COST CENTER
     C                   PARM                    BOCOCD                         COMPANY
     C                   PARM                    OUCCDS                         C.C. DESCR
     C                   PARM                    JDEFL                          ERROR FLAG
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARD CODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *----------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *
      *  FEED DELIVERY ZONE
      *
     C     K1P013        KLIST
     C                   KFLD                    DFFMBO
     C                   KFLD                    DFDZCD
      *
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    DFFSBO
     C                   PARM                    DFFSCD
     C                   PARM                    DFFSNM
     C                   PARM                    RTNCD
      *
      * GLOBAL DEFINES
      *
     C                   MOVEL     *BLANK        PGM              10            USED IN MSG
     C                   MOVEL     *BLANK        MSGFL            10            USED IN MSG
     C                   MOVEL     *BLANK        ROUTNE            6
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    FSFAJD        INAJD                          JDE ADDR IN
     C     *LIKE         DEFINE    DFCCDS        OUCCDS                         COST CNTR DESC
     C     *LIKE         DEFINE    FSFAJD        OUAJD                          JDR ADDR OUT
     C     *LIKE         DEFINE    FSCJD         OUCJD                          COST CENTER SC
     C     *LIKE         DEFINE    FSCJD         INCJD                          COST CENTER SC
      *
      * INITIALIZE RETURN CODE
      *
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
      *
      * ALPHA PARMS FOR MESSAGES
      *
     C                   MOVE      *BLANK        XXFSCD            5
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
     C                   ENDSR
      /EJECT
