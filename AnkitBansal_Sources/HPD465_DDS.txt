     A*%%TS  SD  20080415  132233  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD465
     A*  TITLE:         SPECIFY OPTIONS FOR PURGE
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       02/14/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP465R1
     A*%%TS  SD  20080415  132233  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 23'SPECIFY OPTIONS FOR PURGE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Run in night jobq ...'
     A                                  4 24'?'
     A            D1NITEQ   R        B  4 26REFFLD(@@FLAG)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                  6  2'Leave Purge Date blank if you want-
     A                                       the system to calculate a date.'
     A                                  7  2'The calculated date will be Decemb-
     A                                      er 31 three years prior to the most'
     A                                  8  2'recent December 31. The date will -
     A                                      be calculated in the submission CLP-
     A                                      .'
     A                                 10 40'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 10 46'Period'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 11  2'Purge thru date .....'
     A            D1MDY     R        B 11 26REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1YR      R        O 11 40REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1PE      R        O 11 48REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP465K1
     A*%%TS  SD  20040928  155440  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP465ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP465EC                   SFLCTL(HP465ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
