     A*%%TS  SD  19941213  132950  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD160
     A*  TITLE:         MAINTAIN RATION ADDITIVE COSTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/07/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE BUSINESS OFFICE THE USER HAS
     A*               SELECTED FROM THE SPECIFY HAS BEEN DELETED
     A*               BY ANOTHER USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N60                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP160S1                   SFL
     A*%%TS  SD  19941213  130837  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDRBLB    R        H      REFFLD(RNRBLB)
     A            DFOPT          2A  B 10  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N90                            DSPATR(HI)
     A  60N30N90                            DSPATR(UL)
     A            DFRNCD    R        B 10  6REFFLD(RCRNCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A N31N60                               DSPATR(HI)
     A  61N31N90                            DSPATR(UL)
     A            DFADCD    R        B 10 13REFFLD(RCADCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A N32N60                               DSPATR(HI)
     A  61N32N90                            DSPATR(UL)
     A            DFIBRT    R        B 10 20REFFLD(RCIBRT)
     A                                      EDTCDE(K)
     A  62
     AO 90                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A N33N90                               DSPATR(UL)
     A            DFMBRT    R        B 10 36REFFLD(RCMBRT)
     A                                      EDTCDE(K)
     A  62
     AO 90                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(HI)
     A N34N90                               DSPATR(UL)
     A            DFDS      R        O 10 52REFFLD(RNRNDS)
     A            DFACT          8A  O 10 73
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP160C1                   SFLCTL(HP160S1)
     A*%%TS  SD  19941213  132950  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0012)
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 21'MAINTAIN RATION ADDITIVE COSTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ..:'
     A            DFFMBO    R        O  4 23REFFLD(RCFMBO)
     A            DFBODS    R        O  4 33REFFLD(BOBODS)
     A                                  5  2'Effective date ...:'
     A            DFMDY     R        O  5 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N61                              7  2'4=Delete'
     A N60                              8  8'?'
     A                                      DSPATR(HI)
     A N60                              8 15'?'
     A                                      DSPATR(HI)
     A                                  8 20'------- Cost per Batch -------'
     A                                      DSPATR(HI)
     A N61                              9  1'Opt'
     A                                      DSPATR(HI)
     A                                  9  5'Ration'
     A                                      DSPATR(HI)
     A                                  9 12'Additive'
     A                                      DSPATR(HI)
     A                                  9 24'Ingredient'
     A                                      DSPATR(HI)
     A                                  9 40'Medication'
     A                                      DSPATR(HI)
     A                                  9 52'Description'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP160K1
     A*%%TS  SD  19940321  103222  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N60N90                          23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F6=Create'
     A                                 23 45'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP160ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP160EC                   SFLCTL(HP160ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
