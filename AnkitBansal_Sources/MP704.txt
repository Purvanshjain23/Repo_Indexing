      *****************  RPG PROGRAM HEADING  ***********************
      *
      * ENVIRONMENT:  PORK DIVISION
      * SYSTEM:       AS/400
      * PROGRAM:      MP704
      * DESCRIPTION:  MP2: View Invoice
      * PROGRAMMER:   LeAnne Fedor
      * DATE:         04/30/02
      *
      *
      * FUNCTION: This function is used to view an invoice.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/24/02  LeAnne Fedor
      *           Changed all amount fields from 7,2 to 9,2.
      *
      * 03/30/11  Barb Gutierrez
      *           Qty and unit cost field sizes changed which caused us
      *           to change the screen size.  RPG was only recompiled
      *           because of DSPF changes.  E001398
      *
      * 03/06/14  LeAnne Ramsey  (E2992)
      *           Added Purchaing Center and Accounting Company to Header.
      *
      * 03/26/19  Brad Baden     (E14631)
      *           Change "move" to "movel" to field C1PCID
      *
      * 05/25/21  Danny Nguyen   (S17068)
      *           DBFC on MPP103 file. Unit Cost Per Item 'PDITEMRT' field length changed
      *           from 9.4 to 11.4.
      *           DBFC on MPP104 file. The following field lengths changed from 9.2 to 11.2:
      *             Invoiced Amount 'IHINAM'.
      *             Amount To Apply 'IHTAAM'.
      *             Applied Amount 'IHAPAM'.
      *             Sales Tax Amount 'IHSTAM'.
      *             Shipping/Handling Amount 'IHSHAM'.
      *           DBFC on MPP105 file. Applied Amount 'IDAPAM' field length changed from
      *           9.2 to 11.2.
      *           Recompile only.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fmpd704    cf   e             workstn
     F                                     sfile(mp704s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fmpp102    if   e           k disk
      *  MP2: Purchase order header
      *
      *
     Fmpp103    if   e           k disk
      *  MP2: Purchase order detail
      *
      *
     Fmpp104    if   e           k disk
      *  MP2: Invoice header
      *
      *
     Fmpp105    if   e           k disk
      *  MP2: Invoice detail
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D svrrn1          s                   like(hdrrn1)
      *
      *
      *
      * Parm fields
      *
     D xxposn          s                   like(ihposn)
     D xxinno          s                   like(c1inno)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Retrieve invoice header info
      *
     C     key01         chain     mpp104                             92
     C                   if        *in92 = *off                                 If found
     C                   move      ihinno        c1inno
     C                   z-add     ihinam        c1inam
     C                   z-add     ihtaam        c1taam
     C                   z-add     ihapam        c1apam
     C                   z-add     ihstam        c1stam
     C                   z-add     ihsham        c1sham
     C                   move      ihcom         c1com
      *
      *
      *
      * Flip date to mmddyy format
      *
     C     *iso          test(d)                 ihindt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihindt        wkisodate
     C     *mdy          move      wkisodate     c1mdy
     C                   endif                                                  If OK date
      *
      * Clear/fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
     C                   else
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
     C                   endif                                                  If found
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * This subfile has Fold/Unfold capabilities.  The DDS uses the
      * keywords (SFLDROP, SFLFOLD) to determine whether the user is
      * folded or unfolded and to redisplay the screen in the same folded state as
      * when he last saw it.  Indicators 88 and 89 are assigned to the DDS
      * keywords SFLDROP and SFLFOLD.
      *
     C                   setoff                                       8889
     C                   if        fold = '0'
     C                   seton                                        88
     C                   else
     C                   seton                                        89
     C                   endif
      *
      *
      * Write screen 1 to crt
      *
     C                   write     mp704k1
     C                   write     mp704ec
     C                   exfmt     mp704c1
      *
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      routne = exit
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      routne = set1
     C                   other
      *
      * User pressed ENTER
      *
     C                   eval      routne = set1
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   mp704s1
     C                   seton                                        83        sflclr
     C                   write     mp704c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Subroutine to refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Fill the subfile with all of the detail records for this invoice.
      *
     C     key01         setll     mpp105
      *
     C                   exsr      $load1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Subroutine to load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
      * Load the subfile with the invoice detail records. (Load all records)
      *
     C                   z-add     svrrn1        hdrrn1
     C                   setoff                                       82
      *
     C                   dou       *in82 = *on                                  Do fill loop
     C     key01         reade     mpp105                                 82
     C                   if        *in82 = *off                                 If not EOF
     C                   z-add     idinqt        d1inqt
     C                   move      idovfl        d1ovfl
     C                   z-add     idapam        d1apam
     C                   move      idcom         d1com
      *
      * Retrieve associated receive data from P.O. Detail file
      *
     C     key02         chain     mpp103                             92
     C                   if        *in92 = *off
     C                   movel(p)  pditemno      d1itemno
     C                   movel(p)  pditemds      d1itemds
     C                   z-add     pdtrqt        d1trqt
     C                   z-add     pditemrt      d1itemrt
     C                   move      pdmcu         d1mcu
     C                   move      pdobj         d1obj
     C                   move      pdsub         d1sub
     C                   else
     C                   move      *blank        d1itemno
     C                   move      *blank        d1itemds
     C                   z-add     0             d1trqt
     C                   z-add     0             d1itemrt
     C                   move      *blank        d1mcu
     C                   move      *blank        d1obj
     C                   move      *blank        d1sub
     C                   endif
      *
     C                   seton                                        85
     C                   add       1             hdrrn1
     C                   write     mp704s1
     C                   seton                                        81
     C                   z-add     hdrrn1        hdpag1
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do fill loop
      *
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Set on last record and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
     C     *entry        plist
     C                   parm                    xxposn
     C     c1inno        parm                    xxinno
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxposn
     C                   kfld                    xxinno
      *
     C     key02         klist
     C                   kfld                    idposn
     C                   kfld                    idrcno
     C                   kfld                    idserno
      *
      *
      * Retrieve the JDE vendor info and purchase order info associated with
      * the incoming id number.
      *
     C     xxposn        chain     mpp102                             92
     C                   if        *in92 = *off                                 If found
     C                   z-add     phjdvnno      c1jdvnno
     C                   move      phpono        c1pono
     C                   movel     phjdvnnm      c1jdvnnm
     C***  E14631  JBB   move      phpcid        c1pcid
     C                   movel     phpcid        c1pcid
     C                   z-add     phacono       c1acono
     C                   endif                                                  If found
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
