      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF604
      * TITLE:       Cash Distribution Balance Adjustment Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     11/06/06
      *
      * FUNCTION:  Batch program to print a record(s) in the Adjustments file.
      *            This program is accessed from Work With Cash Distribution Balance
      *            Adjustments.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/17/09  LeAnne Ramsey
      *           Added a selection: Extract on 1) WeekEnding Date or 2) Adjustment Date
      *           Added report totals.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl023b   if   e           k disk
      *  Cash distribution balance adjustments
      *    (records selected by open query...when user elected to extract by WeekEnding Date)
      *
      *
     Ftfl023c   if   e           k disk    rename(barec:barecc)
      *  Cash distribution balance adjustments
      *    (records selected by open query...when user elected to extract by Adjustment Date)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     d l1ajmdy         s              6  0
     d l1wemdy         s              6  0
     d l1crmdy         s              6  0
      *
     d t1scdbaam       s                   like(bascdbaam)
     d t1tcdbaam       s                   like(batcdbaam)
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldyr                   1      4  0
     D  ldwk                   5      6  0
      *
     D  ldfdt                  7     14  0
     D  ldfmdy                15     20  0
      *
     D  ldtdt                 21     28  0
     D  ldtmdy                29     34  0
      *
     D  ldrcexfl              35     35
     D  lddtfl                36     36
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read/print each record selected by the open query from the Cash Distribution
      * Balance Adjustment file.
      *
     C                   dou       *in90 = *on                                  Main do
      *
     C                   select
     C                   when      lddtfl = 'W'
     C                   read      tfl023b                                90
     C                   when      lddtfl = 'A'
     C                   read      tfl023c                                90
     C                   endsl
      *
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      *
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = yes
     C                   except    nodata
     C                   else
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t1tot
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Accumulate for Report Total lines
      *
     C                   add       bascdbaam     t1scdbaam
     C                   add       batcdbaam     t1tcdbaam
      *
      * Flip Adjustment date
      *
     C     *iso          test(d)                 baajdt                 92
     C                   if        *in92 = *off                                 If OK begin
     C                   move      baajdt        wkisodate
     C     *mdy          move      wkisodate     l1ajmdy
     C                   endif                                                  If OK begin
      *
      *
      * Flip Week-ending date
      *
     C     *iso          test(d)                 bawedt                 92
     C                   if        *in92 = *off                                 If OK end
     C                   move      bawedt        wkisodate
     C     *mdy          move      wkisodate     l1wemdy
     C                   endif                                                  If OK end
      *
      * Flip Create date
      *
     C     *iso          test(d)                 bacrdt                 92
     C                   if        *in92 = *off                                 If OK
     C                   move      bacrdt        wkisodate
     C     *mdy          move      wkisodate     l1crmdy
     C                   endif                                                  If OK        14
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   if        lddtfl = 'W'
     C                   seton                                        87
     C                   endif
      *
     C                   if        ldrcexfl = *blank
     C                   seton                                        97
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           91 'CASH DISTRIBUTION'
     O                                          111 ' BALANCE ADJUSTMENTS'
     O                                          119 ' LISTING'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O               87                          21 'From WeekEnding date:'
     O              n87                          21 'From Adjustment date:'
     O                       ldfmdy              31 '  /  /  '
      *
     O          e            h1hdr          1
     O               87                          21 'To WeekEnding date:'
     O              n87                          21 'To Adjustment date:'
     O                       ldtmdy              31 '  /  /  '
      *
      *
     O          e            h1hdr          2
     O                                           21 'Record exempt flag:'
     O              n97      ldrcexfl            24
     O               97                          26 'All'
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            8 'Week'
     O                                           28 'Record'
     O                                           44 'SBF'
     O                                           61 'TF'
     O                                           74 'System'
     O          e            h1hdr       0  1
     O                                            8 'Ending'
     O                                           20 'Adjustment'
     O                                           28 'Exempt'
     O                                           44 'Adjustment'
     O                                           61 'Adjustment'
     O                                           74 'Assigned'
     O                                          139 'Create'
     O                                          150 'Create'
     O                                          160 'Create'
     O          e            h1hdr       0  1
     O                                            8 'Date'
     O                                           18 'Date'
     O                                           27 'Flag'
     O                                           44 'Amount'
     O                                           61 'Amount'
     O                                           74 'Number'
     O                                           84 'Reason'
     O                                          138 'Date'
     O                                          149 'Time'
     O                                          159 'User'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       l1wemdy        b     8 '  /  /  '
     O                       l1ajmdy        b    19 '  /  /  '
     O                       barcexfl            25
     O                       bascdbaam     k     45
     O                       batcdbaam     k     62
     O                       baajsn        z     74
     O                       baajrsn            128
     O                       l1crmdy            140 '  /  /  '
     O                       bacrtm             151 '  :  :  '
     O                       bacrusr            164
      *
      *
      *-------------------------------------------------------------
      * Report total lines
      *-------------------------------------------------------------
      *
     O          e            t1tot          1
     O                                           44 '--------------'
     O                                           61 '--------------'
      *
     O          e            t1tot          1
     O                                           25 'Report Total:'
     O                       t1scdbaam     k     45
     O                       t1tcdbaam     k     62
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
