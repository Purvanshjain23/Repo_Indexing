      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH213 - Datamart Sales: Add Accrual Amounts to Accrual Expense
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/26/10
      *
      * FUNCTION: This program is only for Dailys data...since only Dailys has the Accrual
      *           Expense file.
      *
      *           It retrieves Marketing Accrual Amounts (and Weight Billed) from
      *           SHP204-Datamart Sales: Sales History and plops the values into
      *           SHP211-Datamart Sales: Accrual Expense.
      *
      *           We added 4 fields to SHP211 (they match the SHP204 fields):
      *             1) Total Market Accrual Amount
      *             2) Non-Ledgered Market Accrual Amount
      *             3) Ledgered Market Accrual Amount
      *             4) Weight Billed
      *
      *           We only did this enhancement because Steph A. was unable to make Cognos
      *           join SHP204/SHP211 in a manner that allowed her to get at the data the
      *           way she needs to.
      *
      ****************************************************************
      * Date      Programmer
      *
      * 05/24/10  LeAnne Ramsey
      *           Recompile only.
      *           Added a field to SHP204-Datamart Sales History:
      *           1) SHWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      *
      * 09/03/10  LeAnne Ramsey    (C976)
      *           We should be retrieving the Market/Salesperson info (like SH211)
      *           when we write a record; added this logic.
      *
      * 11/29/11  LeAnne Ramsey  (E1818)
      *           Recompile only.
      *           Added 5 fields to SHP204-Datamart Sales History.
      *
      * 11/30/11  LeAnne Ramsey  (E1818)
      *           Recompile only.
      *           Changed our 3 new SalesRoute fields to say "Order"...like
      *           our SalesPerson fields.
      *
      * 02/14/13  LeAnne Ramsey  (E2450)
      *           Recompile only.
      *           Adam Gross had Rose C. add 'sales channel' to the order entry screen. Now,
      *           the users will have to enter a valid Sales Channel (aka: Customer Type) on
      *           each Order!!  They were not happy with having it at the Customer level!!
      *           Rose will hold/store this new value in PMD0CPP-Sales History Extension.
      *           I have added 3 fields to our Datamart Sales History file:
      *             SHORCHCD-Order Sales Channel Code
      *             SHORCHDS-Order Sales Channel Description
      *             SHORCHCDDS-Order Sales Channel Code/Description
      *
      * 07/16/13  LeAnne Ramsey  (E2682)
      *           Recompile only.
      *           Adam Gross had us add Gate/Final Price and Gate/Final Price Adjustment to
      *           Sales History.  These will come from the Sales History Extension file (PMD0CPP).
      *
      * 07/08/14  LeAnne Ramsey  (E3174)
      *           Recompile only.
      *           Added a new field to SHP204-Sales History:
      *               Spot/Buy Flag    (SHSPBYFL)
      *
      * 08/14/14  LeAnne Ramsey  (E3320)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               Last Quantity Change Date  (SHLQCDT)
      *               Last Quantity Change Date  (SH80LQCDT)
      *               Last Quantity Change Time  (SHLQCTM)
      *
      * 09/23/14  Rose Centonze  (E3329)
      *           Recompile only.
      *           Added 7 new fields to SHP204-Sales History:
      *               DP Location      (SHDPLOC)
      *               DP Region        (SHDPREGION)
      *               DP Channel Type  (SHDPCHTYP)
      *               Req Shp Date     (SHRQSHDT)
      *               Req Shp w/end    (SHRQWEDT)
      *               Req Dlv Date     (SHRQDLDT)
      *               Req Dlv w/end    (SHDLWEDT)
      *
      * 10/07/14  LeAnne Ramsey  (E3499)
      *           Recompile only.
      *           Added 1 new field to SHP204-Sales History:
      *               Stop             (SHSTOP)
      *
      * 11/24/14  LeAnne Ramsey  (E3640)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               DP Salesperson Code        (SHDPSPCD)
      *               DP Salesperson Name        (SHDPSPNM)
      *               DP Salesperson Code/Name   (SHDPSPCDNM)
      *
      * 07/23/15  Lara Buser  (E004044)
      *           Recompile Only. Field added to SHP203-Datamart: Customer
      *               Field vs. In-House Managed Flag (CUMANAGED)
      *
      * 10/28/16  Danny Nguyen   (E7720)
      *           Recompile only.
      *           Added 2 new fields to SHP204-Sales History:
      *               Requested Delivery Time      (SHRQDLTM)
      *               Requested Delivery Timestamp (SHRQDLSTMP)
      *
      * 05/23/18  Danny Nguyen  (R12926 - Absorbed Freight Rate Override)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               ABSORBED FREIGHT RATE        (SHABFRRT)
      *               ORIG ORD ABS FREIGHT RATE    (SHORABFRRT)
      *               INVOICED MODE OF TRANSPTN    (SHINMOTCD)
      *
      *  7/08/21  Brad Baden WHD80179 - Add Parent Customer Name field.
      *           Recompile only. Added Parent Customer Name field in
      *           file SHP203.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fshp203    if   e           k disk
      *    Datamart: Customer
      *
      *
     Fshl204a   if   e           k disk
      *    Datamart: Sales history
      *
      *
     Fshp211    uf a e           k disk
      *  Datamart: Accrual expense
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svcono          s                   like(shcono)
     D svshcuno        s                   like(shshcuno)
      *
      *
      * Workfields
      *
     D wkkram          s                   like(aekram)
     D wkkrsbam        s                   like(aekrsbam)
     D wkkrotam        s                   like(aekrotam)
     D wkblwt          s                   like(aeblwt)
      *
      *
      * Parms
      *
     D xxcorg          s                   like(shcorg)
      *
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read/process the Sales History file using a key of:
      *  1) Year
      *  2) Company organization
      *
     C     key01         setll     shl204a
      *
     C                   dou       *in91 = *on                                  Do history
     C     key01         reade     shl204a                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     shcono        svcono
     C                   z-add     shshcuno      svshcuno
      *
     C                   when      shcono <> svcono or
     C                             shshcuno <> svshcuno
     C                   exsr      $rtv211
     C                   z-add     shcono        svcono
     C                   z-add     shshcuno      svshcuno
     C                   endsl
      * Detail
     C                   add       shkram        wkkram
     C                   add       shkrsbam      wkkrsbam
     C                   add       shkrotam      wkkrotam
     C                   add       shblwt        wkblwt
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do history
      *
      * Last record
     C                   if        first = no
     C                   exsr      $rtv211
     C                   endif
      *
     C                   seton                                        lr
      *
      /eject
      *---------------------------------------------------------------
      * Retrieve/Update or Write a Datamart Accrual Expense record
      *---------------------------------------------------------------
      *
     C     $rtv211       begsr
      *
      * Retrieve/Update Accrual Expense record for this:
      *  1) Company organization
      *  2) Accounting company
      *  3) Accounting year
      *  4) Ship to customer number
      *
     C     key02         chain     shp211                             92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     wkkram        aekram
     C                   z-add     wkkrsbam      aekrsbam
     C                   z-add     wkkrotam      aekrotam
     C                   z-add     wkblwt        aeblwt
     C                   update    aerec
     C                   else
      *
      * Write a record
     C                   clear                   aerec
     C                   move      xxcorg        aecorg
     C                   z-add     svcono        aecono
     C                   z-add     svshcuno      aeshcuno
     C                   z-add     *year         aeacyr
      *
     C                   z-add     wkkram        aekram
     C                   z-add     wkkrsbam      aekrsbam
     C                   z-add     wkkrotam      aekrotam
     C                   z-add     wkblwt        aeblwt
      *
      * Retrieve some "customer" fields from the Datamart Customer file.
      *
     C     key03         chain     shp203                             92
     C                   if        *in92 = *off
     C                   move      cucmmkcd      aecmmkcd
     C                   move      cusrcd        aesrcd
     C                   move      cumkcd        aemkcd
     C                   move      cumkbkcd      aemkbkcd
     C                   move      cumkspcd      aemkspcd
     C                   endif
      *
     C                   write     aerec
     C                   endif                                                  If hit
      *
      * Clear accumulators
     C                   z-add     0             wkkram
     C                   z-add     0             wkkrsbam
     C                   z-add     0             wkkrotam
     C                   z-add     0             wkblwt
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      *  Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxcorg
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    *year
     C                   kfld                    xxcorg
      *
     C     key02         klist
     C                   kfld                    xxcorg
     C                   kfld                    svcono
     C                   kfld                    *year
     C                   kfld                    svshcuno
      *
     C     key03         klist
     C                   kfld                    aeshcuno
     C                   kfld                    aecorg
      *
     C                   endsr
      /EJECT
