      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP610
      * TITLE:       Expense Class/Subclass/Code Listing: By Code
      * PROGRAMMER:  Mary Palmer
      * CREATED:     04/12/94
      *
      * FUNCTION: Batch program to list Expense Classes/Subclasses/Expense Codes.  The
      *           listing is sequenced by the 'codes'.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/24/03  LeAnne Fedor
      *           We now have various 'versions' of the listings. So, this
      *           prgram was rewritten to use the new workfile.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp3410   if   e           k disk
      *   Workfile
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *--------------------------------------------------------------
      * CONSTANTS
      *--------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *--------------------------------------------------------------
      * STANDALONE FIELDS
      *--------------------------------------------------------------
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D sveccd          s                   like(w1eccd)
     D svescd          s                   like(w1escd)
      *
      *--------------------------------------------------------------
      * DATA STRUCTURES
      *--------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Position in workfile and print headings.
      *
     C     *loval        setll     hsp3410
     C                   exsr      $h1hdr
      *
      * Read/print each record in the workfile.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3410                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Set indicators to control which fields print on the detail line.
      *
     C                   setoff                                       9798
      *
     C                   select
     C                   when      sveccd <> w1eccd
     C                   seton                                        9798
     C                   move      w1eccd        sveccd
     C                   move      w1escd        svescd
      *
     C                   when      svescd <> w1escd
     C                   seton                                        98
     C                   move      w1escd        svescd
     C                   endsl
      *
      *
      * Print detail line
      *
     C                   exsr      $d1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   seton                                        9798
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $d1dtl        begsr
      *
     C                   select
     C                   when      *inof = *on
     C                   exsr      $h1hdr
      *
     C                   when      *in97 = *on
     C                   except    blank
     C                   except    blank
      *
     C                   when      *in98 = *on
     C                   except    blank
     C                   endsl
      *
     C                   except    d1dtl
     C                   move      yes           prtfl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           88 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         Y    198
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           80 'LISTING OF EXPENSE CL'
     O                                           98 'ASS/SUBCLASS/CODES'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                           82 'BY CODE'
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           26 '------ EXPENSE CLASS -----'
     O                                         +  0 '-'
      *
     O                                           55 '---- EXPENSE SUBCLASS ----'
     O                                         +  0 '-'
      *
     O                                           81 '------ EXPENSE CODE '
     O                                         +  0 '-------'
      *
     O                                          111 '--------- REPORT ---'
     O                                         +  0 '-------'
      *
      *
     O          e            h1hdr          1
      * Class
     O                                            4 'CODE'
     O                                           18 'DESCRIPTION'
      * Subclass
     O                                           33 'CODE'
     O                                           47 'DESCRIPTION'
      * Expense code
     O                                           65 'CODE'
     O                                           79 'DESCRIPTION'
      * Report code
     O                                           95 'CODE'
     O                                          109 'DESCRIPTION'
      *
     O                                          141 '-------------------'
     O                                         +  0 ' G/L ACCOUNT'
     O                                         +  0 '-------------------'
      *
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            d1dtl       1
      *
      * Expense class
     O               97      w1eccd               5
     O               97      w1ecds              27
      *
      * Expense subclass
      *
     O               98      w1escd              34
     O               98      w1esds              56
      *
      * Expense code
     O                       w1excd              66
     O                       w1exds              88
     O                       w1rpcd              96
     O                       w1rlds             118
     O                       w1glds             172
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
