      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP251 - INTERACTIVE EDIT OF MOVEMENT IN
      *                      HOG PROCUREMENT AND EVALUATION SYSTEM
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     09/11/95
      *
      * FUNCTION: THIS PROGRAM INTERFACES THE HOG PROCUREMENT AND
      *           EVALUATION SYSTEM TO THE HOG PRODUCTION SYSTEM.
      *
      *           ITS PURPOSE IS TO EDIT THE MOVEMENT NUMBER ENTERED
      *           IN THE HOG PROCUREMENT AND EVALUATION SYSTEM. THUS,
      *           THIS PROGRAM IS CALLED BY A SYNON PROGRAM IN HPES.
      *
      *           A RETURN CODE IS USED TO INDICATE TO HPES THAT THERE
      *           IS AN ERROR ON THE MOVEMENT.
      *
      *           THE SYNON PROGRAM CAN ONLY HANDLE ONE ERROR MESSAGE
      *           ON EACH CALL. SO, THIS PROGRAM LOGIC ONLY ISSUES A
      *           MESSAGE FOR THE FIRST ERROR ENCOUNTERED.  CURRENTLY,
      *           THE SAME MESSAGE IS SENT FOR EACH ERROR.   IN
      *           THE FUTURE, UNIQUE ERROR MESSAGES MAY BE REQUIRED
      *           FOR EACH ERROR CONDITION.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/06/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO SALES MOVEMENT HEADER FILE.
      *
      * 01/11/98  LEANNE FEDOR
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 01/13/98  LEANNE FEDOR
      *           Added scheduled kill date as a parm.
      *
      * 11/08/99  LEANNE FEDOR
      *           As requested by Purva Droge, we will now return the total
      *           scheduled head for the movement to the calling program.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      *  1/19/18  Brad Baden  E010812   Market Hogs to OTC
      *           When validating the customer number, check if it exists
      *           in the Ship To SBD Customer file HSL195A.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP084    IF   E           K DISK
      *  SALES MOVEMENT HEADER
      *
      *
     FHSP085    IF   E           K DISK
      *  SALES MOVEMENT DETAIL
      *
      *
     Fhsl195a   if   e           k disk
      *  Ship To SBD Customer
      *
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PERFORM THE FOLLOWING EDITS FOR THE MOVEMENT:
      *    MUST BE A SALES MOVEMENT
      *    MUST BE A SALES TYPE OF MARKET
      *    THE CUSTOMER MUST BE 'GUYMON PROCESSING PLANT'
      *
     C     WKMVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL     'HPSM1  '     RETURN
     C                   ELSE
      *
      * Move scheduled kill date to output parm.
      *
     C                   z-add     shsckldt      xxsckldt
      *
      * THIS SALES MOVEMENT MUST BE FOR MARKET HOGS
      *
     C                   SELECT
     C     SHSTCD        WHENNE    'MRKTS'
     C                   MOVEL     'HPSM1  '     RETURN
      *
      * CUSTOMER MUST BE 'GUYMON PROCESSING PLANT'. (WE
      * HARD-CODE THE CUSTOMER NUMBER FOR GUYMON TO DO THIS
      * CHECK.)
      * E010812 JBB 1/19/2017  Market Hogs to OTC
      * Changed logic to check and see if the Customer exists
      * in the Ship To SBD Customer file when the Customer is
      * not 360516.
      *
     C     SHCVNO        WHENNE    360516
     C     shcvno        chain     hsl195a
     C                   if        not %found
     C                   MOVEL     'HPSM1  '     RETURN
     C                   endif
      *
     C                   ENDSL
      *
     C                   ENDIF                                                  IF NOT FOUND
      *
      * If return field is blank (ie: there are no errors), read the
      * sales movement detail records accumulating scheduled head to
      * return to the calling program.
      *
     C                   if        return = *blank                              If no errors
     C     wkmvsn        setll     hsp085
     C                   dou       *in90 = *on                                  Do detail
     C     wkmvsn        reade     hsp085                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   add       sgschd        xxschd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
     C                   endif                                                  If no errors
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    RETURN
     C                   PARM                    WKMVSN
     C                   parm                    xxsckldt
     C                   parm                    xxschd
      *
     C                   MOVE      *BLANK        RETURN            7
     C     *LIKE         DEFINE    SHMVSN        WKMVSN
     C     *LIKE         DEFINE    shsckldt      xxsckldt
     C     *LIKE         DEFINE    sgschd        xxschd
     C                   z-add     0             xxsckldt
     C                   z-add     0             xxschd
      *
     C                   ENDSR
      /EJECT
