     A*%%TS  SD  19940331  100906  ISRCENT     REL-V2R2M0  5738-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD514 - SELECT JDE COST CENTER
     A*  RPG PROGRAM:   HP514  - SELECT JDE COST CENTER
     A*  CREATED BY:    ROSE CENTONZE
     A*  DATE:          03/30/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CHGINPDFT(UL FE)
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP514R1
     A*%%TS  SD  19940330  163245  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 45)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP514R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP514R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP514S1                   SFL
     A*%%TS  SD  19940330  163245  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFMCU     R        O  6  1REFFLD(I0006/MCMCU *LIBL/F0006)
     A            DFDL01    R        O  6 15REFFLD(I0006/MCDL01 *LIBL/F0006)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP514C1                   SFLCTL(HP514S1)
     A*%%TS  SD  19940331  100906  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP514R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  5'SELECT JDE COST CENTER'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Cost Center'
     A                                      DSPATR(HI)
     A                                  4 15'Description'
     A                                      DSPATR(HI)
     A            C1MCU     R        B  5  1REFFLD(I0006/MCMCU *LIBL/F0006)
     A                                      TEXT('cost center positioner')
     A                                      DSPATR(HI)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A          R HP514K1
     A*%%TS  SD  19940330  163245  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP514R1)
     A                                 16  1'F12=Previous'
