     H/TITLE EUP Bld Cust Accrual      Execute user program
     H DATFMT(*YMD) DATEDIT(*YMD) DEBUG(*YES)
     Y* ADDLIBLE LIB(COMMON) POSITION(*LAST)
     Y* ADDLIBLE LIB(DFDATA) POSITION(*LAST)
     Y* ADDLIBLE LIB(DLY029) POSITION(*LAST)
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *
     FJRCUSTS   if   e           k disk
      *  JRD Customer Masterfile AS/400 Format
      *
     FACCRUECU  if   e           k disk    prefix(cu_)
      *  JRD Customer Accrual Masterfile
      *
     FACCRUEPR  if   e           k disk    prefix(pr_)
      *  JRD Customer Accrual Product Masterfile
      *
     FCSTMSTL1  if   e           k disk
      *  Active Customer Conversion - By DLYCST
      *
     FITMCPKL1  if   e           k disk    RENAME(ITMCPK:ITMCPK1)
      *  Daily's CoPack Item/Seaboard Item Cross Ref
      *
     FOMBYREL1  if   e           k disk
      *  Item Default              Retrieval index
      *
     FOMHOREL1  uf a e           k disk
      *  Customer Accrual          Update index
      *
     FOMHPREL0  uf a e           k disk
      *  Customer Accrual Item     Update index
      *
      * Data structures:
     D PGMDS         ESDS                  EXTNAME(Y2PGDSP)
      * Modified Program data structure
     D JBDTTM          DS
      * Job date/time
     D  ##JDT                  1      7  0
     D  ##JCC                  1      1  0
     D  ##JYY                  2      3  0
     D  ##JMM                  4      5  0
     D  ##JDD                  6      7  0
     D  ##JTM                  8     13  0
     D  ##JHH                  8      9  0
     D  ##JNN                 10     11  0
     D  ##JSS                 12     13  0
      *
      * Standalone fields
     Dcontinue         s              1A                                        Continue processing
     DCU_Found         s              1A                                        ACCRUECU rcd found?
     DPR_Found         s              1A                                        ACCRUEPR rcd found?
      *
      /EJECT
      *****************************************************************
      * Entry parameters
     C     *ENTRY        PLIST
     C                   PARM                    p1ojcd            3            Accrual Code
     C                   PARM                    library          10            Library Name
      *****************************************************************
      * Initialize
     C                   EXSR      ZZINIT
      *
      * Process JRCUSTS - JRD Customer Masterfile AS/400 Format
     C                   read      jrcust                                 95
     C                   dow       *in95 = *off                                 do loop
      * Determine if JRCUSTS record should be processed
     C                   exsr      $process
      *
      * Continue processing record?
     C                   if        continue = 'Y'                               continue = Y
      *
      * Create Customer Accrual
     C                   exsr      $cusacc
      *
     C                   endif                                                  continue = Y
      * Next record
     C                   read      jrcust                                 95
     C                   enddo                                                  do loop
      *
      *----------------------------------------------------------------
      * Terminate program
     C                   SETON                                        LR
      *
      * Exit program
     C                   RETURN
      *
      *================================================================
      /EJECT
      *---------------------------------------------------------------
      * Determine if JRCUSTS record should be processed
      *---------------------------------------------------------------
      *
     C     $process      begsr
      *
      * Get Ship to Customer
     C                   exsr      $cstmst
      *
      * Continue processing if Customer found in CSTMST
     C                   if        continue = 'Y'                               CSTMST
      *
      * Check if customer already processed
     C                   exsr      $chkcst
      *
      * Continue if customer not already processed
     C                   if        continue = 'Y'                               CSTACR
      *
      * Check for records in JRD Customer Accrual files
     C                   exsr      $chkjrd
      *
      * Continue if item in not already processed in DFDATA
     C*                  if        continue = 'Y'                               JRD
      *
      * Check for record in Daily's CoPack Item/Seaboard Item XREF
     C*                  exsr      $itmcpk
      *
      * Check Item Default file
     C*                  exsr      $itmdft
      *
     C*                  endif                                                  JRD
      *
     C                   endif                                                  CSTACR
      *
     C                   endif                                                  CSTMST
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Get Ship to Customer
      *---------------------------------------------------------------
      *
     C     $cstmst       begsr
      *
      * Set default values for flags
     C                   eval      continue = 'N'
      *
     C     cust          chain     cstmst                             91
      *
      * If record found
     C                   if        *in91 = *off                                 91 = off
     C                   eval      hobkc7 = shipto                              Ship to Customer
     C                   eval      continue = 'Y'
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Check if customer already processed
      *---------------------------------------------------------------
      *
     C     $chkcst       begsr
      *
      * Was customer processed with DFDATA?
     C                   if        library = 'DFDATA'                           library = 'DFDATA'
      * First pass, no need to check
     C                   eval      continue = 'Y'
      *
     C                   elseif    library = 'JRDATA'
      *
      * Check Customer Accrual to see if customer already processed
     C                   eval      hobkc7 = shipto
     C                   eval      hoojcd = p1ojcd
     C     key02         chain     @horelb                            91
      *
      * If record found, do not continue processing. This customer was already
      * processed from DFDATA
     C                   if        *in91 = *off                                 91 = off
     C                   eval      continue = 'N'
      *
     C                   else
     C                   eval      continue = 'Y'
      *
     C                   endif                                                  91 = off
      *
     C                   endif                                                  library = 'DFDATA'
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Check for record in Daily's CoPack Item/Seaboard Item XREF
      *---------------------------------------------------------------
      *
     C     $itmcpk       begsr
      *
     C                   eval      dlyprd = pr_item
     C     dlyprd        chain     ITMCPK1                            91
      *
      * If record found, use sbdprd, else use item
     C                   if        *in91 = *off                                 91 = off
     C                   eval      hphgcd = sbdprd
     C                   else
     C                   eval      hphgcd = pr_item
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Check Item Default file
      *---------------------------------------------------------------
      *
     C     $itmdft       begsr
      *
      * Initialize flag
     C                   eval      continue = 'N'
      *
     C                   eval      byhgcd = hphgcd
     C     byhgcd        chain     @BYREA1                            91
      *
      * If record found, set to continue with record create
     C                   if        *in91 = *off                                 91 = off
     C                   eval      continue = 'Y'
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Check for records in JRD Customer Accrual files
      *---------------------------------------------------------------
      *
     C     $chkjrd       begsr
      *
      * Set default values for flags
     C                   eval      CU_Found = 'N'
     C                   eval      PR_Found = 'N'
     C                   eval      continue = 'N'
      * Check JRD Customer Accrual Masterfile
     C                   eval      cu_cust= cust
     C     cu_cust       chain     accruec                            91
      *
      * Record found?
     C                   if        *in91 = *off                                 91 = off
     C                   eval      CU_Found = 'Y'
      *
     C                   endif                                                  91 = off
      *
      * Check JRD Customer Accrual Product
     C                   eval      pr_cust= cust
     C     pr_cust       chain     accruep                            91
      *
      * Record found?
     C                   if        *in91 = *off                                 91 = off
     C                   eval      PR_Found = 'Y'
      *
     C                   endif                                                  91 = off
      *
      * Determine whether to continue processing
     C                   select
     C                   when      PR_Found = 'Y' and CU_Found = 'Y'
     C                   eval      continue = 'Y'
      *
     C                   when      PR_Found = 'Y' and CU_Found = 'N'
     C                   eval      continue = 'Y'
      *
     C                   when      PR_Found = 'N' and CU_Found = 'Y'
     C                             and cu_accc > 0
     C                   eval      continue = 'Y'
      *
     C                   other
     C                   eval      continue = 'N'
      *
     C                   endsl
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Create Customer Accrual
      *---------------------------------------------------------------
      *
     C     $cusacc       begsr
      *
      * Populate fields
     C                   eval      hobkc7 = shipto                              Ship to Customer
     C                   eval      hoojcd = p1ojcd                              Accural Code
     C                   eval      hoguny = 1                                   CAH Sequence No
     C                   eval      hobfdt = ##JDT                               Period Beginning Dte
     C                   eval      hoa2dt = 1101231                             Period Ending Date
      *
      * Populate Accrual Rate
     C                   if        cu_accc > 0
     C                   eval      hobkpr = cu_accc                             Accrual Rate
     C                   else
     C                   eval      hobkpr = *zeros                              Accrual Rate
     C                   endif
      *
      * Populate All Products (Y/N)
     C                   select
     C                   when      cu_found = 'Y' and cu_accc > 0 and
     C                             pr_found = 'Y'
     C                   eval      hottst = 'X'                                 All Products (Y/N)
      *
     C                   when      cu_found = 'Y' and cu_accc = 0 and
     C                             pr_found ='Y'                                All Products (Y/N)
     C                   eval      hottst = 'N'
      *
     C                   when      cu_found = 'Y' and pr_found ='N'
     C                   eval      hottst = 'Y'                                 All Products (Y/N)
      *
     C                   when      cu_found = 'N' and pr_found ='Y'
     C                   eval      hottst = 'N'                                 All Products (Y/N)
     C                   eval      hobkpr = *zeros                              Accrual Rate
      *
     C                   endsl
      *
     C                   eval      hoshsx = 'W'                                 Accrual Rate Type
     C                   eval      hoqdsx = *blanks                             Comm Threshold Prd
     C                   eval      hopjdt = *zeros                              CAH Unused Date 1
     C                   eval      hopkdt = *zeros                              CAH Unused Date 2
     C                   eval      hoecvl = *zeros                              Comm Min Amt /Period
     C                   eval      hoqesx = *blanks                             Comm Retroactive sts
     C                   eval      hoqfsx = 'I'                                 Accrual Dtl Search
     C                   eval      hohhaa = *blanks                             Broker Code
     C                   eval      hohqvl = *zeros                              Max Accrual Amount
     C                   eval      hoqgsx = '1'                                 Commission/Not Comm
     C                   eval      hoqhsx = *blanks                             CAH Unused sts 3
     C                   eval      hoqisx = *blanks                             CAH Unused sts 4
     C                   eval      hoqjsx = *blanks                             CAH Unused sts 5
     C                   eval      houvst = *blanks                             EDI status 1
     C                   eval      houwst = *blanks                             EDI status 2
     C                   eval      houxst = *blanks                             EDI status 3
     C                   eval      hoaatm = ##JTM                               Job Time
     C                   eval      hoayna = ##USR                               User Id
     C                   eval      hoa0na = ##JOB                               Job Name
     C                   eval      hoaxdt = ##JDT                               Job Date
      *
      * Check for duplicate record
     C     key01         setll     @HORELB                                90
      * If record not found, add
     C                   if        *in90 = *off
     C                   write     @HORELB
      *
     C                   endif
      *
      * If All Products (Y/N) = 'N' or 'X', create Customer Accrual Item records
     C                   if        hottst = 'N' or hottst = 'X'
      * Create Customer Accrual Items
     C                   exsr      $cusaccitm
      *
     C                   endif
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Create Customer Accrual Items
      *---------------------------------------------------------------
      *
     C     $cusaccitm    begsr
      *
      * Process ACCRUEPR - JRD Customer Accrual Product
     C     cust          setll     accruep
     C     cust          reade     accruep                                95
     C                   dow       *in95 = *off                                 do loop
      *
      * Populate fields
     C                   eval      hpbkc7 = hobkc7                              Ship to Customer
     C                   eval      hpojcd = hoojcd                              Accural Code
     C                   eval      hpguny = hoguny                              CAH Sequence No
      * NEW CODE PKD
     C                   exsr      $itmcpk                                      CAH Sequence No
     C                   exsr      $itmdft
     C                   if        continue = 'Y'
      * pr_item filled in $itmcpk                                               Item Code
     C                   eval      hpbkpr = pr_accc                             Accrual Rate
     C                   eval      hpshsx = 'W'                                 Accrual Rate Type
     C                   eval      hpuvst = *blanks                             EDI Status 1
     C                   eval      hpuwst = *blanks                             EDI Status 2
     C                   eval      hpuxst = *blanks                             EDI Status 3
     C                   eval      hpaatm = ##JTM                               Job Time
     C                   eval      hpayna = ##USR                               User Id
     C                   eval      hpa0na = ##JOB                               Job Name
     C                   eval      hpaxdt = ##JDT                               Job Date
      *
      * Check for duplicate record
     C     key03         setll     @HPREK9                                90
      * If record not found, add
     C                   if        *in90 = *off
     C                   write     @HPREK9
      *
     C                   endif
     C                   endif
      *
      * Next record
     C     cust          reade     accruep                                95
     C                   enddo                                                  do loop
      *
     C                   endsr
      /EJECT
     CSR   ZZINIT        BEGSR
      *================================================================
      * Initialisation
      *================================================================
      * Retrieve job attributes
     C                   CALL      'Y2RTJCR'
     C                   PARM                    PGMDS
      * Setup job date/time
     C                   Z-ADD     ##SD7         ##JDT
     C                   TIME                    ##JTM
      *
      * Klist for Customer Accrual          Retrieval index
      * Check for duplicate record before create
     C     KEY01         klist
     C                   kfld                    hobkc7
     C                   kfld                    hoojcd
     C                   kfld                    hoguny
      *
      * Klist for Customer Accrual          Retrieval index
      * Check if customer/accural code already processed
     C     KEY02         klist
     C                   kfld                    hobkc7
     C                   kfld                    hoojcd
      *
      * Klist for Customer Accrual Item     Retrieval index
     C     KEY03         klist
     C                   kfld                    hpbkc7
     C                   kfld                    hpojcd
     C                   kfld                    hpguny
     C                   kfld                    hphgcd
      *
      *================================================================
     CSR   ZZEXIT        ENDSR
