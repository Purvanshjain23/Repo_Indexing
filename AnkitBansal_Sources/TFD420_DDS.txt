     A*%%TS  SD  20050412  072702  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD420
     A*  TITLE:         SPECIFY EOP FEES CLOSE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/12/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A*                                     CHGINPDFT(UL FE)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF420R1
     A*%%TS  SD  20050412  072702  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'SPECIFY EOP FEES CLOSE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Year/period:'
     A                                      DSPATR(HI)
     A            D1YR      R        O  4 15REFFLD(@@YR)
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      COLOR(PNK)
     A            D1PE      R        O  4 21REFFLD(@@PE)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      COLOR(PNK)
     A            D1PBMDY   R     Y  O  4 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  4 37'to'
     A            D1PEMDY   R     Y  O  4 40REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  6  2'Preliminary/final ..'
     A                                  6 23'?'
     A            D1PFCD    R        B  6 25REFFLD(@@PFCD)
     A                                      DSPATR(UL)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1PFDS    R        O  6 28REFFLD(@@PFDS)
     A                                  8  2'Freight Balance ......'
     A  42                                  DSPATR(ND)
     A            D1FRTBAL  R        B  8 25REFFLD(@@FRTBLAM)
     A  42                                  DSPATR(ND)
     A                                      EDTCDE(J)
     A N32                                  DSPATR(HI)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF420K1
     A*%%TS  SD  20010220  091710  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF420ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF420EC                   SFLCTL(TF420ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
