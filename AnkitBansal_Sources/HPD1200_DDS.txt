     A*%%TS  SD  20090521  160809  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1200
     A*  TITLE:         MAINTAIN FEED ADJUSTMENT TICKETS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       05/21/09
     A*
     A*  INDICATORS:
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  62  THE TICKET THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*  65  PROTECT ALL FIELDS. THIS TICKET HAS EITHER BEEN POSTED OR
     A*      IS ASSOCIATED WITH ONE OR MORE CLOSED GROUPS.
     A*****************************************************************
     A* SDN736 RECOMPILED FOR TICKET NBR INCREASE (TKNO & RTNO 7.0 TO 9.0)
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N65N62                               CF04(04 'PROMPT')
     A    N62N65                            CF05(05 'REFRESH')
     A N62N65                               CF09(09 'MAINTAIN')
     A  60N62N65                            CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1200S1                  SFL
     A*%%TS  SD  20090521  152132  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            HDHGSN    R        H      REFFLD(HGHGSN)
     A            HDBLCD    R        H      REFFLD(HGBLCD)
     A            HDRMCD    R        H      REFFLD(HGRMCD)
     A            HDSHARED  R        H      REFFLD(@@FLAG)
      *
     A            D1BNCD    R        B 15  2REFFLD(FDREC/FDBNCD *LIBL/HSP038)
     A  65
     AO 62                                  DSPATR(PR)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N65N62N31                            DSPATR(HI)
     A N65N62N31                            DSPATR(UL)
     A            D1HGCD    R        B 15  9REFFLD(HGREC/HGHGCD *LIBL/HSP034)
     A  65
     AO 62                                  DSPATR(PR)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N65N62N32                            DSPATR(HI)
     A N65N62N32                            DSPATR(UL)
     A            D1RNCD    R        B 15 18REFFLD(FDREC/FDRNCD *LIBL/HSP038)
     A  65
     AO 62                                  DSPATR(PR)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N65N62N33                            DSPATR(HI)
     A N65N62N33                            DSPATR(UL)
     A            D1ADCD    R        B 15 24REFFLD(FDREC/FDADCD *LIBL/HSP038)
     A  65
     AO 62                                  DSPATR(PR)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A N65N62N47                            DSPATR(HI)
     A N65N62N47                            DSPATR(UL)
     A            D1FTCD    R        B 15 30REFFLD(FDFTCD)
     A  65
     AO 62                                  DSPATR(PR)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A N65N62N48                            DSPATR(HI)
     A N65N62N48                            DSPATR(UL)
     A            D1FDLB    R        B 15 32REFFLD(FDREC/FDFDLB *LIBL/HSP038)
     A                                      CHECK(RB)
     A                                      EDTCDE(K)
     A  65
     AO 62                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N62N34N65                            DSPATR(HI)
     A N62N34N65                            DSPATR(UL)
     A            D1INAM    R        B 15 43REFFLD(FDREC/FDINAM *LIBL/HSP038)
     A                                      EDTCDE(K)
     A  65
     AO 62                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35N65N62                            DSPATR(HI)
     A N35N65N62                            DSPATR(UL)
     A            D1MDAM    R        B 15 57REFFLD(FDREC/FDMDAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A  65
     AO 62                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40N62N65                            DSPATR(HI)
     A N40N62N65                            DSPATR(UL)
     A            D1MFAM    R        B 15 69REFFLD(FDREC/FDMFAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A  65
     AO 62                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41N62N65                            DSPATR(HI)
     A N41N62N65                            DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1200C1                  SFLCTL(HP1200S1)
     A*%%TS  SD  20090521  152132  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0007)
     A N62                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(30)
     A                                      SETOF(33)
     A                                      SETOF(50)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 21'MAINTAIN FEED ADJUSTMENT TICKET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..................'
     A N60                              4 31'?'
     A            C1FSCD    R        B  4 33REFFLD(FSREC/FSFSCD *LIBL/HSP018)
     A                                      EDTCDE(M)
     A N60N62N36                            DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N60N62                               DSPATR(UL)
     A  60
     AO 65                                  DSPATR(PR)
      *
     A            C1FSNM    R        O  4 44REFFLD(FSFSNM)
     A                                  5  2'Feed mill business office ..'
     A N60N65                           5 22'?'
     A N60                              5 31'?'
     A            C1FMBO    R        B  5 33REFFLD(FDFMBO)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N60
     A    N65N62                            DSPATR(HI)
     A N30N60
     A    N65N62                            DSPATR(UL)
     A  60
     AO 65                                  DSPATR(PR)
     A            C1FMDS    R        O  5 44REFFLD(BOREC/BOBODS *LIBL/HSP009)
     A                                  6  2'Ticket number ..............'
     A            C1TKNO    R        B  6 33REFFLD(FHREC/FHTKNO *LIBL/HSP037)
     A                                      EDTCDE(M)
     A  60
     AO 65                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N60
     A    N65N62                            DSPATR(HI)
     A N37N60
     A    N65N62                            DSPATR(UL)
     A  60                              6 64'    Costed      '
     A                                      DSPATR(UL)
      *
     A                                  7  2'Ticket date ................'
     A            C1MDY     R        B  7 33REFFLD(@@DT06 *LIBL/HSPREF)
     A                                      EDTCDE(Y)
     A  62
     AO 65                                  DSPATR(PR)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N62N65                            DSPATR(HI)
     A N38N62N65                            DSPATR(UL)
      *
     A N61                              7 44'Ticket status:'
     A N61        C1TSCD    R        O  7 59REFFLD(FHTSCD)
     A  60                              7 64'Date:'
     A  60        C1COMDY   R        O  7 70REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  60                              8 64'Time:'
     A  60        C1COTM    R        O  8 70REFFLD(HSRREF/@@COTM *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
     A                                  9  2'Reference ticket ..'
     A N62N65                           9 22'?'
     A            C1RTNO    R        B  9 24REFFLD(FHRTNO)
     A                                      EDTCDE(M)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N65N62N39                            DSPATR(HI)
     A N65N62N39                            DSPATR(UL)
     A  62
     AO 65                                  DSPATR(PR)
     A*
     A  60                              9 64'User:'
     A  60        C1COUSR   R        O  9 70REFFLD(HSRREF/@@COUSR *LIBL/HSPREF)
     A                                 10 32'---------------------- TOTALS -----
     A                                      --------------'
     A                                      DSPATR(HI)
     A            C1FDLB         7Y 0O 11 32EDTCDE(K)
     A            C1INAM    R        O 11 43REFFLD(FDREC/FDINAM *LIBL/HSP038)
     A                                      EDTCDE(K)
     A            C1MDAM    R        O 11 57REFFLD(FDREC/FDMDAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A            C1MFAM    R        O 11 69REFFLD(FDREC/FDMFAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A N65                             12  5'?'
     A                                      DSPATR(HI)
     A N65                             12 12'?'
     A                                      DSPATR(HI)
     A N65                             12 20'?'
     A                                      DSPATR(HI)
     A N65                             12 26'?'
     A                                      DSPATR(HI)
     A N65                             12 30'?'
     A                                      DSPATR(HI)
     A                                 13 30'F'
     A                                      DSPATR(HI)
     A                                 13 45'Ingredient'
     A                                      DSPATR(HI)
     A                                 13 57'Medication'
     A                                      DSPATR(HI)
     A                                 13 71'Manufact'
     A                                      DSPATR(HI)
     A                                 14  2'Bin'
     A                                      DSPATR(HI)
     A                                 14  9'Group'
     A                                      DSPATR(HI)
     A                                 14 18'Ration/Add'
     A                                      DSPATR(HI)
     A                                 14 30'T'
     A                                      DSPATR(HI)
     A                                 14 35'Pounds'
     A                                      DSPATR(HI)
     A                                 14 51'Cost'
     A                                      DSPATR(HI)
     A                                 14 63'Cost'
     A                                      DSPATR(HI)
     A                                 14 75'Cost'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1200K1
     A*%%TS  SD  19940426  131054  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
      *
     A N62N65                          23 11'F4=Prompt'
     A N62N65                          23 22'F5=Refresh'
     A N62N65 61                       23 34'F9=Create'
     A N62N65 60                       23 34'F9=Revise'
     A  60N62N65                       23 45'F11=Delete'
     A                                 23 57'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP1200ES                  SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP1200EC                  SFLCTL(HP1200ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
