/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4701CL                                      */
/*  PROGRAM NAME:    LISTINGS FROM WORK WITH BUDGET ITEMS          */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     02/11/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4701-WORK WITH BUDGET ITEMS.                */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(41)

             DCL        VAR(&LDREPORT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDITTYCD) TYPE(*CHAR) LEN(6)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDREPORT) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDITTYCD) VALUE(%SST(*LDA 77 6))


/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* LISTING OF BUDGET ITEMS (WITH USER SELECTIONS)                   */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'L') THEN(DO)

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('IMAIST *NE +
                          " "')
/*  IF ITEM TYPE                                                             */

             IF         COND(&LDITTYCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 19)) VALUE('*AND IMITTYCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 35 6)) VALUE(&LDITTYCD)
             CHGVAR     VAR(%SST(&QDATA 41 1)) VALUE('"')
             ENDDO


             OVRDBF     FILE(HSL181C) SHARE(*YES)
             OPNQRYF    FILE((HSL181C)) QRYSLT(&QDATA) KEYFLD(*FILE)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(133) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6703)

             CLOF       OPNID(HSL181C)
             ENDDO


             DLTOVR     FILE(*ALL)

             ENDPGM

