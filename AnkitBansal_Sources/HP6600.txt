      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP6600
      * TITLE:       Expense Class/Subclass/Code Listing: By Report Code
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/24/03
      *
      * FUNCTION: Batch program to list Expense Classes/Subclasses/Expense Codes.  The
      *           listing is sequenced by the 'report codes'.
      *
      ****************************************************************
      * MODIFICATIONS:
      * ***************************************************************************************
      * Date:     PROGRAMMER
      * ***************************************************************************************
      * 01/21/21  ISE                TSN593
      *           Replaced *loval with *start
      *
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp3410   if   e           k disk
      *   Workfile (records keyed/selected in open query)
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *--------------------------------------------------------------
      * CONSTANTS
      *--------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *--------------------------------------------------------------
      * STANDALONE FIELDS
      *--------------------------------------------------------------
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D sveccd          s                   like(w1eccd)
     D svescd          s                   like(w1escd)
     D svrpcd          s                   like(w1rpcd)
      *
      *--------------------------------------------------------------
      * DATA STRUCTURES
      *--------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Position in workfile and print headings.
      *
593  C*    *loval        setll     hsp3410
593  C     *start        setll     hsp3410
     C                   exsr      $h1hdr
      *
      * Read/print each record in the workfile.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3410                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Set indicators to control which fields print on the detail line.
      *
     C                   setoff                                       969798
      *
     C                   select
     C                   when      svrpcd <> w1rpcd
     C                   seton                                        969798
     C                   move      w1rpcd        svrpcd
     C                   move      w1eccd        sveccd
     C                   move      w1escd        svescd
      *
     C                   when      sveccd <> w1eccd
     C                   seton                                        9798
     C                   move      w1eccd        sveccd
     C                   move      w1escd        svescd
      *
     C                   when      svescd <> w1escd
     C                   seton                                        98
     C                   move      w1escd        svescd
      *
     C                   endsl
      *
      * Print detail line
      *
     C                   exsr      $d1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   seton                                        969798
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $d1dtl        begsr
      *
     C                   select
     C                   when      *inof = *on
     C                   exsr      $h1hdr
      *
     C                   when      *in96 = *on
     C                   except    blank
     C                   except    blank
      *
     C                   when      *in97 = *on or
     C                             *in98 = *on
     C                   except    blank
     C                   endsl
      *
     C                   except    d1dtl
     C                   move      yes           prtfl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           88 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         Y    198
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           80 'LISTING OF EXPENSE CL'
     O                                           98 'ASS/SUBCLASS/CODES'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                           86 'BY REPORT CODE'
     O                                          188 'PAGE'
     O                       page          Z    198
      *
      *
     O          e            h1hdr          1
     O                                           20 '--------- REPORT ---'
     O                                         +  0 '-------'
      *
     O                                           48 '----- EXPENSE CLASS'
     O                                         +  1 '-----'
      *
     O                                           77 '---- EXPENSE SUBCLA'
     O                                         +  0 'SS ----'
      *
     O                                          107 '------ EXPENSE CODE '
     O                                         +  0 '-------'
      *
      *
     O          e            h1hdr          1
     O                                            4 'CODE'
     O                                           18 'DESCRIPTION'
      *
     O                                           33 'CODE'
     O                                           47 'DESCRIPTION'
      *
     O                                           62 'CODE'
     O                                           76 'DESCRIPTION'
      *
     O                                           91 'CODE'
     O                                          105 'DESCRIPTION'
      *
     O                                          143 '-------------------'
     O                                         +  0 ' G/L ACCOUNT'
     O                                         +  0 '-----------------'
      *
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            d1dtl       1
      *
      * Report code
     O               96      w1rpcd               5
     O               96      w1rlds              27
      *
      * Expense class
     O               97      w1eccd              34
     O               97      w1ecds              56
      *
      * Expense subclass
      *
     O               98      w1escd              63
     O               98      w1esds              85
      *
      * Expense code
     O                       w1excd              92
     O                       w1exds             114
      *
      *
     O                       w1glds             172
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
