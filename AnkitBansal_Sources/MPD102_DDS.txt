     A*%%TS  SD  20140311  143840  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD102
     A* DESCRIPTION:  MP2: REVISE PURCHASE ORDER HEADER
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         03/18/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP102R1
     A*%%TS  SD  20140311  143840  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(31)
     A                                      OVERLAY
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 22'REVISE PURCHASE ORDER HEADER'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'Purchase order ...:'
     A            D1PONO    R        O  4 22REFFLD(@@PONO)
     A                                  5  2'Purchasing center :'
     A            D1PCID    R        O  5 22REFFLD(@@PCID)
     A                                  6  2'Accounting company:'
     A            D1ACONO   R        O  6 22REFFLD(@@ACONO)
     A                                      EDTCDE(Z)
     A            D1CONM        30A  O  6 33
     A                                  7  2'JDE vendor number .'
     A            D1JDVNNO  R        B  7 22REFFLD(@@JDVNNO)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            D1JDVNNM  R        O  7 33REFFLD(@@JDVNNM)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP102K1
     A*%%TS  SD  20000925  143030  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP102ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R MP102EC                   SFLCTL(MP102ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
