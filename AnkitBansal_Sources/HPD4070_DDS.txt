     A*%%TS  SD  20041012  070445  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4070
     A*  TITLE:         SPECIFY SALES MOVEMENT BOL TO PRINT/EMAIL
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/11/04
     A*
     A*  MODIFICATIONS:
     A*  07/09/2020 DN  P408  - Added email address field D1EMAIL.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF08(08 'SUBMIT')
     A                                      CA12(12 'PREVIOUS')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4070R1
     A*%%TS  SD  20041012  070445  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(51)
     A                                      SETOF(54)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1VERSION     18A  O  2  2DSPATR(HI)
     A                                  2 23'SPECIFY BILL OF LADINGS TO PRINT+
P408 A                                       /EMAIL'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  3 28'SCHEDULED SALES ONLY'
     A                                      DSPATR(HI)
     A  98                              4 33'(REPRINT)'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                      DSPATR(RI)
     A                                  5  2'ENTER:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6  2'Movement number ........'
     A                                  6 27'?'
     A            D1MVSN    R        B  6 29REFFLD(@@MVSN)
     A                                      EDTCDE(M)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39
     AO 59                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                  8  2'OR:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9  2'Transportation office ..'
     A                                  9 27'?'
     A            D1TOCD    R        B  9 29REFFLD(@@TOCD)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1TONM    R        O  9 40REFFLD(@@TONM)
     A                                 10  2'From scheduled date ....'
     A            D1FMDY    R     Y  B 10 29REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 11  2'To scheduled date ......'
     A            D1TMDY    R     Y  B 11 29REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13  2'Sales type .............'
     A                                 13 27'?'
     A            D1STCD    R        B 13 29REFFLD(@@STCD)
     A N37                                  DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1STDS    R        O 13 40REFFLD(STSTDS)
     A                                 14  2'Cell ...................'
     A            D1CELL    R        B 14 29REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 15  2'Farm site ..............'
     A                                 15 27'?'
     A            D1FSCD    R        B 15 29REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1FSNM    R        O 15 40REFFLD(FSFSNM)
     A                                 16  2'Customer number ........'
     A                                 16 27'?'
     A            D1CVNO    R        B 16 29REFFLD(@@AJD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1CNAM    R        O 16 40REFFLD(@@DS30)
     A                                 17  2'Trucker ................'
     A                                 17 27'?'
     A            D1TICD    R        B 17 29REFFLD(@@TICD)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1TINM    R        O 17 40REFFLD(TITINM)
      * P408-Added Email Address Field.
P408 A                                 19  2'Enter Email to Send as PDF:'
P408 A            D1EMAIL       50A  B 19 30
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4070K1
     A*%%TS  SD  19960213  101114  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A N99                             23 33'F12-Previous'
     A N99                             23 47'F17=Print Options'
     A N99                             23 65'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4070ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP4070EC                  SFLCTL(HP4070ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
