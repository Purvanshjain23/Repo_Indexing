      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP630
      * TITLE:       LISTING OF GROUP MOVEMENTS
      * PROGRAMMER:  MARY PALMER
      * CREATED:     6/09/94
      *
      * FUNCTION:    BATCH PROGRAM SUBMITTED FROM:
      *              HP440 - WORK WITH ACTIVE HOG GROUPS
      *              USER'S SELECTIONS ARE PASSED IN THE LDA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/23/94  SHARON ZURN
      *           ADDED MORTALITY COLUMN AND LOGIC TO
      *           RETREIVE THE MORTALITY HEAD COUNT AND WEIGHT.
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 09/27/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED THE FOLLOWING UNUSED
      *           DATABASE FIELDS FROM THE CHECK FILES:
      *             HSP063-CHECK HEADER        CHCHBO CHHRDT
      *             HSP064-CHECK DETAIL        CDCHBO CDCSAM
      *             HSP065-CHECK ADJUSTMENTS   CJCHBO
      *
      * 01/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. SEX ADDED TO HOG GROUP FILE.
      *
      * 03/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. ORIGIN BGF FARM SITE AND ORIGIN
      *           NURSERY FARM SITE ADDED TO HOG GROUP FILE.
      *
      * 04/19/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC
      *
      * 08/17/95  LEANNE FEDOR
      *           REWRITTEN FOR MOVEMENT REWRITE.
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 02/06/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO SALES MOVEMENT HEADER FILE.
      *
      * 02/13/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO TRANSFER MOVEMENT HEADER FILE.
      *
      * 08/12/96  LEANNE FEDOR
      *           INCREASED THE SIZE OF 'POUNDS' FIELDS FROM 5,0 TO
      *           7,0.
      *
      * 03/18/97  LEANNE FEDOR
      *           RECOMPILE ONLY. UPDATE TIME STAMP ADDED TO MOVEMENT
      *           EVENT FILE.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 08/01/00  LeAnne Fedor
      *           Removed 'genetic line description' from report.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 12/11/02  LeAnne Fedor
      *           Per AMB, made the logic for the 'head' and 'pounds' column for Sales
      *           Movements match the logic in HP686.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP031    IF   E           K DISK
      *    BREED STOCK
      *
      *
     FHSL034D   IF   E           K DISK
      *  HOG GROUPS
      *
      *
     FHSL058D   IF   E           K DISK
      *  MOVEMENT EVENT
      *
      *
     FHSP068    IF   E           K DISK
      *  KILLED/DEAD HOGS
      *
      *
     FHSP069    IF   E           K DISK
      *  INVENTORY ADJUSTMENT
      *
      *
     FHSP074    IF   E           K DISK
      *  TRANSFER HEADER
      *
      *
     FHSP075    IF   E           K DISK
      *  TRANSFER MOVEMENT DETAIL
      *
      *
     FHSP076    IF   E           K DISK
      *  MOVEMENT TYPE CODE
      *
      *
     FHSP084    IF   E           K DISK
      *  SALE HEADER
      *
      *
     FHSP085    IF   E           K DISK
      *  SALE DETAIL
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D DAILY           C                   CONST('DAILY DEAD')
     D MORT            C                   CONST('MORTALITY')
     D MOVED           C                   CONST('MOVED')
     D INVEN           C                   CONST('INVENTORY')
      *
     D ADJUST          C                   CONST('INVENTORY ADJUST-
     D                                     MENTS')
      *
     D SCHEDI          C                   CONST('SCHEDULED IN')
     D SCHEDO          C                   CONST('SCHEDULED OUT')
      *
     D TRANO           C                   CONST('TRANSFERRED OUT')
     D DDOA            C                   CONST('   DOA AT DESTIN-
     D                                     ATION')
     D DQUAL           C                   CONST('   ACCEPTED AS Q-
     D                                     UALITY AT DESTIN-
     D                                     ATION')
     D DREJ            C                   CONST('   ACCEPTED AS R-
     D                                     EJECTS AT DESTIN-
     D                                     ATION')
      *
      * DESCRIPTIONS FOR TRANSFERRED IN TOTALS
      *
     D TRANI           C                   CONST('TRANSFERRED IN (-
     D                                     ARRIVED)')
      *
     D DOAI            C                   CONST('   DOA UPON ARRI-
     D                                     VAL')
      *
     D RECVI           C                   CONST('   RECEIVED IN')
     D QUALI           C                   CONST('      QUALITY IN')
     D REJI            C                   CONST('      REJECTS IN')
      *
      * DESCRIPTIONS FOR 'SALES' TOTALS
      *
     D SOLD            C                   CONST('SOLD AND SHIPPED-
     D                                      OUT')
      *
     D CDOA            C                   CONST('   ARRIVED DOA A-
     D                                     T CUSTOMER')
      *
     D CLIVE           C                   CONST('   ARRIVED LIVE -
     D                                     AT CUSTOMER')
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDHGSN                 6     12  0
     D  LDBODS                21     50
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS EACH MOVEMENT IN THE MOVEMENT EVENT FILE FOR THE GROUP
      *
     C     LDHGSN        SETLL     HSL058D
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C     LDHGSN        READE     HSL058D                                90
     C     *IN90         IFEQ      *OFF                                         IF READ
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   MOVEL     NO            FIRST
     C                   ENDIF
      *
      * RETRIEVE ALPHA HOG GROUP CODE FOR THE 'OPPOSING' GROUP
      * ON TRANSFERS.
      *
     C                   MOVE      *BLANK        DTHG
     C     MEMTCD        IFEQ      'T'
     C     MEHSN         CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        DTHG
     C                   ENDIF
     C                   ENDIF
      *
      * FLIP EVENT DATE DATE TO MMDDYY
      *
     C                   Z-ADD     MEMEDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTMDY
      *
      * PROCESS APPROPRIATE MOVEMENT TYPE
      *
      * TRANSFERS
     C                   SELECT
     C     MEMTCD        WHENEQ    'T'
     C                   EXSR      $TRAN
      * SALES
     C     MEMTCD        WHENEQ    'S'
     C                   EXSR      $SALE
      *
      * INVENTORY ADJUSTMENTS
      *
     C     MEMTCD        WHENEQ    'A'
     C                   EXSR      $ADJ
      *
      * DAILY DEADS
      *
     C     MEMTCD        WHENEQ    'D'
     C                   EXSR      $DEAD
     C                   ENDSL
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * RETRIEVE THE SHORT DESCRIPTION FOR THIS MOVEMENT TYPE
      *
     C     MEMTCD        CHAIN     HSP076                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL(P)  MTSMDS        DTSMDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DTSMDS
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
     C                   ENDIF                                                  IF READ
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE; OTHERWISE,
      *  PRINT THE SECTION OF GROUP TOTALS
      *
     C     FIRST         IFEQ      YES                                          IF NOTHING
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
     C                   EXSR      $TOTAL
     C                   ENDIF                                                  IF NOTHING
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $TOMDY - FLIP DATE TO MMDDYY
      *----------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TRAN - PROCESS A TRANSFER MOVEMENT
      *---------------------------------------------------------------
      *
     C     $TRAN         BEGSR
      *
      * RETRIEVE STATUS FROM TRANSFER MOVEMENT HEADER
      *
     C     MEMVSN        CHAIN     HSP074                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     MHMSCD        DTMSCD
     C                   ELSE
     C                   MOVE      *BLANK        DTMSCD
     C                   ENDIF
      *
      * RETREIVE HEAD FROM THE TRANSFER MOVEMENT DETAIL FOR THIS
      * MOVEMENT AND LINE NUMBER.
      *
     C     K1P075        CHAIN     HSP075                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
      *
      * IF THE GROUP THE USER SELECTED FOR VIEWING IS THE
      * ORIGIN GROUP ON THE TRANSFER, SET UP VALUES BASED
      * ON THE MOVEMENT STATUS:
      *   IF SCHEDULED, USE SCHEDULED HEAD
      *                 AND ZERO POUNDS
      *   IF SHIPPED, USE ARRIVED HEAD (QUALITY + REJECT + DOA)
      *               AND SHIPPED POUNDS
      *
     C                   SELECT
     C     MEODFL        WHENEQ    'O'
     C     DTMSCD        IFEQ      'SC'                                         IF SCHED OUT
      *
      * SET UP THE PRINT FIELD AND REVERSE THE SIGN SO THAT
      * THE USER CAN EASILY SEE THAT THIS IS A SCHEUDLED
      * TRANSFER OUT OF THE GROUP. ALSO, FOR TOTALS, ACCUMULATE
      * THE HEAD SCHEDULED OUT.
      *
     C                   Z-SUB     MDSCHD        DTSCHD
     C                   ADD       MDSCHD        AROHD
     C                   ELSE
      *
      * SET UP PRINT FIELDS. REVERSE THE SIGN ON THE PRINT
      * FIELD TO INDICATE TO THE USER THAT THIS IS A TRANSFER OUT.
      *
     C                   Z-SUB     MDQLHD        DTHD
     C                   SUB       MDRJHD        DTHD
     C                   SUB       MDDOHD        DTHD
     C                   Z-SUB     MDSHLB        DTLB
     C                   Z-ADD     MDDOHD        DTDOHD
      *
      * ACCUMULATE THE DOA VALUES RESULTING FROM THIS TRANSFER OUT
      *
     C                   ADD       MDDOHD        ARODHD
     C                   ADD       MDDOLB        ARODLB
      *
      * ACCUMULATE THE QUALITY AND REJECT VALUES RESULTING
      * FROM THIS TRANSFER OUT OF THE GROUP.
      *
     C                   ADD       MDQLHD        AROQHD
     C                   ADD       MDQLLB        AROQLB
     C                   ADD       MDRJHD        ARORHD
     C                   ADD       MDRJLB        ARORLB
      *
      * ACCUMULATE SHIPPED POUNDS OUT OF THE GROUP ON THIS
      * TRANSFER.
     C                   ADD       MDSHLB        ARSHLB
     C                   ENDIF                                                  IF SCHED OUT
      *
      *----------------------------------------------------------------
      * IF THE GROUP THE USER SELECTED FOR VIEWING IS THE DESTINATION
      * GROUP ON THIS TRANSFER:
      *
     C     MEODFL        WHENEQ    'D'
      *
      * IF SCHEDULED, THIS REPRESENTS A SCHEDULED MOVEMENT INTO
      * THE USER'S SELECTED GROUP. SET UP THE PRINT FIELD LEAVING
      * THE HEAD AS A POSITIVE VALUE. AND, FOR TOTALS, ACCUMULATE
      * THE HEAD SCHEDULED ON.
      *
     C     DTMSCD        IFEQ      'SC'
     C                   Z-ADD     MDSCHD        DTSCHD
     C                   ADD       MDSCHD        ARIHD
     C                   ELSE
      *
      * THIS TRANSFER INTO THE GROUP HAS BEEN SHIPPED. SET UP THE
      * HEAD VALUE TO INCLUDED QUALITY AND REJECT HEAD.  DO NOT
      * INCLUDE DOA HEAD ON THIS TRANSFER SINCE THE DOA HEAD ARE NOT
      * PUT INTO THE GROUP.
      *
     C     MDQLHD        ADD       MDRJHD        DTHD
     C     MDQLLB        ADD       MDRJLB        DTLB
      *
      * ACCUMULATE THE QUALITY AND REJECT VALUES THAT WERE
      * RECEIVED INTO THE USER'S SELECTED GROUP.
      *
     C                   ADD       MDQLHD        ARQIHD
     C                   ADD       MDQLLB        ARQILB
     C                   ADD       MDRJHD        ARRIHD
     C                   ADD       MDRJLB        ARRILB
      *
      * ALSO, ACCUMULATE THE VALUES FOR HOGS THAT WERE TRANSFERRED
      * TO THE GROUP BUT ARRIVED DEAD.
      *
     C                   ADD       MDDOHD        ARADHD
     C                   ADD       MDDOLB        ARADLB
      *
     C                   ENDIF
     C                   ENDSL
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SALE - RETRIEVE SALES MOVEMENT
      *---------------------------------------------------------------
      *
     C     $SALE         BEGSR
      *
      * RETRIEVE THE STATUS FROM THE SALES MOVEMENT HEADER
      *
     C     MEMVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     SHMSCD        DTMSCD
     C                   ELSE
     C                   MOVE      *BLANK        DTMSCD
     C                   ENDIF
      *
      * RETREIVE HEAD AND POUNDS FROM THE SALES MOVEMENT DETAIL FOR
      * THIS MOVEMENT/HOG GROUP. SHOW HEAD AND POUNDS AS NEGATIVE
      * VALUES TO INDICATE MOVEMENT OUT OF THE GROUP. ALWAYS RETRIEVE
      * 'SHIPPED' POUNDS BUT USE THE ARRIVED VALUES FOR HEAD SINCE THE
      * USERS BELIEVE THAT THE HEAD ACTUALLY ARRIVING AT THE CUSTOMER
      * IS A MORE ACCURATE COUNT OF THE HEAD THAT WAS SOLD/SHIPPED OUT
      * OF THE GROUP. THUS, THE 'HEAD' LOGIC IS AS FOLLOWS:
      *
      *   IF 'SCHEDULED'
      *      USE SCHEDULED HEAD
      *   IF 'SHIPPED'
      *      USE LIVE HEAD + DOA HEAD RECEIVED AT THE CUSTOMER
      *
     C     K2P085        CHAIN     HSP085                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
      *
     C     DTMSCD        IFEQ      'SC'                                         IF SCHED
     C                   Z-SUB     SGSCHD        DTSCHD
     C                   ADD       SGSCHD        AROHD
     C                   ELSE
      *
     C                   Z-ADD     SGDOHD        DTDOHD
      *
     C     DTMSCD        IFEQ      'FP'
     C     DTMSCD        OREQ      'PP'
     C     DTMSCD        OREQ      'PG'
     C                   Z-SUB     SGLVHD        DTHD
     C                   SUB       SGDOHD        DTHD
     C                   Z-SUB     SGSHLB        DTLB
     C                   ELSE
     C                   Z-SUB     SGSHHD        DTHD
     C                   Z-SUB     SGSHLB        DTLB
     C                   ENDIF
      *
      * ACCUMULATE THE LIVE VALUES RECEIVED AT THE CUSTOMER SITE.
      *
     C                   ADD       SGLVHD        ARSLHD
     C                   ADD       SGLVLB        ARSLLB
      *
      * ACCUMULATE THE DOA VALUES RECEIVED AT THE CUSTOMER SITE.
      *
     C                   ADD       SGDOHD        ARSDHD
     C                   ADD       SGDOLB        ARSDLB
      *
      * ACCUMULATE THE POUNDS SHIPPED OUT ON THIS SALE.
      *
     C                   ADD       SGSHLB        ARSOLB
     C                   ENDIF                                                  IF SCHED
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DEAD - RETRIEVE DAILY DEAD MOVEMENT
      *---------------------------------------------------------------
      *
     C     $DEAD         BEGSR
      *
      * RETRIEVE STATUS AND HEAD. FOR DAILY DEADS, THERE WILL ONLY BE
      * ONE RECORD PER MOVEMENT. ALSO, REVERSE THE SIGN ON THE PRINT
      * FIELD SO THAT THE 'DAILY DEAD' VALUES APPEAR AS NEGATIVE
      * NUMBERS WHICH INDICATES QUICKLY TO THE USER THAT THIS IS A
      * MOVEMENT OUT OF THE SELECTED GROUP.
      *
     C     MEMVSN        CHAIN     HSP068                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     KDMSCD        DTMSCD
     C                   Z-SUB     KDKDHD        DTHD
     C                   Z-SUB     KDKDLB        DTLB
      *
      *  ACCUMULATE THE DAILY DEAD VALUES
      *
     C                   ADD       KDKDHD        ARKDHD
     C                   ADD       KDKDLB        ARKDLB
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ADJ - RETRIEVE ADJUSTMENT MOVEMENT
      *---------------------------------------------------------------
      *
     C     $ADJ          BEGSR
      *
      * RETRIEVE STATUS AND HEAD. FOR ADJUSTMENTS, THERE WILL ONLY BE
      * ONE RECORD PER MOVEMENT. PLEASE NOTE THAT INVENTORY ADJUSTMENTS
      * DO NOT HAVE POUNDS.
      *
     C     MEMVSN        CHAIN     HSP069                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     IAMSCD        DTMSCD
     C                   Z-ADD     IAIAHD        DTHD
      *
      * ACCUMULATE ADJUSTMENT HEAD
      *
     C                   ADD       IAIAHD        ARIAHD
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOTAL - PRINT TOTALS FOR GROUP
      *----------------------------------------------------------------
      *
     C     $TOTAL        BEGSR
      *
      * PRINT HEADER FOR TOTAL SECTION
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    TRHDR
      *
      * MORTALITY =    DAILY DEAD
      *             -  INVENTORY ADJUSTMENTS
      *             +  DOAS FROM TRANSFERS OUT
      *             +  DOAS FROM SALES
      *
     C     ARKDHD        SUB       ARIAHD        TRHD
     C                   ADD       ARODHD        TRHD
     C                   ADD       ARSDHD        TRHD
     C                   MOVEL     MORT          TRDESC
     C                   Z-ADD     TRHD          WKDHD
     C     ARKDLB        ADD       ARODLB        TRLB
     C                   ADD       ARSDLB        TRLB
     C                   EXSR      $PRINT
      *
      * MOVED HEAD =  QUALITY HEAD TRANSFERRED IN
      *             + REJECT HEAD TRANSFERRED IN
      *             - SOLD HEAD THAT ARRIVED LIVE AT THE CUSTOMER
      *             - QUALITY HEAD THAT WAS TRANSFERRED OUT
      *             - REJECT HEAD THAT WAS TRANSFERRED OUT
      *
     C     ARQIHD        ADD       ARRIHD        TRHD
     C                   SUB       ARSLHD        TRHD
     C                   SUB       AROQHD        TRHD
     C                   SUB       ARORHD        TRHD
     C                   MOVEL     MOVED         TRDESC
     C                   Z-ADD     TRHD          WKMHD
     C                   Z-ADD     0             TRLB
     C                   EXSR      $PRINT
      *
      * INVENTORY = MOVED HEAD LESS MORTALITY HEAD
      *
     C                   MOVEL     INVEN         TRDESC
     C     WKMHD         SUB       WKDHD         TRHD
     C                   Z-ADD     0             TRLB
     C                   EXSR      $PRINT
     C                   EXCEPT    BLANK
      *
      * SCHEDULED IN HEAD
      *
     C     ARIHD         IFNE      0
     C                   MOVEL     SCHEDI        TRDESC
     C                   Z-ADD     ARIHD         TRHD
     C                   Z-ADD     0             TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * SCHEDULED OUT HEAD
      *
     C     AROHD         IFNE      0
     C                   MOVEL     SCHEDO        TRDESC
     C                   Z-ADD     AROHD         TRHD
     C                   Z-ADD     0             TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * DAILY DEAD
      *
     C     ARKDHD        IFNE      0
     C                   MOVEL     DAILY         TRDESC
     C                   Z-ADD     ARKDHD        TRHD
     C                   Z-ADD     ARKDLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * INVENTORY ADJUSTMENTS
      *
     C     ARIAHD        IFNE      0
     C                   MOVEL     ADJUST        TRDESC
     C                   Z-ADD     ARIAHD        TRHD
     C                   Z-ADD     0             TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      *---------------------------------------------------------------
      * TRANSFERRED IN
      *---------------------------------------------------------------
      *
     C                   EXCEPT    BLANK
     C     ARQIHD        IFNE      0
     C     ARRIHD        ORNE      0
     C     ARADHD        ORNE      0
     C                   MOVEL     TRANI         TRDESC
     C     ARQIHD        ADD       ARRIHD        TRHD
     C                   ADD       ARADHD        TRHD
     C     ARQILB        ADD       ARRILB        TRLB
     C                   ADD       ARADLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * ARRIVED DOA AT THE FARM AND WAS NOT RECEIVED IN
      *
     C     ARADHD        IFNE      0
     C                   MOVEL     DOAI          TRDESC
     C                   Z-ADD     ARADHD        TRHD
     C                   Z-ADD     ARADLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * RECEIVED IN
      *
     C     ARQIHD        IFNE      0
     C     ARRIHD        ORNE      0
     C                   MOVEL     RECVI         TRDESC
     C     ARQIHD        ADD       ARRIHD        TRHD
     C     ARQILB        ADD       ARRILB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * QUALITY IN
      *
     C     ARQIHD        IFNE      0
     C                   MOVEL     QUALI         TRDESC
     C                   Z-ADD     ARQIHD        TRHD
     C                   Z-ADD     ARQILB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * REJECT IN
      *
     C     ARRIHD        IFNE      0
     C                   MOVEL     REJI          TRDESC
     C                   Z-ADD     ARRIHD        TRHD
     C                   Z-ADD     ARRILB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * TRANSFERRED OUT
      *---------------------------------------------------------------
      *
     C                   EXCEPT    BLANK
     C     AROQHD        IFNE      0
     C     ARORHD        ORNE      0
     C     ARODHD        ORNE      0
     C                   MOVEL     TRANO         TRDESC
     C     AROQHD        ADD       ARORHD        TRHD
     C                   ADD       ARODHD        TRHD
     C                   Z-ADD     ARSHLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * TRANSFERRED OUT BUT ARRIVED DOA AT DESTINATION
      *
     C     ARODHD        IFNE      0
     C                   MOVEL     DDOA          TRDESC
     C                   Z-ADD     ARODHD        TRHD
     C                   Z-ADD     ARODLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * TRANSFERRED OUT AND ACCEPTED AS QUALITY AT DESTINATION
      *
     C     AROQHD        IFNE      0
     C                   MOVEL     DQUAL         TRDESC
     C                   Z-ADD     AROQHD        TRHD
     C                   Z-ADD     AROQLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * TRANSFERRED OUT AND ACCEPTED AS REJECTS AT DESTINATION
      *
     C     ARORHD        IFNE      0
     C                   MOVEL     DREJ          TRDESC
     C                   Z-ADD     ARORHD        TRHD
     C                   Z-ADD     ARORLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      *---------------------------------------------------------------
      * SOLD AND SHIPPED OUT
      *---------------------------------------------------------------
      *
     C                   EXCEPT    BLANK
     C     ARSLHD        ADD       ARSDHD        TRHD
     C     TRHD          IFNE      0
     C                   MOVEL     SOLD          TRDESC
     C                   Z-ADD     ARSOLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * SOLD BUT ARRIVED DOA AT CUSTOMER
      *
     C     ARSDHD        IFNE      0
     C                   MOVEL     CDOA          TRDESC
     C                   Z-ADD     ARSDHD        TRHD
     C                   Z-ADD     ARSDLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
      * SOLD AND ARRIVED LIVE AT CUSTOMER
      *
     C     ARSLHD        IFNE      0
     C                   MOVEL     CLIVE         TRDESC
     C                   Z-ADD     ARSLHD        TRHD
     C                   Z-ADD     ARSLLB        TRLB
     C                   EXSR      $PRINT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PRINT - PRINT TOTAL LINE
      *----------------------------------------------------------------
      *
     C     $PRINT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    TRHDR
     C                   ENDIF
     C                   EXCEPT    TRTOT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LIST
      *
     C     K1P075        KLIST
     C                   KFLD                    MEMVSN
     C                   KFLD                    MELNNO
      *
     C     K2P085        KLIST
     C                   KFLD                    MEMVSN
     C                   KFLD                    MEHGSN
      *
      * WORKFIELDS
     C                   Z-ADD     0             WKDHD            15 0          DEAD HEAD
     C                   Z-ADD     0             WKMHD            15 0          MOVED HEAD
      *
      * DEFINE PRINT FIELDS FOR REPORT HEADING
      *
     C     *LIKE         DEFINE    HGHGCD        HRHGCD
     C                   Z-ADD     0             HRCMDY            6 0
     C                   Z-ADD     0             HROMDY            6 0
     C                   Z-ADD     0             HRDMDY            6 0
     C                   Z-ADD     0             HRMDY             6 0
     C     *LIKE         DEFINE    HGFSCD        HRFSCD
     C     *LIKE         DEFINE    HGBLCD        HRBLCD
     C     *LIKE         DEFINE    HGRMCD        HRRMCD
     C     *LIKE         DEFINE    HGADOI        HRADOI
     C     *LIKE         DEFINE    HGADOO        HRADOO
     C     *LIKE         DEFINE    BSBSDS        HRBSDS
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   Z-ADD     0             DTMDY             6 0
     C     *LIKE         DEFINE    MDDOHD        DTDOHD
     C     *LIKE         DEFINE    MDSCHD        DTSCHD
     C     *LIKE         DEFINE    MDSHHD        DTHD
     C                   Z-ADD     0             DTLB              7 0
     C     *LIKE         DEFINE    HGHGCD        DTHG
     C     *LIKE         DEFINE    MHMSCD        DTMSCD
     C     *LIKE         DEFINE    MTSMDS        DTSMDS
      *
      * DEFINE PRINT FIELDS FOR REPORT TOTAL LINE
      *
     C     *LIKE         DEFINE    MDSHHD        TRHD
     C                   Z-ADD     0             TRLB              7 0
     C                   MOVE      *BLANK        TRDESC           37
      *
      * DEFINE ACCUMULATION FIELDS FOR REPORT TOTALS
      *
      * KILLED/DEAD
     C                   Z-ADD     0             ARKDHD            5 0
     C                   Z-ADD     0             ARKDLB            7 0
      *
      * INVENTORY ADJUSTMENT
     C                   Z-ADD     0             ARIAHD            5 0
      *
      * SCHEDULED
     C                   Z-ADD     0             AROHD             5 0
     C                   Z-ADD     0             ARIHD             5 0
      *
      * TRANSFERRED OUT
     C                   Z-ADD     0             ARSHLB            7 0          SHIPPED
     C                   Z-ADD     0             ARODHD            5 0          DOA
     C                   Z-ADD     0             ARODLB            7 0          DOA
     C                   Z-ADD     0             AROQHD            5 0          QUALITY OUT
     C                   Z-ADD     0             AROQLB            7 0          QUALITY OUT
     C                   Z-ADD     0             ARORHD            5 0          REJECTS OUT
     C                   Z-ADD     0             ARORLB            7 0          REJECTS OUT
      *
      * SOLD
     C                   Z-ADD     0             ARSLHD            5 0          LIVE
     C                   Z-ADD     0             ARSLLB            7 0          LIVE
     C                   Z-ADD     0             ARSDHD            5 0          DOA
     C                   Z-ADD     0             ARSDLB            7 0          DOA
     C                   Z-ADD     0             ARSOLB            7 0
      *
      * TRANSFERRED IN
     C                   Z-ADD     0             ARQIHD            5 0
     C                   Z-ADD     0             ARQILB            7 0
     C                   Z-ADD     0             ARRIHD            5 0
     C                   Z-ADD     0             ARRILB            7 0
      *
      * ARRIVED DOA FROM ANOTHER GROUP
      *
     C                   Z-ADD     0             ARADHD            5 0
     C                   Z-ADD     0             ARADLB            7 0
      *
      *-------------------------------------------------------------
      * RETRIEVE THE REPORT HEADING INFO FOR THE HOG GROUP
      *-------------------------------------------------------------
      *
     C     LDHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF                                         IF GROUP OK
     C                   MOVEL     HGHGCD        HRHGCD
     C                   MOVEL     HGFSCD        HRFSCD
     C                   MOVEL     HGBLCD        HRBLCD
     C                   MOVEL     HGRMCD        HRRMCD
     C                   Z-ADD     HGADOI        HRADOI
     C                   Z-ADD     HGADOO        HRADOO
      *
      * FLIP CREATE DATE
      *
     C                   Z-ADD     HGCRDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HRCMDY
      *
      * FLIP OPEN DATE
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HROMDY
      *
      * FLIP DISPOSE DATE
      *
     C                   Z-ADD     HGDSDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HRDMDY
      *
      * IF CLOSED, FLIP CLOSED DATE
      *
     C     HGCLDT        IFNE      0
     C                   Z-ADD     HGCLDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HRMDY
     C                   SETON                                        89
     C                   ENDIF
      *
      * RETRIEVE BREED STOCK
      *
     C     HGBSCD        CHAIN     HSP031                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     BSBSDS        HRBSDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRBSDS
     C                   ENDIF
      *
     C                   ENDIF                                                  IF GROUP OK
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH             80
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           45 'LISTING OF GROUP'
     O                                           55 'MOVEMENTS'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          2
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           18 'BUSINESS OFFICE ..'
     O                       LDFSBO              24
     O                       LDBODS              57
      *
     O          E            HRHDR          1
     O                                           18 'HOG GROUP ........'
     O                       HRHGCD              26
      *
     O          E            HRHDR          1
     O                                           18 'FARM SITE ........'
     O                       HRFSCD        M     25
      *
     O          E            HRHDR          1
     O                                           18 'BUILDING/ROOM ....'
     O                       HRBLCD              25
     O                                           26 '/'
     O                       HRRMCD              31
      *
     O          E            HRHDR          1
     O               89                          18 'DATE CLOSED ......'
     O               89      HRMDY               27 '  /  /  '
      *
     O          E            HRHDR          1
     O                                           24 'DATE CREATED / OPENED/ D'
     O                                           36 'ISPOSED ....'
     O                       HRCMDY              45 '  -  -  '
     O                                           47 '/'
     O                       HROMDY              56 '  -  -  '
     O                                           58 '/'
     O                       HRDMDY              67 '  -  -  '
      *
     O          E            HRHDR          1
      *
     O                                           24 'BREED STOCK ............'
     O                                           36 '............'
     O                       HRBSDS              57
      *
     O          E            HRHDR          2
     O                                           24 'AVG OLD DAYS IN / AVG OL'
     O                                           36 'D DAY OUT ..'
     O                       HRADOI        Z     40
     O                                           42 '/'
     O                       HRADOO        Z     46
      *
      *-------------------------------------------------------------
      * COLUMN HEADINGS
      *-------------------------------------------------------------
     O          E            HRHDR          1
     O                                           35 'ORIG'
     O                                           61 'DOA'
     O                                           70 'SCHED'
     O                                           80 'OPPOSING'
      *
     O          E            HRHDR          1
     O                                            6 'DATE'
     O                                           17 'MOVEMENT'
     O                                           22 'STAT'
     O                                           28 'TYPE'
     O                                           35 'DEST'
     O                                           42 'HEAD'
     O                                           54 'POUNDS'
     O                                           61 'HEAD'
     O                                           70 'HEAD'
     O                                           77 'GROUP'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * MOVEMENT DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       DTMDY          B     8 '  /  /  '
     O                       MEMVSN        MB    17
     O                       DTMSCD         B    21
     O                       DTSMDS         B    30
     O                       MEODFL         B    33
     O                       DTHD          KB    43
     O                       DTLB          KB    55
     O                       DTDOHD        KB    62
     O                       DTSCHD        KB    71
     O                       DTHG           B    80
      *
      *-------------------------------------------------------------
      * REPORT TOTAL HEADING
      *-------------------------------------------------------------
      *
     O          E            TRHDR       1  2
     O                                           13 'GROUP TOTALS:'
     O                                           49 'HEAD'
     O                                           62 'POUNDS'
      *
      *-------------------------------------------------------------
      * REPORT TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                       TRDESC         B    37
     O                       TRHD          KB    50
     O                       TRLB          KB    63
      *
      *-------------------------------------------------------------
      * BLANK LINE
      *-------------------------------------------------------------
      *
     O          E            BLANK       1
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
