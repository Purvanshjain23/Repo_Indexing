/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP404CL                                       */
/*  PROGRAM NAME:    SPECIFY HPS UPLOAD TO JDE (DRIVER CL)         */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     10/03/01                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP404-SPECIFY HPS UPLOAD TO JDE.              */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/* 07/31/02  LEANNE FEDOR                                          */
/*           REMOVED 'SEMEN DOSES' FROM 'UNITS' LOGIC.             */
/*           SEMEN DOSES IS NOW A SEPARATE FUNCTION.               */
/*                                                                 */
/* 08/01/02  LEANNE FEDOR                                          */
/*           REMOVED 'TRANSFER OUT DIFF PHASE' FROM 'UNITS' LOGIC. */
/*           TRANSFERS OUT DIFFERENT PHASE IS NOW A SEPARATE       */
/*           FUNCTION.                                             */
/*                                                                 */
/* 05/22/03  LEANNE FEDOR                                          */
/*           ADDED NEW FUNCTION FOR 'SEMEN DOSES SHIPPED'          */
/*                                                                 */
/* 11/10/08  LEANNE RAMSEY                                         */
/*           CHANGED THE PARMS ON THE CALL TO HP8001.              */
/*                                                                 */
/* 04/28/09  LEANNE RAMSEY                                         */
/*           ADDED BEGINNING/ENDING PERIOD DATES TO THE CALL TO    */
/*           HP8001.                                               */
/*                                                                 */
/* 02/05/13  LeAnne Ramsey (E2405)                                 */
/*           MTech wants to be able to upload all of their data to */
/*           JDE in a single submission. So, in prepraration for   */
/*           for that, I am removing the override of HSP204 prior  */
/*           to the call to HPJDE204 since I have changed          */
/*           HPJDE204 to use HSL204B which has been rekeyed        */
/*           appropriately. These changes will allow us to         */
/*           continue to make separate/individual runs for the     */
/*           HPS uploads and to make a single run for MTECH..once  */
/*           MTECH goes live.                                      */
/*           Please note that I added 'SOW' and 'NO SOW' to the    */
/*           explanation in HP204FL-Feed Pounds to make the        */
/*           single submission work correctly.                     */
/*                                                                 */
/* 09/20/18  Danny Nguyen  (S13682)                                */
/*           Retrieve Company Code from LDA. Added Company Code    */
/*           to parm when calling HP8001.                          */
/*                                                                 */
/* 07/02/20  Brad Baden    SDN465 - JDE World to JDE Enterprise 1  */
/*           Copied this program from PRKDEVS and added code to    */
/*           retrieve the E1LIVE flag for the company in the LDA.  */
/*           If the E1LIVE flag is "Y" or "P", then create the     */
/*           workfile in library QTEMP.                            */
/*                                                                 */
/*  7/27/2020 Brad Baden     P16169   SDN465                       */
/*                     Call program E1LRXFR to send an email of all*/
/*                     of the Batch Numbers in the E1B9CPP file    */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&LDACYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDACPE) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDHUCD) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)

             DCL        VAR(&RETRN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&CO) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&LIVEJW) TYPE(*CHAR) LEN(1)  /*    LIVE WITH W  */
             DCL        VAR(&LIVEE1) TYPE(*CHAR) LEN(1)  /*    LIVE WITH E1 */

             DCL        VAR(&SOURCE) TYPE(*CHAR) LEN(25) VALUE('HPS')
             DCL        VAR(&BCHDSC)  TYPE(*CHAR) LEN(25) +
                          VALUE('G/L Journal Entry')
             DCL        VAR(&RECIP)   TYPE(*CHAR) LEN(25)
             DCL        VAR(&SUBJECT) TYPE(*CHAR) LEN(40) VALUE('HPS +
                          Journal Entries to JDE')

             CHGVAR     VAR(&LDACYR) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDACPE) VALUE(%SST(*LDA 5 2))
             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDHUCD) VALUE(%SST(*LDA 23 2))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 25 3))
             CHGVAR     VAR(&CO)     VALUE(&LDCOCD)

/*********************************************************************/
/* RETRIEVE USER FOR LATER MESSAGES                                  */
/*********************************************************************/

             RTVJOBA    USER(&USER)

/*********************************************************************/
/*  Determine where to create E1 Run Log Workfile (E1B9CPP)          */
/*********************************************************************/


/*  EXECUTE THE PROGRAM TO RETRIEVE THE COMP VALUE: LIVE WITH E1         */


        CALL       PGM(POMTXFR) PARM(&RETRN &CO 'JWLIVE    ' &LIVEJW)
        CALL       PGM(POMTXFR) PARM(&RETRN &CO 'E1LIVE    ' &LIVEE1)

/*  CREATE E1 RUN LOG WORKFILE - TO USER LATER TO PUSH BATCHES TO E1   */

             IF         COND(&LIVEE1 *EQ 'Y' *OR &LIVEE1 *EQ 'P') +
                          THEN(DO )
             CRTDUPOBJ  OBJ(E1B9CPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(E1B9CPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(E1B9CPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
                          ENDDO

/*********************************************************************/
/*  CLEAR INTERFACE FILE                                             */
/*********************************************************************/

             CLRPFM     FILE(HSP204)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(CANTCLEAR))

/*********************************************************************/
/*  CALL APPROPRIATE UPLOAD CL PROGRAM                               */
/*********************************************************************/

/* HPS PIG DAYS                                                      */

             IF         COND(&LDHUCD *EQ 'PD') THEN(DO)
             CALL       PGM(HP204CLPD)
             ENDDO

/* HPS UNITS                                                         */

             IF         COND(&LDHUCD *EQ 'UN') THEN(DO)
             CALL       PGM(HP204CLUN)
             ENDDO

/* HPS TRANSFER OUT DIFFERENT PHASE                                  */

             IF         COND(&LDHUCD *EQ 'TD') THEN(DO)
             CALL       PGM(HP204CLTD)
             ENDDO

/* HPS SEMEN DOSES DELIVERED                                         */

             IF         COND(&LDHUCD *EQ 'SD') THEN(DO)
             CALL       PGM(HP204CLSD)
             ENDDO

/* HPS SEMEN DOSES SHIPPED                                           */

             IF         COND(&LDHUCD *EQ 'SS') THEN(DO)
             CALL       PGM(HP204CLSS)
             ENDDO

/* HPS CULL SALES                                                  */

             IF         COND(&LDHUCD *EQ 'CS') THEN(DO)
             CALL       PGM(HP204CLCS)
             ENDDO


/* HPS FEED DELIVERY TICKET                                        */

             IF         COND(&LDHUCD *EQ 'FD') THEN(DO)
             CALL       PGM(HP204CLFD)
             ENDDO


/* HPS FEED PICKUP TICKET                                        */

             IF         COND(&LDHUCD *EQ 'FP') THEN(DO)
             CALL       PGM(HP204CLFP)
             ENDDO


/* HPS FEED POUNDS                                               */

             IF         COND(&LDHUCD *EQ 'FL') THEN(DO)
             CALL       PGM(HP204CLFL)
             ENDDO

/*********************************************************************/
/* Call the JDE program to write the upload data into JDE            */
/*********************************************************************/

             CALL       PGM(HPJDE204)

/*********************************************************************/
/* Push the Run Log Header records and push the batches to E1        */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*   E1 GL INVOICE PROCESSING:  IF GL ONLY LIVE WITH E1 OR PARALLEL  */
/*     !!  FOR THIS ONE, AR IS NOT LIVE OR PARALLEL WITH E1    !!    */
/*********************************************************************/

 LIVEE1P:    IF         COND(&LIVEE1 *EQ 'Y' *OR &LIVEE1 *EQ 'P') +
                          THEN(DO)

/*  EXECUTE THE PROGRAM TO READ THE QTEMP RUN LOG HEADER             */
/*      E1B9CPP AND PUSH THE BATCHES TO E1                           */

             CALL       PGM(E1LDXFR) PARM(&RETRN)

             /* Send message with all batch numbers created for PO's */
             CHGVAR     VAR(&RECIP) VALUE(&USER)

             CALL       PGM(E1LRXFR) PARM(&RETRN &SOURCE &BCHDSC +
                          &RECIP &SUBJECT)
             ENDDO                                  /*   END LIVEE1P */

/*********************************************************************/
/*  CALL HPS PROGRAM TO WRITE CONTROL RECORD TO CONTROL FILE         */
/*********************************************************************/
/* S13682 - Added Company Code as parm 1 when calling HP8001. */

             CALL       PGM(HP8001) PARM(&LDCOCD 'U' &LDHUCD &LDACYR +
                          &LDACPE &LDBPDT &LDEPDT 'HP404CL')



             GOTO       CMDLBL(EXIT)

/*--------------------------------------------------------------*/
/* INTERFACE DID NOT RUN                                        */
/*--------------------------------------------------------------*/
 CANTCLEAR:
             SNDMSG     MSG('***** Even though this job may complete +
                          normally, NO DATA WAS SENT TO JDE because +
                          the upload file could not be cleared +
                          prior to starting the interface. Please +
                          submit your upload again. Thank you. *****') +
                          TOUSR(&USER)

/*--------------------------------------------------------------*/
/*  CHANGE DATA AREA "DAJDE"  TO INDICATE THAT THE INTERFACE    */
/*  FUNCTION IS NO LONGER RUNNING                               */
/*--------------------------------------------------------------*/

 EXIT:       CHGDTAARA  DTAARA(DAJDE (1 1)) VALUE(' ')

             ENDPGM

