      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT:  PORK DIVISION
      * SYSTEM:       AS/400
      * PROGRAM:      MP103
      * DESCRIPTION:  MP2: Revise Unposted Receipts
      * PROGRAMMER:   LeAnne Fedor
      * DATE:         03/20/02
      *
      *
      * FUNCTION: Uses an edit record format to change an unposted receipt. There is
      *           no delete or create capability.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/20/03  LeAnne Fedor
      *           As requested by Barb G., when the user revises a record,
      *           this program will update the 'company' field in the Purchase Order
      *           Detail record with the first 3 non-blank values in the cost center.
      *           Prior to this change, Barb was having to DBU the 'company' number
      *           into the record for the user when they changed the cost center to
      *           a different company.
      *
      * 03/04/10  LeAnne Ramsey
      *           Barb G. added a new parm XXPEC (the posting edit code associated with the
      *           account number) to ARACTEDT.
      *
      * 03/29/11  Barb Gutierrez
      *           Recompiled due to qty and rate decimal positions being increased.
      *           E001398
      *
      * 03/26/19  Brad Baden     (E14631)
      *           Change "move" to "movel" to D1PCID field.
      *
      * 05/24/21  Danny Nguyen   (S17068)
      *           DBFC on MPP103 file. PDITEMRT field length changed from 9.4 to 11.4.
      *           Recompile only.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fmpd103    cf   e             workstn
     F                                     infds(iofeed)
      *
      *
     Fmpp102    if   e           k disk
      *  MP2: Purchase order header
      *
      *
     Fmpp103    uf   e           k disk
      *  MP2: Purchase order detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D revise          c                   'REVISE'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D rtncd           s              2
     D action          s                   like(mode)
     D maxmsg          s              2  0  inz(20)
      *
      *
      * Control fields
      *
     D start           s              2  0
      *
      *
      * Workfields
      *
     D wkcono          s                   like(pdcono)
      *
      *
      * Parm fields
      *
     D xxposn          s                   like(pdposn)
     D xxrcno          s                   like(pdrcno)
     D xxserno         s                   like(pdserno)
      *
     d xxactid         s              8
     d xxdesc          s             30
     d xxpec           s              1
     d xxreturn        s              7
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     update        $upd1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Incoming mode will always be REVISE; so, retrieve the database
      * record.  Do not lock the record.
      *
     C                   if        mode = revise                                If revise
     C     key01         chain(n)  mpp103                             92
     C                   if        *in92 = *off                                 If found
     C                   move      pdttcd        d1ttcd
     C                   z-add     pdrcno        d1rcno
     C                   z-add     pdtrdt        d1trdt
     C                   z-add     pdserno       d1serno
     C                   z-add     pdtrqt        d1trqt
     C                   move      pditemno      d1itemno
     C                   move      pditemds      d1itemds
     C                   z-add     pditemrt      d1itemrt
     C                   move      pdmcu         d1mcu
     C                   move      pdobj         d1obj
     C                   move      pdsub         d1sub
     C                   move      pdcom         d1com
      *
      * Retrieve fields from P.O. Header file to display on screen.
      *
     C     xxposn        chain     mpp102                             92
     C                   if        *in92 = *off
     C                   z-add     phjdvnno      d1jdvnno
     C                   move      phpono        d1pono
     C                   z-add     phacono       d1acono
     C*** E14631 JBB     move      phpcid        d1pcid
     C                   movel     phpcid        d1pcid
     C                   endif
      *
     C                   endif                                                  If found
     C                   endif                                                  If revise
      *
      * Display the screen.
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      *
      * Write screen 1 to CRT
      *
     C                   write     mp103k1
     C                   write     mp103ec
     C                   exfmt     mp103r1
      *
      * Clear messages
      *
     C                   exsr      $clmsg
      *
      * Reset action to be equal to the incoming mode.
      *
     C                   eval      action = mode
      *
      * Get user's input and set routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      rtncd = '03'
     C                   eval      routne = exit
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      routne = set1
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = edit1
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * Validate cost center/account
      *
     C                   exsr      $mcu
      *
      * If there are no errors, attempt the update; otherwise,
      * redisplay the screen.
      *
     C                   if        error = 0
     C                   exsr      $upd1
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------------
      * Validate account and cost center
      *-----------------------------------------------------------------------------------------
      *
     C     $mcu          begsr
      *
      * Use the first 3 non-blank characters of the cost center field as the
      * company.
      *
     C                   z-add     0             start
     C                   move      *blank        wkcono
      *
     C     ' '           check     d1mcu         start
     C                   if        start > 0
     C                   if        start <= 10
     C                   eval      wkcono = %subst(d1mcu:start:3)
     C                   else
     C                   eval      wkcono = %subst(d1mcu:start)
     C                   endif
     C                   endif
      *
      *
      * Call a JDE program to validate company/costcenter/account/subsidiary combination.
      *
     C                   call      'ARACTEDT'
     C                   parm                    wkcono
     C                   parm                    d1mcu
     C                   parm                    d1obj
     C                   parm                    d1sub
     C                   parm      *blank        xxactid
     C                   parm      *blank        xxpec
     C                   parm      *blank        xxdesc
     C                   parm      *blank        xxreturn
      *
     C                   if        xxreturn <> *blank
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09181'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Update record
      *----------------------------------------------------------------
      *
     C     $upd1         begsr
      *
      *
      * If user action is revising, determine that the
      * record he is accessing still exists prior to attempting to
      * update.  Lock the record for update.
      *
     C                   if        error = 0 and action = revise                If OK
     C     key01         chain     mpp103                             92
     C                   if        *in92 = *on                                  If not found
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
      *
     C                   endif                                                  If not found
     C                   endif                                                  If OK
      *
      * If there are no errors,
      * Move DSPF fields to database file fields and revise record
      *
     C                   if        error = 0                                    If no error
      *
      * Use the first 3 non-blank characters of the cost center field as the
      * company.
     C                   move      *blank        wkcono
     C                   z-add     0             start
      *
     C     ' '           check     d1mcu         start
     C                   if        start <> 0
     C                   eval      wkcono = %subst(d1mcu:start:3)
     C                   endif
      *
     C                   move      no            pderfl
     C                   move      wkcono        pdcono
     C                   move      d1mcu         pdmcu
     C                   move      d1obj         pdobj
     C                   move      d1sub         pdsub
     C                   move      d1com         pdcom
      *
     C                   update    pdrec
     C                   endif                                                  If no error
      *
      * Set next routine
      *
     C                   select
     C                   when      error <> 0
     C                   eval      routne = scrn1
     C                   other
      *
     C                   eval      routne = exit
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Set on last record indicator and end job
      *----------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxposn
     C                   kfld                    xxrcno
     C                   kfld                    xxserno
      *
      * Parm Lists
      *
     C     *entry        plist
     C     d1mode        parm                    mode
     C                   parm                    xxposn
     C     d1rcno        parm                    xxrcno
     C     d1serno       parm                    xxserno
     C                   parm                    rtncd
      *
     C                   eval      action = mode
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
