     A*%%TS  SD  19941207  144339  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD727
     A*  TITLE:         VIEW RATION ADDITIVES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/07/94
     A*
     A*  INDICATOR 90 IS ON WHEN THE RATION THE USER SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER
     A*               USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP727S1                   SFL
     A*%%TS  SD  19941207  112636  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFADCD    R        O  7  3REFFLD(RAADCD)
     A            DFADDS    R        O  7 13REFFLD(RAADDS)
     A            DFRAST    R        O  7 38REFFLD(RARAST)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP727C1                   SFLCTL(HP727S1)
     A*%%TS  SD  19941207  144339  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0015)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A            HDPAG1         4S 0H      SFLRCDNBR
     A                                  1  2'HP727'
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 25'VIEW RATION ADDITIVES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Ration ..:'
     A            C1RNCD    R        O  4 14REFFLD(RNRNCD)
     A            C1RNDS    R        O  4 21REFFLD(RNRNDS)
     A                                  6  2'Additive'
     A                                      DSPATR(HI)
     A                                  6 13'Description'
     A                                      DSPATR(HI)
     A                                  6 35'Status'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP727K1
     A*%%TS  SD  19940407  133822  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP727ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP727EC                   SFLCTL(HP727ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
