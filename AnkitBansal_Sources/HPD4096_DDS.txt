     A*%%TS  SD  20090813  100028  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4096
     A*  TITLE:         SPECIFY PRODUCTION PHASE/GROUP TYPE CHANGE
     A*  PROGRAMMER:    BARB GUTIERREZ
     A*  DATE:          11/11/03
     A*
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF08(08 'SUBMIT')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP4096R1
     A*%%TS  SD  20090813  100028  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'SPECIFY GROUP CHANGES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4 10'Hog group code ..'
     A                                  4 28'?'
     A            D1HGCD    R        B  4 30REFFLD(@@HGCD)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30
     AO 50                                  DSPATR(PC)
     A                                  4 41'Hog group number:'
     A            D1HGSN    R        O  4 59REFFLD(@@HGSN)
     A                                      EDTCDE(M)
     A                                  5 45'Group status:'
     A            D1GSCD    R        O  5 59REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A                                  6 48'Farm site:'
     A            D1FSCD    R        O  6 59REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                  7  3'Current production type:'
     A            D1CPTCD   R        O  7 30REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            D1CPTDS   R        O  7 38REFFLD(HSRREF/PTPTDS *LIBL/HSPREF)
     A                                  8  5'New production type ..'
     A                                  8 28'?'
     A            D1NPTCD   R        B  8 30REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34
     AO 54                                  DSPATR(PC)
     A            D1NPTDS   R        O  8 38REFFLD(HSRREF/PTPTDS *LIBL/HSPREF)
     A                                 10  2'Current production phase:'
     A            D1CPPCD   R        O 10 30REFFLD(@@PPCD)
     A            D1CPPDS   R        O 10 38REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A                                 11  4'New production phase ..'
     A                                 11 28'?'
     A            D1NPPCD   R        B 11 30REFFLD(@@PPCD)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A            D1NPPDS   R        O 11 38REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A                                 13  8'Current group type:'
     A            D1CGTCD   R        O 13 30REFFLD(@@GTCD)
     A            D1CGTDS   R        O 13 38REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                 14 10'New group type ..'
     A                                 14 28'?'
     A            D1NGTCD   R        B 14 30REFFLD(@@GTCD)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A            D1NGTDS   R        O 14 38REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                 16  2'Current stocking density:'
     A            D1CSDCD   R        O 16 30REFFLD(@@SDCD)
     A            D1CSDDS   R        O 16 38REFFLD(HSRREF/SDSDDS *LIBL/HSPREF)
     A                                 17  4'New stocking density ..'
     A                                 17 28'?'
     A            D1NSDCD   R        B 17 30REFFLD(@@SDCD)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A            D1NSDDS   R        O 17 38REFFLD(HSRREF/SDSDDS *LIBL/HSPREF)
     A                                 19  6'Current stayed/moved:'
     A            D1CSMCD   R        O 19 30REFFLD(@@SDCD)
     A            D1CSMDS   R        O 19 38REFFLD(HSRREF/SDSDDS *LIBL/HSPREF)
     A                                 20  8'New stayed/moved ..'
     A                                 20 28'?'
     A            D1NSMCD   R        B 20 30REFFLD(@@SDCD)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35
     AO 55                                  DSPATR(PC)
     A            D1NSMDS   R        O 20 38REFFLD(HSRREF/SDSDDS *LIBL/HSPREF)
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP4096K1
     A*%%TS  SD  20090811  155414  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F8=Interactive Call to the Change -
     A                                      Program'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP4096ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP4096EC                  SFLCTL(HP4096ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
