      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF228 - Eopfees: Excessive Freight Charge Responsibility Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/10/09
      *
      * Function:    This program prints a listing of Charges and the Responsible
      *              Company. It is not available on-demand; it is generated when
      *              loads are frozen (From the Revenue Close Function.)
      *
      ****************************************************************************************
      * MODIFICATIONS:
      ****************************************************************************************
      * DATE      PROGRAMMER
      *
      * 03/26/09  LeAnne Ramsey
      *           We will now plop the Carrier Payment Date from the Load Header into the
      *           Load Freight Charge record (TFS Period Processed Date).
      *
      * 06/03/09  LeAnne Ramsey
      *           As requested by Gary Martin, we will add a column to the report for
      *           the Load Unit of Measure (Load U/M) retrieved from the Load Header.
      *
      * 03/03/10  LeAnne Ramsey
      *           As requested by Gary Martin, we will add Carrier Paid Date from the
      *           Load Header.
      *
      * 10/26/20  Brad Baden   E16971
      *           Add new workfile TFP0271 to use for a Sequel ViewPoint to email the
      *           data to user.
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Ftfp027    if   e           k disk
      *  Freight variance--detail (records selected by open query)
      *
      *
     Ftfp0271   o    e           k disk
      *  Workfile: Freight variance--detail for Sequel ViewPoint
      *
      *
     Fomfjcpl1  if   e           k disk
      *  Load header
      *
      *
     Fpmdkcpl1  if   e           k disk
      *  Load freight charge reference (records selected by open query)
      *
      *
     Fpmeycpl1  if   e           k disk
      *  Load freight charge comment
      *
      *
     Fpobccpl1  uf   e           k disk
      *  Load freight charge
      *
      *
     Fppb5rel1  if   e           k disk
      *  OnTime reason codes
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arsam           s              9  2 dim(1)
     D artam           s              9  2 dim(1)
     D arshram         s              9  2 dim(1)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash198         s            198    inz(*all'-')
     d rtime           s              6  0
     d count           s              3  0


      * Control fields
      *
     D first           s              1    inz('Y')
     D svload          s                   like(fdload)
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkdt            s              7  0
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      *
      * Print fields
      *
     d l1asdtmdy       s              6  0
     d l1cpdtmdy       s              6  0
     d l1asdt8n        s              8  0
     d l1cpdt8n        s              8  0
     d l1sam           s                   like(bcc5vl)
     d l1tam           s                   like(bcc5vl)
     d l1shram         s                   like(bcc5vl)
     d l1r0sx          s                   like(fjr0sx)
     d l1hocd          s                   like(fjr4sx)
     d l1hods          s                   like(b5htt1)
     d xxsam           s                   like(bcc5vl)
     d xxtam           s                   like(bcc5vl)
     d xxshram         s                   like(bcc5vl)
     d xxhocd          s                   like(fjr4sx)
     d xxhods          s                   like(b5htt1)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldcyr                  2      5  0
     D  ldcpe                  6      7  0
      *
     D  ldcbdt                 8     15  0
     D  ldcbmdy               16     21  0
     D  ldcbsyn               22     28  0
      *
     D  ldcedt                29     36  0
     D  ldcemdy               37     42  0
     D  ldcesyn               43     49  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Print headings.
     C                   exsr      $h1hdr
      *
      * By the time you get to this program, you are processing loads that:
      *          1) Have a Date in the Period being processed
      *          2) Have a Carrier Payment Status of P=Paid
      *
     C     *loval        setll     tfp027
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp027                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * For each unique load, retrieve Excessive Freight Charges
      *
     C                   if        fdload <> svload
     C                   exsr      $charges
     C                   z-add     fdload        svload
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
     C                   exsr      $t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------------------------
      * Load Freight Charges
      *----------------------------------------------------------------------------------
      *
     C     $charges      begsr
      *
      * Get Actual Ship Date into MMDDYY format for printing
      *
     C     *iso          test(d)                 fdasdt                 92
     C                   if        *in92 = *off
     C                   move      fdasdt        wkisodate
     C     *mdy          move      wkisodate     l1asdtmdy
     C                   endif
      *
      * Retrieve Load Header info
      *
     C                   move      *blank        l1hocd
     C                   move      *blank        l1hods
     C                   move      *blank        l1r0sx
     C                   move      *blank        xxhocd
     C                   move      *blank        xxhods
     C                   z-add     0             wkdt
      *
     C     fdload        chain     omfjcpl1                           92
     C                   if        *in92 = *off                                 If load hit
     C                   move      fjr0sx        l1r0sx
     C                   z-add     fjpydt        wkdt
      *
      * Get the Carrier Paid Date into MMDDYY format for printing
      *
     C     *cymd         test(d)                 fjpydt                 92
     C                   if        *in92 = *off
     C     *cymd         move      fjpydt        wkisodate
     C     *mdy          move      wkisodate     l1cpdtmdy
     C                   else
     C                   z-add     0             l1cpdtmdy
     C                   endif
      *
      * If you have a HoldOver Reason, retrieve its description from the
      * OnTime Reason Codes file.
     C                   if        fjr4sx <> *blank                             If holdover
     C                   move      fjr4sx        l1hocd
     C                   move      fjr4sx        xxhocd
     C     fjr4sx        chain     ppb5rel1                           92
     C                   if        *in92 = *off
     C                   move(p)   b5htt1        l1hods
     C                   move(p)   b5htt1        xxhods
     C                   endif
     C                   endif                                                  If holdover
     C                   endif                                                  If load hit
      *
      * Set indicator to control printing Load data on the first detail
      * line for a load.
     C                   seton                                        87
      *
      * For each Load, process all Load Freight Charge records where:
      *  1) Include in Actual Freight = No
      *  2) Include in Total Freight  = Yes
      *  3) TF Printed = blank
      *
     C     fdload        setll     pobccpl1
      *
     C                   dou       *in91 = *on                                  Do charges
     C     fdload        reade     pobccpl1                               91
     C                   if        *in91 = *off and                             If not eof
     C                             bckbsx = no and
     C                             bckssx = yes and
     C                             bckhsx = *blank
      *
      * You have an "excessive" charge; so, find out who is responsible and
      * print.
      *
     C     key01         chain     pmdkcpl1                           92
     C                   if        *in92 = *off                                 If ref
     C                   move      no            first
      *
     C                   select
     C                   when      dkkbny = 360
     C                   z-add     bcc5vl        l1sam
     C                   add       bcc5vl        arsam
     C                   z-add     bcc5vl        xxsam
      *
     C                   when      dkkbny = 960
     C                   z-add     bcc5vl        l1tam
     C                   add       bcc5vl        artam
     C                   z-add     bcc5vl        xxtam
      *
     C                   when      dkkbny = 0
     C                   z-add     bcc5vl        l1shram
     C                   add       bcc5vl        arshram
     C                   z-add     bcc5vl        xxshram
     C                   endsl
      *
      * Print detail line
     C                   exsr      $l1dtl
      *
      *
      * Retrieve/print all comments for this Load/Charge Code/Sequence Number
      *
     C                   exsr      $comments
     C                   endif                                                  If ref
      *
      * Update the Load Freight Charge record as "processed" so that you will
      * never process it again.
      *
     C                   move      'P'           bckhsx
     C                   z-add     wkdt          bceqny
     C                   update    @bccpj8
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do charges
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------------------
      * Retrieve/print all comments for this Load/Charge Code/Sequence Number
      *----------------------------------------------------------------------------------
      *
     C     $comments     begsr
      *
     C     key01         setll     pmeycpl1
      *
     C                   dou       *in93 = *on                                  Do comments
     C     key01         reade     pmeycpl1                               93
     C                   if        *in93 = *off                                 If not eof
     C                   exsr      $l2dtl
     C                   endif                                                  If not eof
     C                   enddo                                                  Do comments
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------------------
      * Print detail line
      *----------------------------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      *
      * Brad Baden - E16971
      * Write detail record to file TFP0271
     C                   exsr      $wrt0271
      *
     C                   except    l1dtl
     C                   setoff                                       87
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------------------
      * Write detail line fields to work file TFP0271
      *----------------------------------------------------------------------------------
      *
     c     $wrt0271      begsr
      *
     c                   clear                   w1rec
      *
      ** Load All but Comment values
      *
     c                   eval      w1cono    = fdcono
     c                   eval      w1load    = fdload
     c                   eval      w1cacd    = fdcacd
     c                   eval      w1asdt    = fdasdt
     c                   eval      w1cpdt    = fjpydt + 19000000
     c                   eval      w1r0sx    = l1r0sx
     c                   eval      w1cdaa    = dkcdaa
     c                   eval      w1sam     = l1sam
     c                   eval      w1tam     = l1tam
     c                   eval      w1shram   = l1shram
     c                   eval      w1r4sx    = l1hocd
     c                   eval      w1htt1    = l1hods
     c
      *
      ** Retrieve 1st comment and include it on the workfile record
      *
     c                   eval      w1s3na    = *blanks                          Default to blanks
      *
     c     key01         setll     pmeycpl1
     c                   if        %equal(pmeycpl1)                             if %equal(pmeycpl1)
      *
     c     key01         reade     pmeycpl1                               93
     c                   if        %found(pmeycpl1)                             If %found(pmeycpl1)
     c                   eval      w1s3na = eys3na
     c                   else
     c                   eval      w1s3na = *blanks
     c                   endif                                                  fi %found(pmeycpl1)
      *
     c                   else
     c                   eval      w1s3na = *blanks
     c                   endif                                                  fi %equal(pymecpl1)
      *
      ** Write workfile record
      *
     c                   write     w1rec
      *
     c                   endsr
      /eject
      *----------------------------------------------------------------------------------
      * Print Comment line
      *----------------------------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l2dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings for Load Section
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
     C                   seton                                        87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Report Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   z-add     1             x
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    bcrmnb
     C                   kfld                    bccdaa
     C                   kfld                    bcepny
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           93 'EXCESSIVE FREIGHT CHARGE '
     O                                          114 'RESPONSIBILITY REPORT'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           96 'Paid Loads'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           18 'Year/Period:'
     O                       ldcyr         z     24
     O                       ldcpe         z     28
      *
     O          e            h1hdr          1
     O                                           18 'Period begin date:'
     O                       ldcbmdy             28 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           18 'Period end date:'
     O                       ldcemdy             28 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings for Section 1: Loads
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            7 'Company'
     O                                           15 'Load'
     O                                           24 'Carrier'
     O                                           34 'Actual'
     O                                           45 'Carrier'
     O                                           54 'Load'
     O                                           63 'Charge'
     O                                           78 'Seaboard'
     O                                           93 'Triumph'
     O                                          108 'Shared'
     O                                          127 'Held Over Reason'
      *
     O          e            h1hdr       0  1
     O                                            6 'Number'
     O                                           14 'ID'
     O                                           22 'Code'
     O                                           35 'Ship Date'
     O                                           46 'Paid Date'
     O                                           54 'U/M'
     O                                           61 'Code'
     O                                           78 'Amount'
     O                                           93 'Amount'
     O                                          108 'Amount'
     O                                          127 'Code/Description'
     O                                          146 'Comments'
      *
     O          e            h1hdr          0
     O                       dash198            198
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e    87      l1dtl       1
     O          e            l1dtl       1
     O                       fdcono        z      5
     O               87      fdload        z     15
     O                       fdcacd              22
     O                       l1asdtmdy           34 '  /  /  '
     O                       l1cpdtmdy           45 '  /  /  '
     O               87      l1r0sx              53
     O                       dkcdaa              63
     O                       l1sam         kb    79
     O                       l1tam         kb    94
     O                       l1shram       kb   109
     O               87      l1hocd         b   114
     O               87      l1hods         b   136
      *
      *
      *-------------------------------------------------------------
      * Comment line
      *-------------------------------------------------------------
      *
     O          e            l2dtl       1
     O                       eys3na             198
      *
      *-------------------------------------------------------------
      * Report total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       1  1
     O                                           78 '------------'
     O                                           93 '------------'
     O                                          108 '------------'
      *
     O          e            t1tot       0  1
     O                                           65 'Report Total:'
     O                       arsam   (x)   k     79
     O                       artam   (x)   k     94
     O                       arshram (x)   k    109
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           23 'There are no excessive '
     O                                           46 'charges for the period.'
      /eject
