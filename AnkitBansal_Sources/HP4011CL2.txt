/***************************************************************** */
/*                                                                 */
/*‚ PROGRAM NUMBER:  HP4011CL2                                     */
/*‚ PROGRAM NAME:    Submit HPS download Sire Line to Agview       */
/*‚ PROGRAMMER:      Jagdish Mistry                                */
/*‚ CREATE DATE:     08/20/24                                      */
/*                                                                 */
/*‚ FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*‚                  HP4011-WORK WITH WEEKLY SIRE LINE DOSES.      */
/*‚                  Business logic is same as HP40341CL           */
/*‚                                                                */
/*‚ PROGRAMMER:      Jagdish Mistry                                */
/*‚ CREATE DATE:     09/10/24                                      */
/*‚                  Populate data library                         */
/*                                                                 */
/*******************************************************************/
/*‚ MODIFIED:                                                      */
/*******************************************************************/
             PGM

             DCL        VAR(&IFSDIR) TYPE(*CHAR) LEN(48)
             DCL        VAR(&ACYR) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&ACPE) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&ACQR) TYPE(*DEC) LEN(1 0)
             DCL        VAR(&ACWK) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&PICDT) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&CDYR) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&CDWK) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&WBDTE) TYPE(*DEC) LEN(8 0)
             DCL        VAR(&WBDTEA) TYPE(*CHAR) LEN(8)
             DCL        VAR(&WEDTE) TYPE(*DEC) LEN(8 0)
             DCL        VAR(&WEDTEA) TYPE(*CHAR) LEN(8)
             DCL        VAR(&CURDTETME) TYPE(*CHAR) LEN(20)
             DCL        VAR(&CURDTETME2) TYPE(*CHAR) LEN(14)

             DCL        VAR(&DTALIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMLSBJ) TYPE(*CHAR) LEN(80)
             DCL        VAR(&EMLMSG) TYPE(*CHAR) LEN(200)
             DCL        VAR(&EMAILDL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&IFSDIR) TYPE(*CHAR) LEN(48)
             DCL        VAR(&FILENM) TYPE(*CHAR) LEN(30) +
                          VALUE('SIRELINELOG')
             DCL        VAR(&TOSTMF) TYPE(*CHAR) LEN(200)
             DCL        VAR(&MSGTXT) TYPE(*CHAR) LEN(150)
             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(100)
             DCL        VAR(&LDFWEDT) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTWEDT) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDFSCD)  TYPE(*DEC)  LEN(5)
             DCL        VAR(&LDSICD)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FLAG)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&TOTALCNT) TYPE(*DEC) LEN(8 0)
             DCL        VAR(&FILENAME) TYPE(*CHAR) LEN(30)

     /* Get System Value For Data File Library to Determine Whether +
               in PROD or TEST Environment */
             CALL       PGM(PPJXXFR) PARM(' ' 'DTALIB' &DTALIB)
/*-----------------------------------------------------------------*/
/*‚SET UP THE  DIRECTORY PATH                                      */
/*-----------------------------------------------------------------*/
             IF         COND(&DTALIB *NE 'PRKFLIB') THEN(DO)
             CHGVAR     VAR(&EMAILDL) VALUE('DL AGVIEW TEST')
             CHGVAR     VAR(&EMLSBJ) VALUE('HPS: HP40341CL-SIRE LINE +
                          LOG EXPORT FOR AGVIEW-TEST')

             CHGVAR     VAR(&IFSDIR) VALUE('/AGVIEW/TEST/')
             ENDDO
             ELSE       CMD(DO)
             CHGVAR     VAR(&EMAILDL) VALUE('DL AGVIEW')
             CHGVAR     VAR(&EMLSBJ) VALUE('HPS: HP40341CL-SIRE LINE +
                          LOG EXPORT FOR AGVIEW')

             CHGVAR     VAR(&IFSDIR) VALUE('/AGVIEW/PROD/')
             ENDDO
/*-----------------------------------------------------------------*/
/*‚CREATE THE WORKFILE                                             */
/*-----------------------------------------------------------------*/
             DLTF       FILE(QTEMP/HSP3034)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(HSP3034) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
/*-------------------------------------------------------------------*/
/*‚ GET WEEKS BEGINNING & ENDING DATE                                */
/*-------------------------------------------------------------------*/
             RTVSYSVAL  SYSVAL(QDATETIME) RTNVAR(&CURDTETME)

/*‚SET &CURDTETME2 AS CCYYMMDDHHMMSS FORMAT  */
             CHGVAR     VAR(&CURDTETME2) VALUE(%SST(&CURDTETME 1 14))

             RTVDTAARA  DTAARA(*LDA (67 8)) RTNVAR(&WEDTEA)
             CHGVAR     VAR(&WEDTE) VALUE(&WEDTEA)
             CALL       PGM(HP8017) PARM((&WEDTE) (&ACYR) (&ACPE) +
                          (&ACQR) (&ACWK) (&PICDT) (&CDYR) (&CDWK))

             CALL       PGM(HP8016) PARM(&CDYR &CDWK &WBDTE &WEDTE)

             CHGVAR     VAR(&WBDTEA) VALUE(&WBDTE)
             CHGVAR     VAR(&WEDTEA) VALUE(&WEDTE)
/*-------------------------------------------------------------------*/
/*‚BUILD/RUN QUERY OVER FILE: WEEKLY SIRE LINE DOSES                 */
/*-------------------------------------------------------------------*/
/*‚ALWAYS SELECT ONLY RECORDS WITH A WEEK-ENDING DATE IN THE SELECTED */
/*‚DATE RANGE.                                                        */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SWWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&WBDTE)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SWWEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&WEDTE)

             OVRDBF     FILE(HSL081D) SHARE(*YES)
             OPNQRYF    FILE((HSL081D)) FORMAT(*FILE) +
                          QRYSLT(&QDATA) KEYFLD(*FILE) SEQONLY(*YES)
/*-----------------------------------------------------------------*/
/*‚BUILD THE WORKFILE                                              */
/*-----------------------------------------------------------------*/
             CALL       PGM(HP3034) PARM((&TOTALCNT))

             CLOF       OPNID(HSL081D)
             DLTOVR     FILE(HSL081D)
/*-------------------------------------------------------------------*/
/*‚DOWNLOAD                                                          */
/*-------------------------------------------------------------------*/
             IF         COND(&TOTALCNT *EQ 0) THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('Sire Line Download for +
                          Agview did not extract any data for the +
                          spreadsheet for Date Range:' *BCAT +
                          &WBDTEA *BCAT 'to' *BCAT &WEDTEA)
             ESNDMAIL   RECIPIENT(&EMAILDL) SUBJECT(&EMLSBJ) +
                          MSG(&MSGTXT)
             MONMSG     MSGID(CPF0000)
             ENDDO

             CHGVAR     VAR(&EMLMSG) VALUE('Date Range:' *BCAT +
                          &WBDTEA *BCAT 'to' *BCAT &WEDTEA)

             IF         COND(&TOTALCNT *GT 0) THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE('Date Range:' *BCAT +
                          &WBDTEA *BCAT 'to' *BCAT &WEDTEA)

/*‚JM-WI628-START-Send XLSX to user with Header                     */
             EXECUTE    SQL('SELECT * FROM HSP3034') PCFMT(*XLSX) +
                          REPLACE(*YES) RECIPIENT(&EMAILDL) +
                          EMLMSG(&EMLMSG) TEXT(&EMLSBJ)
             MONMSG     MSGID(CPF0000)

/*‚JM-WI628-Send CSV to AGVIEW without Header                      */
             CHGVAR     VAR(&FILENAME) VALUE(&FILENM *TCAT +
                          &CURDTETME2 *TCAT '.CSV')
             CHGVAR     VAR(&TOSTMF) VALUE(&IFSDIR *TCAT &FILENAME)
             EXECUTE    SQL('SELECT * FROM HSP3034') +
                          PCFMT(*DELIMITED) TOSTMF(&TOSTMF) +
                          REPLACE(*YES)

             MONMSG     MSGID(CPF0000)
             ENDDO

             DLTF       FILE(QTEMP/HSP3034)
             MONMSG     MSGID(CPF0000)

             ENDPGM
