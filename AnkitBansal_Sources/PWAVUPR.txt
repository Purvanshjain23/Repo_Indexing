     H*****************************************************************
     H*
     H*  Program number:  PWAVUPR
     H*  Program name:    Create GL interface header for the as400 company in parm
     H*  Programmer:      Rose Centonze
     H*  Date created:    01/16/2017
     H*  Program purpose: This program will create the M3 GL interface header for the company
     H*                   in the parm if theres isnt one already in the file in QTEMP,
     H*                   and return the M3 Co|M3 div|M3 Keyvalue for the batch.
     H*                   This is called from the pgm that creates the GL Interface Detail.
      *                   M3 template:  GL_MSCUPL
     H*
     H*                   The user will need to go into M3 and post
     H*                   this batch of journal entries using GLS840
     H*   JDE F0011 is the batch header -->  write M3 PLB9CPP
     H*   JDE F9011 is the batch detail -->  write M3 PLCACPP
     H***
     H*****************************************************************
      * MODIFICATIONS
     H*****************************************************************
      *
     Fplb9cpl2  if   e           k disk
      *   M3 GL Header Staging file   Select not processed
     Fplb9cpp   uf a e             disk
      *   M3 GL Header Staging file
     F*
      * Workfields
      *
     d wkuser          s                   like(b9aavn)
     d wkdesc          s             40    inz('GL MISC UPL      ')
     d wktemp          s             15    inz('GL_MSCUPL')
     D*
     D***  RETRIEVE USER I.D. AND WORKSTATION I.D.
     D*
     D                SDS
     D  PGMSGQ           *PROC
     D  WRKSTN               244    253
     D  USRNAM               254    263
      *
      * Work fields for date manipulation
      *
     D wkyymmdd        s              6  0
     d wksyssyndt      s              7  0
     d wkhhmmss        s              6  0
      *
     d wktime          s             12  0
     D wkcymdiso       s               d   datfmt(*iso)
      *
      * Parm fields
      *
     D*
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      * Workfields
     D*
     D***  m3 key value
     D*
     D                 DS
     D  m3keyvalue             1     15
     D  m3keydt                1      8
     D  m3series               9     12
      *
      *---------------------------------------------------------------
      *
     C*
     C*
     C/EJECT
     C*----------------------------------------------------------------
     C* MAIN PROCESSING LOOP
     C*----------------------------------------------------------------
     C*
     C*
     c***********
     C*
     c* See if header exists in QTEMP for co/division.... if not then create it
     C*
     c     aco           cabeq     0             quit
     C*
     c                   exsr      $newbreak                                    get m3cono m3divi
      *
     c                   move      'N'           createnew         1
     C     m3hkey        chain     @b9cpw5                            80
     c     *in80         ifeq      '1'
     c                   move      'Y'           createnew
     c                   seton                                        LR
     c                   else
     c                   movel     b9ait1        p1keyvalue
     c                   endif

     C*
     c* WRITE RECORD TO  M3 GL Interface Header  -- unique key value
     C*
     c     createnew     ifeq      'Y'
     c                   exsr      $getbatchno                                  new run number
     C                   exsr      $writebatch                                  write header M3
     c                   endif
     C*
     C*
     c     quit          tag
     C                   MOVE      *ON           *INLR
     C/EJECT
     C***************************************************************
     C* SUBROUTINE SECTION
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*
     C*EJECT
      *---------------------------------------------------------------
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*   WRITE Batch Header Record     M3 PLB9CPP
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*
     C     $writebatch   BEGSR
     C*
     C     m3keyvalue    IFNE      *blanks                                     keyvalue not blank
     C                   z-add     m3cono        b9wja1                         company
     C                   movel     m3divi        b9qxcd                         division
     c                   movel     m3keyvalue    b9ait1                         key: yyyymmddnnnn
     c                   movel     m3keyvalue    p1keyvalue       15            key: yyyymmddnnnn
     C                   movel(p)  wktemp        b9ajt1                         interface template
     c     m3deschdr     ifeq      *blanks
     c                   movel(p)  wkdesc        b9akt1
     c                   else
     C                   movel(p)  m3deschdr     b9akt1                         Description
     C                   ENDIF                                                  BATCH# NOT 0
     C                   movel(p)  svusrnam      b9abvn                         User
     C                   move      wkccyymmdd    b9aedx                         M3 Entry Date
     C                   move      wksyssyndt    b9abdt                         date added
     C                   move      wkhhmmss      b9abtm                         time added
     C                   move      '0'           b9g5ss                         erp process sts
     C                   movel     sdpgm         b9agvn                         program updated
      *
     C                   write     @b9cpuj
     C                   ENDIF                                                  BATCH# NOT 0
     C                   ENDSR
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*   Break Logic - setup save fields, re-set accumulators
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*
     C     $newbreak     BEGSR
      *
      * RETRIEVE M3 COMPANY AND DIVISION EQUIVS
      *
     C                   CALL      'PDHGXFR'
     C                   PARM                    RTNCDE
     C                   PARM                    ACO                           COMP
     C                   PARM      'M3COMPANY '  KMV8CD           10            DESC
     C                   PARM                    KMM8NB           15 5          VALUE
     c                   z-add     kmm8nb        m3cono            3 0
     C                   CALL      'POMTXFR'
     C                   PARM                    RTNCDE
     C                   PARM                    ACO
     C                   PARM      'M3DIVISION'  KMV8CD           10            DESC
     C                   PARM                    kmbxtx           40            DIVISION
     c                   movel     kmbxtx        m3divi            3
     C*
     C                   ENDSR
     C/EJECT
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*   get JDE Batch #'s
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*
     C     $getbatchno   BEGSR
     C*
      * CALL PGM TO GENERATE NEXT AVAILABLE BATCH # FROM M3
      *    get next voucher number from M3 voucher number series
     C                   call      'PLW8XFR'
     C                   PARM                    RTNCDE            7
     C                   PARM      'ARS'         P1Q4CD            3            vchr nbr series
     C                   PARM                    P1WVA1            8 0          NEXT NUMBER
     C                   Z-ADD     P1WVA1        m3seriez          4 0          BATCH NUM
     C                   move      m3seriez      m3series          4            BATCH NUM
     C                   movel     m3series      m3series9         9            for Run Number
     C*   Format M3 Key Value for this batch:   yyyymmmddnnnn where nnnn is the m3series #
     C*    example:  201506220001,  then next key for that day would be 201506220002 ,...
     c*
     c                   movel     wkccyymmdd    m3keydt
      *
     C                   ENDSR
     C*
     C/EJECT
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*   Initial Subroutine - -
     C*:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::*
     C*
     C     *inzsr        BEGSR
     C*
     C*----------------------------------------------------------------
     C*
     C* KEY USED FOR BATCH HEADER IN JDE
     C*
     C     *ENTRY        PLIST
     C                   PARM                    P0RTN             7
     C                   PARM                    aco               3 0       company   - input
     c                   PARM                    m3deschdr        40         description-input
     C                   PARM                    m3cono            3 0       company   - output
     C                   PARM                    m3divi                      division  - output
     c                   PARM                    p1keyvalue       15         key value -output
     C*
      *
     c                   movel     'PWAVUPR'     sdpgm            10
     c                   movel     usrnam        svusrnam         10
      *
     C     m3hkey        klist
     C                   kfld                    m3cono
     C                   kfld                    m3divi
      *
      * Put system date into an 8,0 workfield for updating the change date
      *
     C     *mdy          move      udate         wkcymdiso
     C                   move      wkcymdiso     wkccyymmdd        8 0
      *
      * Put system date into an 7,0 workfield for updating the synon tables
      *
     C                   z-add     wkccyymmdd    wkyymmdd
     C                   if        wkccyymmdd >= 20000000
     C     wkyymmdd      add       1000000       wksyssyndt
     C                   else
     C                   z-add     wkyymmdd      wksyssyndt
     C                   endif
      *
      * Save the system time for populating fields.
      *
     C                   time                    wkhhmmss
      *
      *
     c                   endsr
