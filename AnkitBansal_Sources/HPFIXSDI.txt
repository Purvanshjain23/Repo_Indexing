      ****************************************************************
      *
      *  Program ----->  HPFIXSDI  05/21/01
      *  Written by -->  Alice Brownfield
      *
      *  Purpose ----->  Remove duplicate records out of HPS files.  These
      *                  were old records in Safe Data's files that did not
      *                  get cleared prior to the new week being added.
      *
      *                  NOTE:  Must change the hard coded dates to be the
      *                         weekending date and the upload date that
      *                         needs to be deleted.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 07/25/05  Alice and Barb
      *           Changed hard coded dates and ran to fix duplicate
      *           postings on week end 7/23/05.
      * 11/20/06  Barb Gutierrez
      *           Changed hard coded dates and ran to fix duplicate
      *           postings on week end 11/18/2006
      * 03/14/23  Barb Gutierrez
      *           Changed hard coded dates to fix duplicate
      *           postings on week end 03/11/2023.
      * 02/09/24  Santosh Patil-Service Request-3003065,Project-SR3065
      *           When movement entries are deleted, then details
      *           to be logged in HSP080 as well.
      ****************************************************************
      *
     Fhsp058    UF   E           K DISK
      *  movement event file
      *
     FHSP068    UF   E           K DISK
      *   Daily Deads by movement #
      *
      * Project-SR3065-START
     FHSP080    O    E             DISK
      *   Deleted movements
      *
      * -------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D WK_PGMDS      ESDS                  EXTNAME(UTPGFR)
      *
      * TIME FORMAT FOR RETRIEVAL OF SYSTEM TIME FOR RECORD TIME STAMP
      *---------------------------------------------------------------
      *
     D                 DS
     D  WK_HHMMSS              1      6  0
     D  WK_HHMM                1      4  0
      *
      * ---------------------------------
     C                   Z-ADD     UDATE         PFRM8
     C                   EXSR      $DATE
      *
     C     PRTRN         IFEQ      *BLANK
     C                   Z-ADD     PTO8          WKUDAT            8 0
     C                   ELSE
     C                   Z-ADD     0             WKUDAT
     C                   ENDIF
      * Project-SR3065-END

      * Get all of the movements in the event file that are double posted
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      hsp058                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
     C     memtcd        andeq     'D'
     C     memedt        andeq     20230311                                     week end
     C     meupdt        andeq     20230313                                     upload
     C     meuscd        andeq     'Upload'
      *
      * Project-SR3065-START
     C                   Clear                   DMREC
     C                   Move      MEMVSN        DMMVSN
     C                   Move      MEMTCD        DMMTCD
     C                   MOVEL     SDUSR         DMUSCD
     C                   TIME                    WK_HHMMSS
     C                   Z-ADD     WK_HHMM       DMDMTM
     C                   Z-ADD     WKUDAT        DMDMDT
      * Project-SR3065-END
     C     memvsn        setll     hsp068
     C     *IN91         doueq     *On                                          IF NOT EOF
     C     memvsn        READe     hsp068                                 91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      * Project-SR3065-START
     C                   Move      KDMSCD        DMMSCD
     C                   Move      KDKDDT        DMSCKLDT
      * Project-SR3065-END
     C                   delete    kdrec
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
     C                   delete    merec
      * Project-SR3065-START
     C                   write     DMREC
      * Project-SR3065-END
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      *
      *
      * Project-SR3065-START
      *----------------------------------------------------------------
      * MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      * Project-SR3065-END
