     A*%%TS  SD  20080731  092827  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        MEAT COST
     A*  DISPLAY FILE:  ROD116
     A*  TITLE:         REVISE MEAT COST OVERRIDE
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       07/31/08
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 63 IS ON WHEN WE ARE PROTECTING EVERY FIELD EXCEPT STATUS
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62N63                            CF11(11 'DELETE')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO116R1
     A*%%TS  SD  20080731  092827  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(36)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(56)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'REVISE MEAT COST OVERRIDE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 43'Week-ending'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Effective date ...........'
     A            D1EFMDY   R     Y  B  5 31REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  61N30                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A            D1WEMDY   R     Y  O  5 45REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(BLU)
     A                                  7  2'Item code ................'
     A  61                              7 29'?'
     A            D1ITCD    R        B  7 31REFFLD(@@ITCD)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  61N31                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A            D1ITDS    R        O  7 43REFFLD(@@ITDS)
     A                                  8  2'Override with item code ..'
     A  61                              8 29'?'
     A            D1OVITCD  R        B  8 31REFFLD(@@ITCD)
     A                                      EDTCDE(M)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  61N32                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A            D1OVITDS  R        O  8 43REFFLD(@@ITDS)
     A                                  9  2'Active/inactive status ...'
     A N62                              9 29'?'
     A            D1AIST    R        B  9 31REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO116K1
     A  43                             22  2'Message program failed.   Call MIS-
     A                                       user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61N63                       23 34'F11=Delete'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO116ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R RO116EC                   SFLCTL(RO116ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
