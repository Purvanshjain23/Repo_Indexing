/***************************************************************** */
/*                                                                 */
/*  PROGRAM NAME:    HP458CL                                       */
/*  PROGRAMMER:      Brad Baden                                    */
/*  CREATE DATE:      3/07/2018                                    */
/*                                                                 */
/*  FUNCTION:        This CL will reset the DACOPY and DAHPSEDIT   */
/*                   data areas, and then call program HP458 to    */
/*                   update the Sales Header HSP084 and Sales      */
/*                   Detail HSP085 files.                          */
/*                                                                 */
/*                   Company 440 will have the data areas reset    */
/*                   and files HSP084 and HSP085 updated. Company  */
/*                   440 needs to start the Edit process with the  */
/*                   Shipped dates and quantites set to zero.      */
/*                                                                 */
/*                   Company 360 will only have the data areas     */
/*                   reset.  Guymon sends us the Sales Header and  */
/*                   Sales Detail with the Shipped information.    */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&COMPANY &KILLDATE)

/* DECLARE VARIABLES                                               */
             DCL        VAR(&COMPANY)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&KILLDATE)  TYPE(*CHAR) LEN(7)
             DCL        VAR(&MESSAGE)  TYPE(*CHAR) LEN(80)
             DCL        VAR(&GRPPRFL)  TYPE(*CHAR) LEN(10)

/*******************************************************************/
/*  Save the current library list                                  */
/*******************************************************************/
             PSHLIBL

/*********************************************************************/
/*  Retrieve the user's Group Profile.  Set the library list         */
/*  based upon the combination of the group profile and plant.       */
/*********************************************************************/

             RTVUSRPRF  GRPPRF(&GRPPRFL)
             SELECT
             WHEN       COND(&GRPPRFL = 'SBDPGMR ') THEN(DO)
             IF         COND(&COMPANY = '360') THEN(SETLIBL +
                          JOBD(PRKTSTGUP))
             IF         COND(&COMPANY = '440') THEN(SETLIBL +
                          JOBD(PRKTSTSTP))
             ENDDO

             OTHERWISE  CMD(DO)
             IF         COND(&COMPANY = '360') THEN(SETLIBL +
                          JOBD(PRKPRDGUY))
             IF         COND(&COMPANY = '440') THEN(SETLIBL +
                          JOBD(PRKPRDSTP))
             ENDDO

             ENDSELECT


/*******************************************************************/
/*  Do not process if Company Number is 000                        */
/*******************************************************************/
             IF         COND((&COMPANY = '000') *OR (&COMPANY = '   +
                          ')) THEN(DO)
             SNDPGMMSG  MSG('Company Number is not entered, HPRESET +
                          process not run.')
             GOTO       CMDLBL(ENDIT)
             ENDDO

/*******************************************************************/
/*  Reset data area DACOPY                                         */
/*******************************************************************/
             SELECT
             WHEN       COND(&COMPANY = '360') THEN(DO)
             CHGDTAARA  DTAARA(DACOPY (6 1)) VALUE(' ')
             ENDDO

             WHEN       COND(&COMPANY = '440') THEN(DO)
             CHGDTAARA  DTAARA(DACOPY (14 1)) VALUE(' ')
             ENDDO

             OTHERWISE  CMD(DO)
             CHGVAR     VAR(&MESSAGE) VALUE('Company ' *CAT +
                          &COMPANY *TCAT ' not setup to perform a +
                          reset.')
             SNDPGMMSG  MSG(&MESSAGE)
             GOTO       CMDLBL(ENDIT)
             ENDDO
             ENDSELECT

/*******************************************************************/
/*  Reset data area DAHPSEDIT                                      */
/*******************************************************************/
             SELECT
             WHEN       COND(&COMPANY = '360') THEN(DO)
             CHGDTAARA  DTAARA(DAHPSEDIT (6 1)) VALUE(' ')

             /*‚Exit program when Company is 360 */
             GOTO       CMDLBL(ENDIT)
             ENDDO

             WHEN       COND(&COMPANY = '440') THEN(DO)
             CHGDTAARA  DTAARA(DAHPSEDIT (15 1)) VALUE(' ')
             ENDDO

             OTHERWISE  CMD(DO)
             GOTO       CMDLBL(ENDIT)
             ENDDO
             ENDSELECT

/*******************************************************************/
/*  Call program HPSRESET to update values in the Sales Header and */
/*  Sales Detail files.                                            */
/*******************************************************************/
             /*‚HP458 only called for Company 440 */
             CALL       PGM(HP458) PARM(&COMPANY &KILLDATE)

/*********************************************************************/
/*  Restore saved library list in case this CL called interactively  */
/*********************************************************************/
ENDIT:       POPLIBL

             ENDPGM
