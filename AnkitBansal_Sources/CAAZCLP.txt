/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  CALL SYSTEM VALUES  MAINTENANCE PROGRAM AND         */
/*              UPDATE THE Y2DTFMA DATA AREA WITH MDY OR DMY        */
/*              DATE FORMAT. UPDATE THE YYCOTXA DATA AREA WITH      */
/*              ORGANIZATION NAME.                                  */
/*                                                                  */
/*  COMPANY   - DATAWRIGHT INC.                                     */
/*  SYSTEM    - IDEAS/400 CROSS APPLICATION                         */
/*  PROGRAM   - SYSTEM CONTROL SELECTIONS                           */
/*  USER NAME - LINDA MEINKE/DEBBIE HAMNING                         */
/*  DATE      - 02/13/91                                            */
/*  COPYRIGHT 1991 DATAWRIGHT INC.                                  */
/*                                                                  */
/*      PARAMETERS USED:                                            */
/*                                                                  */
/*          & DTFMT     - DATE FORMAT                               */
/*          &COMP       - COMPANY                                   */
/*          &A          - TRUE FALSE VARIABLE                       */
/*          &LIBL       - LIBRARY LIST                              */
/*          &MSGRPY     - ERROR MESSAGE REPLY                       */
/*                                                                  */
/********************************************************************/

 CAAZCLP:    PGM
             DCL   &DTFMT       *CHAR   3      /* date format      */
             DCL   &NDTFMT      *CHAR   3      /* Date Format new  */
             DCL   &COMP        *CHAR   40     /* Org. Name        */
             DCL   &NORGNAM     *CHAR   40     /* NEW ORG. NAME    */
             DCL   &MSGRPY      *CHAR   1


/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000)

/********************************************************************/
/*   CHECK IF Y2DTFMA DATA AREA EXISTS                              */
/********************************************************************/

 CHKDT:      CHKOBJ     OBJ(Y2DTFMA) OBJTYPE(*DTAARA) /* date format +
                          data area */
             MONMSG     MSGID(CPF9801) EXEC(DO)
                          /* object not found */
             SNDUSRMSG  MSGID(CPF9898) +
                        MSGF(QCPFMSG) +
                        MSGDTA('Y2DTFMA date format data area cannot +
                                be located on you disk.  Enter +
                                a C to Cancel.') +
                        VALUES(C) +
                        MSGRPY(&MSGRPY)
             GOTO       CMDLBL(END)
             ENDDO

                RTVDTAARA  DTAARA(Y2DTFMA) RTNVAR(&DTFMT) /* retrieve +
                          date format from data area */

/********************************************************************/
/*   CHECK IF YYCOTXA DATA AREA EXISTS                              */
/********************************************************************/


 CHKON:      CHKOBJ     OBJ(YYCOTXA) OBJTYPE(*DTAARA) /* organization +
                          name data area */
             MONMSG     MSGID(CPF9801) EXEC(DO)
                          /* object not found */
             SNDUSRMSG  MSGID(CPF9898) +
                        MSGF(QCPFMSG) +
                        MSGDTA('YYCOTXA Company name data area cannot +
                                be located on you disk.  Enter +
                                a C to Cancel.') +
                        VALUES(C) +
                        MSGRPY(&MSGRPY)
             GOTO       CMDLBL(END)
             ENDDO

             RTVDTAARA  DTAARA(YYCOTXA) RTNVAR(&COMP) /* retrieve +
                          organization name from data area */

/********************************************************************/
/*   CALL WORK WITH SYSTEM VALUES PROGRAM                           */
/********************************************************************/

             CALL       PGM(CAXLEFR) PARM('' &NORGNAM &NDTFMT)

/*********************************************************************/
/*   COMPARE TO SYSTEM VALUES TO SEE IF CHANGED                      */
/*********************************************************************/


 DATE:       IF         (&NDTFMT *NE '   ') THEN(DO)
             IF         COND(&NDTFMT *NE &DTFMT) THEN(DO) /* system +
                          control date format not equal to date +
                          format in data area */
                CHGDTAARA  DTAARA(Y2DTFMA) VALUE(&NDTFMT) /* new date +
                          format value */
             ENDDO
             ENDDO

 NAME:       IF         (&NORGNAM *NE '          ') THEN(DO)
             IF         COND(&NORGNAM *NE &COMP) THEN(DO) /* system +
                          control organization name not equal to +
                          organization name in data area */
             CHGDTAARA  DTAARA(YYCOTXA) VALUE(&NORGNAM) /* new +
                          organization name value */
             ENDDO
             ENDDO

END:         ENDPGM
