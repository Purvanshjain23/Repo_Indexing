      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF609
      * TITLE:       Revenue Report by GL Sub
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/13/06
      *
      * FUNCTION:  Batch program to print a Revenue Report by Gl Sub.
      *            This report is generated:
      *                1) on-demand
      *                2) automatically from the Weekly Product Revenue Close function
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/11/07  LeAnne Ramsey
      *           Rewrote for new CoOwned stuff. We now have 2 layouts:
      *                1) CoOwned
      *                2) Not CoOwned
      *
      * 07/03/07  LeAnne Ramsey
      *           Rewrote again for new CoOwned stuff. The users have changed
      *           their minds; we no longer need 2 layouts. We are going back
      *           to a single layout; but, we will still print in 2 sections.
      *                1) CoOwned
      *                2) Not CoOwned
      *
      * 07/10/07  LeAnne Ramsey
      *           Fixed bug. If the Report total hit page overflow, the heading logic
      *           went to a new page and printed both the report headings and the
      *           "CoOwned/Not CoOwned" heading. Changed logic to not print the
      *           "CoOwned/Not CoOwned" heading.
      *
      * 07/20/09  LeAnne Ramsey
      *           Added logic for 'Affiliated Sales' (aka: Contract Sales).
      *
      * 12/01/17  Danny Nguyen  (R12011A-Weekly Product Revenue)
      *           Added STF fields to TFP308 & TFP309 files.
      *           Recompile only. No logic change.
      *           STF (Company 440) data will now be populated into it's own STF fields
      *           in order to create new STF reports like the ones used in Fees & Payments
      *           ystem.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp308    if   e           k disk
      *    Workfile: Revenue Report by GL Sub--Product Summary
      *
      *
     Ftfl308a   if   e           k disk    rename(w2rec:w2reca)
      *    Workfile: Revenue Report by GL Sub--Product Summary
      *       (LF selects normal/regular records...Affiliated Sales Company is zero)
      *
      *
     Ftfp309    if   e           k disk
      *    Workfile: Revenue Report by GL Sub--GL Sub Summary
      *
      *
     Ftfl309a   if   e           k disk    rename(w3rec:w3reca)
      *    Workfile: Revenue Report by GL Sub--GL Sub Summary
      *       (LF selects normal/regular records...Affiliated Sales Company is zero)
      *
      *
     Ftfl309b   if   e           k disk    rename(w3rec:w3recb)
      *    Workfile: Revenue Report by GL Sub--GL Sub Summary
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ************************************************************************************
      * DEFINITION SPECIFICATIONS
      ************************************************************************************
      *
      ************************************************************************************
      * CONSTANTS
      ************************************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D firstreg        s              1    inz('Y')
     D firstcon        s              1    inz('Y')
     D svglsub         s                   like(w2glsub)
     D svdesc          s                   like(w2desc)
     D svcoownfl       s                   like(w2coownfl)
     D svascono        s                   like(w2ascono)
     D count           s              3  0
      *
      *
      * Workfields
      *
     d wkascono        s                   like(w3ascono) inz(0)
     d wkalphcono      s              3
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Array index
      *
     d x               s              1  0 inz(1)
      *
      *
      * Print fields
      *
     D h1fwemdy        s              6  0
     D h1twemdy        s              6  0
     d h1demand        s             25
     d h1dsds          s              7
     d h1zero          s             50
     d h1yn            s              3
     d h1cods          s             11
     d h2desc          s             27
      *
     d l1desc          s             20
      *
     d t1desc          s             26
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arssllb         s             11  2 dim(2)
     D arsnsam         s             11  2 dim(2)
     D arsnpram        s             11  2 dim(2)
     D arsnjam         s             11  2 dim(2)
      *
     D artsllb         s             11  2 dim(2)
     D artnsam         s             11  2 dim(2)
     D artnpram        s             11  2 dim(2)
     D artnjam         s             11  2 dim(2)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
      * Do not use the first 38 LDA positions. We have to generate this from the Revenue
      * Close function as well as on-demand from this program. So, the LDAs have to
      * match up (The Revenue Close function is already using the first 38 positions.)
      *
     D  ldpfcd                37     37
      *
     D  ldfmdy                39     44  0
     D  ldtmdy                45     50  0
     D  ldfwedt               52     59  0
     D  ldtwedt               60     67  0
      *
     D  lddsfl                71     71
      *
     D  lddemand             300    300
      *
     D  ldzerofl             328    328
     D  ldcofl               329    329
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Depending on the user's selections, you may print the Detail or the Summary
      * or both the Detail and Summary.
      * NOTE: As of July 2009 we have new "affiliated sales" (aka: Contract Sales) data/logic.
      *       So, print the regular/normal stuff followed by the affiliated sales stuff.
      *
     C                   select
     C                   when      lddsfl = 'D'
     C                   exsr      $dtlreg
     C                   exsr      $dtlcon
      *
     C                   when      lddsfl = 'S'
     C                   exsr      $sumreg
     C                   exsr      $sumcon
      *
     C                   when      lddsfl = 'B'
     C                   exsr      $dtlreg
     C                   exsr      $dtlcon
     C                   exsr      $sumreg
     C                   exsr      $sumcon
     C                   endsl
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        firstreg = yes and firstcon = yes
     C                   exsr      $h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Normal/Regular: Detail Sections
      *--------------------------------------------------------------------------------------
      *
     C     $dtlreg       begsr
      *
     C                   seton                                        87
     C                   eval      h1dsds = 'DETAIL '
      *
      * This LF selects only the records where Affiliated Sales Company is
      * zero. SO, that gives us only the Regular/Normal records.
      *
     C     *start        setll     tfl308a
      *
     C                   dou       *in91 = *on                                  Do regular dtl
     C                   read      tfl308a                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      firstreg = yes
     C                   move      no            firstreg
     C                   move      w2coownfl     svcoownfl
     C                   move      w2glsub       svglsub
     C                   move      w2desc        svdesc
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
      *
     C                   when      w2coownfl <> svcoownfl
     C                   exsr      $rtv309
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   move      w2coownfl     svcoownfl
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   exsr      $h2hdr
      *
     C                   when      w2glsub <> svglsub
     C                   exsr      $rtv309
     C                   endsl
      *
      * Print detail line
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do regular dtl
      *
      *-----------------------------------
      * End of Regular Detail Processing
      *-----------------------------------
      *
     C                   if        firstreg = no                                If first
      * Print GL Sub Total
     C                   exsr      $rtv309
      *
      * Print Total for CoOwned OR Not CoOwned...whichever you just finished.
      *
     C                   z-add     1             x
     C                   exsr      $t1tot
      *
      * Print Combined Total of CoOwned+Not CoOwned (if user selected Both)
      *
     C                   if        ldcofl = 'B'
     C                   evalr     t1desc = 'CoOwned/Not CoOwned Total:'
     C                   z-add     2             x
     C                   exsr      $t1tot
     C                   endif
     C                   endif                                                  If first
      *
      * Always clear the arrays.
      *
     C                   exsr      $clear
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Contract Sales: Detail (aka: Affiliated Sales)
      *--------------------------------------------------------------------------------------
      *
     C     $dtlcon       begsr
      *
     C     wkascono      setgt     tfp308
      *
     C                   dou       *in91 = *on                                  Do affiliated dtl
     C                   read      tfp308                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      firstcon = yes
     C                   move      no            firstcon
     C                   z-add     w2ascono      svascono
     C                   move      w2glsub       svglsub
     C                   move      w2desc        svdesc
     C                   if        *inof = *on or firstreg = yes
     C                   exsr      $h1hdr
     C                   endif
     C                   exsr      $h2hdr
     C                   exsr      $h3hdr
      *
     C                   when      w2ascono <> svascono
     C                   exsr      $rtv309
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   z-add     w2ascono      svascono
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
     C                   exsr      $h3hdr
      *
     C                   when      w2glsub <> svglsub
     C                   exsr      $rtv309
     C                   endsl
      *
      * Print detail line
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do affiliated dtl
      *
      *---------------------------------
      * End of Affiliated Sales Detail
      *---------------------------------
      *
     C                   if        firstcon = no                                If first
      * Print GL Sub Total
     C                   exsr      $rtv309
      *
      * Print Total for Affiliated Sales Company
      *
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   endif                                                  If first
      *
      * Always clear the arrays.
      *
     C                   exsr      $clear
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------------------------------
      * Retrieve/print the GL Sub Summary record for the "Total" lines in the Detail Section
      *----------------------------------------------------------------------------------------
      *
     C     $rtv309       begsr
      *
      * Retrieve GL Sub Totals using a key of saved:
      *  Affiliated Sales Company
      *  CoOwned Flag
      *  GL Sub
      *
     C     key01         chain     tfp309                             92
     C                   if        *in92 = *off                                 If hit
      *
      * Accumulate values into arrays
      *
     C                   exsr      $accum
      *
      * Print GL Sub Total
     C                   if        count > 1
     C                   exsr      $l1sum
     C                   endif
     C                   endif                                                  If hit
      *
     C                   except    blank
     C                   move      w2glsub       svglsub
     C                   move      w2desc        svdesc
     C                   z-add     0             count
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------------------
      * Normal/Regular: Summary Section
      *-------------------------------------------------------------------------------------
      *
      * The Summary section prints one line for each GL Sub Code
      *
     C     $sumreg       begsr
      *
     C                   setoff                                       87
     C                   seton                                        of
     C                   move      yes           firstreg
     C                   move      *blank        t1desc
     C                   eval      h1dsds = 'SUMMARY'
      *
      * This LF selects only records where Affiliated Sales Company is zero; so,
      * this gives us the normal/regular records.....no affiliated/contract sales.
      *
     C     *loval        setll     tfl309a
      *
     C                   dou       *in91 = *on                                  Do reg summary
     C                   read      tfl309a                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      firstreg = yes
     C                   move      no            firstreg
     C                   move      w3coownfl     svcoownfl
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
      *
     C                   when      w3coownfl <> svcoownfl
     C                   except    blank
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   move      w3coownfl     svcoownfl
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   exsr      $h2hdr
     C                   endsl
      *
      * Accumulate values for section total
      *
     C                   exsr      $accum
      *
      * Print summary line
     C                   eval      l1desc = w3desc
     C                   exsr      $l1sum
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do regular sum
      *
      *------------------------------------------
      * End of Normal/Regular Summary Processing
      *------------------------------------------
      *
     C                   if        firstreg = no
      *
      * Print Total for CoOwned OR Not CoOwned...whichever you just finished.
      *
     C                   except    blank
     C                   z-add     1             x
     C                   exsr      $t1tot
      *
      * Print Combined Total of CoOwned+Not CoOwned (if user selected Both)
      *
     C                   if        ldcofl = 'B'
     C                   z-add     2             x
     C                   evalr     t1desc = 'CoOwned/Not CoOwned Total:'
     C                   exsr      $t1tot
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Contract: Summary
      *---------------------------------------------------------------
      *
      * The Summary section prints one line for each GL Sub Code for the
      * Affiliated Company
      *
     C     $sumcon       begsr
      *
     C                   move      yes           firstcon
     C                   move      *blank        t1desc
     C                   eval      h1dsds = 'SUMMARY'
      *
      * Since the Normal/Regular records always have zero in the Affiliated
      * Sales Company field, positioning GT 0 will get you to the first
      * Affiliated Sales reocord in the file.
      *
     C     wkascono      setgt     tfl309b
      *
     C                   dou       *in91 = *on                                  Do contract sum
     C                   read      tfl309b                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      firstcon = yes
     C                   move      no            firstcon
     C                   z-add     w3ascono      svascono
     C                   if        *inof = *on or firstreg = yes
     C                   exsr      $h1hdr
     C                   endif
     C                   exsr      $h2hdr
     C                   exsr      $h3hdr
      *
     C                   when      w3ascono <> svascono
     C                   except    blank
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   z-add     w3ascono      svascono
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
     C                   exsr      $h3hdr
     C                   endsl
      *
      * Accumulate values for section total
      *
     C                   exsr      $accum
      *
      * Print summary line
     C                   eval      l1desc = w3desc
     C                   exsr      $l1sum
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do contract sum
      *
      *------------------------------------------
      * End of Contract Summary Processing
      *------------------------------------------
      *
     C                   if        firstcon = no
      *
      * Print Total for Affiliated Sales Company
      *
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Accumulate values for Report Total line
      *---------------------------------------------------------------
      *
     C     $accum        begsr
      *
     C                   add       w3ssllb       arssllb
     C                   add       w3snpram      arsnpram
     C                   add       w3snsam       arsnsam
     C                   add       w3snjam       arsnjam
      *
     C                   add       w3tsllb       artsllb
     C                   add       w3tnpram      artnpram
     C                   add       w3tnsam       artnsam
     C                   add       w3tnjam       artnjam
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Report and Column Headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print on-page CoOwned/Not CoOwned/Affiliated Sales Headings
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
      * Normal/Regular
     C                   select
     C                   when      svascono = 0
     C                   if        svcoownfl = yes
     C                   eval      h2desc = 'CO-OWNED:'
     C                   evalr     t1desc = 'Co-Owned:'
     C                   else
     C                   eval      h2desc = 'NOT CO-OWNED:'
     C                   evalr     t1desc = 'Not Co-Owned:'
     C                   endif
      * Affiliated Sales
     C                   when      svascono <> 0
     C                   eval      h2desc = 'AFFILIATED SALES:'
     C                   endsl
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Affiliated Sales: Print on-page Company Heading
      *---------------------------------------------------------------
      *
     C     $h3hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    h3hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print the "detail" line on the Detail Report
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on                                  If overflow
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
      *
     C                   if        svascono <> 0
     C                   exsr      $h3hdr
     C                   endif
     C                   endif                                                  If overflow
      *
     C                   except    l1dtl
      *
     C                   add       1             count
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------------
      * Print the GL Sub line
      *-----------------------------------------------------------------------------------
      *
      * This subroutine prints the values for a GL Sub. It is executed from both the
      * "detail" section logic and the "summary" section logic.
      *  1) in the "Detail" section, it prints the "totals" for a GL Sub
      *  2) in the "Summary" section, it prints the GL Sub line
      *
     C     $l1sum        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    l1sum
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------
      * Print Section Totals (CoOwned/Not CoOwned or Affiliated Sales Company)
      *--------------------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on                                  If overflow
     C                   exsr      $h1hdr
      *
     C                   if        svascono <> 0
     C                   exsr      $h2hdr
     C                   exsr      $h3hdr
     C                   endif
     C                   endif                                                  If overflow
      *
      * For Affiliated Sales only
      *
     C                   if        svascono <> 0
     C                   move      svascono      wkalphcono
     C                   evalr     t1desc = 'Total for Company ' + wkalphcono +
     C                                      ':'
     C                   endif
      *
      * Print total line
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------
      * Clear arrays and save fields
      *--------------------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             arssllb
     C                   z-add     0             arsnsam
     C                   z-add     0             arsnpram
     C                   z-add     0             arsnjam
      *
     C                   z-add     0             artsllb
     C                   z-add     0             artnsam
     C                   z-add     0             artnpram
     C                   z-add     0             artnjam
      *
     C                   z-add     0             svascono
     C                   move      *blank        svglsub
     C                   z-add     0             count
     C                   move      *blank        svdesc
     C                   move      *blank        svcoownfl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    svascono
     C                   kfld                    svcoownfl
     C                   kfld                    svglsub
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Flip From/To Week-ending Dates into MMDDYY format for printing.
      *
     C     *iso          test(d)                 ldfwedt                92
     C                   if        *in92 = *off
     C                   move      ldfwedt       wkisodate
     C     *mdy          move      wkisodate     h1fwemdy
     C                   endif
      *
     C     *iso          test(d)                 ldtwedt                92
     C                   if        *in92 = *off
     C                   move      ldtwedt       wkisodate
     C     *mdy          move      wkisodate     h1twemdy
     C                   endif
      *
      * Set up heading fields:
      *
      * Include Zero Adjustment records
      *
     C                   select
     C                   when      ldzerofl = no
     C                   eval      h1yn = 'No '
      *
     C                   when      ldzerofl = yes
     C                   eval      h1yn = 'Yes'
     C                   endsl
      *
      * Co-Owned
     C                   select
     C                   when      ldcofl = 'C'
     C                   eval      h1cods = 'CoOwned'
      *
     C                   when      ldcofl = 'N'
     C                   eval      h1cods = 'Not CoOwned'
      *
     C                   when      ldcofl = 'B'
     C                   eval      h1cods = 'Both'
     C                   endsl
      *
      * This report is generated on-demand and also from the submitted
      * Weekly Revenue Close function.
      *
     C                   select
     C                   when      lddemand = yes
     C                   eval      h1demand = '        On-Demand        '
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1demand = '   Final Revenue Close   '
      *
     C                   when      ldpfcd = 'P'
     C                   eval      h1demand = 'Preliminary Revenue Close'
     C                   endsl
      *
     C                   eval      h1zero = 'Include records where ' +
     C                                      'adjustment dollars are zero?'
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          110 'REVENUE REPORT BY GL SUB'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1dsds             102
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       h1demand           111
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           10 'From date:'
     O                       ldfmdy              20 '  /  /  '
     O                                           40 'Week-ending date:'
     O                       h1fwemdy            50 '  /  /  '
      *
      *
     O          e            h1hdr          2
     O                                           10 'To date:'
     O                       ldtmdy              20 '  /  /  '
     O                                           40 'Week-ending date:'
     O                       h1twemdy            50 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           45 'Co-Owned/Not Co-Owned/'
     O                                           50 'Both:'
     O                       h1cods              63
      *
      *
     O          e            h1hdr          2
     O                       h1zero              50
     O                       h1yn                55
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           83 'SBF Net'
     O                                          151 'TF Net'
      *
     O          e            h1hdr       0  1
     O                                           66 'SBF'
     O                                           83 'Product'
     O                                          100 'SBF Net'
     O                                          117 'SBF Net'
     O                                          134 'TF'
     O                                          151 'Product'
     O                                          168 'TF Net'
     O                                          185 'TF Net'
      *
     O          e            h1hdr       0  1
     O                                            6 'GL Sub'
     O               87                          49 'Product'
     O                                           66 'Sold'
     O                                           83 'Revenue'
     O                                          100 'Sales'
     O                                          117 'Adjustment'
     O                                          134 'Sold'
     O                                          151 'Revenue'
     O                                          168 'Sales'
     O                                          185 'Adjustment'
      *
     O          e            h1hdr       0  1
     O                                            5 'Code'
     O                                           19 'Description'
     O               87                          47 'Code'
     O                                           66 'Pounds'
     O                                           83 'Amount'
     O                                          100 'Amount'
     O                                          117 'Amount'
     O                                          134 'Pounds'
     O                                          151 'Amount'
     O                                          168 'Amount'
     O                                          185 'Amount'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * On-page header for CoOwned/Not CoOwned/Affiliated Sales
      *-------------------------------------------------------------
      *
      *
     O          e            h2hdr       0  0
     O                       h2desc              27
      *
     O          e            h2hdr          1
     O                       h2desc              27
      *
      *
      *-------------------------------------------------------------
      * On-page header for Affiliated Sales
      *-------------------------------------------------------------
      *
     O          e            h3hdr       1  0
     O                                            8 'Company:'
     O                       svascono            13
      *
     O          e            h3hdr       0  1
     O                                            8 'Company:'
     O                       svascono            13
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       w2glsub              5
     O                       w2desc              38
     O                       w2prcd        z     49
     O
     O                       w2ssllb       k     67
     O                       w2snpram      k     84
     O                       w2snsam       k    101
     O                       w2snjam       k    118
      *
     O                       w2tsllb       k    135
     O                       w2tnpram      k    152
     O                       w2tnsam       k    169
     O                       w2tnjam       k    186
      *
      *-------------------------------------------------------------
      * Summary line
      *-------------------------------------------------------------
      *
     O          e    87      l1sum          1
     O                                           66 '--------------'
     O                                           83 '--------------'
     O                                          100 '--------------'
     O                                          117 '--------------'
      *
     O                                          134 '--------------'
     O                                          151 '--------------'
     O                                          168 '--------------'
     O                                          185 '--------------'
      *
     O          e            l1sum          1
      *
     O              n87      w3glsub              5
     O              n87      w3desc              38
     O                       w3ssllb       k     67
     O                       w3snpram      k     84
     O                       w3snsam       k    101
     O                       w3snjam       k    118
      *
     O                       w3tsllb       k    135
     O                       w3tnpram      k    152
     O                       w3tnsam       k    169
     O                       w3tnjam       k    186
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       1  0
     O                       t1desc              50
      *
     O          e            t1tot       0  2
     O                       t1desc              50
     O                       arssllb (x)   kb    67
     O                       arsnpram(x)   kb    84
     O                       arsnsam (x)   kb   101
     O                       arsnjam (x)   kb   118
      *
     O                       artsllb (x)   kb   135
     O                       artnpram(x)   kb   152
     O                       artnsam (x)   kb   169
     O                       artnjam (x)   kb   186
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
