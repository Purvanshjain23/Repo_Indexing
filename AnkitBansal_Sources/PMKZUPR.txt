     H/TITLE EUP Bld Broker Commission Execute user program
     H DATFMT(*YMD) DATEDIT(*YMD) DEBUG(*YES)
     Y* ADDLIBLE LIB(COMMON) POSITION(*LAST)
     Y* ADDLIBLE LIB(DLY029) POSITION(*LAST)
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *
     FJRCUSTS   if   e           k disk
      *  JRD Customer Masterfile AS/400 Format
      *
     FCSTMSTL1  if   e           k disk
      *  Active Customer Conversion - By DLYCST
      *
     FBRKMSTL1  if   e           k disk
      *  Broker Master XREF for Accrual Code - By DLYBRK
     FOMHLREL1  if   e           k disk
      *  Accrual Code Master       Retrieval index
      *
     FOMHOREL5  if   e           k disk    prefix(l5)
      *  Customer Accrual          Shipto/Acrl/Dte/Agr(d
      *
     FOMHOREL1  uf a e             disk
      *  Customer Accrual          Update index
      *
      * Data structures:
     D PGMDS         ESDS                  EXTNAME(Y2PGDSP)
      * Modified Program data structure
     D JBDTTM          DS
      * Job date/time
     D  ##JDT                  1      7  0
     D  ##JCC                  1      1  0
     D  ##JYY                  2      3  0
     D  ##JMM                  4      5  0
     D  ##JDD                  6      7  0
     D  ##JTM                  8     13  0
     D  ##JHH                  8      9  0
     D  ##JNN                 10     11  0
     D  ##JSS                 12     13  0
      *
      * Standalone fields
     Dcontinue                        1A
     IBRKMST
     I              ZIP                         BRKZIP
     I              DELETE                      BRKDELETE
      *
      /EJECT
      *****************************************************************
      * Initialize
     C                   EXSR      ZZINIT
      *
      * Process JRCUSTS - JRD Customer Masterfile AS/400 Format
      *
     C                   read      jrcust                                 95
     C                   dow       *in95 = *off                                 do loop
      *
      * If at least one broker code exists, check to see if customer
      * exists in the customer XREF file
     C                   if        broker > 0 OR broke2 > 0 OR broke3 > 0
      * Get Ship to Customer
     C                   exsr      $cstmst
      *
      * Continue processing if Customer found in CSTMST
     C                   if        continue = 'Y'
      *
     C* Process Broker 1
     C                   if        broker > 0
      *
     C                   eval      dlybrk=broker
      * Process Broker from JRD Customer Masterfile
     C                   exsr      $prcbrk
     C                   endif                                                  Broker 1
      *
      * Process Broker 2
     C                   if        broke2 > 0
      *
     C                   eval      dlybrk=broke2
      * Process Broker from JRD Customer Masterfile
     C                   exsr      $prcbrk
     C                   endif                                                  Broker 2
      *
      * Process Broker 3
     C                   if        broke3 > 0
      *
     C                   eval      dlybrk=broke3
      * Process Broker from JRD Customer Masterfile
     C                   exsr      $prcbrk
     C                   endif                                                  Broker 3
      *
     C                   endif                                                  Broker exists
      *
     C                   endif                                                  continue = 'Y'
      * Next record
     C                   read      jrcust                                 95
     C                   enddo                                                  do loop
      *
      *----------------------------------------------------------------
      * Exit program
      * Terminate program
     C                   SETON                                        LR
      *
      * Exit program
     C                   RETURN
      *
      *================================================================
      /EJECT
      *---------------------------------------------------------------
      * Get Ship to Customer
      *---------------------------------------------------------------
      *
     C     $cstmst       begsr
      *
      * Set default values for flags
     C                   eval      continue = 'N'
      *
     C     cust          chain     cstmst                             91
      *
      * If record found
     C                   if        *in91 = *off                                 91 = off
     C                   eval      hobkc7 = shipto                              Ship to Customer
     C                   eval      continue = 'Y'
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Process Broker from JRD Customer Masterfile
      *---------------------------------------------------------------
      *
     C     $prcbrk       begsr
      *
      * Get Dailys Accrual Code
     C                   exsr      $brkmst
      * Get Seaboard Accrual details
     C                   exsr      $acrmst
      * Create Customer Accrual
     C                   exsr      $cusacc
      * Create Customer Accrual Items
     C                   call      'PMK0XFR'
     C                   parm                    w0rtn             7            Return code
     C                   parm                    hobkc7                         Ship To Customer
     C                   parm                    hoojcd                         Accrual Code
     C                   parm                    hoguny                         CAH Sequence No
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Get Dailys Accrual Code
      *---------------------------------------------------------------
      *
     C     $brkmst       begsr
      *
     C     dlybrk        chain     brkmst
      *                                                               91
      * If record found
     C                   if        *in91 = *off                                 91 = off
     C                   eval      hoojcd = sbdacr                              Accrual Code
     C                   eval      hohhaa = sbdbrk                              Broker Code
      *
     C                   endif                                                  91 = off
      *
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Get Seaboard Accrual Code Master
      *---------------------------------------------------------------
      *
     C     $acrmst       begsr
      *
      * Get Accrual Code Master
     C     hoojcd        chain     @HLREK4
      *
      * If record found
     C                   if        *in91 = *off                                 91 = off
     C                   eval      hobkpr = hlbkpr                              Accrual Rate
     C                   eval      hottst = hlttst                              All Products (Y/N)
     C                   eval      hoshsx = hlshsx                              Accrual Rate Type
     C                   eval      hoqfsx = hlqfsx                              Accrual Dtl Search
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Create Customer Accrual
      *---------------------------------------------------------------
      *
     C     $cusacc       begsr
      *
      * Populate rest of fields
     C                   eval      hobfdt = 1071028                             Period Beginning Dte
     C                   eval      hoa2dt = 1101231                             Period Ending Date
     C                   eval      hoqdsx = 'AP'                                Comm Threshold Prd
     C                   eval      hopjdt = *zeros                              CAH Unused Date 1
     C                   eval      hopkdt = *zeros                              CAH Unused Date 2
     C                   eval      hoecvl = *zeros                              Comm Min Amt /Period
     C                   eval      hoqesx = *blanks                             Comm Retroactive sts
     C                   eval      hohqvl = *zeros                              Max Accrual Amount
     C                   eval      hoqgsx = '9'                                 Commission/Not Comm
     C                   eval      hoqhsx = *blanks                             CAH Unused sts 3
     C                   eval      hoqisx = *blanks                             CAH Unused sts 4
     C                   eval      hoqjsx = *blanks                             CAH Unused sts 5
     C                   eval      houvst = *blanks                             EDI status 1
     C                   eval      houwst = *blanks                             EDI status 2
     C                   eval      houxst = *blanks                             EDI status 3
     C                   eval      hoaatm = ##JTM                               Job Time
     C                   eval      hoayna = ##USR                               User Id
     C                   eval      hoa0na = ##JOB                               Job Name
     C                   eval      hoaxdt = ##JDT                               Job Date
      *
      * Get next sequence number
     C     key01         setll     @HOREVB
     C     key01         reade     @HOREVB                                95
     C                   if        *in95 = *on
      * First record for this group, set to 1
     C                   eval      hoguny = 1
     C                   else
      * Increment sequence number
     C                   eval      hoguny = l5hoguny + 1
     C                   endif
      *
     C                   write     @HORELB
      *
     C                   endsr
      /EJECT
     CSR   ZZINIT        BEGSR
      *================================================================
      * Initialisation
      *================================================================
      * Retrieve job attributes
     C                   CALL      'Y2RTJCR'
     C                   PARM                    PGMDS
      * Setup job date/time
     C                   Z-ADD     ##SD7         ##JDT
     C                   TIME                    ##JTM
      *
      * klist for Customer Accrual          Shipto/Acrl/Dte/Agr(d
     C     KEY01         klist
     C                   kfld                    hobkc7
     C                   kfld                    hoojcd
      *
     C                   eval        w0rtn  = *blanks
      *================================================================
     CSR   ZZEXIT        ENDSR
