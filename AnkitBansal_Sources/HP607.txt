      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP607
      * TITLE:       LISTING OF RATION TYPES
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     04/06/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           RATION TYPE MASTER.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      *
      * 12/02/94  LEANNE FEDOR
      *           REMOVED FIELDS FROM RATION TYPE FILE.
      *
      * 05/31/07  LeAnne Ramsey
      *           Some users are having printing problems. So, we are switching
      *           from QPRINT to PRINT132 for the printer file and replacing
      *           John Erikson's customized printer override command in the CL with
      *           the standard override command.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP070    IF   E           K DISK
      *    RATION TYPE             (KEY: RTRTCD)
      *
     Fprint132  O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C     *LOVAL        SETLL     HSP070
     C                   EXCEPT    HDRPT
      *
      * READ AND PRINT ALL RECORDS IN THE RATION TYPE MASTER
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP070                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
     C                   EXCEPT    DTTYPE
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oprint132  E            HDRPT          1 01
     O                                            5 'HP607'
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           49 'LISTING OF RATION TYPES'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HDRPT          2
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HDRPT          1
     O                                           11 'RATION TYPE'
     O                                           25 'DESCRIPTION'
     O                                           42 'STATUS'
      *
     O          E            HDRPT          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * RATION TYPE DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTTYPE      1
     O                       RTRTCD               5
     O                       RTRTDS              34
     O                       RTRTST              40
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /EJECT
