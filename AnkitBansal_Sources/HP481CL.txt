/***************************************************************** */
/*  ENVIRONMENT:       HOG PRODUCTION SYSTEM                       */
/*  PROGRAM NUMBER:    HP481CL                                     */
/*  PROGRAM NAME:      PRINT TRANSFER MOVEMENT BILL OF LADING      */
/*  PROGRAMMER:        LEANNE FEDOR                                */
/*  DATE:              02/14/96                                    */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/***************************************************************** */

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(275)

             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDMVSN) TYPE(*DEC) LEN(7)

             DCL        VAR(&LDORBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORPT) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORPP) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDORFS) TYPE(*DEC) LEN(5)

             DCL        VAR(&LDDNBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNPT) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNPP) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDDNFS) TYPE(*DEC) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDORBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDMVSN) VALUE(%SST(*LDA 27 7))

             CHGVAR     VAR(&LDORPT) VALUE(%SST(*LDA 34 5))
             CHGVAR     VAR(&LDORPP) VALUE(%SST(*LDA 39 5))
             CHGVAR     VAR(&LDORFS) VALUE(%SST(*LDA 44 5))

             CHGVAR     VAR(&LDDNBO) VALUE(%SST(*LDA 49 5))
             CHGVAR     VAR(&LDDNPT) VALUE(%SST(*LDA 54 5))
             CHGVAR     VAR(&LDDNPP) VALUE(%SST(*LDA 59 5))
             CHGVAR     VAR(&LDDNFS) VALUE(%SST(*LDA 64 5))

/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE TRANSFER MOVEMENT HEADER FILE               */
/*-------------------------------------------------------------------*/

/*  IF THE USER ELECTED TO SEE ONE SPECIFIC MOVEMENT NUMBER,      */
/*  SET THE QUERY UP FOR THAT MOVEMENT ONLY                       */

             IF         COND(&LDMVSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MHMVSN *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 7)) VALUE(&LDMVSN)
             ENDDO
             ELSE       CMD(DO)

/*  IF THE USER DID NOT LIMIT HIS SELECTION TO ONE MOVEMENT       */

/*  ALWAYS SELECT ONLY TRANSFER MOVEMENTS THAT MATCH THE USER'S  */
/*  SELECTED ORIGIN BUSINESSS OFFICE                             */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('MHORBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 5)) VALUE(&LDORBO)
             CHGVAR     VAR(%SST(&QDATA 18 1)) VALUE('"')

/*  ALWAYS SELECT ONLY SCHEDULED TRANSFER MOVEMENTS              */

             CHGVAR     VAR(%SST(&QDATA 20 20)) VALUE('*AND MHMSCD +
                          *EQ "SC"')

/*  ALWAYS SELECT ONLY TRANSFERS WITH A SCHEDULED SHIP DATE       */
/*  IN THE USERS REQUESTED DATE RANGE                             */

             CHGVAR     VAR(%SST(&QDATA 41 15)) VALUE('*AND MHSCDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 57 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 66 15)) VALUE('*AND MHSCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 82 8)) VALUE(&LDTYMD)


/*  ALWAYS SELECT ONLY TRANSFERS THAT HAVE NOT YET HAD A          */
/*  BILL OF LADING PRINTED                                        */

             CHGVAR     VAR(%SST(&QDATA 91 17)) VALUE('*AND MHPTCT +
                          *EQ 0')


/*  IF ORIGIN PRODUCTION TYPE                                      */

             IF         COND(&LDORPT *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 109 17)) VALUE('*AND MHORPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 126 5)) VALUE(&LDORPT)
             CHGVAR     VAR(%SST(&QDATA 131 1)) VALUE('"')
             ENDDO


/*  IF ORIGIN PRODUCTION PHASE                                     */

             IF         COND(&LDORPP *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 133 17)) VALUE('*AND MHORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 150 5)) VALUE(&LDORPP)
             CHGVAR     VAR(%SST(&QDATA 155 1)) VALUE('"')
             ENDDO

/*  IF ORIGIN FARM SITE                                          */

             IF         COND(&LDORFS *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 157 15)) VALUE('*AND MHORFS +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 173 5)) VALUE(&LDORFS)
             ENDDO


/*  IF DESTINATION BUSINESS OFFICE                               */

             IF         COND(&LDDNBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 179 17)) VALUE('*AND MHDNBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 196 5)) VALUE(&LDDNBO)
             CHGVAR     VAR(%SST(&QDATA 201 1)) VALUE('"')
             ENDDO


/*  IF DESTINATION PRODUCTION TYPE                                 */

             IF         COND(&LDDNPT *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 203 17)) VALUE('*AND MHDNPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 220 5)) VALUE(&LDDNPT)
             CHGVAR     VAR(%SST(&QDATA 225 1)) VALUE('"')
             ENDDO


/*  IF DESTINATION PRODUCTION PHASE                                */

             IF         COND(&LDDNPP *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 227 17)) VALUE('*AND MHDNPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 244 5)) VALUE(&LDDNPP)
             CHGVAR     VAR(%SST(&QDATA 249 1)) VALUE('"')
             ENDDO


/*  IF DESTINATION FARM SITE                                     */

             IF         COND(&LDDNFS *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 251 15)) VALUE('*AND MHDNFS +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 267 5)) VALUE(&LDDNFS)
             ENDDO

             ENDDO                            /* MULTIPLE MOVEMENTS */


/*-------------------------------------------------------------------*/
/* OVERRIDE THE TRANSFER MOVEMENT HEADER FILE                        */
/*-------------------------------------------------------------------*/

             OVRDBF     FILE(HSP074) SHARE(*YES)
             OPNQRYF    FILE((HSP074)) OPTION(*INP *UPD) +
                          QRYSLT(&QDATA) KEYFLD((MHORFS) +
                          (MHSCDT) (MHMVSN)) SEQONLY(*NO)

/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 OVRPRTF:    OVRPRTF    FILE(QPRINT) TOFILE(PRT8X11) OUTQ(&OUTQ) +
                          FORMTYPE(TRANSBOL) HOLD(&HOLD)

/*-------------------------------------------------------------------*/
/* PRINT THE BILL OF LADINGS                                         */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP681)

             DLTOVR     FILE(*ALL)

             ENDPGM

