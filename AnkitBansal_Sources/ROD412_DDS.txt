     A*%%TS  SD  20071227  105754  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD412
     A*  TITLE:         SPECIFY BYPRODUCT VALUES REPORT
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       01/19/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO412R1
     A*%%TS  SD  20071227  105754  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 23'SPECIFY BYPRODUCT VALUES REPORT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  5  2'Company ............'
     A            D1CONO    R        B  5 25REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  5 40'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5 59'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5 66'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  6  2'Date ...............'
     A            D1MDY     R        B  6 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O  6 43REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  6 59REFFLD(@@YR)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  6 67REFFLD(@@WK)
     A                                      EDTCDE(Z)
     A                                  7  2'Detail or summary ..'
     A                                  7 23'?'
     A            D1DSFL    R        B  7 25REFFLD(ROREF/@@FLAG *LIBL/ROPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A            D1DSDS        10A  O  7 29
     A                                 11  2'Optional selections for the DETAIL-
     A                                       version only:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                 12  2'Meat cost group code ....'
     A                                 12 28'?'
     A            D1CGCD    R        B 12 30REFFLD(ROREF/@@CGCD *LIBL/ROPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A            D1CGDS    R        O 12 39REFFLD(ROREF/@@ITDS *LIBL/ROPREF)
     A                                 12 70'(Blank=All)'
     A                                 13  9'--OR--'
     A                                      DSPATR(HI)
     A                                 14  2'Item code ...............'
     A                                 14 28'?'
     A            D1ITCD    R        B 14 30REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A            D1ITDS    R        O 14 39REFFLD(ROREF/@@ITDS *LIBL/ROPREF)
     A                                 14 70'(Blank=All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO412K1
     A*%%TS  SD  20060928  082122  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO412ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R RO412EC                   SFLCTL(RO412ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
