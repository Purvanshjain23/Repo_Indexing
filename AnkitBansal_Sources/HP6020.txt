      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6020
      * TITLE:       Listing of BGF Daily Production for a Farm Site
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/10/02
      *
      *
      * Function:    This batch listing prints one week of daily data for each
      *              selected farm. Each farm prints on a single page with 7 'daily'
      *              columns--1 for each day of the week.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/23/03  LeAnne Fedor
      *           Added fields:
      *               gilt total stillborns
      *               gilt total mummies
      *               17+ day weaned sows
      *               euthanized gilts
      *               euthanized sows
      *           Changed text on fields:
      *               from: gilts dead     to:  gilts dead naturally
      *               from: sows dead      to:  sows dead naturally
      *
      * 02/03/05  LeAnne Fedor
      *           Added field: Transferred in Open Gilts
      *
      * 10/19/07  LeAnne Ramsey
      *           The users are going to start entering the "average" manhours
      *           instead of the "total" manhours for a day. So, our logic
      *           for the "total" manhours for the week, was changed to be the
      *           "average" for the week.
      *
      * 08/04/08  LeAnne Ramsey
      *           Deleted fields: BDIOGLHD-Transferred in Open Gilts
      *                           BDOOGLHD-Gilts Transferred Out Open
      *           Deleted field:  BD17WNLT-17+ Day Weaned Sows
      *                           Replaced with a calc'd field: Total Weaned Sows
      *           Changed text:   BDEWNLT is now 0-15 Day Weaned Sows
      *                           BDWNLT  is now  16+ Day Weaned Sows
      *
      * 09/19/08  LeAnne Ramsey
      *           Recompile only.
      *           The users are switching "average manhours" back to "total manhours".
      *           So, removed the word "average" from the screen text.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm site (open query limits records to BGF farms)
      *
      *
     Fhsl082a   if   e           k disk
      *  BGF daily production  (open query limits records to single year/week)
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Print fields
      *
     D rtime           s              6  0
      *
     D h1fscd          s                   like(fsfscd)
     D h1fsnm          s                   like(fsfsnm)
     D d1adwn          s                   like(bdadwn)
     D d1manhr         s                   like(bdmanhr)
     D d1opglhd        s                   like(bdopglhd)
     D d1opsohd        s                   like(bdopsohd)
      *
      * Array index
      *
     d x               s              1  0
      *
      *
      *
      * Control fields and accumulators
      *
     D prtfl           s              1
     D gotsomefl       s              1    inz('N')
     D first           s              1    inz('y')
     D countage        s              1  0
     D counthr         s              1  0
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arwnsv          s              5  0 dim(8)
     D arlwnsv         s              5  0 dim(8)
     D arrtsv          s              5  0 dim(8)
     D arglsv          s              5  0 dim(8)
      *
     D aropglhd        s              5  0 dim(8)
     D aropsohd        s              5  0 dim(8)
      *
     D arabhd          s              5  0 dim(8)
     D arglabhd        s              5  0 dim(8)
     D arnipqt         s              5  0 dim(8)
      *
     D argllt          s              5  0 dim(8)
     D arglbahd        s              5  0 dim(8)
     D arglsbhd        s              5  0 dim(8)
     D arglmmhd        s              5  0 dim(8)
     D artolt          s              5  0 dim(8)
     D arbahd          s              5  0 dim(8)
      *
     D arsbhd          s              5  0 dim(8)
     D armmhd          s              5  0 dim(8)
      *
     D artolshd        s              5  0 dim(8)
      *
     D arewnlt         s              5  0 dim(8)
     D arwnlt          s              5  0 dim(8)
     D artotwnlt       s              5  0 dim(8)
     D artownhd        s              5  0 dim(8)
     D aradwn          s              3  1 dim(8)
      *
     D ararglhd        s              5  0 dim(8)
     D aribglhd        s              5  0 dim(8)
     D arslglhd        s              5  0 dim(8)
     D arddglhd        s              5  0 dim(8)
     D areuglhd        s              5  0 dim(8)
     D arobglhd        s              5  0 dim(8)
      *
     D ararsohd        s              5  0 dim(8)
     D aribsohd        s              5  0 dim(8)
     D arslsohd        s              5  0 dim(8)
     D arddsohd        s              5  0 dim(8)
     D areusohd        s              5  0 dim(8)
     D arobsohd        s              5  0 dim(8)
     D aroosohd        s              5  0 dim(8)
      *
     D ararbohd        s              5  0 dim(8)
     D arslbohd        s              5  0 dim(8)
     D arddbohd        s              5  0 dim(8)
      *
     D armanhr         s              7  2 dim(8)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard printer file data structure
      *---------------------------------------------------------------
      *    externally defined as UTPPFR (record format:FDBCKP)
     D prtds         e ds                  extname(utppfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfscd                 1      5  0
     D  ldcdyr                 6      9  0
     D  ldcdwk                10     11  0
     D  ldfsnm                13     37
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      *
     C                   select
      * One BGF farm
     C                   when      ldfscd <> 0
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   exsr      $farm
     C                   endif
     C                   other
      *
      * All BGF farms
      *
      * Before starting for 'all' farms, determine if there is any data for
      * the selected year/week.
      *
     C     *loval        setll     hsl082a
     C                   read      hsl082a                                92
     C                   if        *in92 = *off                                 If anything
      *
     C     *loval        setll     hsp018
     C                   dou       *in90 = *on                                  Do mult farm
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If read
     C                   exsr      $farm
     C                   endif                                                  If read
     C                   enddo                                                  Do mult farm
     C                   endif                                                  If anything
      *
     C                   endsl
      *
      *---------------------------------------------------------------
      * End of file processing
      *---------------------------------------------------------------
      *
      * If you did not have anything printed...nothing met the user's
      * selections, print the No Data message.
      *
     C                   if        gotsomefl = no
      *
     C                   if        ldfscd <> 0
     C                   z-add     ldfscd        h1fscd
     C                   move      ldfsnm        h1fsnm
     C                   endif
      *
     C                   except    h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Process the 7 daily records for the farm
      *---------------------------------------------------------------
      *
     C     $farm         begsr
      *
     C                   move      yes           first
     C                   z-add     fsfscd        h1fscd
     C                   move      fsfsnm        h1fsnm
      *
     C     fsfscd        setll     hsl082a
      *
     C                   dou       *in91 = *on                                  Do daily
     C     fsfscd        reade     hsl082a                                91
     C                   if        *in91 = *off                                 If not EOF
     C                   exsr      $bldarrays
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do daily
      *
     C                   exsr      $print
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Build arrays
      *---------------------------------------------------------------
      *
     C     $bldarrays    begsr
      *
     C                   move      yes           prtfl
      *
     C                   add       1             x
      *
     C                   z-add     bdwnsv        arwnsv(x)
     C                   z-add     bdlwnsv       arlwnsv(x)
     C                   z-add     bdrtsv        arrtsv(x)
     C                   z-add     bdglsv        arglsv(x)
      *
     C                   z-add     bdopglhd      aropglhd(x)
     C                   z-add     bdopglhd      d1opglhd
      *
     C                   z-add     bdopsohd      aropsohd(x)
     C                   z-add     bdopsohd      d1opsohd
      *
     C                   z-add     bdglabhd      arglabhd(x)
     C                   z-add     bdabhd        arabhd(x)
     C                   z-add     bdnipqt       arnipqt(x)
      *
     C                   z-add     bdgllt        argllt(x)
     C                   z-add     bdglbahd      arglbahd(x)
     C                   z-add     bdglsbhd      arglsbhd(x)
     C                   z-add     bdglmmhd      arglmmhd(x)
     C                   z-add     bdtolt        artolt(x)
      *
     C                   z-add     bdbahd        arbahd(x)
     C                   z-add     bdsbhd        arsbhd(x)
     C                   z-add     bdmmhd        armmhd(x)
      *
     C                   z-add     bdtolshd      artolshd(x)
     C                   z-add     bdewnlt       arewnlt(x)
     C                   z-add     bdwnlt        arwnlt(x)
     C                   z-add     bdewnlt       artotwnlt(x)
     C                   add       bdwnlt        artotwnlt(x)
     C                   z-add     bdtownhd      artownhd(x)
      *
     C                   z-add     bdadwn        aradwn(x)
     C                   if        bdadwn <> 0
     C                   add       1             countage
     C                   endif
      *
      *
     C                   z-add     bdarglhd      ararglhd(x)
     C                   z-add     bdibglhd      aribglhd(x)
     C                   z-add     bdslglhd      arslglhd(x)
     C                   z-add     bdddglhd      arddglhd(x)
     C                   z-add     bdeuglhd      areuglhd(x)
     C                   z-add     bdobglhd      arobglhd(x)
      *
     C                   z-add     bdarsohd      ararsohd(x)
     C                   z-add     bdibsohd      aribsohd(x)
     C                   z-add     bdslsohd      arslsohd(x)
     C                   z-add     bdddsohd      arddsohd(x)
     C                   z-add     bdeusohd      areusohd(x)
     C                   z-add     bdobsohd      arobsohd(x)
     C                   z-add     bdoosohd      aroosohd(x)
      *
     C                   z-add     bdarbohd      ararbohd(x)
     C                   z-add     bdslbohd      arslbohd(x)
     C                   z-add     bdddbohd      arddbohd(x)
      *
     C                   z-add     bdmanhr       armanhr(x)
     C                   if        bdmanhr <> 0
     C                   add       1             counthr
     C                   endif
      *
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print page for a farm
      *---------------------------------------------------------------
      *
     C     $print        begsr
      *
     C                   except    h1hdr
      *
     C                   select
     C                   when      prtfl = yes
     C                   exsr      $total
     C                   except    d1dtl
     C                   other
     C                   except    nodata
     C                   endsl
      *
     C                   move      yes           gotsomefl
      *
      * Clear/initialize for next group
      *
     C                   exsr      $clear
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Total arrays
      *---------------------------------------------------------------
      *
     C     $total        begsr
      *
     C                   z-add     8             x
      *
     C                   xfoot     arwnsv        arwnsv(x)
     C                   xfoot     arlwnsv       arlwnsv(x)
     C                   xfoot     arrtsv        arrtsv(x)
     C                   xfoot     arglsv        arglsv(x)
      *
     C                   xfoot     aropglhd      aropglhd(x)
     C                   xfoot     aropsohd      aropsohd(x)
      *
     C                   xfoot     arglabhd      arglabhd(x)
     C                   xfoot     arabhd        arabhd(x)
     C                   xfoot     arnipqt       arnipqt(x)
      *
     C                   xfoot     argllt        argllt(x)
     C                   xfoot     arglbahd      arglbahd(x)
     C                   xfoot     arglsbhd      arglsbhd(x)
     C                   xfoot     arglmmhd      arglmmhd(x)
     C                   xfoot     artolt        artolt(x)
      *
     C                   xfoot     arbahd        arbahd(x)
     C                   xfoot     arsbhd        arsbhd(x)
     C                   xfoot     armmhd        armmhd(x)
      *
     C                   xfoot     artolshd      artolshd(x)
     C                   xfoot     arewnlt       arewnlt(x)
     C                   xfoot     arwnlt        arwnlt(x)
     C                   xfoot     artotwnlt     artotwnlt(x)
     C                   xfoot     artownhd      artownhd(x)
      *
     C                   xfoot     aradwn        aradwn(x)
     C                   if        countage <> 0
     C     aradwn(x)     div(h)    countage      d1adwn
     C                   endif
      *
     C                   xfoot     ararglhd      ararglhd(x)
     C                   xfoot     aribglhd      aribglhd(x)
     C                   xfoot     arslglhd      arslglhd(x)
     C                   xfoot     arddglhd      arddglhd(x)
     C                   xfoot     areuglhd      areuglhd(x)
     C                   xfoot     arobglhd      arobglhd(x)
      *
     C                   xfoot     ararsohd      ararsohd(x)
     C                   xfoot     aribsohd      aribsohd(x)
     C                   xfoot     arslsohd      arslsohd(x)
     C                   xfoot     arddsohd      arddsohd(x)
     C                   xfoot     areusohd      areusohd(x)
     C                   xfoot     arobsohd      arobsohd(x)
     C                   xfoot     aroosohd      aroosohd(x)
      *
     C                   xfoot     ararbohd      ararbohd(x)
     C                   xfoot     arslbohd      arslbohd(x)
     C                   xfoot     arddbohd      arddbohd(x)
      *
     C                   xfoot     armanhr       armanhr(x)
     C                   if        counthr <> 0
     C     armanhr(x)    div(h)    counthr       d1manhr
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Clear arrays and initialize for next farm
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   move      no            prtfl
     C                   z-add     0             x
     C                   z-add     0             countage
     C                   z-add     0             counthr
      *
     C                   z-add     0             arwnsv
     C                   z-add     0             arlwnsv
     C                   z-add     0             arrtsv
     C                   z-add     0             arglsv
      *
     C                   z-add     0             aropglhd
     C                   z-add     0             aropsohd
      *
     C                   z-add     0             arglabhd
     C                   z-add     0             arabhd
     C                   z-add     0             arnipqt
      *
     C                   z-add     0             argllt
     C                   z-add     0             arglbahd
     C                   z-add     0             arglsbhd
     C                   z-add     0             arglmmhd
     C                   z-add     0             artolt
      *
     C                   z-add     0             arbahd
     C                   z-add     0             arsbhd
     C                   z-add     0             armmhd
      *
     C                   z-add     0             artolshd
     C                   z-add     0             arewnlt
     C                   z-add     0             arwnlt
     C                   z-add     0             artotwnlt
     C                   z-add     0             artownhd
      *
     C                   z-add     0             aradwn
      *
     C                   z-add     0             ararglhd
     C                   z-add     0             aribglhd
     C                   z-add     0             arslglhd
     C                   z-add     0             arddglhd
     C                   z-add     0             areuglhd
     C                   z-add     0             arobglhd
      *
     C                   z-add     0             ararsohd
     C                   z-add     0             aribsohd
     C                   z-add     0             arslsohd
     C                   z-add     0             arddsohd
     C                   z-add     0             areusohd
     C                   z-add     0             arobsohd
     C                   z-add     0             aroosohd
      *
     C                   z-add     0             ararbohd
     C                   z-add     0             arslbohd
     C                   z-add     0             arddbohd
      *
     C                   z-add     0             armanhr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report Headings
      *-------------------------------------------------------------
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           76 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           70 'LISTING OF BGF DAILY'
     O                                           81 ' PRODUCTION'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          1
     O                                           19 'Farm site:'
     O                       h1fscd        z     25
     O                       h1fsnm              52
      *
      *
     O          e            h1hdr          2
     O                                           19 'Calendar year/week:'
     O                       ldcdyr        z     25
     O                       ldcdwk        z     29
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
      *
     O          e            h1hdr          2
     O                                           45 'Sun'
     O                                           55 'Mon'
     O                                           65 'Tues'
     O                                           75 'Wed'
     O                                           85 'Thurs'
     O                                           95 'Fri'
     O                                          104 'Sat'
     O                                          116 'Totals'
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            d1dtl       1
     O                                           30 'Number of weaners'
     O                       arwnsv (1)    k     46
     O                       arwnsv (2)    k     56
     O                       arwnsv (3)    k     66
     O                       arwnsv (4)    k     76
     O                       arwnsv (5)    k     86
     O                       arwnsv (6)    k     96
     O                       arwnsv (7)    k    105
     O                       arwnsv (8)    k    117
      *
     O          e            d1dtl       1
     O                                           24 'Number of open sow pool '
     O                                           30 'served'
     O                       arlwnsv  (1)  k     46
     O                       arlwnsv  (2)  k     56
     O                       arlwnsv  (3)  k     66
     O                       arlwnsv  (4)  k     76
     O                       arlwnsv  (5)  k     86
     O                       arlwnsv  (6)  k     96
     O                       arlwnsv  (7)  k    105
     O                       arlwnsv  (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of returns'
     O                       arrtsv   (1)  k     46
     O                       arrtsv   (2)  k     56
     O                       arrtsv   (3)  k     66
     O                       arrtsv   (4)  k     76
     O                       arrtsv   (5)  k     86
     O                       arrtsv   (6)  k     96
     O                       arrtsv   (7)  k    105
     O                       arrtsv   (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of gilts'
     O                       arglsv   (1)  k     46
     O                       arglsv   (2)  k     56
     O                       arglsv   (3)  k     66
     O                       arglsv   (4)  k     76
     O                       arglsv   (5)  k     86
     O                       arglsv   (6)  k     96
     O                       arglsv   (7)  k    105
     O                       arglsv   (8)  k    117
      *
      *
      *
     O          e            d1dtl       1
     O                                           30 'Open gilt pool-eligible'
     O                       aropglhd (1)  k     46
     O                       aropglhd (2)  k     56
     O                       aropglhd (3)  k     66
     O                       aropglhd (4)  k     76
     O                       aropglhd (5)  k     86
     O                       aropglhd (6)  k     96
     O                       aropglhd (7)  k    105
     O                       d1opglhd      kb   117
      *
     O          e            d1dtl       1
     O                                           30 'Open sow pool'
     O                       aropsohd (1)  k     46
     O                       aropsohd (2)  k     56
     O                       aropsohd (3)  k     66
     O                       aropsohd (4)  k     76
     O                       aropsohd (5)  k     86
     O                       aropsohd (6)  k     96
     O                       aropsohd (7)  k    105
     O                       d1opsohd      kb   117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of gilt abortions'
     O                       arglabhd (1)  k     46
     O                       arglabhd (2)  k     56
     O                       arglabhd (3)  k     66
     O                       arglabhd (4)  k     76
     O                       arglabhd (5)  k     86
     O                       arglabhd (6)  k     96
     O                       arglabhd (7)  k    105
     O                       arglabhd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of abortions'
     O                       arabhd   (1)  k     46
     O                       arabhd   (2)  k     56
     O                       arabhd   (3)  k     66
     O                       arabhd   (4)  k     76
     O                       arabhd   (5)  k     86
     O                       arabhd   (6)  k     96
     O                       arabhd   (7)  k    105
     O                       arabhd   (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of NIPs'
     O                       arnipqt  (1)  k     46
     O                       arnipqt  (2)  k     56
     O                       arnipqt  (3)  k     66
     O                       arnipqt  (4)  k     76
     O                       arnipqt  (5)  k     86
     O                       arnipqt  (6)  k     96
     O                       arnipqt  (7)  k    105
     O                       arnipqt  (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilt litters farrowed'
     O                       argllt   (1)  k     46
     O                       argllt   (2)  k     56
     O                       argllt   (3)  k     66
     O                       argllt   (4)  k     76
     O                       argllt   (5)  k     86
     O                       argllt   (6)  k     96
     O                       argllt   (7)  k    105
     O                       argllt   (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilt total born alive'
     O                       arglbahd (1)  k     46
     O                       arglbahd (2)  k     56
     O                       arglbahd (3)  k     66
     O                       arglbahd (4)  k     76
     O                       arglbahd (5)  k     86
     O                       arglbahd (6)  k     96
     O                       arglbahd (7)  k    105
     O                       arglbahd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilt total stillborns'
     O                       arglsbhd (1)  k     46
     O                       arglsbhd (2)  k     56
     O                       arglsbhd (3)  k     66
     O                       arglsbhd (4)  k     76
     O                       arglsbhd (5)  k     86
     O                       arglsbhd (6)  k     96
     O                       arglsbhd (7)  k    105
     O                       arglsbhd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilt total mummies'
     O                       arglmmhd (1)  k     46
     O                       arglmmhd (2)  k     56
     O                       arglmmhd (3)  k     66
     O                       arglmmhd (4)  k     76
     O                       arglmmhd (5)  k     86
     O                       arglmmhd (6)  k     96
     O                       arglmmhd (7)  k    105
     O                       arglmmhd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total litters farrowed'
     O                       artolt   (1)  k     46
     O                       artolt   (2)  k     56
     O                       artolt   (3)  k     66
     O                       artolt   (4)  k     76
     O                       artolt   (5)  k     86
     O                       artolt   (6)  k     96
     O                       artolt   (7)  k    105
     O                       artolt   (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Total born alive'
     O                       arbahd   (1)  k     46
     O                       arbahd   (2)  k     56
     O                       arbahd   (3)  k     66
     O                       arbahd   (4)  k     76
     O                       arbahd   (5)  k     86
     O                       arbahd   (6)  k     96
     O                       arbahd   (7)  k    105
     O                       arbahd   (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Total stillborns'
     O                       arsbhd   (1)  k     46
     O                       arsbhd   (2)  k     56
     O                       arsbhd   (3)  k     66
     O                       arsbhd   (4)  k     76
     O                       arsbhd   (5)  k     86
     O                       arsbhd   (6)  k     96
     O                       arsbhd   (7)  k    105
     O                       arsbhd   (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total mummies'
     O                       armmhd   (1)  k     46
     O                       armmhd   (2)  k     56
     O                       armmhd   (3)  k     66
     O                       armmhd   (4)  k     76
     O                       armmhd   (5)  k     86
     O                       armmhd   (6)  k     96
     O                       armmhd   (7)  k    105
     O                       armmhd   (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total pre-wean losses'
     O                       artolshd (1)  k     46
     O                       artolshd (2)  k     56
     O                       artolshd (3)  k     66
     O                       artolshd (4)  k     76
     O                       artolshd (5)  k     86
     O                       artolshd (6)  k     96
     O                       artolshd (7)  k    105
     O                       artolshd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 '0-15 day weaned sows'
     O                       arewnlt  (1)  k     46
     O                       arewnlt  (2)  k     56
     O                       arewnlt  (3)  k     66
     O                       arewnlt  (4)  k     76
     O                       arewnlt  (5)  k     86
     O                       arewnlt  (6)  k     96
     O                       arewnlt  (7)  k    105
     O                       arewnlt  (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 '16+ day weaned sows'
     O                       arwnlt (1)    k     46
     O                       arwnlt (2)    k     56
     O                       arwnlt (3)    k     66
     O                       arwnlt (4)    k     76
     O                       arwnlt (5)    k     86
     O                       arwnlt (6)    k     96
     O                       arwnlt (7)    k    105
     O                       arwnlt (8)    k    117
      *
     O          e            d1dtl       1
     O                                           30 'Total weaned sows'
     O                       artotwnlt(1)  k     46
     O                       artotwnlt(2)  k     56
     O                       artotwnlt(3)  k     66
     O                       artotwnlt(4)  k     76
     O                       artotwnlt(5)  k     86
     O                       artotwnlt(6)  k     96
     O                       artotwnlt(7)  k    105
     O                       artotwnlt(8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Total pigs weaned'
     O                       artownhd (1)  k     46
     O                       artownhd (2)  k     56
     O                       artownhd (3)  k     66
     O                       artownhd (4)  k     76
     O                       artownhd (5)  k     86
     O                       artownhd (6)  k     96
     O                       artownhd (7)  k    105
     O                       artownhd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           23 'Avg age of piglet'
     O                                           30 ' weaned'
     O                       aradwn   (1)  k     48
     O                       aradwn   (2)  k     58
     O                       aradwn   (3)  k     68
     O                       aradwn   (4)  k     78
     O                       aradwn   (5)  k     88
     O                       aradwn   (6)  k     98
     O                       aradwn   (7)  k    107
     O                       d1adwn        kb   119
      *
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilts arrived'
     O                       ararglhd (1)  k     46
     O                       ararglhd (2)  k     56
     O                       ararglhd (3)  k     66
     O                       ararglhd (4)  k     76
     O                       ararglhd (5)  k     86
     O                       ararglhd (6)  k     96
     O                       ararglhd (7)  k    105
     O                       ararglhd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Transferred in bred gilts'
     O                       aribglhd (1)  k     46
     O                       aribglhd (2)  k     56
     O                       aribglhd (3)  k     66
     O                       aribglhd (4)  k     76
     O                       aribglhd (5)  k     86
     O                       aribglhd (6)  k     96
     O                       aribglhd (7)  k    105
     O                       aribglhd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Sows arrived'
     O                       ararsohd (1)  k     46
     O                       ararsohd (2)  k     56
     O                       ararsohd (3)  k     66
     O                       ararsohd (4)  k     76
     O                       ararsohd (5)  k     86
     O                       ararsohd (6)  k     96
     O                       ararsohd (7)  k    105
     O                       ararsohd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Transferred in bred sows'
     O                       aribsohd (1)  k     46
     O                       aribsohd (2)  k     56
     O                       aribsohd (3)  k     66
     O                       aribsohd (4)  k     76
     O                       aribsohd (5)  k     86
     O                       aribsohd (6)  k     96
     O                       aribsohd (7)  k    105
     O                       aribsohd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Gilts sold'
     O                       arslglhd (1)  k     46
     O                       arslglhd (2)  k     56
     O                       arslglhd (3)  k     66
     O                       arslglhd (4)  k     76
     O                       arslglhd (5)  k     86
     O                       arslglhd (6)  k     96
     O                       arslglhd (7)  k    105
     O                       arslglhd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Sows sold'
     O                       arslsohd (1)  k     46
     O                       arslsohd (2)  k     56
     O                       arslsohd (3)  k     66
     O                       arslsohd (4)  k     76
     O                       arslsohd (5)  k     86
     O                       arslsohd (6)  k     96
     O                       arslsohd (7)  k    105
     O                       arslsohd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilts dead-naturally only'
     O                       arddglhd (1)  k     46
     O                       arddglhd (2)  k     56
     O                       arddglhd (3)  k     66
     O                       arddglhd (4)  k     76
     O                       arddglhd (5)  k     86
     O                       arddglhd (6)  k     96
     O                       arddglhd (7)  k    105
     O                       arddglhd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Sows dead-naturally only'
     O                       arddsohd (1)  k     46
     O                       arddsohd (2)  k     56
     O                       arddsohd (3)  k     66
     O                       arddsohd (4)  k     76
     O                       arddsohd (5)  k     86
     O                       arddsohd (6)  k     96
     O                       arddsohd (7)  k    105
     O                       arddsohd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilts euthanized'
     O                       areuglhd (1)  k     46
     O                       areuglhd (2)  k     56
     O                       areuglhd (3)  k     66
     O                       areuglhd (4)  k     76
     O                       areuglhd (5)  k     86
     O                       areuglhd (6)  k     96
     O                       areuglhd (7)  k    105
     O                       areuglhd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Sows euthanized'
     O                       areusohd (1)  k     46
     O                       areusohd (2)  k     56
     O                       areusohd (3)  k     66
     O                       areusohd (4)  k     76
     O                       areusohd (5)  k     86
     O                       areusohd (6)  k     96
     O                       areusohd (7)  k    105
     O                       areusohd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Transferred out bred gilts'
     O                       arobglhd (1)  k     46
     O                       arobglhd (2)  k     56
     O                       arobglhd (3)  k     66
     O                       arobglhd (4)  k     76
     O                       arobglhd (5)  k     86
     O                       arobglhd (6)  k     96
     O                       arobglhd (7)  k    105
     O                       arobglhd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Transferred out open sows'
     O                       aroosohd (1)  k     46
     O                       aroosohd (2)  k     56
     O                       aroosohd (3)  k     66
     O                       aroosohd (4)  k     76
     O                       aroosohd (5)  k     86
     O                       aroosohd (6)  k     96
     O                       aroosohd (7)  k    105
     O                       aroosohd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Transferred out bred sows'
     O                       arobsohd (1)  k     46
     O                       arobsohd (2)  k     56
     O                       arobsohd (3)  k     66
     O                       arobsohd (4)  k     76
     O                       arobsohd (5)  k     86
     O                       arobsohd (6)  k     96
     O                       arobsohd (7)  k    105
     O                       arobsohd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Boars in'
     O                       ararbohd (1)  k     46
     O                       ararbohd (2)  k     56
     O                       ararbohd (3)  k     66
     O                       ararbohd (4)  k     76
     O                       ararbohd (5)  k     86
     O                       ararbohd (6)  k     96
     O                       ararbohd (7)  k    105
     O                       ararbohd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Boars sold'
     O                       arslbohd (1)  k     46
     O                       arslbohd (2)  k     56
     O                       arslbohd (3)  k     66
     O                       arslbohd (4)  k     76
     O                       arslbohd (5)  k     86
     O                       arslbohd (6)  k     96
     O                       arslbohd (7)  k    105
     O                       arslbohd (8)  k    117
      *
     O          e            d1dtl       1
     O                                           30 'Boars dead'
     O                       arddbohd (1)  k     46
     O                       arddbohd (2)  k     56
     O                       arddbohd (3)  k     66
     O                       arddbohd (4)  k     76
     O                       arddbohd (5)  k     86
     O                       arddbohd (6)  k     96
     O                       arddbohd (7)  k    105
     O                       arddbohd (8)  k    117
      *
      *
     O          e            d1dtl       1
     O                                           30 'Manhours'
     O                       armanhr  (1)  k     49
     O                       armanhr  (2)  k     59
     O                       armanhr  (3)  k     69
     O                       armanhr  (4)  k     79
     O                       armanhr  (5)  k     89
     O                       armanhr  (6)  k     99
     O                       armanhr  (7)  k    108
     O                       d1manhr       kb   120
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /eject
