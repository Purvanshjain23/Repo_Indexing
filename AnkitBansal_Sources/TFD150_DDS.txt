     A*%%TS  SD  20090114  163839  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD150
     A*  TITLE:         REVISE KILL/CUT DATA
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       03/27/07
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 62 IS TYPICALLY ON IN DELETE MODE. BUT, WE DO NOT ALLOW
     A*  4=DELETE FROM THE WORK WITH. SO, WE WILL NEVER BE IN DELETE "MODE".
     A*  SO, IN THIS PROGRAM WE WILL USE 62 TO PROTECT A RECORD AND INACTIVATE
     A*  F9=ACCEPT AND F11=DELETE WHEN A RECORD HAS BEEN USED IN A CLOSED WEEK.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A N62                                  CF09(09 'ACCEPT')
     A  60N62                               CF11(11 'DELETE')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF150R1
     A*%%TS  SD  20090114  152414  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'REVISE KILL/CUT DATA'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 35'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 41'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Week-ending date ..'
     A            D1WEMDY   R     Y  B  5 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  61N30                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A            D1YR      R        O  5 35REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      COLOR(BLU)
     A                                      EDTCDE(M)
     A            D1WK      R        O  5 42REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      COLOR(BLU)
     A                                      EDTCDE(M)
     A                                  8 35'Hot'
     A                                  8 63'Cold'
     A                                  9 15'Killed Head'
     A                                      DSPATR(UL)
     A                                  9 29' Weight Pounds'
     A                                      DSPATR(UL)
     A                                  9 47' Cut Head'
     A                                      DSPATR(UL)
     A                                  9 59'Carcass Pounds'
     A                                      DSPATR(UL)
     A                                 11  4'Seaboard:'
     A                                      COLOR(BLU)
     A            D1SKLHD   R        B 11 16REFFLD(TFREF/@@TCTHD *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1SHCLB   R        B 11 29REFFLD(TFREF/@@SCCLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N35                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1SCTHD   R        B 11 47REFFLD(TFREF/@@TCTHD *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N33                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1SCCLB   R        B 11 59REFFLD(TFREF/@@TCCLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N62N31                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 13  5'Triumph:'
     A                                      COLOR(BLU)
     A            D1TKLHD   R        B 13 16REFFLD(TFREF/@@TCTHD *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N38                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1THCLB   R        B 13 29REFFLD(TFREF/@@SCCLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TCTHD   R        B 13 47REFFLD(TFREF/@@TCTHD *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N34                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TCCLB   R        B 13 59REFFLD(TFREF/@@TCCLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N62N32                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A                                 15  2'Sioux City:'
     A                                      COLOR(BLU)
     A            D1XKLHD   R        B 15 16REFFLD(TFREF/@@XKLHD *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N62N39                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1XHCLB   R        B 15 29REFFLD(TFREF/@@XHCLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N62N40                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1XCTHD   R        B 15 47REFFLD(TFREF/@@XCTHD *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A N62N41                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1XCCLB   R        B 15 59REFFLD(TFREF/@@XCCLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A N62N42                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF150K1
     A*%%TS  SD  20070330  110014  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F5=Refresh'
     A N62                             23 23'F9=Accept'
     A N62N61                          23 34'F11=Delete'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF150ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF150EC                   SFLCTL(TF150ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
