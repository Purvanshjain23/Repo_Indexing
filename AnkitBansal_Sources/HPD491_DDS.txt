     A*%%TS  SD  20000208  111117  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD491
     A*  TITLE:         SPECIFY CLOSED GROUPS FOR CALORIC UPDATING
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          08/15/96
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP491R1
     A*%%TS  SD  20000208  111117  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(48)
     A                                      SETOF(51)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  3'HP491'
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 17'SPECIFY CLOSED GROUPS FOR CALORIC -
     A                                      UPDATING'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .....'
     A                                  4 24'?'
     A            DFFSBO    R        B  4 26REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 34REFFLD(BOBODS)
     A                                  4 65'(Blank for all)'
     A                                  5  2'From closed date ....'
     A            DFFMDY    R     Y  B  5 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To closed date ......'
     A            DFTMDY    R     Y  B  6 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Production type .....'
     A                                  7 24'?'
     A            DFPTCD    R        B  7 26REFFLD(PTPTCD)
     A N48                                  DSPATR(HI)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  7 34REFFLD(PTPTDS)
     A                                  7 65'(Blank for all)'
     A                                  8  2'Production phase ....'
     A                                  8 24'?'
     A            DFPPCD    R        B  8 26REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  8 34REFFLD(PPPPDS)
     A                                  8 65'(Blank for all) '
     A                                  9  2'Farm site ...........'
     A                                  9 24'?'
     A            DFFSCD    R        B  9 26REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O  9 34REFFLD(FSFSNM)
     A                                  9 65'(Blank for all)'
     A                                 10  2'Closed hog group ....'
     A                                 10 24'?'
     A            DFHGCD    R        B 10 26REFFLD(HGHGCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 10 65'(Blank for all)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP491K1
     A*%%TS  SD  19941010  124917  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP491ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP491EC                   SFLCTL(HP491ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
