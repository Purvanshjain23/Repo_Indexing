      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF630
      * TITLE:       Listing of Charge Codes
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/03/05
      *
      * FUNCTION:  Batch program to print a record(s) in the Charge Codes file.
      *
      *            This listing is submitted from W/W Charge Codes when the user takes:
      *                1) F7=Listing
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcaabrel1  if   e           k disk
      *    Company name and address
      *
      *
     Ftfp005    if   e           k disk
      *    Charge codes  (records selected by open query)
      *
      *
     Ftfp006    if   e           k disk
      *    System modules
      *
      *
     Ftfp098    if   e           k disk
      *    Charge frequency codes
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Parm fields
      *
     D xxcono          s                   like(cccono)
      *
     D xxglcd          s                   like(ccgldbcd)
     D xxglcdds        s             25
      *
     D xxstatus        s              1
     D xxsystem        s              3
      *
      *
      * Print fields
      *
     D h1conm          s             30
     D h1ccds          s                   like(ccccds)
     D h1cfqds         s                   like(cfcfqds)
     D h1gldbcdds      s             25
     D h1glcrcdds      s             25
     D h1ucds          s              3
     D h1inds          s              3
     D h1smds          s                   like(smsmds)
     D h1aids          s              8
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcccd                 1     10
     D  ldcono                11     13  0
     D  ldgldbcd              14     21
     D  ldglcrcd              22     29
     D  ldcfqcd               30     30
     D  lducfl                31     31
     D  ldinfl                32     32
     D  ldsmcd                33     34
     D  ldaist                35     35
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read/print each record selected by the open query.
      *
     C     *loval        setll     tfp005
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp005                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Set up heading selections:
      *
      * Charge Code
     C                   if        ldcccd = *blank
     C                   seton                                        80
     C                   else
      *
     C     ldcccd        chain     tfp005                             92
     C                   if        *in92 = *off
     C                   move      ccccds        h1ccds
     C                   endif
     C                   endif
      *
      * Company
     C                   if        ldcono = 0
     C                   seton                                        81
     C                   else
      *
     C     ldcono        chain     caabrel1                           92
     C                   if        *in92 = *off
     C                   move      abadtx        h1conm
     C                   endif
     C                   endif
      *
      *
      * GL Code-Debit
     C                   if        ldgldbcd = *blank
     C                   seton                                        82
     C                   else
      *
     C                   if        ldcono <> 0
     C                   call      'TF805'
     C                   parm      ldcono        xxcono
     C                   parm      ldgldbcd      xxglcd
     C     h1gldbcdds    parm      *blank        xxglcdds
     C                   parm      *blank        xxsystem
     C                   parm      *blank        xxstatus
     C                   endif
      *
     C                   endif
      *
      *
      * GL Code-Credit
     C                   if        ldglcrcd = *blank
     C                   seton                                        83
     C                   else
      *
     C                   if        ldcono <> 0
     C                   call      'TF805'
     C                   parm      ldcono        xxcono
     C                   parm      ldglcrcd      xxglcd
     C     h1glcrcdds    parm      *blank        xxglcdds
     C                   parm      *blank        xxsystem
     C                   parm      *blank        xxstatus
     C                   endif
     C                   endif
      *
      * Charge Frequency Code
      *
     C                   if        ldcfqcd = *blank
     C                   seton                                        84
     C                   else
      *
     C     ldcfqcd       chain     tfp098                             92
     C                   if        *in92 = *off
     C                   move      cfcfqds       h1cfqds
     C                   endif
     C                   endif
      *
      * User Controlled flag
      *
     C                   if        lducfl = *blank
     C                   seton                                        85
     C                   else
      *
     C                   select
     C                   when      lducfl = yes
     C                   eval      h1ucds = 'Yes'
     C                   other
     C                   eval      h1ucds = 'No '
     C                   endsl
     C                   endif
      *
      * Include on Invoice Total Flag
      *
     C                   if        ldinfl = *blank
     C                   seton                                        86
     C                   else
      *
     C                   select
     C                   when      ldinfl = yes
     C                   eval      h1inds = 'Yes'
     C                   other
     C                   eval      h1inds = 'No '
     C                   endsl
     C                   endif
      *
      * System Module Code
      *
     C                   if        ldsmcd = *blank
     C                   seton                                        87
     C                   else
      *
     C     ldsmcd        chain     tfp006                             92
     C                   if        *in92 = *off
     C                   move      smsmds        h1smds
     C                   endif
     C                   endif
      *
      * Active/inactive status
      *
     C                   if        ldaist = *blank
     C                   seton                                        88
     C                   else
      *
     C                   select
     C                   when      ldaist = 'A'
     C                   eval      h1aids = 'Active'
     C                   other
     C                   eval      h1aids = 'Inactive'
     C                   endsl
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           80 'LISTING OF CHARGE CODES'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          1
     O                                           24 'Charge code ............'
     O                                           30 '.....:'
     O               80                          35 'All'
     O              n80      ldcccd              42
     O              n80      h1ccds              69
      *
     O          e            h1hdr          1
     O                                           24 'Company number .........'
     O                                           30 '.....:'
     O               81                          35 'All'
     O              n81      ldcono        z     35
     O              n81      h1conm              74
      *
     O          e            h1hdr          1
     O                                           24 'G/L code - debit .......'
     O                                           30 '.....:'
     O               82                          35 'All'
     O              n82      ldgldbcd            40
     O              n82      h1gldbcdds          69
      *
     O          e            h1hdr          1
     O                                           24 'G/L code - credit ......'
     O                                           30 '.....:'
     O               83                          35 'All'
     O              n83      ldglcrcd            40
     O              n83      h1glcrcdds          69
      *
     O          e            h1hdr          1
     O                                           24 'Charge frequency code ..'
     O                                           30 '.....:'
     O               84                          35 'All'
     O              n84      ldcfqcd             33
     O              n84      h1cfqds             54
      *
     O          e            h1hdr          1
     O                                           24 'User-controlled flag ...'
     O                                           30 '.....:'
     O               85                          35 'All'
     O              n85      lducfl              33
     O              n85      h1ucds              47
      *
     O          e            h1hdr          1
     O                                           24 'Include in invoice total'
     O                                           30 ' flag:'
     O               86                          35 'All'
     O              n86      ldinfl              33
     O              n86      h1inds              47
      *
     O          e            h1hdr          1
     O                                           24 'System module code .....'
     O                                           30 '.....:'
     O               87                          35 'All'
     O              n87      ldsmcd              34
     O              n87      h1smds              64
      *
     O          e            h1hdr          1
     O                                           24 'Active/inactive status .'
     O                                           30 '.....:'
     O               88                          35 'All'
     O              n88      ldaist              33
     O              n88      h1aids              52
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           80 'Charge'
     O                                           91 'User'
     O                                          108 'Include in'
     O                                          118 'System'
     O                                          129 'Active/'
      *
     O          e            h1hdr       0  1
     O                                            6 'Charge'
     O                                           49 'Company'
     O                                           69 '---- GL Codes ----'
     O                                           81 'Frequency'
     O                                           94 'Controlled'
     O                                          109 'Invoice Total'
     O                                          118 'Module'
     O                                          129 'Inactive'
      *
     O          e            h1hdr       0  1
     O                                            4 'Code'
     O                                           26 'Description'
     O                                           48 'Number'
     O                                           56 'Debit'
     O                                           67 'Credit'
     O                                           79 'Code'
     O                                           91 'Flag'
     O                                          105 'Flag'
     O                                          117 'Code'
     O                                          128 'Status'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       cccccd              10
     O                       ccccds              40
     O                       cccono        z     47
     O                       ccgldbcd            59
     O                       ccglcrcd            69
     O                       cccfqcd             77
     O                       ccucfl              90
     O                       ccinfl             103
     O                       ccsmcd             116
     O                       ccaist             126
      *
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
