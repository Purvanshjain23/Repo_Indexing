     A*%%TS  SD  20090501  092325  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4033
     A*  TITLE:         WORK WITH CONTROL RECORDS FOR HPS UPLOADS TO JDE
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/10/08
     A*****************************************************************
     A* MODIFICATIONS:
     A*----------------------------------------------------------------
     A* 10/02/18 DN  S13682 - Added Company Code to subfile control &
     A*    subfile format level.
     A*  - Shifted existing fields to the right 4 positions in subfile
     A*    control & subfile format level.
     A*  - Added Business Office to subfile control to control what
     A*    company to display in the subfile.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4033S1                  SFL
     A*%%TS  SD  20090429  104309  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDBPDT    R        H      REFFLD(@@BPDT)
     A            HDEPDT    R        H      REFFLD(@@EPDT)
     A            D1OPT     R        B 12  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
13682A            D1COCD    R        O 12  5REFFLD(HSRREF/@@COCD *LIBL/HSPREF)
     A            D1ACYR    R        O 12  9REFFLD(HSRREF/@@ACYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1ACPE    R        O 12 15REFFLD(HSRREF/@@ACPE *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1UPLSCD  R        O 12 19REFFLD(HSRREF/@@UPLSCD *LIBL/HSPREF)
     A            D1HUCD    R        O 12 22REFFLD(@@HUCD)
     A            D1HUDS        26A  O 12 25
     A            D1MDY     R        O 12 52REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1TM      R        O 12 61REFFLD(HSRREF/@@TM06 *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
     A            D1USR     R        O 12 70REFFLD(HSRREF/@@USR *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4033C1                  SFLCTL(HP4033S1)
     A*%%TS  SD  20090501  092325  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
13682A**                                    SFLPAG(0011)
13682A                                      SFLPAG(0010)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 14'WORK WITH CONTROL RECORDS FOR HPS -
     A                                      UPLOADS TO JDE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
      * S13682-Added Business Office as input capable.
13682A                                  4  2'Business office .. ?'
  |  A            C1FSBO    R        B  4 23REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
  |  A  31
  |  AO 51                                  DSPATR(PC)
  |  A  31                                  DSPATR(RI)
  |  A N31                                  DSPATR(HI)
  |  A  99                                  DSPATR(PR)
13682A            C1BODS    R        O  4 30REFFLD(HSRREF/@@DS30 *LIBL/HSPREF)
      *
     A                                  6  2'2=Change Status to Rerun'
     A                                  6 32'5=View Status Changes'
     A                                  7  2'7=Change Status to Uploaded'
     A                                  8 19'?'
     A                                      DSPATR(HI)
     A                                  8 23'?'
     A                                      DSPATR(HI)
     A                                  9 19'-Upl-'
     A                                      DSPATR(HI)
     A                                  9 53'----- Status Changed ----'
     A                                      DSPATR(HI)
     A                                 10  2'Op'
     A                                      DSPATR(HI)
     A                                 10  5'Co'
     A                                      DSPATR(HI)
     A                                 10  9'Year/Per'
     A                                      DSPATR(HI)
     A                                 10 19'St'
     A                                      DSPATR(HI)
     A                                 10 22'Cd'
     A                                      DSPATR(HI)
     A                                 10 25'Description'
     A                                      DSPATR(HI)
     A                                 10 53'Date'
     A                                      DSPATR(HI)
     A                                 10 62'Time'
     A                                      DSPATR(HI)
     A                                 10 70'User'
     A                                      DSPATR(HI)
     A            C1COCD    R        O 11  5REFFLD(HSRREF/@@COCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      TEXT('Company Code')
     A            C1ACYR    R        B 11  9REFFLD(HSRREF/@@ACYR *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A                                      TEXT('Accounting Year')
     A            C1ACPE    R        B 11 15REFFLD(HSRREF/@@ACPE *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A                                      TEXT('Accounting Period')
     A            C1UPLSCD  R        B 11 19REFFLD(@@UPLSCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1HUCD    R        B 11 22REFFLD(@@HUCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1HUDS        26A  B 11 25DSPATR(HI)
     A                                      CHECK(LC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4033K1
     A*%%TS  SD  20011015  111921  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 53'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4033ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4033EC                  SFLCTL(HP4033ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
