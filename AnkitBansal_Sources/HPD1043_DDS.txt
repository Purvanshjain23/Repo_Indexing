     A*%%TS  SD  20171102  145958  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1043
     A*  TITLE:         REVISE CUSTOMER TYPE MASTER FILE
     A*  PROGRAMMER:    BRAD BADEN
     A*  DATE:          10/30/2017
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A** INDICATOR 62 IS ON WHEN IN DELETE MODE (No Delete mode)
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP1043R1
     A*%%TS  SD  19941216  100454  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(53)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A  60                              2 28'REVISE CUSTOMER TYPE'
     A  60                                  DSPATR(HI)
     A  61                              2 28'CREATE CUSTOMER TYPE'
     A  61                                  DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Customer type ................'
     A            DFTYPCD   R        B  4 33REFFLD(CTTYPCD)
     A                                      EDTCDE(4)
     A  61N31                               DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                  5  2'Customer type description ....'
     A            DFTYPDS   R        B  5 33REFFLD(CTTYPDS)
     A N62N32                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  6  2'Status ..................... ?'
     A            DFCTST    R        B  6 33REFFLD(CTCTST)
     A N62N33                               DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP1043K1
     A*%%TS  SD  19941209  114808  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A                                 23 34'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP1043ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP1043EC                   SFLCTL(HP1043ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
