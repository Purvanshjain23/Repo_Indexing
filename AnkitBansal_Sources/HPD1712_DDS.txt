     A*%%TS  SD  20040225  083843  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1712
     A*  TITLE:         CREATE AN INACTIVE BUDGET FOR A FARM
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/24/04
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF09(09 'ACCEPT')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1712R1
     A*%%TS  SD  20040225  083843  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 20'CREATE AN INACTIVE BUDGET FOR A FA-
     A                                      RM'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  3'The system automatically generates-
     A                                       active Weekly and Rotational budge-
     A                                      ts for'
     A                                  5  3'Farms linked to Budget Templates. -
     A                                      The system date becomes the From Da-
     A                                      te.'
     A                                  5 80'  '
     A                                  6  3'Annual Farm Budgets are generated -
     A                                      on-demand from a menu option for Fa-
     A                                      rms'
     A                                  7  3'linked to Budget Templates using F-
     A                                      rom/To Dates entered on the screen.'
     A                                  9  3'This CREATE function should only b-
     A                                      e used when you want to specify the'
     A                                      DSPATR(HI)
     A                                 10  3'From/To Dates that will be used fo-
     A                                      r the first budget for a Farm inste-
     A                                      ad of'
     A                                      DSPATR(HI)
     A                                 11  3'letting the system generate the Da-
     A                                      te(s).'
     A                                      DSPATR(HI)
     A                                 13  2'Farm site code ........'
     A                                 13 26'?'
     A            D1FSCD    R        B 13 28REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            D1FSNM    R        O 13 36REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                 14  2'Budget template code ..'
     A                                 14 26'?'
     A            D1BTCD    R        B 14 28REFFLD(@@BTCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 14 35' ----------- TEMPLATE INFORMATION -
     A                                      -----------'
     A                                      DSPATR(HI)
     A                                 15 42'Description:'
     A            D1BTDS    R        O 15 55REFFLD(@@BTDS)
     A                                 16 36'Creation schedule:'
     A            D1CSDS    R        O 16 55REFFLD(HSRREF/@@CSDS *LIBL/HSPREF)
     A                                 17 39'Template weeks:'
     A            D1BTWK    R        O 17 55REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 18 34'Budget availability:'
     A            D1BADS    R        O 18 55REFFLD(HSRREF/@@BADS *LIBL/HSPREF)
     A                                 20  2'From date .............'
     A            D1FMDY    R        B 20 28REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A                                 21  2'To date ...............'
     A            D1TMDY    R        B 21 28REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A N34                                  DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1712K1
     A*%%TS  SD  20040205  085247  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F9=Accept'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1712ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1712EC                  SFLCTL(HP1712ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
