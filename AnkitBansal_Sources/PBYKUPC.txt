/***************************************************************** */
/*  PROGRAM:       PBYKUPC                                         */
/*  PROGRAM DESC:  CNV PCKG ITEM DESC    UP  EXECUTE USER PROGRAM  */
/*  PROGRAMMER:    D.NGUYEN                                        */
/*  DATE:          09/13/2016                                      */
/*  PROJECT ID:    E7176                                           */
/*                                                                 */
/*  PURPOSE:       One-time conversion (or on demand) program to   */
/*                 update package item description field (XITX)    */
/*                 in Packaging Item (POARREP) file via SEQUEL.    */
/*                 pckgitem.csv file will be saved to a mapped     */
/*                 network drive on the local pc which is linked   */
/*                 to the IFS directory '/home/ISDNGUY'.           */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*******************************************************************/
             PGM

             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

             DCL        VAR(&LIBEXIST) TYPE(*CHAR) LEN(1)
             DCL        VAR(&JOBUSR) TYPE(*CHAR) LEN(10)

/*-----------------------------------------------------------------*/
/* Retrieve Job Attributes & Format Data.                          */
/*-----------------------------------------------------------------*/
             RTVJOBA    USER(&JOBUSR)

/*-----------------------------------------------------------------*/
/* Copy From Import FileT ARE BEING CHANGED                        */
/*-----------------------------------------------------------------*/
             CPYFRMIMPF FROMSTMF('/home/isdnguy/pckgitem.csv') +
                          TOFILE(*LIBL/POARREPFIX) MBROPT(*REPLACE) +
                          RCDDLM(*CRLF) RMVCOLNAM(*YES)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/*-----------------------------------------------------------------*/
/* Execute SEQUEL command to update Package Item Description (XITX)*/
/* field in POARREP file.                                          */
/*-----------------------------------------------------------------*/
             CHGVAR     VAR(&LIBEXIST) VALUE('N')
             ADDLIBLE   LIB(SEQUEL) POSITION(*LAST)
             MONMSG     MSGID(CPF2103) EXEC(CHGVAR VAR(&LIBEXIST) +
                          VALUE('Y'))

             UPDATE     SET((ARXITX PIDESC) (ARMKDT CYYMMDD) (ARBFTM +
                          CURTME) (ARCEVN '"PDLSUDE   "') (ARCFVN +
                          '"PBYKUPC   "')) VIEW(PKGITEM)

    /* Remove SEQUEL Library */
             IF         COND(&LIBEXIST *EQ 'N') THEN(RMVLIBLE +
                          LIB(SEQUEL))

             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
