      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP5038
      * TITLE:       Select Budget Item
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/30/04
      *
      *
      * FUNCTION: This is a select function window display of some of the
      *           records in the Item Master.
      *
      *           We do not have a 'Budget Item' Master in HPS. So, we will be reading
      *           a logical view of the Item Master. This logical view is limited to active
      *           items that are allowed to be budgeted.  We will read the records and
      *           write 1 single record to our subfile for each unique Budget Item value.
      *
      *           A record is selected by positioning the cursor on the line
      *           and pressing enter.
      *
      *           Restrictors:
      *              none
      *
      *           Positioners:
      *              none (the Budget Item is a 25-alpha field---there is no Budget Item Code)
      *
      *           Selectors:
      *              budget item
      *
      *
      * NOTE:    This is a pop-up window program.  Please note that in window
      *          programs the record format containing the window definition
      *          must be written first.  So, we write the error message subfile
      *          control record first followed by the command line record format.
      *          This is opposite of the non-window programs.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd5038   cf   e             workstn
     F                                     sfile(hp5038s1:hdrrn1)
     F                                     infds(iofeed)
      *
     Fhsl181c   if   e           k disk
      *    Items (logical selects only active records where Budget Flag = yes)
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      *
     D upper           c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     D lower           c                   'abcdefghijklmnopqrstuvwxyz'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D norecs          s              1    inz('N')
     D reset           s              1    inz('N')
     D count           s              2  0
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
     D wkflow          s                   like(idflow)
      *
      *
      * Control fields
      *
     D length          s              2  0
     D nbrchr          s              2  0
     D procfl          s              1
      *
     D svbgit          s                   like(c1bgit)
     D svittycd        s                   like(c1ittycd)
     D svdesc          s                   like(c1desc)
      *
      *
      *
      * Workfields
      *
     D wkrollbgit      s                   like(c1bgit)
     D wkdesc          s                   like(c1bgit)
     D wkbgit          s                   like(c1bgit)
      *
     D wkname          s                   like(c1desc)
     D wkbgitds        s                   like(c1desc)
      *
      *
      * Parm fields
      *
     D xxbgit          s                   like(d1bgit)
      *
     D xxaist          s              1
     D xxittycd        s                   like(d1ittycd)
     D xxittyds        s             30
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *---------------------------------------------------------------
      * Set environment for screen 1
      *---------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear/fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Perform operator I/O for screen 1
      *---------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp5038ec
     C                   write     hp5038k1
     C                   exfmt     hp5038c1
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in12 = *on                                  F12=previous
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
     C                   eval      routne = scrn1
      *
      * Reposition the subfile if the user has changed any control record values
      *
     C                   when      svbgit <> c1bgit or
     C                             svdesc <> c1desc or
     C                             svittycd <> c1ittycd
     C                   exsr      $chksv
      *
     C                   when      *in25 = *on                                  F25-rollup
     C                   exsr      $load1
     C                   eval      routne = scrn1
     C                   other
      *
      * User has pressed enter
      *
     C                   if        norecs = yes                                 If empty sfl
     C                   eval      routne = set1
     C                   else
     C                   exsr      $proc1
     C                   endif                                                  If empty sfl
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
     C                   select
      * Item type
     C                   when      fld = 'C1ITTYCD'
     C                   call      'HP5036'
     C                   parm      'A'           xxaist
     C                   parm      *blank        xxittycd
     C                   parm      *blank        xxittyds
     C                   seton                                        53
     C                   if        xxittycd <> *blank
     C                   movel     xxittycd      c1ittycd
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09011'     mgid(e)
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Process user's selection
      *----------------------------------------------------------------
      *
     C     $proc1        begsr
      *
      * If cursor location returned is the rrn of a record in the
      * subfile, chain to the subfile record and load the data into
      * the parms to return to the calling program.
      *
     C                   if        hdrrn <> 0
     C                   eval      hdrrn1 = hdrrn
     C     hdrrn1        chain     hp5038s1                           92
     C                   move      d1bgit        xxbgit
     C                   endif
      *
      * If the user selected a record, set the routine to exit.  If no record
      * was selected, redisplay the screen.
      *
     C                   if        xxbgit = *blank
     c                   eval      routne = set1
     C                   else
     c                   eval      routne = exit
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   clear                   hp5038s1
     C                   seton                                        83        sflclr
     C                   write     hp5038c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C     *loval        setll     hsl181c
      *
      * The lowest subfile RRN on the screen from the DSPF feedback area (IDFLOW)
      * was retrieved earlier and put in WKFLOW. Add to WKFLOW the number of lines
      * on your subfile page or the number less 1 to determine a subfile RRN to
      * reposition to. RESET is a flag field to indicate whether you are attempting
      * to reposition the user close to where he was in the subfile. If RESET is
      * 'NO' don't reposition--just start over.
      *
     C                   if        reset = yes
     C     wkflow        add       wkrlin        maxrrn
     C                   else
     C                   z-add     wkplin        maxrrn
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       hdrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *                                                                        z
     C     $chksv        begsr
      *
     C                   eval      routne = set1
     C                   eval      reset = no
      *
     C                   movel     c1ittycd      svittycd
      *
      * Prepare to scan on Budget Item
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      c1bgit = %trim(c1bgit)
     C     ' '           checkr    c1bgit        length
     C                   movel     c1bgit        svbgit
      *
      * Prepare to scan on Budget Item Description
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      c1desc = %trim(c1desc)
     C     ' '           checkr    c1desc        nbrchr
     C                   movel     c1desc        svdesc
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
      *
     C                   read      hsl181c                                82
     C                   if        *in82 = *off and                             If read
     C                             wkrollbgit <> imbgit
      *
     C                   move      imbgit        wkrollbgit
     C                   move      yes           procfl
      *
      * Populate 'description'
      *
     C                   if        imitcd = imbgit
     C                   eval      d1desc = imitds
     C                   else
     C                   eval      d1desc = imbgit
     C                   endif
      *
      * Only include records that match the user's selectors
      *
      * Budget item
     C                   if        c1bgit  <> *blank
     C     lower:upper   xlate     c1bgit        wkdesc
     C     lower:upper   xlate     imbgit        wkbgit
     C     wkdesc:length scan      wkbgit                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      * Budget item description
      *
     C                   if        c1desc <> *blank
     C     lower:upper   xlate     c1desc        wkname
     C     lower:upper   xlate     d1desc        wkbgitds
     C     wkname:nbrchr scan      wkbgitds                               92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      * Item type
     C                   if        c1ittycd <> *blank and c1ittycd <> imittycd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        procfl = yes                                 If process
      *
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   move      imbgit        d1bgit
     C                   move      imittycd      d1ittycd
     C                   write     hp5038s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into the
      * sflrcdnbr field (HDPAG1)
      *
     C                   z-add     hdrrn1        hdpag1
     C                   endif                                                  If process
     C                   endif                                                  If read
     C                   enddo                                                  Do fill
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   eval      norecs = yes
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
      *
     C                   else
     C                   eval      norecs = no
     C                   endif                                                  If no recs
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * End job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *     None
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxbgit
      *
      *
      * To support subfile paging, logic requires the number of lines on
      * the subfile page.
      *
     C                   z-add     9             wkplin                         sflpag
     C     wkplin        sub       1             wkrlin
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
      * Execute required window format
      *
     C                   write     assume
      *
     C                   endsr
      /EJECT
