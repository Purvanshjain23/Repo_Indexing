      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      M3 GL Inteface Detail line per parm pass in.
      * PROGRAM:     PWAXUPR
      * TITLE:       Write GL journal entry detail lines to M3
      * PROGRAMMER:  Rose Centonze
      * CREATED:     01/16/17
      *
      *  FUNCTION:   This program will write M3 GL Detail lines to the interface table PLCACPP
      *              for the JDE cost center/obj/sub .  First it will create the header in QTEMP
      *              with program pmavupr created the Header PLB9CPP for this batch detail.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 1/16/2017  Rose Centonze
      *            Copied from M3JDE200 and stripped to just write the detail
      *
      * 8/19/2019  Rose Centonze
      *            Skip to the endpgm if the GBAMT is .00 .. dont want .00 records
      /EJECT
      ****************************************************************
      * File specifications
      ****************************************************************
     Fplb8rel3  if a e           k disk
      *   G/L JDE to M3 Cross Ref   K= b8aht1, b8qncd, b8qocd
      *
     Fplcacpp   uf a e             disk
      *   M3 GL Detail Staging file
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Control fields
      *
     d first           s              1    inz('Y')
     d procfl          s              1    inz('N')
     d rtime           s              6  0
      *
      * Workfields
      *
     D wkdescr         s             20    inz('Journal Entry       ')
     d wkuser          s                   like(caaavn)
     D wkmdy           s              6  0
     d wkaa            s             15s 0
     d wkamt           s             15s 2
     d wk$$            s             17
     d dsamt           s             17
     d wkfdate         s              8  0
     d wktdate         s              8  0
     d wkpedte         s              8  0
      *
      * Work fields for date manipulation
      *
     D wkyymmdd        s              6  0
     d wksyssyndt      s              7  0
     d wkccyymmdd      s              8  0
     D wkisodate       s               d   datfmt(*iso)
     d wkhhmmss        s              6  0
      *
     d wktime          s             12  0
     D wkcymdiso       s               d   datfmt(*iso)
      *
      * Parm fields
      *
     d xxcono          s              3  0
     d xxcc            s              2  0
     d xxyr            s              2  0
     d xxpn            s              2  0
     d xxgrp           s              3
      *
      *--------------------------------------------------------------------------------
      * Set up the Key Value field that will tie the hdr file and detail file together
      *--------------------------------------------------------------------------------
      *
     D                 ds
     D   m3keyvalue            1     15
     D   m3keydt               1      8
     D   m3series              9     12
      *
      *--------------------------------------------------------------------------------
      * Set up description to have the old JDE company number
      *--------------------------------------------------------------------------------
      *
     D m3jdedesc       ds
     D   m3descr                     24
     D   jdeco                        3    overlay(m3descr:1)
     D   wkdesc                      20    overlay(m3descr:5)
      *
      *--------------------------------------------------------------------------------
      * Set up 4 digit year
      *--------------------------------------------------------------------------------
      *
     D wkyear          ds
     D   wkccyy                       4  0
     D   wkcc                         2  0 overlay(wkccyy:1)
     D   wkyy                         2  0 overlay(wkccyy:3)
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldacono               86     88  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *----------------------------------------------------------------
      * MAINLINE
      *---------------------------------------------------------------
      *  RMC 8/19/19
      *  Dont do anything if the parm amt is .00
      *
     C     gbamt         cabeq     .00           endpgm
      *
      *  Validate header exists ....
      *      Create it if it doesnt and use the keyvalue for the detail
     C                   exsr      $header
      *
      *  Write Detail line if no error
      *
     c     p0rtn         ifeq      *blank
     C                   exsr      $writeln
     c                   end
      *
      *
     c     endpgm        tag
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Write header  if it doesnt exist in QTEMP for Cc.obj.sub
      *---------------------------------------------------------------
      *
     C     $header       begsr
      *
     C                   call      'PWAVUPR'
     C                   PARM                    P0RTN             7
     C                   PARM                    aco               3 0       company   - input
     c                   PARM                    xxdeschdr        40         description-input
     C                   PARM                    m3cono            3 0       company   - output
     C                   PARM                    m3divi            3         division  - output
     c                   PARM                    p1keyvalue       15         key value -output
     C*
      *
     C*                  move      wkccyymmdd    caaedx                         M3 Entry Date
     C*                  move      wksyssyndt    caabdt                         date added
     C*                  move      wkhhmmss      caabtm                         time added
     C*                  move      sdpgm         caagvn                         program updated
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Get last line number to increment for write to M3
      *---------------------------------------------------------------
      *
     C     $nextnbr      begsr
      *
      *    get next Line number for this co/div/keyvalue
      *
     c                   call      'PWAYXFR'
     C                   parm                    rtncde            7
     C                   parm                    m3cono
     C                   parm                    m3divi
     C                   parm                    m3keyvalue
     c                   parm                    lstline           7 0        cawka1
     c*
     c**** lstline       add       1             cawka1
     c*
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write detail line items
      *---------------------------------------------------------------
      *
     C     $writeln      begsr
      *
     C                   move      *blanks       dsdim           200
     C                   move      *blanks       dsdim1
     C                   move      *blanks       dsdim2
     C                   move      *blanks       dsdim3
     C                   move      *blanks       dsdim4
     C                   move      *blanks       dsdim5
     C                   move      *blanks       dsdim6
     C                   move      *blanks       dsdim7            8
      *
     C********           move      '001'         cawga1                         Status
     C**
     C** Setup key for detail per values returned from $header
     C**
     C                   move      m3cono        CAWJA1                           company
     C                   move      m3divi        CAQXCD                           division
     c                   move      p1keyvalue    CAAIT1                           key value
     c                   move      p1keyvalue    m3keyvalue                       key value
      *
     c* Get next line number for co   div   key value
      *
     C                   exsr      $nextnbr
     C     lstline       add       1             cawka1                         line number
      *
      *  Setup 200 char param
      *
     C                   move      'I1'          dsid              2
     C                   movel     '1       '    dsgrnr            8            group number
     C                   movel     m3divi        dsdivi            3            division
     C                   movel     '0'           dseicd            1            exernal/internal
     C                   movel     'USD'         dscucd            3            currency
     C                   movel     '1 '          dscrtp            2            exch rate type
     C                   movel     *blanks       dsarat           13            exch rate
     c     xxdate        ifne      0
     c                   movel     xxdate        dsacdt
     c                   else
     C                   movel     wkpedte       dsacdt            8            accounting date
     c                   endif
     C                   movel     '1 '          dsvtcd            2            VAT code
     C                   eval      caabvn = sdusr
      *
      *  Translate JDE cost center/object/sub to M3 dimensions.
      *      ....take cc/obj/sub and look up in table for new.....
      *
     C     key02         chain     plb8rel3                           92
      *
     C                   if        *in92 = *off
     C                   move      b8qpcd        dsdim1            8
     C                   move      b8qqcd        dsdim2            8
     C                   move      b8qrcd        dsdim3            8
     C                   move      b8qscd        dsdim4            8
     C                   move      b8qtcd        dsdim5            8
     c                   movel     b8awt1        m3descr                        desc from cross ref
      *
      *  If JDE subledger exists, trim off leading zeros and move into DIM7.
      *
     C                   if        gbsbl <> *blanks                             have subledger
     c                   evalr     dsdim7=%subst(gbsbl:%check('0':gbsbl))       left adj, remv 00-
     C                   eval      dsdim7= %triml(dsdim7)
     c                   endif
      *
     C                   move      b8qucd        dsdim6            8
     C                   setoff                                       88
     C                   else
      *
      *  If the JDE account is not in the cross reference table, move the JDE values into the
      *  first three dimensions to aid the user when fixing in M3.
      *
     C                   move      gbmcu         dsdim1
     C                   movel     gbobj         dsdim2
     C                   movel     gbsub         dsdim3
     C                   move      *blanks       dsdim4
     C                   move      *blanks       dsdim5            8
     C                   move      *blanks       dsdim6
     C                   move      *blanks       dsdim7
     C                   seton                                        88
     C                   endif
      *
      *  Set values in parameter field
      *
     C                   eval      wk$$ = %triml(%editc(gbamt:'Q'))
     C                   move      wk$$          dsamt
      *
      *  We might have units... dft to blank if not there
      *
     c                   move      *blanks       wkqty            17
     C                   eval      wkqty = %triml(%editc(gbqty:'M'))
     C                   move      wkqty         dsacqt           17            quantity
      *
     C                   movel     m3series      dsrnno            9            run number
     c     xxdescdtl     ifeq      *blanks
     C                   movel(p)  m3descr       dsvtxt           40            description
     c                   else
     c                   movel(p)  xxdescdtl     dsvtxt
     c                   endif
     C                   eval      dsdim = dsid +
     C                                     dsrnno +
     C                                     dsgrnr +
     C                                     dsdivi +
     C                                     dseicd +
     C                                     dsdim1 +
     C                                     dsdim2 +
     C                                     dsdim3 +
     C                                     dsdim4 +
     C                                     dsdim5 +
     C                                     dsdim6 +
     C                                     dsdim7 +
     C                                     dsacqt +
     C                                     dscucd +
     C                                     dscrtp +
     C                                     dsarat +
     C                                     dsamt +
     C                                     dsacdt +
     C                                     dsvtcd +
     C                                     dsvtxt
     C                   movel     dsdim         caplt1                         parameter
     C                   movel(p)  wkuser        caaavn
     C                   move      wkccyymmdd    caaedx                         M3 Entry Date
     C                   move      wksyssyndt    caabdt                         date added
     C                   move      wkhhmmss      caabtm                         time added
     C                   move      sdpgm         caagvn                         program updated
      *
      *
     C                   write     @cacpuq
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *   Parms passed in from calling program
      *
      * Parm list   -- all are INPUT
     C     *entry        plist
     C                   parm                    p0rtn             7
     c                   parm                    aco               3 0         jde company
     C                   parm                    gbmcu            12           cost center
     C                   parm                    gbobj             6           object
     C                   parm                    gbsub             8           sub
     C                   parm                    gbsbl             8           subledger
     C                   parm                    gbamt            15 2         $ amount
     C                   parm                    gbqty            15 0         units
     C                   parm                    xxdate            8 0         use date if passed in
     C                   parm                    xxdeschdr        40           use desc if passed in
     C                   parm                    xxdescdtl        40           use desc if passed in
      *
      *
      * Key lists
      *
     C*    m3hkey        klist
     C*                  kfld                    m3cono
     C*                  kfld                    m3divi
     C*                  kfld                    m3keyvalue
      *
     C     key02         klist
     C                   kfld                    gbmcu
     C                   kfld                    gbobj
     C                   kfld                    gbsub
      *
     C     key03         klist
     C                   kfld                    xxgrp
     C                   kfld                    wkccyy
     C                   kfld                    xxpn
      *
      * Move incoming century and year into data structure to make one field
      *
     C                   move      xxcc          wkcc
     C                   move      xxyr          wkyy
      *
      *
      * Put system date into an 8,0 workfield for updating the change date
      *
     C     *mdy          move      udate         wkcymdiso
     C                   move      wkcymdiso     wkccyymmdd
      *
      * Put system date into an 7,0 workfield for updating the synon tables
      *
     C                   z-add     wkccyymmdd    wkyymmdd
     C                   if        wkccyymmdd >= 20000000
     C     wkyymmdd      add       1000000       wksyssyndt
     C                   else
     C                   z-add     wkyymmdd      wksyssyndt
     C                   endif
     C                   z-add     wkccyymmdd    wkpedte
      *
      *
      * Save the system time for populating fields.
      *
     C                   time                    wkhhmmss
     C                   time                    rtime
      *
     C                   endsr
      /eject
