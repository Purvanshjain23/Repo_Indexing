      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF270
      * TITLE:       YIELDS: Set LDA for Weekly Yield Processing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     12/02/08
      *
      * FUNCTION:  This function sets the LDA values for Weekly Yield processing.
      *            These values are based on the "current" week for the Margin
      *            Adjustment Close.
      *
      *            We call the Meat Costing and Margin Close functions behind
      *            the scenes to build this Weekly Yield data. So, we will create/set
      *            our LDA to be in-synch with the Meat Costing/Margin Close LDAs.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/02/09  LeAnne Ramsey
      *           Added "program" to the LDA. It is a new selection on the Product
      *           Exception Listing which is called from the Margin Close. I am
      *           still attempting to keep the LDAs for Yields/Meat Costing/Margin
      *           Close in synch.
      *
      * 02/09/09  LeAnne Ramsey
      *           Added another "yield report" field to the LDA. We must still
      *           keep the LDAs for Yields/Meat Costing/Margin Close in synch.
      *
      * 03/06/09  LeAnne Ramsey
      *           Removed the LDA field for the Converting % Report.
      *           We are still generating it; but, we no longer need the LDA value.
      *
      * 05/07/09  LeAnne Ramsey
      *           Since we are now making the Margin/Meat Costing print logic the same.
      *           We added Hold/Copies/Save/Outq to the LDA/Logic in this program.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp099    if   e           k disk
      *  Function control
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parms
      *
     D xxyr            s              4  0
     D xxpe            s              2  0
      *
     D xxpbdt          s              8  0
     D xxpbsyndt       s              7  0
     D xxpbmdy         s              6  0
      *
     D xxpedt          s              8  0
     D xxpesyndt       s              7  0
     D xxpemdy         s              6  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwbdt                 8     15  0
     D  ldwbsyn               16     22  0
      *
     D  ldwedt                29     36  0
     D  ldwesyn               37     43  0
     D  ldwemdy               44     49  0
      *
     D  ldpe                  51     52  0
     D  ldpbdt                53     60  0
     D  ldpbmdy               61     66  0
      *
     D  ldpedt                67     74  0
     D  ldpemdy               75     80  0
      *
     D  ldfzfl                81     81
     D  ldcono                82     84  0
      *
     D  ldfwedt               85     92  0
     D  ldfwemdy              93     98  0
      *
     D  ldtwedt               99    106  0
     D  ldtwemdy             107    112  0
      *
     D  ldrpt                113    113
     D  ldrptds              114    133
      *
     D  ldwpfl               134    134
     D  lddsfl               135    135
     D  ldmixfl              136    136
     D  ldvolfl              137    137
     D  ldsysfl              138    138
      *
     D  ldtfcgcd             139    140
     D  ldtfcgds             141    155
      *
     D  ldtfclcd             156    158
     D  ldtfclds             159    188
      *
     D  ldistycd             189    191  0
     D  ldistyds             192    221
      *
     D  ldprcd               222    228  0
     D  ldprds               229    258
      *
     D  ldpgcd               259    264
     D  ldpgds               265    294
      *
     D  ldcgcd               295    298
     D  ldplcd               299    304
      *
     D  ldpgm                386    395
      *
      * The following 5 fields are for "Yield" Reporting.
      *
     D  lddtl                397    397
     D  ldsum                398    398
     D  ldcap                399    399
     D  ldprimby             400    400
      *
     D  ldoutq               401    410
     D  ldhold               411    414
     D  ldsave               415    418
     D  ldcopy               419    419  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
     C                   z-add     1             ldcopy
     C                   move      '*YES'        ldsave
     C                   move      '*YES'        ldhold
     C                   move      *blank        ldoutq
      *
     C                   move      'P'           ldpfcd
     C                   move      'W'           ldwpfl
     C                   move      yes           ldfzfl
     C                   z-add     360           ldcono
      *
      * Yield Reports:
      *   1) Weekly Yield (Detail)
      *   2) Weekly Yield (Summary)
      *   3) Weekly Yield (Recap)
      *   4) a blank in LDPRIMBY says to generate both the Primal and the ByProduct
      *      Yield reports.
      *
     C                   move      'X'           lddtl
     C                   move      'X'           ldsum
     C                   move      'X'           ldcap
     C                   move      *blank        ldprimby
      *
      * All processing will be for the "Current" Margin Adjustment Week.
      *
     C     'MARGIN    '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If hit
      *
     C                   z-add     fncyr         ldyr
     C                   z-add     fncpe         ldpe
     C                   z-add     fncwk         ldwk
      *
     C                   z-add     fncedt        ldwedt
     C                   z-add     fncedt        ldfwedt
     C                   z-add     fncedt        ldtwedt
      *
     C                   z-add     fncemdy       ldwemdy
     C                   z-add     fncemdy       ldfwemdy
     C                   z-add     fncemdy       ldtwemdy
      *
     C                   z-add     fncesyn       ldwesyn
      *
     C                   z-add     fncbdt        ldwbdt
     C                   z-add     fncbsyn       ldwbsyn
     C                   endif                                                  If hit
      *
      * Retrieve the Beginning/Ending Period Dates for this Year/Period
      *
     C                   call      'TF816'
     C                   parm      ldyr          xxyr
     C                   parm      ldpe          xxpe
     C     ldpbdt        parm      0             xxpbdt
     C                   parm      0             xxpbsyndt
     C     ldpbmdy       parm      0             xxpbmdy
     C     ldpedt        parm      0             xxpedt
     C                   parm      0             xxpesyndt
     C     ldpemdy       parm      0             xxpemdy
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Initialize LDA fields
      *
     C                   z-add     0             ldyr
     C                   z-add     0             ldwk
      *
     C                   z-add     0             ldwbdt
     C                   z-add     0             ldwbsyn
      *
     C                   z-add     0             ldwedt
     C                   z-add     0             ldwesyn
     C                   z-add     0             ldwemdy
      *
     C                   z-add     0             ldpe
     C                   z-add     0             ldpedt
     C                   z-add     0             ldpemdy
      *
     C                   z-add     0             ldpbdt
     C                   z-add     0             ldpbmdy
      *
     C                   move      *blank        ldfzfl
     C                   z-add     0             ldcono
      *
     C                   z-add     0             ldfwedt
     C                   z-add     0             ldfwemdy
      *
     C                   z-add     0             ldtwedt
     C                   z-add     0             ldtwemdy
      *
     C                   move      *blank        ldrpt
     C                   move      *blank        ldrptds
      *
     C                   move      *blank        ldwpfl
     C                   move      *blank        lddsfl
     C                   move      *blank        ldmixfl
     C                   move      *blank        ldvolfl
     C                   move      *blank        ldsysfl
      *
     C                   move      *blank        ldtfcgcd
     C                   move      *blank        ldtfcgds
      *
     C                   move      *blank        ldtfclcd
     C                   move      *blank        ldtfclds
      *
     C                   z-add     0             ldistycd
     C                   move      *blank        ldistyds
      *
     C                   z-add     0             ldprcd
     C                   move      *blank        ldprds
      *
     C                   move      *blank        ldpgcd
     C                   move      *blank        ldpgds
      *
     C                   move      *blank        ldcgcd
     C                   move      *blank        ldplcd
     C                   move      *blank        ldpgm
      *
     C                   endsr
      /EJECT
