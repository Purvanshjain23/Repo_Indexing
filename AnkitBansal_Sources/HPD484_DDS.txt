     A*%%TS  SD  20011205  153210  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD484
     A*  TITLE:         SPECIFY SALES MOVEMENT/CHECK REPORT OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/06/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP484R1
     A*%%TS  SD  19960508  081057  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(51)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(70)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 16'SPECIFY SALES MOVEMENT/CHECK REPOR-
     A                                      T OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ......'
     A                                  4 25'?'
     A            DFFSBO    R        B  4 27REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 35REFFLD(BOBODS)
     A                                  4 66'(Blank for all)'
     A                                  5  2'From receive date ....'
     A            DFFMDY    R     Y  B  5 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To receive date ......'
     A            DFTMDY    R     Y  B  6 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Production type ......'
     A                                  7 25'?'
     A            DFPTCD    R        B  7 27REFFLD(PTPTCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  7 38REFFLD(PTPTDS)
     A                                  7 66'(Blank for all)'
     A                                  8  2'Production phase .....'
     A                                  8 25'?'
     A            DFPPCD    R        B  8 27REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS        25A  O  8 38
     A                                  8 66'(Blank for all)'
     A                                  9  2'Sales type code ......'
     A                                  9 25'?'
     A            DFSTCD    R        B  9 27REFFLD(STSTCD)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFSTDS    R        O  9 38REFFLD(STSTDS)
     A                                  9 66'(Blank for all)'
     A                                 10  2'Farm site ............'
     A                                 10 25'?'
     A            DFFSCD    R        B 10 27REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 10 38REFFLD(FSFSNM)
     A                                 10 66'(Blank for all)'
     A                                 11  2'Customer A/R number ..'
     A                                 11 25'?'
     A            DFCVNO    R        B 11 27REFFLD(HSRREF/CDCVNO *LIBL/HSPREF)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38
     AO 58                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFCNAM        23A  O 11 38
     A                                 11 66'(Blank for all)'
     A                                 12  2'Breed stock ..........'
     A                                 12 25'?'
     A            DFBSCD    R        B 12 27REFFLD(@@BSCD)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30
     AO 70                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFBSDS    R        O 12 38REFFLD(BSBSDS)
     A                                 12 66'(Blank for all)'
     A                                 13  2'Hog group ............'
     A                                 13 25'?'
     A            DFHGCD    R        B 13 27REFFLD(HSRREF/HGHGCD *LIBL/HSPREF)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39
     AO 59                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 13 66'(Blank for all)'
     A                                 15  2'Instead of printing the report, em-
     A                                      ail the data as a spreadsheet to:'
     A                                      DSPATR(HI)
     A                                 16  2'E-mail address ....'
     A            DFEMAIL   R        B 16 24REFFLD(TFREF/@@EMAIL *LIBL/TFPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP484K1
     A*%%TS  SD  20011205  153210  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP484ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP484EC                   SFLCTL(HP484ES)
     A*%%TS  SD  19940616  144303  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
