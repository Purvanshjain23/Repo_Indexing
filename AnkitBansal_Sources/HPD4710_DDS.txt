     A*%%TS  SD  20040301  085001  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4710
     A*  TITLE:         SPECIFY FOR CREATING INACTIVE ANNUAL BUDGETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/19/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4710R1
     A*%%TS  SD  20040301  085001  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 13'SPECIFY FOR CREATING INACTIVE ANNU-
     A                                      AL BUDGETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A N99                              4  2'From date ...'
     A N99        D1FMDY    R     Y  B  4 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A N99                              5  2'To date .....'
     A N99        D1TMDY    R     Y  B  5 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A*
     A  67                              6  2'There are no Annual Budget Templat-
     A                                      es. Create Annual Templates first.'
     A                                      DSPATR(HI)
     A  68                              7  2'There are no Active Annual Budget -
     A                                      Templates. Activate Templates first-
     A                                      .'
     A                                      DSPATR(HI)
     A  66                              8  2'No Farms are linked to any Active -
     A                                      Annual Budget Template. Link Farms -
     A                                      first.'
     A                                      DSPATR(HI)
     A  65                              9  2'No Items exist for any Active Annu-
     A                                      al Budget Template. Set up Items fi-
     A                                      rst.'
     A                                      DSPATR(HI)
     A  64                             10  2'Every Item on every Active Annual -
     A                                      Budget Template has zero quantity.'
     A                                      DSPATR(HI)
     A  64                             11  2'Set up quantities first.'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4710K1
     A*%%TS  SD  19980601  163600  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 41'F17=Print Options'
     A N99                             23 60'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4710ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4710EC                  SFLCTL(HP4710ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
