      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP497
      * TITLE:       SPECIFY DEATHS BY WEEK REPORT OPTIONS
      *              (CLOSED GROUPS ONLY)
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     09/13/95
      *
      * FUNCTION: ALLOWS USER TO GENERATE THE DEATHS BY WEEK REPORT.
      *           THIS REPORT IS FOR CLOSED GROUPS ONLY.  DEATHS
      *           ARE SUMMED INTO 20 WEEKS IN REPORTED IN COLUMNS.
      *
      *           SUBMITS HP497CL AND PASSES SELECTION IN LDA.
      *
      *           THIS PROGRAM IS CALLED FROM A MENU.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/07/00  LeAnne Fedor
      *           Removed print options (hold, copies, outque, etc.) from screen
      *           and replaced them with a function key that calls the pop-up
      *           window of print options.
      *
      * 02/14/01  LeAnne Fedor
      *           In preparation for allowing groups on a single farm to have different
      *           production types, we changed the edit for a farm.  The production type
      *           of a selected farm no longer has to "match" the production type that
      *           the user may have keyed on the screen.
      *
      * 09/24/01  LeAnne Fedor
      *           Added 'group type' as a selector.
      *
      * 07/22/02  LeAnne Fedor
      *           In preparation for allowing groups on a single farm to have different
      *           production phases, we changed the edit for a farm.  The production phase
      *           of a selected farm no longer has to "match" the production phase that
      *           the user may have keyed on the screen.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD497    CF   E             WORKSTN
     F                                     INFDS(IOFEED)
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS                 (KEY: FNFNCD)
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE           (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE          (KEY: PPPPCD)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                 (KEY: FSFSCD)
      *
      *
     FHSP025    IF   E           K DISK
      *    GROUP TYPE
      *
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP                 (KEY: HGHGCD)
      *
     FHSP041    IF   E           K DISK
      *  DEATH/DISEASE REASON        (KEY: DRDRCD)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      *  COMPILE TIME ARRAYS FOR SUBMIT JOB COMMAND USED IN QCMDEXC
      *  CMD SUBMITS HP497CL.
      *---------------------------------------------------------------
     D CMD             S              1    DIM(80) CTDATA PERRCD(80)
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D LDA             DS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDGTCD                47     47
     D  LDPTCD                48     52
     D  LDHGSN                53     59  0
     D  LDFMDY                60     65  0
     D  LDTMDY                66     71  0
     D  LDDRCD                72     76
     D  LDCELL                77     79  0
     D  LDRPFL                80     80
     D  LDDOFL                81     81
      *
     D  LDBODS                82    111
     D  LDPTDS               112    121
     D  LDPPDS               122    151
     D  LDFSNM               152    176
     D  LDHGCD               177    183
     D  LDRPDS               184    193
     D  LDDRDS               194    213
     D  LDDODS               214    243
     D  LDGTDS               244    258
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412  0
     D  LDOUTQ               413    422
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D ALL             C                   CONST('A')
      *
      * VALUES TO DISPLAY ON THE SCREEN FOR THE SELECTED
      * REPORT DESCRIPTION
      *
     D DETAIL          C                   CONST('DETAIL')
     D SUM             C                   CONST('SUMMARY')
      *
      * VALUES TO DISPLAY ON THE SCREEN FOR THE SELECTED
      * DOA OPTIONS
      *
     D IDOA            C                   CONST('INCLUDE DOAS WIT-
     D                                     H DAILY DEATHS')
     D ODOA            C                   CONST('OMIT DOAS FROM R-
     D                                     EPORT')
     D SDOA            C                   CONST('SELECT DOAS ONLY-
     D                                     -NO DAILY DEAD')
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C     $SET1         BEGSR
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
     C     TERMFL        IFEQ      YES
     C                   SETON                                        99
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP497K1
     C                   WRITE     HP497EC
     C                   EXFMT     HP497R1
      *
      * CLEAR THE MESSAGES FROM THE MESSAGE DATA STRUCTURES AND PROGRAM
      * MESSAGE QUEUE.
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND SET THE NEXT ROUTINE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C     *IN12         OREQ      *ON                                          F12-PREVIOUS
     C     TERMFL        OREQ      YES                                          BAD USER
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN17         WHENEQ    *ON                                          F17-PRINT OPT
     C                   MOVEL     SCRN1         ROUTNE
     C                   CALL      'HP451'
     C                   PARM                    SDPGM
     C     LDCOPY        PARM      LDCOPY        XXCOPY
     C                   PARM                    LDHOLD
     C                   PARM                    LDOUTQ
     C                   PARM                    XXNITE
     C                   PARM                    XXJOBQ
      *
     C     *IN20         WHENEQ    *ON                                          F20-WRKSPLF
     C                   CALL      'QEZOUTPT'                                       API
     C                   MOVEL     SCRN1         ROUTNE
     C                   OTHER
      *
      * CHECK FOR A USER PROMPT FIRST.
      *
     C     *IN04         IFEQ      *ON                                          IF PROMPT
     C                   EXSR      $F4S1
     C                   ENDIF                                                  IF PROMPT
      *
      * ALWAYS CHECK SECURITY FOR BUSINESS OFFICE
      *
     C                   MOVEL(P)  'HP497'       WKFNCD
     C                   EXSR      $CHKBO
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * CLEAR FROM/TO DATES IN LDA
      *
     C                   Z-ADD     0             LDFYMD
     C                   Z-ADD     0             LDTYMD
      *
      * VALIDATE FROM DATE
      *
     C                   SELECT
      *
      * DATE CAN BE BLANK WHEN USER ENTERS A SPECIFIC GROUP
      *
     C     DFHGCD        WHENNE    *BLANK
     C     DFFMDY        ANDEQ     0
      *
      * DATE IS REQUIRED IF THE USER HAS NOT ENTERED A SPECIFIC GROUP
      *
     C     DFFMDY        WHENEQ    0
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C                   Z-ADD     DFFMDY        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
      *
     C     PRTRN         IFEQ      *BLANK                                       IF GOOD FROM
     C                   Z-ADD     PTO8          LDFYMD
     C                   ELSE
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD FROM
     C                   ENDSL
      *
      * VALIDATE THE 'TO' DATE
      *
     C                   SELECT
      *
      * DATE CAN BE BLANK WHEN USER ENTERS A SPECIFIC GROUP
      *
     C     DFHGCD        WHENNE    *BLANK
     C     DFTMDY        ANDEQ     0
      *
      * DATE IS REQUIRED IF THE USER HAS NOT ENTERED A SPECIFIC GROUP
      *
     C     DFTMDY        WHENEQ    0
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C                   Z-ADD     DFTMDY        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
      *
     C     PRTRN         IFEQ      *BLANK                                       IF GOOD TO
     C                   Z-ADD     PTO8          LDTYMD
     C                   ELSE
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD TO
     C                   ENDSL
      *
      * VERIFY THAT THE "TO" DATE IS NOT EARLIER THAN THE "FROM" DATE
      *
     C     *IN32         IFEQ      *OFF                                         IF EARLIER
     C     *IN33         ANDEQ     *OFF
     C     LDTYMD        ANDLT     LDFYMD
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09009'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF EARLIER
      *
      * IF PRODUCTION TYPE KEYED, VALIDATE
      *
     C                   MOVE      *BLANK        DFPTDS
      *
     C     DFPTCD        IFNE      *BLANK                                       IF TYPE
     C     DFPTCD        CHAIN     HSP010                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD TYPE
     C                   MOVEL     PTPTDS        DFPTDS
     C                   ELSE
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00180'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFPTCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD TYPE
     C                   ENDIF                                                  IF TYPE
      *
      * IF KEYED, VALIDATE PRODUCTION PHASE; BGF NOT ALLOWED
      *
     C                   MOVE      *BLANK        DFPPDS
     C                   SELECT
     C     DFPPCD        WHENEQ    'BGF  '                                      WH BGF PHASE
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00392'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFPPCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
      *
     C     DFPPCD        WHENNE    *BLANK                                       WH PHASE
     C     DFPPCD        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD
     C                   MOVEL     PPPPDS        DFPPDS
     C                   ELSE
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00170'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFPPCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD
     C                   ENDSL
      *
      * IF CELL IS KEYED, IT CANNOT BE NEGATIVE.
      *
     C     DFCELL        IFLT      0                                            IF NEG CELL
     C                   SETON                                        41
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NEG CELL
      *
      *
      * Validate Group Type
      *
     C                   exsr      $gtcd
      *
      * IF FARM SITE KEYED, VALIDATE
      *
     C                   MOVE      *BLANK        DFFSNM
     C     DFFSCD        IFNE      0                                            IF FARM
     C                   EXSR      $fscd
     C                   ENDIF                                                  IF FARM
      *
      * IF THE HOG GROUP IS KEYED, VALIDATE
      *
     C                   Z-ADD     0             WKHGSN
     C     DFHGCD        IFNE      *BLANK                                       IF GROUP
     C                   EXSR      $hgcd
     C                   ENDIF                                                  IF GROUP
      *
      * IF KEYED, VALIDATE DEATH REASON CODE
      *   A) REASON CODE MUST BE BLANK IF USER HAS ELECTED
      *      TO SEE ONLY THE DOAS ON THE REPORT.
      *
     C                   MOVE      *BLANK        DFDRDS
     C     DFDRCD        IFNE      *BLANK                                       IF REASON
      *
     C                   SELECT
     C     DFDOFL        WHENEQ    'S'
     C                   SETON                                        49
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00719'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C     DFDRCD        CHAIN     HSP041                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD
     C                   MOVEL     DRDRDS        DFDRDS
     C                   ELSE
     C                   SETON                                        49
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00221'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFDRCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD
     C                   ENDSL
     C                   ENDIF                                                  IF REASON
      *
      * VALIDATE DEAD-ON-ARRIVAL OPTIONS
      *
     C                   MOVE      *BLANK        DFDODS
     C                   SELECT
     C     DFDOFL        WHENEQ    'I'
     C                   MOVEL     IDOA          DFDODS
      *
     C     DFDOFL        WHENEQ    'O'
     C                   MOVEL     ODOA          DFDODS
      *
     C     DFDOFL        WHENEQ    'S'
     C                   MOVEL     SDOA          DFDODS
     C                   OTHER
      *
     C                   SETON                                        48
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00718'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
      * VALIDATE REPORT FLAG
      *
     C                   MOVE      *BLANK        DFRPDS
     C                   SELECT
     C     DFRPFL        WHENEQ    'S'
     C                   MOVEL     SUM           DFRPDS
      *
     C     DFRPFL        WHENEQ    'D'
     C                   MOVEL     DETAIL        DFRPDS
     C                   OTHER
      *
     C                   SETON                                        42
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00454'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
      *
      * IF THERE ARE NO ERRORS, EXECUTE THE SUBROUTINE TO PROCESS THE
      * USERS REQUEST
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
     C     *IN44         IFEQ      *OFF                                         IF NO F8
     C                   SETON                                        44
     C                   ELSE
     C     *IN08         IFEQ      *ON                                          IF F8=SBMJOB
     C                   EXSR      $PROC1
     C                   ENDIF                                                  IF F8=SBMJOB
     C                   ENDIF                                                  IF NO F8
     C                   ELSE
     C                   SETOFF                                       44
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CHKBO - CHECK THE BUSINESS OFFICE ENTERED AS A RESTRICTOR
      *----------------------------------------------------------------
      *
      * THE BUSINESS OFFICE, IF ENTERED, MUST:
      *    BE A VALID OFFICE IN THE BUSINESS OFFICE MASTER
      *    HAVE A SECURITY LOCATION TO WHICH THE USER IS AUTHORIZED
      *
     C     $CHKBO        BEGSR
      *
      * SET THE 'VALIDATE B.O.' FLAG DEPENDING ON WHETHER THE
      * USER LEFT B.O. BLANK TO GET ALL BUSINESS OFFICES OR ENTERED
      * A SPECIFIC OFFICE.
      *
     C     DFFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           INBOFL
     C                   ELSE
     C                   MOVEL     YES           INBOFL
     C                   ENDIF
      *
      * CHECK THE USER'S SECURITY TO THIS BUSINESS OFFICE.
      * SET INDVFL TO YES TO TELL SECURITY PROGRAM TO COMPARE THE
      * FUNCTION DIVISION CODE TO THE B.O. DIVISION CODE.
      *
     C                   MOVEL     YES           INDVFL
     C                   EXSR      $SEC
     C                   MOVEL     NO            INDVFL
      *
     C                   MOVEL     OUBODS        DFBODS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SEC   - CALL SECURITY PROGRAM
      *---------------------------------------------------------------
      *
     C     $SEC          BEGSR
      *
      * SETUP THE FIELDS TO SEND INTO THE SECURITY PROGRAM
      *
     C                   MOVEL     WKFNCD        INFNCD                         PGM NAME
     C                   MOVEL     DFFSBO        INBOBO                         SCREEN BO
      *
     C                   MOVE      *BLANK        OUBOBO                         BO CODE
     C                   MOVE      *BLANK        OUBODS                         BO DESC
     C                   MOVE      *BLANK        OUDVCD                         FUNCTION DIV
     C                   MOVEL     NO            TERMFL            1            TERMINAL FLG
      *
     C                   CALL      'HPSEC  '
      **** IN
     C                   PARM                    MGID                           MSGID ARRAY
     C                   PARM                    MGDT                           MSGDTA DATA
     C                   PARM                    ERROR                          # OF ERRORS
     C                   PARM                    INFNCD           10            FUNC CODE
     C                   PARM                    INBOBO            5            BO CODE
     C                   PARM                    INDVFL            1            CHECK DIV?
     C                   PARM                    INBOFL            1            CHECK B.O.?
      **** OUT
     C                   PARM                    OUBOBO            5            BO CODE
     C                   PARM                    OUBODS           30            BO DESC.
     C                   PARM                    OUDVCD            5            DIV CODE
     C                   PARM                    SECFL             1            SEC FLAG
     C                   PARM                    OUBOFL            1            BO ERRFLG
     C                   PARM                    OURMFL            1
      *
     C     OUBOFL        IFEQ      YES
     C                   SETON                                        31
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE GROUP TYPE
      *----------------------------------------------------------------
      *
     C     $GTCD         BEGSR
      *
     C                   MOVE      *BLANK        DFGTDS
      *
      * IF GROUP TYPE KEYED, CHECK TO SEE IF IT IS VALID
      *
     C     DFGTCD        IFNE      *BLANK                                       IF GROUP TYPE
     C     DFGTCD        CHAIN     HSP025                             90
     C     *IN90         IFEQ      *OFF                                         IF OK TYPE
     C                   MOVEL     GTGTDS        DFGTDS
     C                   ELSE
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00137'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFGTCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF OK TYPE
     C                   ENDIF                                                  IF GROUP TYPE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * EDIT FARM SITE
      *---------------------------------------------------------------
      *
     C     $fscd         BEGSR
      *
     C     DFFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *ON                                          IF BAD FARM
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
     C                   MOVEL     FSFSNM        DFFSNM
      *
      * FARM B.O. AND SCREEN B.O. MUST MATCH
      *
     C     FSFSBO        IFNE      DFFSBO                                       IF BUS OFF
     C     DFFSBO        ANDNE     *BLANK
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00158'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     FSFSBO        MGW(6)
     C                   MOVEA     DFFSBO        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BUS OFF
      *
     C                   ENDIF                                                  IF BAD FARM
      *
      * CHECK FOR CELL MATCH
      *
     C     DFCELL        IFGT      0                                            IF CELL
     C     FSCELL        ANDNE     DFCELL
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00705'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVE      FSCELL        XXCELL
     C                   MOVEA     XXCELL        MGW(6)
     C                   MOVE      DFCELL        XXCELL
     C                   MOVEA     XXCELL        MGW(8)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF CELL
      *
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * EDIT HOG GROUP
      *---------------------------------------------------------------
      *
     C     $hgcd         BEGSR
      *
     C     DFHGCD        CHAIN     HSP034                             92
     C     *IN92         IFEQ      *ON                                          IF BAD GROUP
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00261'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     HGHGSN        WKHGSN
      *
      * CHECK FOR MATCH ON BUSINESS OFFICE
      *
     C     HGFSBO        IFNE      DFFSBO                                       IF DIFF B/O
     C     DFFSBO        ANDNE     *BLANK
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00459'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGFSBO        MGW(8)
     C                   MOVEA     DFFSBO        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF B/O
      *
      * THE GROUP'S PRODUCTION TYPE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGPTCD        IFNE      DFPTCD                                       IF DIFF PT
     C     DFPTCD        ANDNE     *BLANK
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00463'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGPTCD        MGW(8)
     C                   MOVEA     DFPTCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF PT
      *
      * THE GROUP'S PRODUCTION PHASE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGPPCD        IFNE      DFPPCD                                       IF DIFF PP
     C     DFPPCD        ANDNE     *BLANK
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00462'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGPPCD        MGW(8)
     C                   MOVEA     DFPPCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF PP
      *
      * THE GROUP'S FARM SITE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGFSCD        IFNE      DFFSCD                                       IF DIFF FARM
     C     DFFSCD        ANDNE     0
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00468'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVE      HGFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(8)
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF FARM
      *
      *
      * THE GROUP'S GROUP TYPE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGGTCD        IFNE      DFGTCD                                       IF DIFF TYPE
     C     DFGTCD        ANDNE     *BLANK
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00725'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGGTCD        MGW(8)
     C                   MOVEA     DFGTCD        MGW(9)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF TYPE
      *
      *
      * THE GROUP'S CELL MUST MATCH THE ONE KEYED BY THE USER.
      *
     C     DFCELL        IFGT      0                                            IF CELL
     C     hgfscd        CHAIN     HSP018                             92
     C                   if        *in92 = *on or                               If diff cell
     C                             dfcell <> fscell
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00706'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVE      FSCELL        XXCELL
     C                   MOVEA     XXCELL        MGW(8)
     C                   MOVE      DFCELL        XXCELL
     C                   MOVEA     XXCELL        MGW(10)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  If diff cell
     C                   ENDIF                                                  IF CELL
      *
     C                   ENDIF                                                  IF BAD GROUP
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO PROCESS THE USER'S REQUEST
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE IS ONLY EXECUTED IF THE USER SUBMITTED A REPORT
      *
     C     $PROC1        BEGSR
      *
      * MOVE DISPLAY FILE FIELDS INTO LDA
      *
     C                   MOVEL     DFFSBO        LDFSBO
     C                   MOVEL     DFBODS        LDBODS
     C                   MOVEL     DFPTCD        LDPTCD
     C                   MOVEL     DFPTDS        LDPTDS
     C                   MOVEL     DFPPCD        LDPPCD
     C                   MOVEL     DFPPDS        LDPPDS
     C                   Z-ADD     DFCELL        LDCELL
     C                   Z-ADD     DFFSCD        LDFSCD
     C                   MOVEL     DFFSNM        LDFSNM
     C                   MOVEL     DFDRCD        LDDRCD
     C                   MOVEL     DFDRDS        LDDRDS
     C                   MOVEL     DFRPFL        LDRPFL
     C                   MOVEL     DFRPDS        LDRPDS
     C                   MOVEL     DFDOFL        LDDOFL
     C                   MOVEL     DFDODS        LDDODS
      *
     C                   Z-ADD     WKHGSN        LDHGSN
     C                   MOVEL     DFHGCD        LDHGCD
     C                   Z-ADD     DFFMDY        LDFMDY
     C                   Z-ADD     DFTMDY        LDTMDY
      *
     C                   MOVEL     DFGTCD        LDGTCD
     C                   MOVEL     DFGTDS        LDGTDS
      *
     C                   OUT       LDA
      *
      * MOVE THE 80-CHARACTER COMPILE TIME ARRAY 'CMD' TO A
      * 80-CHARACTER FIELD FOR SUBMITTING HP497CL TO QCMDEXC.
      *
     C                   MOVEA     XXJOBQ        CMD(50)
     C                   MOVEA     CMD           CMDSBM
     C                   CALL      'QCMDEXC'
     C                   PARM                    CMDSBM
     C                   PARM                    LENGTH
      *
      * ISSUE STANDARD REQUEST SUBMISSION MESSAGE
      *
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09003'     MGID(E)
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $F4S1 - F4 PROMPT ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      * IF BUSINESS OFFICE
      *
     C     FLD           WHENEQ    'DFFSBO'
     C                   MOVE      *BLANK        XXFSBO
     C                   MOVE      *BLANK        XXBODS
     C                   CALL      'HP503'
     C                   PARM                    XXFSBO
     C                   PARM                    XXBODS
     C                   PARM                    SVDVCD
     C                   SETON                                        51
     C     XXFSBO        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXFSBO        DFFSBO
     C                   MOVEL     XXBODS        DFBODS
     C                   ENDIF                                                  IF SELECTED
      *
      * PRODUCTION TYPE
      *
     C     FLD           WHENEQ    'DFPTCD'
     C                   MOVE      *BLANK        XXPTCD
     C                   MOVE      *BLANK        XXPTDS
     C                   CALL      'HP504'
     C                   PARM                    XXPTCD
     C                   PARM                    XXPTDS
     C                   SETON                                        54
     C     XXPTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPTCD        DFPTCD
     C                   MOVEL     XXPTDS        DFPTDS
     C                   ENDIF                                                  IF SELECTED
      *
      * PRODUCTION PHASE
      *
     C     FLD           WHENEQ    'DFPPCD'
     C                   MOVE      *BLANK        XXPPCD
     C                   MOVE      *BLANK        XXPPDS
     C                   CALL      'HP596'
     C                   PARM                    XXPPCD
     C                   PARM                    XXPPDS
     C                   SETON                                        55
     C     XXPPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPPCD        DFPPCD
     C                   MOVEL     XXPPDS        DFPPDS
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * IF GROUP TYPE
      *
     C     FLD           WHENEQ    'DFGTCD'
     C                   CALL      'HP531'
     C                   PARM      *BLANK        XXGTCD
     C                   PARM      *BLANK        XXGTDS
     C                   SETON                                        58
     C     XXGTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXGTCD        DFGTCD
     C                   MOVEL     XXGTDS        DFGTDS
     C                   ENDIF                                                  IF SELECTED
      *
      * FARM SITE
      *
     C     FLD           WHENEQ    'DFFSCD'
     C                   MOVEL     DFFSBO        XXFSBO
     C                   MOVE      *BLANK        WKFSCD
     C                   MOVE      *BLANK        XXFSNM
     C                   MOVEL     DFPPCD        XXPPCD
     C                   MOVEL     DFPTCD        XXPTCD
     C                   CALL      'HP551'
     C                   PARM                    XXFSBO
     C                   PARM                    WKFSCD
     C                   PARM                    XXFSNM
     C                   PARM                    XXPPCD
     C                   PARM                    XXPTCD
     C                   SETON                                        56
     C     WKFSCD        IFNE      0                                            IF SELECTED
     C                   Z-ADD     WKFSCD        DFFSCD
     C                   MOVEL     XXFSNM        DFFSNM
     C                   ENDIF                                                  IF SELECTED
      *
      *  HOG GROUP
      *
     C     FLD           WHENEQ    'DFHGCD'
     C                   MOVEL     DFFSBO        XXFSBO
     C                   MOVEL     DFPTCD        XXPTCD
     C                   MOVEL     DFPPCD        XXPPCD
     C                   Z-ADD     DFFSCD        WKFSCD
     C                   Z-ADD     DFFMDY        XXFYMD
     C                   Z-ADD     DFTMDY        XXTYMD
     C                   MOVE      *BLANK        XXHGCD
     C                   CALL      'HP593'
     C                   PARM                    XXFSBO
     C                   PARM                    XXPTCD
     C                   PARM                    XXPPCD
     C                   PARM                    WKFSCD
     C                   PARM                    XXFYMD
     C                   PARM                    XXTYMD
     C                   PARM                    XXHGCD
     C                   SETON                                        57
      *
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        DFHGCD
     C                   ENDIF                                                  IF SELECTED
      *
      * DEATH REASON
      *
     C     FLD           WHENEQ    'DFDRCD'
     C                   MOVE      *BLANK        XXDRCD
     C                   MOVE      *BLANK        XXDRDS
     C                   CALL      'HP510'
     C                   PARM                    XXDRCD
     C                   PARM                    XXDRDS
     C                   SETON                                        69
     C     XXDRCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXDRCD        DFDRCD
     C                   ENDIF                                                  IF SELECTED
      *
      *  DOA OPTION
      *
     C     FLD           WHENEQ    'DFDOFL'
     C                   MOVE      *BLANK        XXDOFL
     C                   MOVE      *BLANK        XXDODS
     C                   CALL      'HP535'
     C                   PARM                    XXDOFL
     C                   PARM                    XXDODS
     C                   SETON                                        68
      *
     C     XXDOFL        IFNE      *BLANK
     C                   MOVEL     XXDOFL        DFDOFL
     C                   MOVEL     XXDODS        DFDODS
     C                   ENDIF
      *
      *  REPORT
      *
     C     FLD           WHENEQ    'DFRPFL'
     C                   MOVE      *BLANK        XXRPFL
     C                   MOVE      *BLANK        XXRPDS
     C                   CALL      'HP565'
     C                   PARM                    XXRPFL
     C                   PARM                    XXRPDS
     C                   SETON                                        52
      *
     C     XXRPFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXRPFL        DFRPFL
     C                   MOVEL     XXRPDS        DFRPDS
     C                   ENDIF                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *---------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *----------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *
      * PARM LISTS
      *
      * DEFINE LDA
     C     *DTAARA       DEFINE    *LDA          LDA
     C                   IN        LDA
      *
      * Initialize LDA fields for printing
      *
     C                   MOVE      *BLANK        LDHOLD
     C                   Z-ADD     0             LDCOPY
     C                   MOVEL     LDOUTU        LDOUTQ
     C                   MOVEL     '*JOBD'       XXJOBQ           10
     C                   MOVEL     NO            XXNITE            1
     C                   Z-ADD     0             XXCOPY            1 0
      *
      * GET THIS FUNCTION'S NUMBER OF COPIES, HOLD YES/NO
      *
     C     SDPGM         CHAIN     HSP002                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     FNCOPY        LDCOPY
     C                   MOVEL     FNHOLD        LDHOLD
     C                   ENDIF
      *
     C     LDCOPY        IFEQ      0
     C                   Z-ADD     1             LDCOPY
     C                   ENDIF
      *
     C     LDHOLD        IFEQ      *BLANK
     C                   MOVEL     NO            LDHOLD
     C                   ENDIF
      *
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6
     C     *LIKE         DEFINE    PGM           WKFNCD
      *
      * NUMERIC PARM FIELDS
      *
     C     *LIKE         DEFINE    DFFSCD        WKFSCD
     C                   Z-ADD     0             XXFYMD            6 0
     C                   Z-ADD     0             XXTYMD            6 0
      *
      * ALPHA PARM FIELDS
      *
     C     *LIKE         DEFINE    DFFSBO        XXFSBO
     C     *LIKE         DEFINE    DFBODS        XXBODS
     C                   MOVE      *BLANK        XXFSCD            5
     C                   MOVE      *BLANK        XXCELL            2
     C     *LIKE         DEFINE    DFFSNM        XXFSNM
     C     *LIKE         DEFINE    DFPPCD        XXPPCD
     C     *LIKE         DEFINE    DFPPDS        XXPPDS
     C     *LIKE         DEFINE    DFPTCD        XXPTCD
     C     *LIKE         DEFINE    DFPTDS        XXPTDS
     C     *LIKE         DEFINE    DFHGCD        XXHGCD
     C     *LIKE         DEFINE    DFRPFL        XXRPFL
     C     *LIKE         DEFINE    DFRPDS        XXRPDS
     C     *LIKE         DEFINE    DRDRCD        XXDRCD
     C     *LIKE         DEFINE    DRDRDS        XXDRDS
     C     *LIKE         DEFINE    DFDOFL        XXDOFL
     C     *LIKE         DEFINE    DFDODS        XXDODS
     C     *LIKE         DEFINE    DFGTCD        XXGTCD
     C     *LIKE         DEFINE    DFGTDS        XXGTDS
      *
      * WORKFIELD
     C     *LIKE         DEFINE    HGHGSN        WKHGSN
      *
      *  PARMS FOR USE WITH QCMDEXC.  'LENGTH' IS THE LENGTH OF THE
      *  COMMAND THAT WILL BE ISSUED WITH QCMDEXC.  QCMDEXC REQUIRES
      *  THE FIELD CONTAINING LENGTH TO BE DEFINED AS 15 WITH 5 DECIMAL
      *  POSITIONS.  THE FIELD 'CMDSBM' WILL HOLD THE VALUES OF THE
      *  THE SUBMIT JOB COMMAND WHEN PASSED AS A PARM ON THE CALL TO
      *  QCMDEXC.
      *
     C                   Z-ADD     80            LENGTH           15 5          QCMDEXC PARM
     C                   MOVE      *BLANK        CMDSBM           80            QCMDEXC PARM
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
      * EXECUTE THE SECURITY SUBROUTINE TO DETERMINE USER
      * AUTHORIZATIONS.
      *
     C                   MOVEL(P)  SDPGM         WKFNCD
     C                   MOVEL     NO            INBOFL
     C                   EXSR      $SEC
     C                   MOVEL     YES           INBOFL            1
      *
      * IF ERRORS ON INITIAL CALL, SET TERMINAL FLAG TO YES.
      *
     C     SECFL         IFEQ      YES
     C                   MOVEL     YES           TERMFL
     C                   ENDIF
      *
      * DEFALUT BUSINESS OFFICE ONTO SCREEN
      *
     C                   MOVEL     OUBOBO        DFFSBO
     C                   MOVEL     OUBODS        DFBODS
      *
      * SAVE THIS FUNCTION'S DIVISION CODE
      *
     C     *LIKE         DEFINE    OUDVCD        SVDVCD
     C                   MOVEL     OUDVCD        SVDVCD
      *
      *
      * DEFAULT THE DOA OPTION FIELD ON THE SCREEN TO 'OMIT DOAS'
      * AND DEFAULT A REPORT OPTION OF 'DETAIL'
      *
     C                   MOVEL     'O'           DFDOFL
     C                   MOVEL     'D'           DFRPFL
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * COMPILE TIME ARRAY NAMED 'CMD' USED IN QCMDEXC
      *****************************************************************
** CMD TABLE
SBMJOB CMD(CALL PGM(HP497CL)) JOB(DEATH20W) JOBQ(__________)
