     A*%%TS  SD  19960513  163128  ISLRAMS     REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD513E1 - SELECT J.D. EDWARDS NUMBER for E1
     A*                 FOR CUSTOMER, PRODUCER OR FARM SITE
     A*  CREATED BY:    ROSE CENTONZE
     A*  DATE:          03/30/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP513R1
     A*%%TS  SD  19940719  094912  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 51)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP513R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP513R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP513S1                   SFL
     A*%%TS  SD  19940719  094912  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFALPH    R        O  6  1REFFLD(I0101/ABALPH *LIBL/F0101)
     A            DFAN8     R        O  6 43REFFLD(I0101/ABAN8 *LIBL/F0101)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP513C1                   SFLCTL(HP513S1)
     A*%%TS  SD  19960513  163128  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP513R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'HP513E1'
     A                                      DSPATR(HI)
     A  88                              1 11'SELECT PRODUCER JDE NUMBER'
     A                                      DSPATR(HI)
     A N88N89                           1 15'SELECT CUSTOMER'
     A                                      DSPATR(HI)
     A  89                              1 10'SELECT FARM SITE JDE NUMBER'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Name'
     A                                  4 44'Number'
     A            C1ALPH    R        B  5  1REFFLD(I0101/ABALPH *LIBL/F0101)
     A                                      TEXT('JDE cust name')
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
     A          R HP513K1
     A*%%TS  SD  19940330  162428  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP513R1)
     A                                 16  1'F12=Previous'
