/********************************************************************/
/*  SYNOPSIS - PRINT DAILY HOLDOVER REPORT TYPE 1 FROM THE          */
/*             JOB SCHEDULER THEN EMAIL TO THE SELECTED CARRIER     */
/*             CALLED FROM PBQXUPC (WHICH IS CALLED FROM PBQWUPC)   */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PBQ3UPC-EMAIL DAILY HOLDOVER REPORT TO CARRIER      */
/*  DATE      - 04/21/2015                                          */
/*                                                                  */
/*  PARAMETERS RECEIVED:                                            */
/*  &FRMDATN    - PRIOR DATE                                        */
/*  &THRDATN    - PRIOR DATE PLUS 3 DAYS                            */
/*  &RPTTYP     - REPORT TYPE 1: ALL HOLDOVERS                      */
/*  &CARRIER    - CARRIER CODE                                      */
/*                                                                  */
/********************************************************************/
/* 04/17/2015 LJB E004076  PASS IN A SEND Y/N AND EMAIL ADDRESS     */
/*    FIELDS. DON'T EMAIL IF SEND Y/N IS BLANKS OR IF EMAIL IS      */
/*    MISSING                                                       */
/********************************************************************/
 PBQ3UPC:    PGM        PARM(&FRMDATN &THRDATN &RPTTYP &CARRIER +
                          &COMPNYN)

/* PARMS                                                                      */
             DCL        VAR(&COMPNYN) TYPE(*DEC)
             DCL        VAR(&COMPANY) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&COMPA)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&FRMDATN) TYPE(*DEC)
             DCL        VAR(&FRMDAT)  TYPE(*DEC) LEN(7 0)
             DCL        VAR(&FRMDATA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&THRDATN) TYPE(*DEC)
             DCL        VAR(&THRDAT)  TYPE(*DEC) LEN(7 0)
             DCL        VAR(&THRDATA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&FRMTIMN) TYPE(*DEC)
             DCL        VAR(&THRTIMN) TYPE(*DEC)
             DCL        VAR(&FRMTIM)  TYPE(*DEC) LEN(6 0)
             DCL        VAR(&THRTIM)  TYPE(*DEC) LEN(6 0)
             DCL        VAR(&DELIVDTN) TYPE(*DEC)
             DCL        VAR(&DELIVDT) TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&RPTTYP)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DESC)    TYPE(*CHAR) LEN(35)
             DCL        VAR(&COPYN)   TYPE(*DEC) VALUE(1.00000)
             DCL        VAR(&COPY)    TYPE(*DEC) LEN(2 0)
             DCL        VAR(&OUTQ)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&COMMAND) TYPE(*CHAR) LEN(512)
             DCL        VAR(&SELOMIT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WHSE1)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE2)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE3)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE4)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE5)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE6)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE7)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE8)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE9)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSE10)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&CARRIER) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SENDYN)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&EMAIL)   TYPE(*CHAR) LEN(50)
             DCL        VAR(&PRTF)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&SEQN)    TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&SUBJECT) TYPE(*CHAR) LEN(100)

             DCL        VAR(&CURDAT)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&CURTIM)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&CURTIM6) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CURYYA)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&CURYY)   TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&ENDDAT)  TYPE(*DEC) LEN(7 0)
             DCL        VAR(&ENDTIM) TYPE(*DEC) LEN(6 0)
             DCL        VAR(&CURDAT7)  TYPE(*DEC) LEN(7 0)
             DCL        VAR(&FRMTIM6) TYPE(*DEC) LEN(6)
             DCL        VAR(&HOURS) TYPE(*DEC) LEN(5 0) VALUE(12)
             DCL        VAR(&ENDDATA)  TYPE(*CHAR) LEN(7)
             DCL        VAR(&ENDTIMA) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CURDATA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&FRMTIMA) TYPE(*CHAR) LEN(6)
             DCL        VAR(&THRTIMA) TYPE(*CHAR) LEN(6)

/* OPNQRYF FIELDS FOR DAILY HOLDOVER REPORT                                   */

             DCL        VAR(&A)       TYPE(*CHAR) LEN(25)
             DCL        VAR(&B)       TYPE(*CHAR) LEN(75)
             DCL        VAR(&BB)      TYPE(*CHAR) LEN(50)
             DCL        VAR(&C)       TYPE(*CHAR) LEN(150)
             DCL        VAR(&D)       TYPE(*CHAR) LEN(500)
             DCL        VAR(&F)       TYPE(*CHAR) LEN(150)
             DCL        VAR(&G)       TYPE(*CHAR) LEN(200)
             DCL        VAR(&H)       TYPE(*CHAR) LEN(150)

/* OPNQRYF FIELDS FOR LOADS NOT ON YARD REPORT                                */

             DCL     &I        *CHAR   40
             DCL     &J        *CHAR   50
             DCL     &L        *CHAR  120
/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/
             CHGVAR     VAR(&DELIVDT) VALUE(&DELIVDTN)
             CHGVAR     VAR(&SELOMIT) VALUE('O')
             CHGVAR     VAR(&WHSE1) VALUE('GB1')
             CHGVAR     VAR(&WHSE2) VALUE('SB1')
             CHGVAR     VAR(&SEQN) VALUE(1)
             CHGVAR     VAR(&SENDYN) VALUE('N')

/*********************************************************************/
/* DATE AND TIME ROUTINES COPIED FROM PDX9CLPJ AND PDX9CLP           */
/*********************************************************************/
             RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&CURDAT)
             CVTDAT     DATE(&CURDAT) TOVAR(&CURDATA) FROMFMT(*MDY) +
                          TOFMT(*CYMD) TOSEP(*NONE)

/* GET CURRENT DATE/TIME                                          */
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&CURTIM)
             CHGVAR VAR(&CURTIM6) VALUE(%SST(&CURTIM 1 6))

/* GET CURRENT DATE/TIME + 12 HOURS                                */
             CHGVAR VAR(&FRMTIM6) VALUE(&CURTIM6)
             CHGVAR VAR(&CURDAT7) VALUE(&CURDATA)
             CALL       PGM(POIUXFR) PARM('' &CURDAT7 &FRMTIM6 +
                          &ENDDAT &ENDTIM &HOURS)
             CHGVAR VAR(&ENDDATA) VALUE(&ENDDAT)
             CHGVAR VAR(&ENDTIMA) VALUE(&ENDTIM)

/* FROM TIME AND THRU TIME, FROM AND THRU DATES                     */
             CHGVAR     VAR(&FRMTIM) VALUE(&FRMTIMN)
             CHGVAR     VAR(&FRMTIMA) VALUE(&FRMTIM)
             CHGVAR     VAR(&THRTIM) VALUE(&THRTIMN)
             CHGVAR     VAR(&THRTIMA) VALUE(&THRTIM)
             CHGVAR     VAR(&THRDAT) VALUE(0) /* RDD IS ZEROS HERE */
             CHGVAR     VAR(&THRDAT) VALUE(&THRDATN)
             CHGVAR     VAR(&THRDATA) VALUE(&THRDAT)
             CHGVAR     VAR(&FRMDAT) VALUE(&FRMDATN)
             CHGVAR     VAR(&FRMDATA) VALUE(&FRMDAT)

             CHGVAR     VAR(&SENDYN) VALUE('N')

/* * * * * * * * * * *  -DAILY HOLDOVER REPORT-  * * * * * * * * * * */
/*********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES        */
/*********************************************************************/
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&DESC) VALUE('DAILY HOLDOVER REPORT FOR +
                          CARRIER')
             CHGVAR     VAR(&OUTQ) VALUE('QPRINT3')
             CHGVAR     VAR(&PRTF) VALUE(PDX9PFR$)
             OVRPRTF    FILE(&PRTF) DRAWER(1) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(*NO) SAVE(*NO)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE FOR HOLDOVERS FOR GUYMON                 */
/*********************************************************************/
/* COMPANY                                                           */
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&A) VALUE('FJAIC3 *EQ ' *CAT &COMPA *BCAT '*AND')
             ENDDO

/* REPORT TYPE 1 FOR DAILY HOLDOVER REPORT                          */
/*'SCHED SHP DT EQ FRMDATA' & 'BOL COMPLETE DATE GT SCHED SHP DT'   */
             CHGVAR     VAR(&BB) VALUE('*AND FJJ7DT *GT FJAODT')

             CHGVAR     VAR(&B) VALUE('*AND FJAODT *EQ ' *CAT +
                          &FRMDATA *BCAT &BB)

/* OR 'LOAD IS NOT SHIPPED AND SCHED SHIP DATE IS LE FRMDATA'  */
             CHGVAR     VAR(&G) VALUE('*OR *NOT (FJPKST *EQ +
                          %VALUES("I" "S")) *AND FJAODT *LE ' *CAT +
                          &FRMDATA)

/* EXCLUDE CR,PR SHIPPING METHOD   (RAIL)                           */
             CHGVAR   VAR(&C) VALUE('*NOT (FJQ2CD *EQ +
                %VALUES("CR" "PR"))')
             CHGVAR     VAR(&F) VALUE(&A *BCAT &C *BCAT &B)
             CHGVAR     VAR(&D) VALUE(&F *BCAT &G)

             OVRDBF     FILE(OMFJCPLF) SHARE(*YES)
             OPNQRYF    FILE((OMFJCPLF)) +
                            QRYSLT(&D) +
                            KEYFLD(*FILE)

/*********************************************************************/
/*   CALL PRINT PROGRAM FOR DAILY HOLDOVERS REPORT TYPE 1            */
/*********************************************************************/
             CALL       PGM(PDX9PFR) PARM(' ' &COMPANY &FRMDAT +
                          &RPTTYP &THRDAT &DELIVDT &FRMTIM &THRTIM +
                          &WHSE1 &WHSE2 &WHSE3 &WHSE4 &WHSE5 &WHSE6 +
                          &WHSE7 &WHSE8 &WHSE9 &WHSE10 &SELOMIT +
                          &CARRIER &SENDYN)

             IF         COND(&SENDYN *EQ 'Y') THEN(DO)
             IF         COND(&CARRIER *NE 'BUY') THEN(DO)
             CHGVAR     VAR(&SUBJECT) VALUE('Daily Holdover Report' +
                          *BCAT 'for' *BCAT &CARRIER *CAT '/' *CAT +
                          &COMPA)
             CHGVAR     VAR(&PRTF) VALUE(PDX9PFR$)
             CALL       PGM(PBQ1XFR) PARM('' &COMPANY &CARRIER &PRTF +
                          &SUBJECT)
             ENDDO
             ENDDO

             CLOF       OPNID(OMFJCPLF)

/* * * * * * * * * * * * -LOADS NOT PICKED UP- * * * * * * * * * * * */
/*********************************************************************/
/* SET VARIABLES FOR LOADS NOT PICKED UP                             */
/*********************************************************************/
             CHGVAR     VAR(&SENDYN) VALUE('N')

             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&OUTQ) VALUE('QPRINT3')
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&DESC) VALUE('Loads Not Picked Up for +
                          Carrier')

/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/
             CHGVAR     VAR(&PRTF) VALUE('PDUEPFR$')
             CHGVAR     VAR(&OUTQ) VALUE('QPRINT3')
             OVRPRTF    FILE(&PRTF) DRAWER(1) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(*NO) SAVE(*NO)

/*********************************************************************/
/*   CALL PRINT PROGRAM FOR LOADS NOT PICKED UP                      */
/*********************************************************************/
             CALL       PGM(PDUEPFR) PARM(' ' &COMPANY &CARRIER +
                          &SENDYN)

             IF         COND(&SENDYN *EQ 'Y') THEN(DO)
             IF         COND(&CARRIER *NE 'BUY') THEN(DO)
             CHGVAR     VAR(&SUBJECT) VALUE('Loads Not Picked Up' +
                          *BCAT 'for' *BCAT &CARRIER *CAT '/' *CAT +
                          &COMPA)
             CHGVAR     VAR(&PRTF) VALUE(PDUEPFR$)
             CALL       PGM(PBQ1XFR) PARM('' &COMPANY &CARRIER &PRTF +
                          &SUBJECT)
             ENDDO
             ENDDO

/* * * * * * * * * * * * - LOADS NOT ON YARD - * * * * * * * * * * * */
/*********************************************************************/
/* OVERRIDE PRINTER FILE FOR LOADS NOT ON YARD REPORT                */
/*********************************************************************/
             CHGVAR     VAR(&SENDYN) VALUE('N')

             CHGVAR     VAR(&PRTF) VALUE('PDHLPFR$')
             CHGVAR     VAR(&OUTQ) VALUE('QPRINT3')
             OVRPRTF    FILE(&PRTF) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(*NO) SAVE(*NO)

/*********************************************************************/
/* CREATE OPEN QUERY FILE TO BE USED FOR LOADS NOT PICKED UP REPORT  */
/*********************************************************************/
             IF         COND(&COMPA *GT '000') THEN(DO)
             CHGVAR     VAR(&I) VALUE('FJAIC3 *EQ ' *CAT &COMPA *BCAT '*AND')
             ENDDO

             CHGVAR     VAR(&J) VALUE('(FJAODT *GE ' *CAT +
                          &FRMDATA *CAT ' *AND FJAODT *LE ' *CAT +
                          &THRDATA *CAT ')')

             CHGVAR     VAR(&L) VALUE(&I *BCAT &J)

/*  RUN QUERY TO READ LOAD HEADER BY DATE RANGE                      */

             OVRDBF     FILE(OMFJCPLN) SHARE(*YES)
             OPNQRYF    FILE((OMFJCPLN)) QRYSLT(&L) KEYFLD(*FILE)

/*********************************************************************/
/* CALL PRINT PROGRAM FOR LOADS NOT ON YARD                          */
/*********************************************************************/
             CALL       PGM(PDHLPFR) PARM(' ' &COMPANY &FRMDAT +
                          &THRDAT &CARRIER &SENDYN)

/* EMAIL REPORT IF AT LEAST ONE RECORD WAS PRINTED                   */
             IF         COND(&SENDYN *EQ 'Y') THEN(DO)
             IF         COND(&CARRIER *NE 'BUY') THEN(DO)
             CHGVAR     VAR(&SUBJECT) VALUE('Loads Not On Yard' +
                          *BCAT 'for' *BCAT &CARRIER *CAT '/' *CAT +
                          &COMPA)
             CHGVAR     VAR(&PRTF) VALUE(PDHLPFR$)
             CALL       PGM(PBQ1XFR) PARM('' &COMPANY &CARRIER &PRTF +
                          &SUBJECT)
             ENDDO
             ENDDO

             CLOF       OPNID(OMFJCPLN)

 END:        ENDPGM
/*********************************************************************/
