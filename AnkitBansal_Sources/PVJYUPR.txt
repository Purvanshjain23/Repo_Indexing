     H DATFMT(*YMD) DATEDIT(*YMD) DEBUG(*YES)
     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) DBGVIEW(*SOURCE) CVTOPT(*DATETIME)
     Z* ACTGRP(*CALLER) OPTIMIZE(*BASIC)
     H****************** ILE RPG PROGRAM ***********************************************************
     ‚* SYSTEM:      Seaboard Foods
     ‚* PROGRAM:     PVJYUPR
     ‚* PROGRAM DSC: Exc TRG PDMRREP WrapperUP Execute user program
     ‚* PROGRAMMER:  Jagdish Mistry
     ‚* CREATED:     02/19/2025
     ‚* PROJECT:     WI 642-USDA FAS 98 and FAS99-Trigger approach
     ‚* PURPOSE:     This is a trigger wrapper program which accepts IBM defined parameters for
     ‚*              the trigger buffer.
     H*
     H* COMPILING NOTES:
     H*    ˆCRTBNDRPG
     H*    ˆDFTACTGRP(*NO) DBGVIEW(*SOURCE) CVTOPT(*DATETIME)
     H*    ˆACTGRP(*CALLER) OPTIMIZE(*BASIC)
     H*
     H* DB2 TRIGGER NOTES:
     ‚*     1. Files with trigger MUST be journal to save Before & After images.
     ‚*     2. Create CL program to add the physical file trigger. Reference the trigger wrapper
     ‚*        program in the ADDPFTRG command. See PUABUPC program for example.
     ‚*     3. If a file is regenerated and recompiled the RMVPFTRG & ADDPFTRG commands need to be
     ‚*        reapplied. Also, change/recompile the TRG Process and TRG Wrapper program.
     ‚*     4. If adding/removing trigger to an existing file, the file must NOT be in use to
     ‚*        perform the RMVPFTRG & ADDPFTRG command. Typically, if the file is heavily used
     ‚*        then this could be resolve after an IPL in a 'restricted state'.
     ‚*     5. Be caution when manually updating files with triggers via SQL/DBU/YWRK, etc or
     ‚*        fix/conversion program since these events will call the triggering program.
     ‚*     6. IBM command:
     ‚*        PRTTRGPGM will create a report to list all files with triggers.
     ‚*        RMVPFTRG will remove physical file trigger.
     ‚*     7. You can clone PTPTUPR program and apply this to any DB2 file.
     H**********************************************************************************************
     H* MODIFICATIONS:
     H**********************************************************************************************
     H* DATE      INT  PROJECT  DESCRIPTION
     H* --------  ---  -------  -------------------------------------------------------------------
     H**********************************************************************************************
      *
     ‚* Data Structure Passed in by the IBM Defined Database
     D TrgBuffer       DS
     D  TFileName                    10
     D  TLibraryName                 10
     D  TMemberName                  10
     D  TEvent                        1
     D  TTime                         1
     D  TCommitLock                   1
     D  TFill01                       3
     D  TCCSID                       10I 0
     D  TRRN                         10I 0
     D  TFill02                      10I 0
     D  TOldOffset                   10I 0
     D  TOldLength                   10I 0
     D  TOldNullOff                  10I 0
     D  TOldNullLen                  10I 0
     D  TNewOffset                   10I 0
     D  TNewLength                   10I 0
     D  TNewNullOff                  10I 0
     D  TNewNullLen                  10I 0
      *
     D TrgBufferLen    S             10I 0
     ‚* Before and After Record Images: Export Doc Order Hdr
     D OldRecord     E DS                  Extname(PDMRREP) Prefix(O_)
     D                                     Based(OldRecPtr)
      *
     D NewRecord     E DS                  Extname(PDMRREP)
     D                                     Based(NewRecPtr)
      *
      *
      **********************************************************************************************
      * Entry Parameter List
      **********************************************************************************************
     C     *entry        plist
     C                   parm                    TrgBuffer
     C                   parm                    TrgBufferLen
      *
      *
     ‚* Map External DS to Trigger Buffer Using Pointers.
     C                   eval      OldRecPtr = %addr(TrgBuffer) + TOldOffSet
     C                   eval      NewRecPtr = %addr(TrgBuffer) + TNewOffSet
      *
     ‚* Clear Old Buffer for Insert and New Buffer for Delete To Prevent Previous Record Data.
     C                   If        TEvent = '1'                                 Insert
     C                   Clear                   OldRecord
     C                   ElseIf    TEvent = '2'                                 Delete
     C                   Clear                   NewRecord
     C                   EndIf
      *
     ‚* Call 2E Program for Processing.
     C                   Call      'PVJZXFR'
     C                   Parm      *Blank        W0RTN             7
     C                   Parm                    OldRecord
     C                   Parm                    NewRecord
     C                   Parm                    TEvent
      *
      *
     C                   return
      *
