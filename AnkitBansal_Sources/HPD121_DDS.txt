     A*%%TS  SD  20010416  102942  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD121
     A*  TITLE:         REVISE FEED PICKUP TICKET
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/05/94
     A*
     A*  INDICATORS:
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  63  RECOST MODE - ALL FIELDS ARE PROTECTED.
     A*  65  PROTECT ALL FIELDS. THIS TICKET HAS EITHER BEEN POSTED OR
     A*      IS ASSOCIATED WITH ONE OR MORE CLOSED GROUPS.
     A*  88  TICKET HAS A STATUS OF CO=COSTED; SO, DISPLAY COSTED DATE/TIME/USER
     A*  90  THE TICKET THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A* SDN736 RECOMPILED FOR TICKET NBR INCREASE (TKNO & RTNO 7.0 TO 9.0)
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90N63N65                            CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90N65                               CF09(09 'MAINTAIN')
     A N90N65 60                            CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP121R1
     A*%%TS  SD  20010416  102942  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 27'REVISE FEED PICKUP TICKET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Ticket date ............'
     A            DFMDY     R        B  4 29REFFLD(@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  -')
     A  63
     AO 65                                  DSPATR(PR)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N63N65
     A N90 42                               DSPATR(HI)
     A N38N63N65
     A N90 42                               DSPATR(UL)
     A                                  5  2'Ticket status ..........'
     A N61        DFTSCD    R        O  5 29REFFLD(FHTSCD)
     A                                  7  2'PICK UP FROM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  2'Farm site ..............'
     A N90N63N65                        8 27'?'
     A            DFFSFR    R        B  8 29REFFLD(FSREC/FSFSCD *LIBL/HSP018)
     A                                      EDTCDE(M)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N63N65
     A N90 42                               DSPATR(HI)
     A N36N63N65
     A N90 42                               DSPATR(UL)
     A  63
     AO 65                                  DSPATR(PR)
     A            DFNMFR    R        O  8 41REFFLD(FSFSNM)
     A                                  9  2'Bin ....................'
     A N90N63N65                        9 27'?'
     A            DFBNFR    R        B  9 29REFFLD(BNBNCD)
     A  63
     AO 65
     AO 90                                  DSPATR(PR)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N63N65
     A N90 42                               DSPATR(HI)
     A N31N63N65
     A N90 42                               DSPATR(UL)
     A            DFFR          13A  O  9 41
     A                                 10  2'Pounds .................'
     A            DFFDLB    R     Y  B 10 29REFFLD(FDFDLB)
     A                                      EDTCDE(K)
     A                                      CHECK(RB)
     A  63
     AO 65
     AO 90                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34N63N65
     A N90 42                               DSPATR(HI)
     A N34N63N65
     A N90 42                               DSPATR(UL)
     A                                 10 41'Ration/additive .........:'
     A            DFRNCD    R        O 10 69REFFLD(RARNCD)
     A            DFADCD    R        O 10 75REFFLD(RAADCD)
     A                                 11 41'Avg cost of feed/ton ....:'
     A            DFTNRT    R        O 11 69REFFLD(@@NU52)
     A                                      EDTCDE(K)
     A                                 13  2'Pick up ticket number ..'
     A            DFTKNO    R        B 13 29REFFLD(FHREC/FHTKNO *LIBL/HSP037)
     A                                      EDTCDE(M)
     A  60
     AO 63
     AO 65                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N60N63
     A N65N90 42                            DSPATR(HI)
     A N37N60N63
     A N65N90 42                            DSPATR(UL)
     A                                 13 41'Reference ticket ........:'
     A            DFRTNO    R        O 13 69REFFLD(FDRTNO)
     A                                      EDTCDE(M)
     A  30                                  DSPATR(RI)
     A                                 14  2'Pick-up feed mill ......'
     A            DFTKNOFMBOR        O 14 29REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A                                 14 41'Reference feed mill .....:'
     A            DFRTNOFMBOR        O 14 69REFFLD(FDFMBO)
     A                                 15 41'Reference ticket date ...:'
     A            DFRMDY    R        O 15 69REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                 16 41'Hog group ...............:'
     A            DFHGFR    R        O 16 69REFFLD(HGHGCD)
     A  39                                  DSPATR(RI)
     A                                 17  2'DELIVER TO'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 18  2'Farm site ..............'
     A N90N63N65                       18 27'?'
     A            DFFSTO    R        B 18 29REFFLD(FDFSCD)
     A                                      EDTCDE(M)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N63N65
     A N90 42                               DSPATR(HI)
     A N33N63N65
     A N90 42                               DSPATR(UL)
     A  63
     AO 65                                  DSPATR(PR)
     A            DFNMTO    R        O 18 41REFFLD(FSFSNM)
     A                                 19  2'Bin ....................'
     A N90N63N65                       19 27'?'
     A            DFBNTO    R        B 19 29REFFLD(BNBNCD)
     A  63
     AO 65
     AO 90                                  DSPATR(PR)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35N63N65
     A N90 42                               DSPATR(HI)
     A N35N63N65
     A N90 42                               DSPATR(UL)
     A            DFTO          13A  O 19 41
     A                                 20  2'Hog group ..............'
     A N90N63N65                       20 27'?'
     A            DFHGTO    R        B 20 29REFFLD(HGHGCD)
     A  63
     AO 65
     AO 90                                  DSPATR(PR)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N63N65
     A N90 42                               DSPATR(HI)
     A N32N63N65
     A N90 42                               DSPATR(UL)
     A*
     A  88                              4 64'    Costed     '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A  88                              5 64'Date:'
     A                                      COLOR(BLU)
     A  88        DFCOMDY   R        O  5 70REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      COLOR(BLU)
     A  88                              6 64'Time:'
     A                                      COLOR(BLU)
     A  88        DFCOTM    R        O  6 70REFFLD(HSRREF/@@COTM *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
     A                                      COLOR(BLU)
     A  88                              7 64'User:'
     A                                      COLOR(BLU)
     A  88        DFCOUSR   R        O  7 70REFFLD(HSRREF/@@COUSR *LIBL/HSPREF)
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP121K1
     A*%%TS  SD  19940405  152432  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90N63N65                       23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90N65 61                       23 34'F9=Create'
     A N90N65 60                       23 34'F9=Revise'
     A N90N65 63                       23 34'F9=Recost'
     A N90N65 60                       23 45'F11=Delete'
     A                                 23 57'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP121ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP121EC                   SFLCTL(HP121ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
