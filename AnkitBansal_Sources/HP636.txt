      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP636 - LISTING OF FEED TICKET RECEIVING FILE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     04/28/97
      *
      * FUNCTION: THIS PROGRAM PRINTS A LISTING OF THE UPLOADED
      *           FEED TICKET RECEIVING FILE.
      *
      *           THIS LISTING IS GENERATED AUTOMATICALLY WHEN THE
      *           USER MOVES THE RECEIVING ATA TO THE EDITING FILES
      *           BY TAKING THE 'MOVE' FUNCTION (HP437, HP437CL).
      *
      *           THIS LISTING IS ALSO GENERATED ON DEMAND WHEN THE
      *           USER TAKES HP436, HP436CL TO PRINT THE LISTING
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/24/01  LeAnne Fedor
      *           Added totals for 2 columns: pounds and ingredient cost
      *
      * 05/02/01  LeAnne Fedor
      *           Recompile only. Grossman ticket number expanded from
      *           6 to 7 positions.
      *
      * 05/07/01  LeAnne Fedor
      *           Recompile only. New processing flag field added to staging file.
      *
      * 01/07/02  LeAnne Fedor
      *           Ticket number is now alphanumeric in the staging file...changed
      *           logic accordingly.
      *
      * 10/26/09  LeAnne Ramsey
      *           David Weaver is rewriting the p.c. side.
      *           We no longer have/need the "staging" file. So, this listing is now
      *           over the "receiving" file.
      *
      * 10/30/19  Brad Baden    E15736
      *           Add new field Ingredient Cost from the HSP235 file to the report.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           File change on HSP235. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *           5A to 6A.
      *           Shifted 1 space to right for Bin Code (u1bncd).
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp235    if   e           k disk
      *  Uploaded feed tickets-Receiving file
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D d1mdy           s              6  0
     d t1lbs           s             11  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read/print each record in the Receiving file.
      *
     C     *loval        setll     hsp235
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp235                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Print headings the First Time
      *
     C                   if        first = yes
     C                   except    hrhdr
     C                   move      no            first
     C                   endif
      *
      * Accumulate totals
      *
     C                   add       u1lbs         t1lbs
      *
      * Flip Ticket Date from CCYYMMDD to MMDDYY.
      *
     C     *iso          test(d)                 u1tkdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      u1tkdt        wkisodate
     C     *mdy          move      wkisodate     d1mdy
     C                   else
     C                   z-add     0             d1mdy
     C                   endif                                                  If OK date
      *
      * Print record
      *
     C                   if        *inof = *on
     C                   except    hrhdr
     C                   endif
     C                   except    dtline
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------
      * EOF processing
      *----------------
     C                   if        first = yes
     C                   except    hrhdr
     C                   except    nodata
     C                   else
      * Print totals
     C                   if        *inof = *on
     C                   except    hrhdr
     C                   endif
     C                   except    t1tot
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oqprint    e            hrhdr          1 01
     O                       sdpgm               10
     O                                          113 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         Y    198
      *
     O          e            hrhdr          1
     O                       sdusr               10
     O                                           91 'LISTING OF'
     O                                         +  1 'FEED TICKET RECEIVING FILE'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            hrhdr          2
     O                                          188 'PAGE'
     O                       page          Z    198
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            hrhdr          1
     O                                           20 'MILL NUMBER'
     O                                           38 'TICKET NUMBER'
     O                                           52 'TICKET DATE'
     O                                           71 'INVOICE NUMBER'
     O                                           90 'FARM SITE'
     O                                          102 'BIN CODE'
     O                                          129 'RATION/ADDITIVE'
     O                                          151 'POUNDS'
     O                                          168 'INGRED COST'
      *
     O          e            hrhdr          2
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            dtline         1
     O                       u1mmno        m     17
     O                       u1tkno              35
     O                       d1mdy               52 '  /  /  -'
     O                       u1inv               68
     O                       u1fscd        m     86
P405 O**                     u1bncd              99
P405 O                       u1bncd             100
     O                       u1rncd             119
     O                       u1adcd             126
     O                       u1lbs         k    152
     O                       u1ingr        3    168
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot          1
     O                                          151 '-------------------'
     O          e            t1tot          1
     O                       t1lbs         K    152
      *
      *-------------------------------------------------------------
      * No Data
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           19 'No data meets your'
     O                                           38 'selection criteria'
