     A*%%TS  SD  20090811  164200  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD566-SELECT OPEN, DISPOSED OR CLOSED HOG
     A*                        GROUP
     A*  CREATED BY:    ROSE CENTONZE
     A*  DATE:          07/15/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP566R1
     A*%%TS  SD  19950420  162146  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 56)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP566R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP566R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP566S1                   SFL
     A*%%TS  SD  20090811  164200  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFSBO    R        O  6  1REFFLD(HSRREF/HGFSBO *LIBL/HSPREF)
     A            DFFSCD    R        O  6  8REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFHGCD    R        O  6 15REFFLD(HGHGCD)
     A            DFBLCD    R        O  6 24REFFLD(HSRREF/HGBLCD *LIBL/HSPREF)
     A            DFRMCD    R        O  6 31REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A            DFPPCD    R        O  6 38REFFLD(HGPPCD)
     A            DFGTCD    R        O  6 47REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFGSCD    R        O  6 52REFFLD(HGGSCD)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP566C1                   SFLCTL(HP566S1)
     A*%%TS  SD  20090811  164200  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP566R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  5'SELECT OPEN, DISPOSED OR CLOSED HO-
     A                                      G GROUP'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  3 38'Prod'
     A                                      DSPATR(HI)
     A                                  3 45'Group'
     A                                      DSPATR(HI)
     A                                  4  2'B/O'
     A                                      DSPATR(HI)
     A                                  4  9'Farm'
     A                                      DSPATR(HI)
     A                                  4 15'Group'
     A                                      DSPATR(HI)
     A                                  4 24'Bldg'
     A                                      DSPATR(HI)
     A                                  4 31'Room'
     A                                      DSPATR(HI)
     A                                  4 38'Phase'
     A                                      DSPATR(HI)
     A                                  4 45'Type'
     A                                      DSPATR(HI)
     A                                  4 52'St'
     A                                      DSPATR(HI)
     A            C1FSBO    R        B  5  1REFFLD(HSRREF/HGFSBO *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  5  8REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1HGCD         7A  B  5 15DSPATR(HI)
     A            C1PPCD    R        B  5 38REFFLD(HGPPCD)
     A                                      DSPATR(HI)
     A            C1GTCD    R        B  5 47REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1GSCD    R        B  5 52REFFLD(HGGSCD)
     A                                      DSPATR(HI)
     A          R HP566K1
     A*%%TS  SD  19940715  095726  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      WINDOW(HP566R1)
     A                                 15  1'F12=Previous'
