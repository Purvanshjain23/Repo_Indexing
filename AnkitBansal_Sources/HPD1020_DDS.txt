     A*%%TS  SD  20080919  150003  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1020
     A*  TITLE:         REVISE BGF DAILY PRODUCTION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/30/00
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R HP1020R1
     A*%%TS  SD  20050203  110235  ISLRAMS     REL-V5R2M0  5722-WDS
     A N90                                  CF04(04 'PROMPT')
     A                                      SETOF(29)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(41)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(67)
     A                                      SETOF(68)
     A                                      SETOF(79)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 26'REVISE BGF DAILY PRODUCTION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Daily production date ...'
     A            D1DPMDY   R        B  4 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A                                  4 42'PIC date ..................:'
     A            D1PICDT   R        O  4 71REFFLD(HSRREF/@@PICDT *LIBL/HSPREF)
     A                                      EDTWRD('  -   ')
     A                                  5  2'Farm site ...............'
     A  61                              5 28'?'
     A            D1FSCD    R        B  5 30REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A  31
     AO 29                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A                                  5 42'Week-ending date ..........:'
     A            D1WEMDY   R        O  5 71REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            D1FSNM    R        O  6  2REFFLD(FSFSNM)
     A                                  6 42'Calendar year/week ........:'
     A            D1CDYR    R        O  6 71REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1CDWK    R        O  6 77REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                  7 42'Safedata reference number .:'
     A            D1SDINO   R        O  7 71REFFLD(HSRREF/@@SDINO *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  9  2'Number of'
     A                                  9 15'Nbr of Open'
     A                                  9 30'Number of'
     A                                  9 42'Number of'
     A                                  9 55'Total'
     A                                 10  3'Weaners'
     A                                 10 13'Sow Pool Served'
     A                                 10 31'Returns'
     A                                 10 44'Gilts'
     A                                 10 54'Services'
     A            D1WNSV    R        B 11  4REFFLD(HSRREF/@@WNSV *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N33                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1LWNSV   R        B 11 17REFFLD(HSRREF/@@LWNSV *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N34                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1RTSV    R        B 11 31REFFLD(HSRREF/@@RTSV *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N35                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1GLSV    R        B 11 43REFFLD(HSRREF/@@GLSV *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TOSV    R        O 11 55REFFLD(HSRREF/@@TOSV *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 13  2'Open Gilt Pool'
     A                                 13 18'Open Sow Pool'
     A                                 13 34'Nbr of Gilt'
     A                                 13 50'Nbr of'
     A                                 13 61'Number of'
     A                                 14  5'Eligible'
     A                                 14 34'Abortions'
     A                                 14 47'Abortions'
     A                                 14 63'NIPs'
     A            D1OPGLHD  R        B 15  6REFFLD(HSRREF/@@OPGLHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OPSOHD  R        B 15 20REFFLD(HSRREF/@@OPSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N38                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1GLABHD  R        B 15 36REFFLD(HSRREF/@@GLABHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N62N30                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1ABHD    R        B 15 50REFFLD(HSRREF/@@ABHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A N62N46                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1NIPQT   R        B 15 63REFFLD(HSRREF/@@NIPQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A N62N41                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A                                 17 14'Gilt'
     A                                 17 23'Gilt'
     A                                 18  6'Gilt'
     A                                 18 13'Total'
     A                                 18 22'Total'
     A                                 18 32'Gilt'
     A                                 18 41'Total'
     A                                 18 49'Total'
     A                                 18 58'Born'
     A                                 18 65'Total'
     A                                 18 74'Still'
     A                                 19  3'Litters'
     A                                 19 14'Born'
     A                                 19 22'Still'
     A                                 19 31'Total'
     A                                 19 39'Litters'
     A                                 19 50'Born'
     A                                 19 56'Alive/'
     A                                 19 65'Still'
     A                                 19 74'Borns'
     A                                 20  2'Farrowed'
     A                                 20 13'Alive'
     A                                 20 22'Borns'
     A                                 20 29'Mummies'
     A                                 20 38'Farrowed'
     A                                 20 49'Alive'
     A                                 20 56'Litter'
     A                                 20 65'Borns'
     A                                 20 72'/Litter'
     A            D1GLLT    R        B 21  3REFFLD(HSRREF/@@GLLT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A N62N47                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1GLBAHD  R        B 21 12REFFLD(HSRREF/@@GLBAHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A N62N79                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1GLSBHD  R        B 21 21REFFLD(HSRREF/@@GLSBHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A N62N67                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1GLMMHD  R        B 21 30REFFLD(HSRREF/@@GLMMHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A N62N68                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TOLT    R        B 21 40REFFLD(HSRREF/@@TOLT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A N62N48                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1BAHD    R        B 21 48REFFLD(HSRREF/@@BAHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A N62N49                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1LTBAHD  R        O 21 56REFFLD(HSRREF/@@LTBAHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1SBHD    R        B 21 64REFFLD(HSRREF/@@SBHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A N62N50                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1LTSBHD  R        O 21 73REFFLD(HSRREF/@@LTSBHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*-----------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*-----------------------------------------------------------------
     A          R HP1020R2
     A*%%TS  SD  20080919  150003  ISLRAMS     REL-V5R4M0  5722-WDS
     A N90                                  CF12(12 'PREVIOUS')
     A N90                                  CF09(09 'ACCEPT')
     A N90N61                               CF11(11 'DELETE')
     A                                      SETOF(39)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(54)
     A                                      SETOF(53)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(70)
     A                                      SETOF(71)
     A                                      SETOF(72)
     A                                      SETOF(73)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      SETOF(76)
     A                                      SETOF(77)
     A                                      SETOF(78)
     A                                      SETOF(81)
     A                                      SETOF(82)
     A                                      SETOF(84)
     A                                      SETOF(44)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 26'REVISE BGF DAILY PRODUCTION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4 15'Total'
     A                                  4 23'0-15 Day'
     A                                  4 34'16+ Day'
     A                                  4 46'Total'
     A                                  4 56'Total'
     A                                  4 63'Avg Age'
     A                                  4 72'Piglets'
     A                                  5  4'Total'
     A                                  5 12'Pre-Wean'
     A                                  5 25'Weaned'
     A                                  5 35'Weaned'
     A                                  5 45'Weaned'
     A                                  5 57'Pigs'
     A                                  5 64'Piglet'
     A                                  5 73'Weaned'
     A                                  6  2'Mummies'
     A                                  6 14'Losses'
     A                                  6 27'Sows'
     A                                  6 37'Sows'
     A                                  6 47'Sows'
     A                                  6 55'Weaned'
     A                                  6 64'Weaned'
     A                                  6 72'/Litter'
     A            D2MMHD    R        B  7  3REFFLD(HSRREF/@@MMHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A N62N51                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2TOLSHD  R        B  7 14REFFLD(HSRREF/@@TOLSHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A N62N52                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2EWNLT   R        B  7 25REFFLD(HSRREF/@@EWNLT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A N62N54                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2WNLT    R        B  7 35REFFLD(HSRREF/@@WNLT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A N62N55                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2TOTWNLT R        O  7 45REFFLD(HSRREF/@@WNLT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D2TOWNHD  R        B  7 55REFFLD(HSRREF/@@TOWNHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A N62N56                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2ADWN    R        B  7 66REFFLD(HSRREF/@@ADWN *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A N62N57                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2LTWNHD  R        O  7 73REFFLD(HSRREF/@@LTWNHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                  9  4'Gilts'
     A                                  9 12'T/I Bred'
     A                                  9 37'Sows'
     A                                  9 45'T/I Bred'
     A                                 10  2'Arrived'
     A                                 10 15'Gilts'
     A                                 10 34'Arrived'
     A                                 10 47'Sows'
     A            D2ARGLHD  R        B 11  3REFFLD(HSRREF/@@ARGLHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A N62N59                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2IBGLHD  R        B 11 14REFFLD(HSRREF/@@IBGLHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A N62N81                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2ARSOHD  R        B 11 35REFFLD(HSRREF/@@ARSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A N62N53                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2IBSOHD  R        B 11 46REFFLD(HSRREF/@@IBSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A N62N82                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 14  3'Gilts'
     A                                 14 13'Sows'
     A                                 14 22'Gilts Dead'
     A                                 14 35'Sows Dead'
     A                                 14 56'--Euthanized--'
     A                                 15  4'Sold'
     A                                 15 13'Sold'
     A                                 15 23'Naturally'
     A                                 15 35'Naturally'
     A                                 15 56'Gilts'
     A                                 15 66'Sows'
     A            D2SLGLHD  R        B 16  2REFFLD(HSRREF/@@SLGLHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N62N70                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2SLSOHD  R        B 16 11REFFLD(HSRREF/@@SLSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N62N71                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2DDGLHD  R        B 16 26REFFLD(HSRREF/@@DDGLHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N62N72                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2DDSOHD  R        B 16 38REFFLD(HSRREF/@@DDSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A N62N73                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2EUGLHD  R        B 16 55REFFLD(HSRREF/@@EUGLHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N62N74                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2EUSOHD  R        B 16 64REFFLD(HSRREF/@@EUSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A N62N78                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 18  2'Transferred out'
     A                                 18 23'Transferred out'
     A                                 19  7'Bred'
     A                                 19 25'Open'
     A                                 19 33'Bred'
     A                                 19 41'-------- Boars --------'
     A                                 20  6'Gilts'
     A                                 20 25'Sows'
     A                                 20 33'Sows'
     A                                 20 45'In'
     A                                 20 51'Sold'
     A                                 20 59'Dead'
     A                                 20 69'Manhours'
     A            D2OBGLHD  R        B 21  5REFFLD(HSRREF/@@OBGLHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A N62N58                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2OOSOHD  R        B 21 23REFFLD(HSRREF/@@OOSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  84                                  DSPATR(PC)
     A  84                                  DSPATR(RI)
     A N62N84                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2OBSOHD  R        B 21 31REFFLD(HSRREF/@@OBSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N62N39                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2ARBOHD  R        B 21 41REFFLD(HSRREF/@@ARBOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A N62N75                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2SLBOHD  R        B 21 49REFFLD(HSRREF/@@SLBOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A N62N76                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2DDBOHD  R        B 21 57REFFLD(HSRREF/@@DDBOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A N62N77                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2MANHR   R        B 21 68REFFLD(HSRREF/@@MANHR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A N62N44                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R HP1020K1
     A*%%TS  SD  20000601  071703  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  61N90                          23 12'F4=Prompt'
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 2
     A*-----------------------------------------------------------------
     A          R HP1020K2
     A*%%TS  SD  20010227  091702  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N90                          23 12'F9=Accept'
     A N90N61                          23 24'F11=Delete'
     A N90                             23 37'F12=Previous'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R HP1020ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R HP1020EC                  SFLCTL(HP1020ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
