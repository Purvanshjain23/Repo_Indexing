/********************************************************************/
/*                                                                  */
/*  PROGRAM  -  PPW9UPC                                             */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES REQUIRED PARMS.                */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT A/R CLAIM DEPARTMENT REPORT                   */
/*  DATE      - 02/24/2006                                          */
/*  NAME      - LARA BUSER                                          */
/*                                                                  */
/*  CHANGE    - E4057 JJH 06/22/15                                  */
/*              ADD RECORD SELECTION BY RECORD STATUS ON THE        */
/*              PRINT A/R CLAIM DEPARTMENT (PPBKPFR) REPORT.        */
/*                                                                  */
/*  PARAMETERS:                                                     */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - RPG PROGRAM TO SUBMIT                     */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     I    &RCDSTS     - RECORD STATUS                             */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COPYN      - NO. OF COPIES  (15.5)                     */
/*                                                                  */
/********************************************************************/
 PPW9UPC:    PGM        PARM(&RETURN &CLMDEPTCD &PGM &RCDSTS &DESC +
                          &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ &COPYN +
                          &APPLIC)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (8)           /* CLAIM DEPARTMENT CD       */
        DCL    &CLMDEPTCD  *CHAR   (1)           /* CLAIM DEPARTMENT CD       */
        DCL    &PGM        *CHAR   (10)          /* RPG TO SUBMIT             */
        DCL    &RCDSTS     *CHAR   (1)           /* RECORD STATUS             */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (1)           /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMMAND    *CHAR   (512)         /* COMMAND        */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PPKBPFR$) OUTQ(&OUTQ) +
                          COPIES(&COPYN) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/
             CALL       PGM(&PGM) PARM(&RETURN &CLMDEPTCD &RCDSTS)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
