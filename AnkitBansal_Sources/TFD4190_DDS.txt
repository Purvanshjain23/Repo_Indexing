     A*%%TS  SD  20090202  113122  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD4190
     A*  TITLE:         SPECIFY PRODUCT EXCEPTION LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          09/11/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'SPOOL FILES')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R TF4190R1
     A*%%TS  SD  20090202  113122  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(TF4190EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY PRODUCT EXCEPTION LISTING -
     A                                      OPTIONS'
     A                                      DSPATR(HI)
     A                                  3 52'Week-ending'
     A                                      COLOR(BLU)
     A                                  4 52'   Date    '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  1'From date .......'
     A            D1FMDY    R        B  5 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  5 35'(Blank for all)'
     A            D1FWEMDY  R        O  5 53REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(BLU)
     A                                  6  1'To date .........'
     A            D1TMDY    R        B  6 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  6 35'(Blank for all)'
     A            D1TWEMDY  R        O  6 53REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(BLU)
     A                                  7  1'Product code ....'
     A                                  7 19'?'
     A            D1PRCD    R        B  7 21REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A                                  7 35'(Blank for all)'
     A                                  8  1'Program .........'
     A            D1PGM     R        B  8 21REFFLD(TFREF/@@PGM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                  8 35'(Blank for all)'
     A                                 10  1'Mix flag ................'
     A                                 10 27'?'
     A            D1MIXFL   R        B 10 29REFFLD(TFREF/@@MIXFL *LIBL/TFPREF)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                 10 35'(Blank for all)'
     A                                 11  1'Volume flag .............'
     A                                 11 27'?'
     A            D1VOLFL   R        B 11 29REFFLD(TFREF/@@VOLFL *LIBL/TFPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                 11 35'(Blank for all)'
     A                                 12  1'System-generated flag ...'
     A                                 12 27'?'
     A            D1SYSFL   R        B 12 29REFFLD(TFREF/@@VOLFL *LIBL/TFPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                 12 35'(Blank for all)'
     A                                 13  1'Meat cost group code ....'
     A                                 13 27'?'
     A            D1CGCD    R        B 13 29REFFLD(TFREF/@@CGCD *LIBL/TFPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                 13 35'(Blank for all)'
     A                                 14  1'TF class group code .....'
     A                                 14 27'?'
     A            D1TFCGCD  R        B 14 29REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                 14 35'(Blank for all)'
     A                                 15  1'TF classification code ..'
     A                                 15 27'?'
     A            D1TFCLCD  R        B 15 29REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A                                 15 35'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R TF4190K1
     A*%%TS  SD  20060911  154226  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      WINDOW(TF4190EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             17  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F3=Exit'
     A                                 18  9'F4=Prompt'
     A                                 18 19'F8=Submit'
     A                                 18 30'F17=Print Options'
     A                                 18 48'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R TF4190ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(19)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R TF4190EC                  SFLCTL(TF4190ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
