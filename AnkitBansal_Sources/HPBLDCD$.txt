/*********************************************************************/
/*                                                                   */
/*  PROGRAM NUMBER:  HPBLDCD$                                        */
/*  PROGRAM NAME:    DATAMART:  END OF HPS PERIOD PROCESS            */
/*  PROGRAMMER:      Brad Baden                                      */
/*  CREATE DATE:     07/25/2020    SDN465                            */
/*                                                                   */
/*  FUNCTION:        SDN465 - JDE World to JDE Enterprise 1          */
/*                   This CLP determines whether to call program     */
/*                   HPBLDCD$E1 or HPBLDCD$W based upon the values   */
/*                   for E1LIVE and JWLIVE.                          */
/*                                                                   */
/*********************************************************************/
/*  MODIFIED:                                                        */
/*                                                                   */
/* xx/xx/xxxx  xxxxxxxxxxxxxxx  Exxxxx                               */
/*             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      */
/*             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      */
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&COCD)

             DCL        VAR(&COCD)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&COMPN)  TYPE(*DEC)  LEN(3 0)

             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7)

             DCL        VAR(&E1LIVE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&JWLIVE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&M3LIVE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&E1PARA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&JWPARA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&M3PARA) TYPE(*CHAR) LEN(1)

             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PGMLIB) TYPE(*CHAR) LEN(10)

             PSHLIBL35

/*-----------------------------------------------------------------*/
/* Check Test/Prod flag.  If in Test, set libray list = PRKINTT.   */
/* If in Production, add PRKDMFLIB so we will process directly     */
/* into that library. We do not expect PRKDMFLIB to already be in  */
/* the library list; but, since we want it at the top of the list, */
/* we will remove/add PRKDMFLIB.                                   */
/*-----------------------------------------------------------------*/
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)

             SELECT
             WHEN       COND(&TSTPRDFL *EQ 'T') THEN(DO)

             RTVOBJD    OBJ(HPBLDCD$) OBJTYPE(*PGM) RTNLIB(&PGMLIB)

          /* Set library list based upon program library for HPBLDCD$ */
             SELECT
             WHEN       COND(&PGMLIB *EQ 'E1ITESTP  ') THEN(SETLIBL +
                          JOBD(PRKTSTE1))
             WHEN       COND(&PGMLIB *EQ 'PRKTESTP  ') THEN(SETLIBL +
                          JOBD(PRKINTT))
             ENDSELECT

/* Place test library version of PRKDMFLIB at top of library list */
             RMVLIBLE   LIB(TSTDMFLIB)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(TSTDMFLIB)
             ENDDO

/* Else, place PRKDMFLIB at top of library list                   */
             OTHERWISE  CMD(DO)
             RMVLIBLE   LIB(PRKDMFLIB)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(PRKDMFLIB)
             ENDDO
             ENDSELECT

/*-------------------------------------------------------------------*/
/*  Retrieve E1, JW, and M3 Live/Parallel flags from Company Values  */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&COMPN) VALUE(&COCD)

             CALL       PGM(E1JUXFR) PARM(&RETURN &COMPN &E1LIVE +
                          &JWLIVE &M3LIVE &E1PARA &JWPARA &M3PARA)

/*-------------------------------------------------------------------*/
/*  Select E1 or JW version of program HP3082                        */
/*-------------------------------------------------------------------*/

               IF         COND((&E1LIVE *EQ 'Y') *OR (&E1LIVE *EQ +
                          'P')) THEN(DO)
                 CALL       PGM(HPBLDCD$E1) PARM(&COCD &TSTPRDFL)
               ENDDO

               IF         COND(&JWLIVE *EQ 'Y')  THEN(DO)
                 CALL       PGM(HPBLDCD$W) PARM(&COCD &TSTPRDFL)
               ENDDO

             POPLIBL35

 END:        ENDPGM
