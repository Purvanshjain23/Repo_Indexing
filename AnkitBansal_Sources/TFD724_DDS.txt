     A*%%TS  SD  20070712  144442  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD724
     A*  TITLE:         VIEW WIP USAGE
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          07/12/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF13(13 'FOLD/DROP')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF724S1                   SFL
     A*%%TS  SD  20070712  144231  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1PRCD    R        O  9  2REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WIPLB   R        O  9 10REFFLD(TFREF/@@SSLLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1MQWG         9Y 0O  9 27EDTCDE(K)
     A            D1MSWG         9Y 0O  9 40EDTCDE(K)
     A            D1NAWG         9Y 0O  9 54EDTCDE(K)
     A            D1M5WG         9Y 0O  9 67EDTCDE(K)
     A            D1PRDS    R        O 10 10REFFLD(TFREF/@@ICCOM *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF724C1                   SFLCTL(TF724S1)
     A*%%TS  SD  20070712  141405  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 33'VIEW WIP USAGE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Week-ending date:'
     A                                      DSPATR(HI)
     A            C1WEMDY   R        O  3 20REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  3 32'Warehouse company:'
     A                                      DSPATR(HI)
     A            C1CONO    R        O  3 51REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                  4 35'Warehouse code:'
     A                                      DSPATR(HI)
     A            C1WHS          3A  O  4 51
     A                                  6 21'WIP'
     A                                      DSPATR(HI)
     A                                  6 31'Beg Inv'
     A                                      DSPATR(HI)
     A                                  6 46'Floor'
     A                                      DSPATR(HI)
     A                                  6 60'Floor'
     A                                      DSPATR(HI)
     A                                  6 71'End Inv'
     A                                      DSPATR(HI)
     A                                  7  2'Item'
     A                                      DSPATR(HI)
     A                                  7 19'Usage'
     A                                      DSPATR(HI)
     A                                  7 33'Final'
     A                                      DSPATR(HI)
     A                                  7 41'Production'
     A                                      DSPATR(HI)
     A                                  7 55'Production'
     A                                      DSPATR(HI)
     A                                  7 73'Final'
     A                                      DSPATR(HI)
     A                                  8  2'Code'
     A                                      DSPATR(HI)
     A                                  8 18'Pounds'
     A                                      DSPATR(HI)
     A                                  8 32'Pounds'
     A                                      DSPATR(HI)
     A                                  8 45'Pounds'
     A                                      DSPATR(HI)
     A                                  8 55'Adj Pounds'
     A                                      DSPATR(HI)
     A                                  8 72'Pounds'
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R TF724K1
     A*%%TS  SD  20070712  144442  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 40'F13=Fold/Unfold'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF724ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R TF724EC                   SFLCTL(TF724ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
