     A*%%TS  SD  19960329  111330  ISLRAMS     REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD716
     A*  TITLE:         VIEW DELETED MOVEMENTS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/27/96
     A*
     A*
     A*****************************************************************
     A* RMC 2/22/2016 E5190  ADD BUSINESS OFFICE RESTRICTOR
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP716S1                   SFL
     A*%%TS  SD  19960328  092141  ISLRAMS     REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFMVSN    R        O 10  2REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A            DFMTCD    R        O 10 13REFFLD(@@MTCD)
     A            DFDMDY    R     Y  O 10 17REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFDMTM    R     Y  O 10 27REFFLD(@@TM04)
     A                                      EDTWRD('  :  ')
     A            DFUSCD    R        O 10 34REFFLD(@@USCD)
     A            DFSMDY    R     Y  O 10 47REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFORFS    R        O 10 57REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            DFDNFS    R        O 10 64REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            DFCVNO    R     Y  O 10 71REFFLD(@@AJD)
     A                                      EDTCDE(M)
     A            DFDMCM    R        O 11 17REFFLD(DMDMCM)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP716C1                   SFLCTL(HP716S1)
     A*%%TS  SD  19960328  101937  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A                                      CF03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CA07(07 'LISTING')
     A                                      CF12(12 'PREVIOUS')
     A N99                                  CF13(13 'FOLD/DROP')
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A  82                                  SFLEND
     A            FOLD           1A  H
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP716'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 26'VIEW DELETED MOVEMENTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .. ?'
     A            C1FSBO    R        B  4 23REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A N99                              6 13'?'
     A                                      DSPATR(HI)
     A N99                              6 38'?'
     A                                      DSPATR(HI)
     A N99                              6 60'?'
     A                                      DSPATR(HI)
     A N99                              6 66'?'
     A                                      DSPATR(HI)
     A N99                              6 74'?'
     A                                      DSPATR(HI)
     A                                  7 17'--------- Deleted ---------'
     A                                      DSPATR(HI)
     A                                  7 46'Scheduled'
     A                                      DSPATR(HI)
     A                                  7 57'Origin'
     A                                      DSPATR(HI)
     A                                  7 65'Dest'
     A                                      DSPATR(HI)
     A                                  7 71'Customer'
     A                                      DSPATR(HI)
     A                                  8  2'Movemnt'
     A                                      DSPATR(HI)
     A                                  8 11'Type'
     A                                      DSPATR(HI)
     A                                  8 19'Date'
     A                                      DSPATR(HI)
     A                                  8 27'Time'
     A                                      DSPATR(HI)
     A                                  8 37'User'
     A                                      DSPATR(HI)
     A                                  8 46'Ship Date'
     A                                      DSPATR(HI)
     A                                  8 58'Farm'
     A                                      DSPATR(HI)
     A                                  8 65'Farm'
     A                                      DSPATR(HI)
     A                                  8 73'Number'
     A                                      DSPATR(HI)
     A            C1MVSN    R        B  9  2REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1MTCD    R        B  9 13REFFLD(@@MTCD)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1DMDY    R     Y  B  9 17REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1USCD    R        B  9 34REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1SMDY    R     Y  B  9 47REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1ORFS    R        B  9 57REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1DNFS    R        B  9 64REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1CVNO    R     Y  B  9 71REFFLD(@@AJD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A*
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP716K1
     A*%%TS  SD  19960329  111330  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  1'F3=Exit'
     A N99                             23 10'F4=Prompt'
     A N99                             23 21'F5=Refresh'
     A                                 23 33'F12=Previous'
     A                                 23 46'F7=Listing'
     A N99                             23 58'F13=Fold/Unfold Comment'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP716ES                   SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *----------------------------------------------------------------
     A          R HP716EC                   SFLCTL(HP716ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
