/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4702CL                                      */
/*  PROGRAM NAME:    LISTINGS FROM WORK WITH FARM BUDGETS          */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     02/24/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4702-WORK WITH FARM BUDGETS.                */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(152)

             DCL        VAR(&LDREPORT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDBTCD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFBSCD)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDYNFL)   TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDREPORT) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDFSCD)   VALUE(%SST(*LDA 9 5))
             CHGVAR     VAR(&LDBTCD)   VALUE(%SST(*LDA 14 6))
             CHGVAR     VAR(&LDFCYMD)  VALUE(%SST(*LDA 27 8))
             CHGVAR     VAR(&LDTCYMD)  VALUE(%SST(*LDA 35 8))
             CHGVAR     VAR(&LDFBSCD)  VALUE(%SST(*LDA 43 1))
             CHGVAR     VAR(&LDYNFL)   VALUE(%SST(*LDA 44 1))


/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* LISTING OF A SINGLE FARM BUDGET                                  */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'B') THEN(DO)

             CRTDUPOBJL OBJ(HSP3719) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(HP3719)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6705)
             ENDDO


/*------------------------------------------------------------------*/
/* LISTING OF FARM BUDGETS (WITH USER SELECTIONS)                   */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'L') THEN(DO)

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('JHFBSN *NE 0')

/*  IF A TEMPLATE                                                */

             IF         COND(&LDBTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 17)) VALUE('*AND JHBTCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 31 6)) VALUE(&LDBTCD)
             CHGVAR     VAR(%SST(&QDATA 37 1)) VALUE('"')
             ENDDO

/*  IF A FARM                                                    */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 39 15)) VALUE('*AND JHFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 55 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF A FROM DATE                                               */

             IF         COND(&LDFCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 61 15)) VALUE('*AND JHBFDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 77 8)) VALUE(&LDFCYMD)
             ENDDO

/*  IF A TO DATE                                                 */

             IF         COND(&LDTCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 86 15)) VALUE('*AND JHBTDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 102 8)) VALUE(&LDTCYMD)
             ENDDO

/*  IF STATUS                                                    */

             IF         COND(&LDFBSCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 111 18)) VALUE('*AND JHFBSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 129 1)) VALUE(&LDFBSCD)
             CHGVAR     VAR(%SST(&QDATA 130 1)) VALUE('"')
             ENDDO

/*  IF INCLUDE CLOSED BUDGETS FLAG IS NO                         */

             IF         COND(&LDYNFL *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 132 20)) VALUE('*AND JHFBSCD +
                          *NE "C"')
             ENDDO

             OVRDBF     FILE(HSL188H) SHARE(*YES)
             OPNQRYF    FILE((HSL188H)) QRYSLT(&QDATA) KEYFLD(*FILE)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(157) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6706)

             CLOF       OPNID(HSL188H)
             ENDDO


             DLTOVR     FILE(*ALL)

             ENDPGM

