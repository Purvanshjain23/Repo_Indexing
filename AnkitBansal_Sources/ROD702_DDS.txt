     A*%%TS  SD  20070205  095134  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD702
     A*  TITLE:         VIEW ITEM CONTROL DATA
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          01/17/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF13(13 'FOLD/DROP')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO702S1                   SFL
     A*%%TS  SD  20070205  095134  ISLRAMS     REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1ITCD    R        O 10  2REFFLD(@@ITCD)
     A                                      EDTCDE(M)
     A            D1SRITCD  R        O 10 11REFFLD(ICREC/ICSRITCD *LIBL/ROP012)
     A                                      EDTCDE(M)
     A            D1DOCD    R        O 10 21REFFLD(ICREC/ICDOCD *LIBL/ROP012)
     A            D1DOBSPR  R        O 10 27REFFLD(ICREC/ICDOBSPR *LIBL/ROP012)
     A                                      EDTCDE(K)
     A            D1MUPC    R        O 10 39REFFLD(ICREC/ICMUPC *LIBL/ROP012)
     A                                      EDTCDE(K)
     A            D1DFPR    R        O 10 52REFFLD(ICREC/ICDFPR *LIBL/ROP012)
     A                                      EDTCDE(K)
     A            D1FRXFL   R        O 10 71REFFLD(@@FRXFL)
     A                                 11  2'Item:'
     A                                      COLOR(PNK)
     A            D1ITDS    R        O 11  8REFFLD(@@ITDS)
     A                                      COLOR(BLU)
     A            D1SRTEXT       7A  O 11 41COLOR(PNK)
     A            D1SRITDS  R        O 11 49REFFLD(@@ITDS)
     A                                      COLOR(BLU)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO702C1                   SFLCTL(RO702S1)
     A*%%TS  SD  20070205  095134  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 28'VIEW ITEM CONTROL DATA'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Company:'
     A            C1CONO    R        O  4 11REFFLD(@@CONO)
     A                                      DSPATR(HI)
     A                                  4 17'Week-ending date:'
     A            C1WEMDY   R        O  4 35REFFLD(ROREF/@@DT06 *LIBL/ROPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A                                  5 71'?'
     A                                      DSPATR(HI)
     A                                  6  6'?'
     A                                      DSPATR(HI)
     A                                  6 14'?'
     A                                      DSPATR(HI)
     A                                  7  5'Item'
     A                                      DSPATR(HI)
     A                                  7 12'Source'
     A                                      DSPATR(HI)
     A                                  7 21'Default/Override'
     A                                      DSPATR(HI)
     A                                  7 39'Multiplier'
     A                                      DSPATR(HI)
     A                                  7 52'Difference'
     A                                      DSPATR(HI)
     A                                  7 65'Fresh Product'
     A                                      DSPATR(HI)
     A                                  8  5'Code'
     A                                      DSPATR(HI)
     A                                  8 13'Item'
     A                                      DSPATR(HI)
     A                                  8 21'Code'
     A                                      DSPATR(HI)
     A                                  8 27'Base Price'
     A                                      DSPATR(HI)
     A                                  8 40'Percent'
     A                                      DSPATR(HI)
     A                                  8 53'Price/Lb'
     A                                      DSPATR(HI)
     A                                  8 65'Exception Flag'
     A                                      DSPATR(HI)
     A            C1ITCD    R        B  9  2REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A  51                                  DSPATR(PC)
     A            C1SRITCD  R        B  9 11REFFLD(ICREC/ICSRITCD *LIBL/ROP012)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A  52                                  DSPATR(PC)
     A            C1DOCD    R        B  9 21REFFLD(ICREC/ICDOCD *LIBL/ROP012)
     A                                      DSPATR(HI)
     A            C1FRXFL   R        B  9 71REFFLD(@@FRXFL)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO702K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F13=Fold/Unfold'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO702ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R RO702EC                   SFLCTL(RO702ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
