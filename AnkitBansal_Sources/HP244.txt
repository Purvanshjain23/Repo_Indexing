      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP244
      * TITLE:       Hog Group FOS Download for Nursery and BGF Groups
      * PROGRAMMER:  Brad Baden
      * CREATED:     11/11/2016
      *
      *  FUNCTION:   This program populates a file for the FOS download
      *              of all Nursery and BGF Hog Group Codes related to
      *              Destination Group Codes.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * xx/xx/xx  xxxxxxxxxxxxxxxxxxxx
      *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp315    if   e           k disk
      *   Workfile: Destination hog groups
      *
      *
     Fhsp317    if   e           k disk
      *   Workfile: BGF groups
      *
      *
     Fhsl318a   if   e           k disk
      *   Workfile: Nursery groups
      *
      *
     Fhsp244    o    e             disk
      *   Hog groups for FOS download
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECifICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * DATA STRUCTURES
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process all records in the Destination Group workfile.
      *
     C     *loval        setll     hsp315
      *
     C                   dou       %eof(hsp315)                                 Main do
     C                   read      hsp315                                 90
     C                   if        not %eof(hsp315)                             If not EOF
      *
      * Detail processing
     C                   exsr      $detail
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /EJECT
      *-------------------------------------------------------------------
      * $detail - Detail processing to write records to file HSP244
      *-------------------------------------------------------------------
      *
     C     $detail       begsr
      *
      ** Move Destination Hog Group Code to HSP244 field
     C                   movel     wdhgcd        ojhgcd
      *
      * Build BGF Group records
     C     wdhgsn        setll     hsp317
     C                   if        %found(hsp317)
     C                   dou       %eof(hsp317)
     C     wdhgsn        reade     hsp317
     C                   if        not %eof(hsp317)
     C                   movel     wbsrhgcd      ojsrsccd
     C                   write     ojrec
     C                   endif
     C                   enddo
     C                   endif
      *
      * Build Nursery Group records
     C     wdhgsn        setll     hsl318a
     C                   if        %found(hsl318a)
     C                   dou       %eof(hsl318a)
     C     wdhgsn        reade     hsl318a
     C                   if        not %eof(hsl318a)
     C                   movel     wnsrhgcd      ojsrsccd
     C                   write     ojrec
     C                   endif
     C                   enddo
     C                   endif
      *
     C                   endsr
