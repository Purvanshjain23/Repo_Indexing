/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  THIS CL IS CALLED FROM POQ2XFR AND WILL FAX THE     */
/*              COVER SHEET.                                        */
/*                                                                  */
/*  SYSTEM    - ACCOUNTS RECIEVABLES                                */
/*  PROGRAM   - POD2UPC - PRC CALL TELEX INVOICE UP                 */
/*  DATE      - 05/16/2007                                          */
/*                                                                  */
/*  PARAMETERS:                                                     */
/*                                                                  */
/*  I    &PRTF       - PRINTER FILE TO EMAIL                        */
/*  I    &SNDDAT     - JOB DATE                                     */
/*  I    &STMTTYPE   - STATEMENT TYPE - A=AGEN N=NOT AGED           */
/*                   - INVOICES = BLANKS (NOT NEEDED)               */
/*  I    &OUTQ       - OUTPUT QUEUE - FAXQ OR QPRINT                */
/*  I    &OUTQLIB    - OUTPUT QUEUE LIBRARY = *LIBL                 */
/*  I    &COUNTRY    - COUNTRY                                      */
/*  I    &COMP       - COMPANY NUMBER                               */
/*  I    &FAXEML     - FAX NUMBER OR EMAIL ADDRESS                  */
/*******************************************************************/
/*******************************************************************/
/* RMC 5/02/02  CHANGE TLX400J TO TLX400P                          */
/* RMC 5/30/03  DONT FAX IF PURVA, ROSE, PRKTEST                   */
/* RMC 1/20/05  ADDED CODE FOR GENIFAX SOFTWARE                    */
/* LJB 5/16/06  CHANGED LENGTH OF $FAX# AND                        */
/*            LENGTH OF &FAXPHONE TO 25 TO ACCOMMODATE             */
/*            INTERNATIONAL FAX NUMBER LENGTH                      */
/*            ALSO, COMMENTED OUT REFERENCES TO TLX400P LIB        */
/*            IT IS NO LONGER BEING USED (PER RMC)                 */
/* 05/15/2007 LJB DLY029   A SAVE COPY NAMED POD2UPCSV IS IN       */
/*            OMSDEVGEN. THIS PROGRAM WAS HEAVILY MODIFIED SO IT   */
/*            WAS COPIED RATHER THAN FILLED WITH COMMENTED LINES   */
/*            OF CODE. CALL THE XF TO RETRIEVE THE SPECS FOR FAXES.*/
/* 07/02/2007 SLM DLY029   REVIEW AND TO SEE IF CHANGES            */
/*            ADDED LOGIC FOR TESTING UNDER ISSMASO                */
/* 09/02/2007 SLM DLY029                                           */
/*            INVOICING HAS BEEN PROCESSING IN PRODUCTION WITH     */
/*            THIS RELEASE. SO NOW * OUT SEND THIS TO SUSAN MASON. */
/* 12/18/2007 RMC FP1110                                           */
/*            PAGE CPI NEEDS TO BE 3.1 INSTEAD OF 2.0 LONG.(16.7)  */
/* 03/13/2008 RMC FP1110                                           */
/*            CHANGE  MSG(&EMAILMSG) MSGTYPE(*TEXT)  TO  MSG(*NONE)*/
/*            SO PRECEDING BLANK PAGE WONT GET FAXED               */
/* 07/22/2008 RMC FP1110                                           */
/*            PASS SPOOL FILE SELECTION 2 FOR HPB BOL TO FAX-&PRTF2*/
/*            EXEC ANOTHER ESND STMT -- BOTH ATTACHMENTS ON ONE MAY*/
/*            BE TOO BIG OF A FILE TO FAX                          */
/* 03/31/2010 LJB P000198      (FOR INVOICES AND STATEMENTS)       */
/*            FAX NUMBERS & AREA CODES ARE STORED AS ONE NUMBER IN */
/*            NEW CUST DISTRIBUTION ENTRY. CHANGE TO ACCOMMODATE.  */
/*            ADD NEW PARM: &FAXEMAIL, CAN BE FAX OR EMAIL (CHAR). */
/*           *NOTE* THIS MAY BE EXECUTED MULTIPLE TIMES PER        */
/*            CUSTOMER, IF THEY HAVE MULTIPLE FAX NUMBERS OR       */
/*            EMAIL ADDRESSES (THIS PROGRAM FAXES).                */
/*           *SO DON'T DELETE THE &PRTF AND &PRTF2 SPOOLED FILES   */
/* 05/04/2012 LJB E002085    CORRECT SUBJECT LINE TO INCLUDE       */
/*           "STATEMENT" TO REDUCE CUSTOMER CONFUSION:             */
/*             INVOICE/STATEMENT.              ADD NEW             */
/*           COVERSHEET PGM TO CALL IF COMPANY NUMBER IS ZEROS.    */
/*******************************************************************/
 POD2UPC:    PGM        PARM(&PRTF &STMTTYPE &SNDDAT &OUTQ &OUTQLIB +
                          &COUNTRY &PRTF2 &COMP &TYPE &DEPT +
                          &RECIPIENT &FAXEMAIL)

             DCL  VAR(&PRTF)      TYPE(*CHAR) LEN(10)
             DCL  VAR(&PRTF2)     TYPE(*CHAR) LEN(10)
             DCL  VAR(&DESC)      TYPE(*CHAR) LEN(50)
             DCL  VAR(&SNDDAT)    TYPE(*DEC)  LEN(6 0)
             DCL  VAR(&SNDDATA)   TYPE(*CHAR) LEN(6)
             DCL  VAR(&STMTTYPE)  TYPE(*CHAR) LEN(1)
             DCL  VAR(&OUTQ)      TYPE(*CHAR) LEN(10)
             DCL  VAR(&OUTQLIB)   TYPE(*CHAR) LEN(10)
             DCL  VAR(&COUNTRY)   TYPE(*CHAR) LEN(25)
             DCL  VAR(&COMPNYN)   TYPE(*DEC)  LEN(15 5)
             DCL  VAR(&EMAILMSG)  TYPE(*CHAR) LEN(30) VALUE(*NONE)
             DCL  VAR(&FTX2)      TYPE(*CHAR) LEN(75)
             DCL  VAR(&ADDR)      TYPE(*CHAR) LEN(25)
             DCL  VAR(&COMP)      TYPE(*DEC)  LEN(3 0)
             DCL  VAR(&FAXEMAIL)  TYPE(*CHAR) LEN(25)
             DCL  VAR(&RECIPIENT) TYPE(*CHAR) LEN(25)
/*----------------------------------------------------------------- */
/* PARMS FOR THE RETRIEVE OVERLAY PARAMETERS PROGRAM                */
/*----------------------------------------------------------------- */
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DEPT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&OVERLAY) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FAXPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FAXFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ADDLOBJ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDPHONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDFAX) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OVOUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&DRAWER) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&PRTDEVTYPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LENGTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&WIDTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&CPI) TYPE(*DEC) LEN(3 1)
             DCL        VAR(&LPI) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&OVERFLOW) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&ROTATE) TYPE(*CHAR) LEN(5)
             DCL        VAR(&COMMENT1) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT2) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT3) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT4) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT5) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RECSTS) TYPE(*CHAR) LEN(1)

             CHGJOB     LOG(4 00 *SECLVL)
             CHGVAR     VAR(&SNDDATA) VALUE(&SNDDAT)

/*----------------------------------------------------------------- */
/* CALL THE RETRIEVE COMPANY DOCUMENT DEFAULTS FIELDS PROGRAM TO    */
/* RETRIEVE DOCUMENT DEFAULTS FOR THE FAX COVERSHEET.               */
/* SELECT PGM TO CALL BASED ON COMPANY# = ZEROS OR NOT.  E002085    */
/*----------------------------------------------------------------- */
/* IF COMPANY# IS ZEROS, CALL PGM THAT PASSES BACK THE 1ST RECORD   */
             IF         COND(&COMP *EQ 000) THEN(CALL PGM(PBDJXFR) +
                          PARM(' ' &COMP &TYPE &DEPT &OVERLAY +
                          &FAXPGM &FAXFILE &ADDLOBJ &SENDPHONE +
                          &SENDFAX &OVOUTQ &HOLD &SAVE &DRAWER +
                          &PRTDEVTYPE &LENGTH &WIDTH &LPI &CPI +
                          &OVERFLOW &ROTATE &COMMENT1 &COMMENT2 +
                          &COMMENT3 &COMMENT4 &COMMENT5 &RECSTS))

/* IF COMPANY# IS NOT ZEROS, CALL PGM THAT GETS CO SPECIFIC RECORD  */
             IF         COND(&COMP *NE 000) THEN(CALL PGM(PMG9XFR) +
                          PARM(' ' &COMP &TYPE &DEPT &OVERLAY +
                          &FAXPGM &FAXFILE &ADDLOBJ &SENDPHONE +
                          &SENDFAX &OVOUTQ &HOLD &SAVE &DRAWER +
                          &PRTDEVTYPE &LENGTH &WIDTH &LPI &CPI +
                          &OVERFLOW &ROTATE &COMMENT1 &COMMENT2 +
                          &COMMENT3 &COMMENT4 &COMMENT5 &RECSTS))

             IF         COND(&OUTQ *EQ '     ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(&OVOUTQ))

/*----------------------------------------------------------------- */
/* OVERRIDE THE COVER SHEET TO THE DOCUMENT DEFAULTS                */
/*----------------------------------------------------------------- */
 OVRCOVER:   OVRPRTF    FILE(&FAXFILE) DEVTYPE(&PRTDEVTYPE) +
                          PAGESIZE(&LENGTH &WIDTH) LPI(&LPI) +
                          CPI(&CPI) OVRFLW(&OVERFLOW) +
                          DRAWER(&DRAWER) PAGRTT(&ROTATE) +
                          FRONTOVL(&OVERLAY) OUTQ(&OUTQ) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* PRINT THE COVER SHEET (AREA AND PHONE ARE STORED AS ONE NUMBER)  */
/* &FAXEMAIL = CDE DESCRIPTION FROM CUST DISTRIBUTION ENTRY         */
/* WHICH IS THE CUSTOMERS FAX NUMBER OR EMAIL ADDRESS FOR THIS.     */
             CHGVAR     VAR(&ADDR) VALUE(&FAXEMAIL)
             CALL       PGM(&FAXPGM) PARM('' &COMP &TYPE &DEPT +
                          &OVERLAY &RECIPIENT &COUNTRY &ADDR)

/* GET THE EMAIL ADDRESS FOR THE FAX SERVER FROM THE SYSTEM VALUES   */
/* FILE, I.E. '@FAX.SEABOARDCORP.COM'-FORMAT THE EMAIL ADDRESS       */
             CHGVAR     VAR(&ADDR) VALUE('                        ')
             CALL       PGM(PPJXXFR) PARM(' ' 'FAXSERVER' &ADDR)
             CHGVAR     VAR(&FTX2) VALUE(&FAXEMAIL)
             CHGVAR     VAR(&FTX2) VALUE(&FTX2 *TCAT &ADDR)
             CHGVAR     VAR(&DESC) VALUE('Invoice/Statement for ' +
                          *TCAT &RECIPIENT *BCAT &SNDDATA)

/* FAX THE COVER SHEET AND THE INVOICE OR STATEMENT                  */
             ESEND/ESNDMAIL RECIPIENT(&FTX2) SUBJECT(&DESC) +
                          MSG(*NONE) ATTLIST((* *PDF *N &FAXFILE) +
                          (* *PDF *N &PRTF))

/* FAX THE COVER SHEET AND THE 2ND PRINTER FILE                      */
             IF         COND(&PRTF2 *NE '          ') THEN(DO)
             ESEND/ESNDMAIL RECIPIENT(&FTX2) SUBJECT(&DESC) +
                          MSG(*NONE) ATTLIST((* *PDF *N &FAXFILE) +
                          (* *PDF *N &PRTF2))
             ENDDO

/* DELETE ONLY THE COVER SHEET SPOOL FILE HERE                      */
             DLTSPLF    FILE(&FAXFILE) SELECT(*ALL)
             MONMSG     MSGID(CPF3309)
             GOTO       CMDLBL(END)

 NOSPOOL:    SNDMSG     MSG('Faxing attempt failed, please retry or +
                          call Information Services') TOUSR(*REQUESTER)
 END:        ENDPGM
