      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6080
      * TITLE:       Activity Variance Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/14/02
      *
      * FUNCTION:  Batch program to compare the A) weekly head values reported by the farm with
      *            B) the actual movement values.
      *
      *            This 'report' can be generated in 2 versions (ie: sort sequences).
      *            So, control breaks/totals are dependent upon the 'report' being generated.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/17/10  LeAnne Ramsey
      *           Recompile only. Corrected spelling on Workfile field text.
      *
      * 05/20/11  LeAnne Ramsey (E1557)
      *           Added Yard Dead to HSJ085E and to the Report and the Download.
      *
      * 09/17/12  LeAnne Ramsey  (E2256)
      *           Sami W. needs the ability to select whether to print Shipped or Reported
      *           Head in the following 4 columns:
      *                transferred out, transferred in, cull sale, market sale
      *           So, I added a selection on HPD4080 and print conditionally based on the
      *           user's selection:
      *              IN83 on ---print Shipped
      *              IN83 off---print Reported
      *
      * 10/04/12  LeAnne Ramsey  (E2256)
      *           Per Sami Wilson, we are making more changes for E2256 from 9/17/12.
      *           We are adding 2 more fields/columns.
      *                   Shipped Ending Inventory Head
      *                   Variance Shipped Head
      *           To make room, I scrunched the entire layout.
      *
      *           Also, the calc for Transfer In Shipped Head (HP3080) will now be:
      *                    shipped head - DOA - KOA
      *           Also, we removed the optional selection based on the number of Reported
      *                    Variance head (Below):
      *
      *        You may further limit the groups that are included on the report by entering
      *        either one or both of the following options:
      *        Groups to include .. ?                                           (Blank = All)
      *        Groups whose Ending Inventory Variance Head is: ?                (GT GE LT LE)
      *                                  an absolute value of:
      *
      * 01/21/21  ISE             TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp3080   if   e           k disk
      *    Workfile: Activity variance report  (keyed in CL override)
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     D dash2           s            160    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svfscd          s                   like(ldfscd)
     D svcell          s                   like(ldcell)
     D svppcd          s                   like(ldppcd)
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D h1satmdy        s              6  0
     D h1fsbo          s              5
     D h1ppcd          s              5
     D h1ptcd          s              5
     D h1gtcd          s              3
     D h1hgcd          s              7
     D h1tfcd          s              5
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arabginhd       s              9  0 dim(3)
     D aratihd         s              9  0 dim(3)
     D arstihd         s              9  0 dim(3)
     D aratohd         s              9  0 dim(3)
     D arstohd         s              9  0 dim(3)
     D aracuhd         s              9  0 dim(3)
     D arscuhd         s              9  0 dim(3)
     D aramkhd         s              9  0 dim(3)
     D arsmkhd         s              9  0 dim(3)
     D araydhd         s              9  0 dim(3)
     D araddhd         s              9  0 dim(3)
     D aradshd         s              9  0 dim(3)
     D arakdlb         s              9  0 dim(3)
     D araiahd         s              9  0 dim(3)
     D araeninhd       s              9  0 dim(3)
      *
     D arrbginhd       s              9  0 dim(3)
     D arrtihd         s              9  0 dim(3)
     D arrtohd         s              9  0 dim(3)
     D arrcuhd         s              9  0 dim(3)
     D arrmkhd         s              9  0 dim(3)
     D arreninhd       s              9  0 dim(3)
     D arseninhd       s              9  0 dim(3)
      *
     D arrveninhd      s              9  0 dim(3)
     D arsveninhd      s              9  0 dim(3)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcdyr                 1      4  0
     D  ldcdwk                 5      6  0
     D  ldfcymd                7     14  0
     D  ldtcymd               15     22  0
      *
     D  ldrpfl                23     23
     D  ldtfcd                24     28
     D  ldblank               29     30
      *
     D  ldfsbo                31     35
     D  ldptcd                36     40
     D  ldppcd                41     45
     D  ldcell                46     47  0
     D  ldfscd                48     52  0
     D  ldhgcd                53     59
     D  ldhgsn                60     66  0
     D  ldgtcd                67     67
      *
     D  ldbods                75    104
     D  ldptds               105    114
     D  ldppds               115    144
     D  ldfsnm               145    169
     D  ldgtds               170    184
     D  ldtfds               185    202
     D  ldrpds               264    298
      *
     D  ldsrfl               300    300
     D  ldsrds               301    308
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print headings
      *
     C                   except    h1hdr
      *
      * Process each record in the workfile
      *
593  C*    *loval        setll     hsp3080
593  C     *start        setll     hsp3080
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3080                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   exsr      $first
      *
     C                   when      ldrpfl = 'V' and w1ppcd <> svppcd
     C                   exsr      $phasetot
      *
     C                   when      ldrpfl = 'C' and
     C                             (w1ppcd <> svppcd or
     C                              w1cell <> svcell or
     C                              w1fscd <> svfscd)
     C                   exsr      $break
     C                   endsl
      *
      * Accumulate detail line values into total arrays
      *
     C                   exsr      $accum
      *
      * Print detail line
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
      *
     C                   when      ldrpfl = 'V'
     C                   exsr      $phasetot
      *
     C                   when      ldrpfl = 'C'
     C                   exsr      $farmtot
     C                   exsr      $celltot
     C                   exsr      $phasetot
     C                   endsl
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Accumulate detail line values into total arrays
      *---------------------------------------------------------------
      *
     C     $accum        begsr
      *
     C                   add       w1abginhd     arabginhd
     C                   add       w1atihd       aratihd
     C                   add       w1stihd       arstihd
     C                   add       w1atohd       aratohd
     C                   add       w1stohd       arstohd
     C                   add       w1acuhd       aracuhd
     C                   add       w1scuhd       arscuhd
     C                   add       w1amkhd       aramkhd
     C                   add       w1smkhd       arsmkhd
     C                   add       w1aydhd       araydhd
     C                   add       w1addhd       araddhd
     C                   add       w1adshd       aradshd
     C                   add       w1akdlb       arakdlb
     C                   add       w1aiahd       araiahd
     C                   add       w1aeninhd     araeninhd
      *
     C                   add       w1rbginhd     arrbginhd
     C                   add       w1rtihd       arrtihd
     C                   add       w1rtohd       arrtohd
     C                   add       w1rcuhd       arrcuhd
     C                   add       w1rmkhd       arrmkhd
     C                   add       w1reninhd     arreninhd
     C                   add       w1seninhd     arseninhd
      *
     C                   add       w1rveninhd    arrveninhd
     C                   add       w1sveninhd    arsveninhd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print header
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------
      * Print 'Cell' total
      *-----------------------------------------------------------------------------
      *
     C     $celltot      begsr
      *
     C                   z-add     2             x
      *
     C                   seton                                        87
     C                   exsr      $total
     C                   setoff                                       87
      *
     C                   z-add     w1cell        svcell
      *
      * Print a blank line
      *
     C                   except    blank
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------
      * Print 'Farm' total
      *-----------------------------------------------------------------------------
      *
     C     $farmtot      begsr
      *
     C                   z-add     1             x
      *
     C                   if        ldrpfl = 'C'
     C                   seton                                        84
     C                   endif
      *
     C                   seton                                        86
     C                   exsr      $total
     C                   setoff                                       8486
      *
     C                   z-add     w1fscd        svfscd
      *
      * Print a blank line
      *
     C                   except    blank
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------
      * Print phase total
      *-----------------------------------------------------------------------------
      *
     C     $phasetot     begsr
      *
     C                   z-add     3             x
      *
     C                   if        ldrpfl = 'V'
     C                   seton                                        84
     C                   endif
      *
     C                   seton                                        85
     C                   exsr      $total
     C                   setoff                                       8485
      *
     C                   move      w1ppcd        svppcd
      *
      * Clear the total arrays.
      *
     C                   exsr      $clear
      *
      * Set on overflow to force a page break
      *
     C                   seton                                        of
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------
      * Control break logic when selected report is C=sort by phase/cell/farm
      *-----------------------------------------------------------------------------
      *
     C     $break        begsr
      *
     C                   select
     C                   when      w1ppcd <> svppcd
     C                   exsr      $farmtot
     C                   exsr      $celltot
     C                   exsr      $phasetot
      *
     C                   when      w1cell <> svcell
     C                   exsr      $farmtot
     C                   exsr      $celltot
      *
     C                   when      w1fscd <> svfscd
     C                   exsr      $farmtot
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------
      * Print totals line
      *-----------------------------------------------------------------------------
      *
     C     $total        begsr
      *
      * Print the dashed underscore...unless you have hit page break.
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   else
     C                   except    t1tot
     C                   endif
      *
      * Print first line of totals
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t2tot
      *
      * Print second line of totals
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t3tot
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------
      * Clear total arrays
      *-----------------------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             arabginhd
     C                   z-add     0             aratihd
     C                   z-add     0             arstihd
     C                   z-add     0             aratohd
     C                   z-add     0             arstohd
     C                   z-add     0             aracuhd
     C                   z-add     0             arscuhd
     C                   z-add     0             aramkhd
     C                   z-add     0             arsmkhd
     C                   z-add     0             araydhd
     C                   z-add     0             araddhd
     C                   z-add     0             aradshd
     C                   z-add     0             arakdlb
     C                   z-add     0             araiahd
     C                   z-add     0             araeninhd
      *
     C                   z-add     0             arrbginhd
     C                   z-add     0             arrtihd
     C                   z-add     0             arrtohd
     C                   z-add     0             arrcuhd
     C                   z-add     0             arrmkhd
     C                   z-add     0             arreninhd
     C                   z-add     0             arseninhd
      *
     C                   z-add     0             arrveninhd
     C                   z-add     0             arsveninhd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * First time subroutine
      *---------------------------------------------------------------
      *
     C     $first        begsr
      *
     C                   move      no            first
      *
     C                   select
     C                   when      ldrpfl = 'V'
     C                   move      w1ppcd        svppcd
      *
     C                   when      ldrpfl = 'C'
     C                   move      w1ppcd        svppcd
     C                   z-add     w1cell        svcell
     C                   z-add     w1fscd        svfscd
     C                   endsl
      *
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      * Flip user's selected Saturday date from ccyymmdd to mmddyy.
      *
     C                   move      ldtcymd       wkisodate
     C     *mdy          move      wkisodate     h1satmdy
      *
      *
      * Set up user's selections for Report Heading
      *
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   endif
      *
     C                   if        ldcell = 0
     C                   seton                                        88
     C                   endif
      *
     C                   if        ldfsbo = *blank
     C                   movel     'All'         h1fsbo
     C                   else
     C                   movel     ldfsbo        h1fsbo
     C                   endif
      *
     C                   if        ldppcd = *blank
     C                   movel     'All'         h1ppcd
     C                   else
     C                   movel     ldppcd        h1ppcd
     C                   endif
      *
     C                   if        ldptcd = *blank
     C                   movel     'All'         h1ptcd
     C                   else
     C                   movel     ldptcd        h1ptcd
     C                   endif
      *
     C                   if        ldtfcd = *blank
     C                   movel     'All'         h1tfcd
     C                   else
     C                   movel     ldtfcd        h1tfcd
     C                   endif
      *
     C                   if        ldgtcd = *blank
     C                   movel     'All'         h1gtcd
     C                   else
     C                   movel     ldgtcd        h1gtcd
     C                   endif
      *
     C                   if        ldhgcd = *blank
     C                   movel     'All'         h1hgcd
     C                   else
     C                   movel     ldhgcd        h1hgcd
     C                   endif
      *
     C                   if        ldsrfl = 'S'
     C                   seton                                        83
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          102 'ACTIVITY VARIANCE REPORT'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          E            h1hdr          1
     O                                           22 'Saturday date ........'
     O                       h1satmdy            32 '  /  /  '
      *
     O                                           89 'Production type ...'
     O                       h1ptcd              96
     O                       ldptds             110
      *
     O                                          144 'Cell .......'
     O              n88      ldcell        z    148
     O               88                         149 'All'
      *
      *
     O          e            h1hdr          1
     O                                           22 'Calendar year/week ...'
     O                       ldcdyr        z     28
     O                       ldcdwk        z     32
      *
     O                                           89 'Production phase ..'
     O                       h1ppcd              96
     O                       ldppds             130
      *
     O                                          144 'Farm site ..'
     O              n89      ldfscd        m    152
     O               89                         149 'All'
     O                       ldfsnm             180
      *
      *
     O          e            h1hdr          1
     O                                           22 'Report ...............'
     O                       ldrpfl              25
     O                       ldrpds              68
      *
     O                                           89 'Group type ........'
     O                       h1gtcd              94
     O                       ldgtds             115
      *
     O                                          144 'Hog group ..'
     O                       h1hgcd             153
      *
      *
     O          e            h1hdr          2
     O                                           22 'Business office ......'
     O                       h1fsbo              29
     O                       ldbods              63
      *
     O                                           89 'Type of farm ......'
     O                       h1tfcd              96
     O                       ldtfds             118
      *
     O                                          144 'Head .......'
     O                       ldsrds             154
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                          127 'Actual'
     O                                          147 'Actual Killed/Dead'
     O                                          155 'Actual'
     O                                          169 '------------'
     O                                          185 'Ending Inventory'
     O                                          198 '-------------'
      *
     O          e            h1hdr          1
     O                                           10 'Farm'
     O                                           15 'Hog'
     O                                           37 'Phas'
     O                                           52 'Begin Inventry'
     O                                           68 'Trans In Head'
     O                                           84 'Trans Out Head'
     O                                          101 'Cull Sale Head'
     O                                          118 'Mrkt Sale Head'
     O                                          127 'Yd Dead'
     O                                          133 'Dead'
     O                                          139 'Dest'
     O                                          147 'Total'
     O                                          155 'InvAdj'
      *
     O                                          180 'Reportd'
     O                                          198 'Shipped'
      *
     O          e            h1hdr          1
     O                                            4 'Cell'
     O                                           10 'Site'
     O                                           17 'Group'
     O                                           25 'Bldg'
     O                                           31 'Room'
     O                                           37 'WkDz'
      * Beginning inventory
     o                                           44 'Actual'
     O                                           52 'Reportd'
      * Transferred in head
     O                                           60 'Actual'
     O               83                          68 'Shipped'
     O              n83                          68 'Reportd'
      * Transferred out head
     O                                           76 'Actual'
     O               83                          84 'Shipped'
     O              n83                          84 'Reportd'
      * Cull sale head
     O                                           93 'Actual'
     O               83                         101 'Shipped'
     O              n83                         101 'Reportd'
      * Market sale head
     O                                          110 'Actual'
     O               83                         118 'Shipped'
     O              n83                         118 'Reportd'
      * Actual yard dead head
     O                                          127 'Head'
      * Actual daily dead
     O                                          133 'Head'
     O                                          139 'Head'
     O                                          147 'Lbs'
      * Actual inventory adjustment head
     O                                          155 'Head'
      * Ending inventory
     O                                          163 'Actual'
     O                                          171 'Reportd'
     O                                          180 'Varince'
     O                                          189 'Shipped'
     O                                          198 'Varince'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Production phase heading
      *-------------------------------------------------------------
      *
     O          e            h2hdr          1
     O                                           17 'Production Phase:'
     O                       svppcd              24
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       w1cell        z      2
     O                       w1cast               4
     O                       w1fscd        z     10
     O                       w1hgcd              19
     O                       w1rast              20
     O                       w1blcd              26
     O                       w1rmcd              32
     O                       w1phswd             37
      *
     O                       w1abginhd     k     45
     O                       w1rbginhd     k     53
      *
     O                       w1atihd       k     61
     O               83      w1stihd       k     69
     O              n83      w1rtihd       k     69
      *
     O                       w1atohd       k     77
     O               83      w1stohd       k     85
     O              n83      w1rtohd       k     85
      *
     O                       w1acuhd       k     93
     O                       w1acuast            94
     O               83      w1scuhd       k    102
     O              n83      w1rcuhd       k    102
      *
     O                       w1amkhd       k    110
     O                       w1amkast           111
     O               83      w1smkhd       k    119
     O              n83      w1rmkhd       k    119
      *
     O                       w1aydhd       k    127
      *
     O                       w1addhd       m    134
     O                       w1adshd       m    140
     O                       w1akdlb       k    148
      *
     O                       w1aiahd       m    156
      *
     O                       w1aeninhd     k    164
     O                       w1reninhd     k    172
     O                       w1rveninhd    k    181
     O                       w1seninhd     k    190
     O                       w1sveninhd    k    198
      *
      *-------------------------------------------------------------
      * Total lines
      *-------------------------------------------------------------
      *
     O          e            t1tot       1
     O               84      dash2              198
      *
     O          e            t2tot       1
      *
     O               85                          26 'Total for Phase'
     O               85      svppcd              32
      *
     O               86                          26 'Total for Farm'
     O               86      svfscd        z     32
      *
     O               87                          26 'Total for Cell'
     O               87      svcell        z     32
      *
     O                       arabginhd (x) kb    45
     O                       aratihd   (x) kb    61
     O                       aratohd   (x) kb    77
     O                       aracuhd   (x) kb    93
     O                       aramkhd   (x) kb   110
     O                       araydhd   (x) kb   127
     O                       aradshd   (x) kb   140
     O                       araiahd   (x) kb   156
     O                       arreninhd (x) kb   172
     O                       arseninhd (x) kb   190
      *
     O          e            t3tot       1
      *
     O                       arrbginhd (x) kb    53
     O               83      arstihd   (x) kb    69
     O              n83      arrtihd   (x) kb    69
     O               83      arstohd   (x) kb    85
     O              n83      arrtohd   (x) kb    85
     O               83      arscuhd   (x) kb   102
     O              n83      arrcuhd   (x) kb   102
     O               83      arsmkhd   (x) kb   119
     O              n83      arrmkhd   (x) kb   119
     O                       araddhd   (x) kb   134
     O                       arakdlb   (x) kb   148
     O                       araeninhd (x) kb   164
     O                       arrveninhd(x) kb   181
     O                       arsveninhd(x) kb   198
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
