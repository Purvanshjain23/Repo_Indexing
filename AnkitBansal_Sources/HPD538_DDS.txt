     A*%%TS  SD  20131021  142420  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD538 - SELECT TRUCKER
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          01/07/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP538S1                   SFL
     A*%%TS  SD  20131021  142420  ISLRAMS     REL-V6R1M0  5761-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1TICD    R        O  8  2REFFLD(@@TICD)
     A            D1TINM    R        O  8  9REFFLD(HSRREF/@@TINM *LIBL/HSPREF)
     A            D1TTCD    R        O  8 38REFFLD(HSRREF/@@TTCD *LIBL/HSPREF)
     A            D1AIST    R        O  8 46REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A            D1MTECHREFR        O  8 51REFFLD(HSRREF/@@MTECHREF +
     A                                             *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP538C1                   SFLCTL(HP538S1)
     A*%%TS  SD  20131021  142420  ISLRAMS     REL-V6R1M0  5761-WDS
     A  *DS3                                SFLSIZ(0100)
     A  *DS4                                SFLSIZ(0100)
     A  *DS3                                SFLPAG(0009)
     A  *DS4                                SFLPAG(0009)
     A  *DS3                                WINDOW(HP538EC)
     A  *DS4                                WINDOW(HP538EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 34'SELECT TRUCKER'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 26REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  4 38'?'
     A                                      DSPATR(HI)
     A N62                              4 46'?'
     A                                      DSPATR(HI)
     A                                  5 36'Trucker'
     A                                      DSPATR(HI)
     A                                  5 45'A/I'
     A                                      DSPATR(HI)
     A                                  6  2'Code'
     A                                      DSPATR(HI)
     A                                  6  9'Name'
     A                                      DSPATR(HI)
     A                                  6 37'Type'
     A                                      DSPATR(HI)
     A                                  6 45'Sta'
     A                                      DSPATR(HI)
     A                                  6 51'MTech Reference'
     A                                      DSPATR(HI)
     A            C1TICD    R        B  7  2REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1TINM    R        B  7  9REFFLD(HSRREF/@@TINM *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1TTCD    R        B  7 38REFFLD(HSRREF/@@TTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A N62        C1AIST    R        B  7 46REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1MTECHREFR        B  7 51REFFLD(HSRREF/@@MTECHREF +
     A                                             *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP538K1
     A                                      WINDOW(HP538EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP538ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP538EC                   SFLCTL(HP538ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
