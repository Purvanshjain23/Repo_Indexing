/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:    HP410CL                                     */
/*  PROGRAM NAME:      LISTINGS: EXPENSE CLASS/SUBCLASS/CODE       */
/*  PROGRAMMER:        LEANNER FEDOR                               */
/*  DATE:              6/17/97                                     */
/*                                                                 */
/*                                                                 */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&LISTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LISTCD) VALUE(%SST(*LDA 1 1))

/*------------------------------------------------------------------*/
/* CREATE/BUILD WORKFILE                                            */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3410) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL3410A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP3410)

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS LISTING PRINTS AT 10 CPI ON STANDARD PAPER.                 */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) DRAWER(*PRTF) COPIES(&COPYS) +
                          HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/* LISTING BY CODE                                                  */
/*------------------------------------------------------------------*/

             IF         COND(&LISTCD *EQ 'C') +
                          THEN(DO)
             CALL       PGM(HP610)
             ENDDO

/*------------------------------------------------------------------*/
/* LISTING BY REPORT CODE                                           */
/*------------------------------------------------------------------*/

             IF         COND(&LISTCD *EQ 'R') +
                          THEN(DO)
             OVRDBF     FILE(HSP3410) SHARE(*YES)
             OPNQRYF    FILE((HSP3410)) QRYSLT('W1RPCD *NE " "') +
                          KEYFLD((W1RPCD) (W1ECCD) (W1ESCD) (W1EXCD))
             CALL       PGM(HP6600)
             ENDDO

/*------------------------------------------------------------------*/
/* LISTING BY SEQUENCE                                              */
/*------------------------------------------------------------------*/

             IF         COND(&LISTCD *EQ 'S') +
                          THEN(DO)
             CALL       PGM(HP6601)
             ENDDO


             DLTOVR     FILE(*ALL)

             ENDPGM

