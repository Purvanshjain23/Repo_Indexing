     A*%%TS  SD  20171109  110305  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD136
     A*  TITLE:         MAINTAIN CHECK
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/12/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  62  THE CHECK HAS CLOSED GROUPS OR POSTED MOVEMENTS; INACTIVIATE
     A*      DELETE AND PROTECT CUSTOMER NUMBER, SALES TYPE, CHECK DATE.
     A*  64  F9 ACCEPT IS VALID
     A*  65  PROTECT SUBFILE FIELDS ON SCREEN 2-CHECK DETAIL. THE MOVEMENT
     A*      IS POSTED OR THE HOG GROUP IS CLOSED.
     A*  90  THE CHECK THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A* MODIFICATIONS
     A* 02/17/98  LEANNE FEDOR
     A*           CHANGED FROM ONE SCREEN TO TWO.
     A* 11/09/17  BRAD BADEN   E010812
     A*           ADDED BUSINESS OFFICE TO SCREEN.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A*
     A*****************************************************************
     A* SCREEN 1 - CHECK HEADER INFO
     A*
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT 1 - CHECK HEADER INFO
     A*----------------------------------------------------------------
     A*
     A          R HP136R1
     A*%%TS  SD  19980226  092045  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      TEXT('CHECK HEADER INFO')
     A                                      CF03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90 64                               CF09(09 'ACCEPT')
     A N90N62N61                            CF11(11 'DELETE')
     A N90                                  CF13(13 'DEDUCT/ADDONS')
     A N90                                  CF14(14 'CHECK DETAIL')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(51)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(71)
     A                                      SETOF(72)
     A                                      SETOF(73)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 30'MAINTAIN CHECK'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Sale type ........'
     A N90N62                           4 21'?'
     A            D1STCD    R        B  4 23REFFLD(HSRREF/STSTCD *LIBL/HSPREF)
     A N75N90N62                            DSPATR(HI)
     A N75N90N62                            DSPATR(UL)
     A  75                                  DSPATR(RI)
     A  75
     AO 55                                  DSPATR(PC)
     A  62
     AO 90                                  DSPATR(PR)
     A            D1STDS    R        O  4 33REFFLD(HSRREF/STSTDS *LIBL/HSPREF)
     A                                  4 55'Business Office:'
     A            D1BOCD    R        O  4 72REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A                                  6  2'Customer number ..'
     A N90N62                           6 21'?'
     A            D1CVNO    R        B  6 23REFFLD(CHCVNO)
     A                                      EDTCDE(Z)
     A N74N90N62                            DSPATR(HI)
     A N74N90N62                            DSPATR(UL)
     A  74                                  DSPATR(RI)
     A  74
     AO 54                                  DSPATR(PC)
     A  90
     AO 62                                  DSPATR(PR)
     A            D1CNAM    R        O  6 33REFFLD(@@DS30 HSPREF)
     A                                  8  2'Check date .......'
     A            D1CHDTMDY R        B  8 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N71N90N62                            DSPATR(HI)
     A N71N90N62                            DSPATR(UL)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  90
     AO 62                                  DSPATR(PR)
     A                                  9  2'Check number .....'
     A            D1CHNO    R        B  9 23REFFLD(HSRREF/CHCHNO *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N72N90                               DSPATR(HI)
     A N72N90                               DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  2'Check amount .....'
     A            D1CHAM    R        B 10 23REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A N73N90                               DSPATR(HI)
     A N73N90                               DSPATR(UL)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 49'**Per CWT**'
     A                                 12 49'Carcass lbs'
     A                                 14 14'Base amount ......:'
     A            D1BAAM    R        O 14 35REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            D1BCWT    R        O 14 51REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A                                 15 14'Premium/discount .:'
     A            D1PDAM    R        O 15 35REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            D1PCWT    R        O 15 51REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A                                 16 52'Live lbs'
     A                                 17 14'Gross amount .....:'
     A            D1GRAM    R        O 17 35REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            D1GCWT    R        O 17 51REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A                                 18 14'Deduction/add-on .:'
     A            D1DAAM    R        O 18 35REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            D1DCWT    R        O 18 51REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A                                 19 14'Net amount .......:'
     A            D1NTAM    R        O 19 35REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            D1NCWT    R        O 19 51REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A*
     A*
     A*----------------------------------------------------------------
     A* COMMAND RECORD 1
     A*----------------------------------------------------------------
     A          R HP136K1
     A*%%TS  SD  19980218  101008  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N90                             22 11'F4=Prompt'
     A N90                             22 22'F5=Refresh'
     A N90 64                          22 34'F9=Accept'
     A N90N62N61                       22 45'F11=Delete'
     A N90                             22 57'F13-Deductions/Add-ons'
     A N90                             23 57'F14-Check Detail'
     A*
     A*****************************************************************
     A* SCREEN 2 - CHECK DETAIL
     A*****************************************************************
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 2 - CHECK DETAIL
     A*---------------------------------------------------------------
     A*
     A          R HP136S2                   SFL
     A                                      TEXT('CHECK DETAIL')
     A  85                                  SFLNXTCHG
     A            HDRRN          4Y 0H
     A            HDLNNO    R        H      REFFLD(CDLNNO)
     A            HDHGSN    R        H      REFFLD(CDHGSN)
     A            HDGSCD    R        H      REFFLD(HGGSCD)
     A            HDPOFL    R        H      REFFLD(MSPOFL)
     A            HDCRLB    R        H      REFFLD(CDCRLB)
     A            HDBFAM    R        H      REFFLD(CDBFAM)
     A            HDLNPR    R        H      REFFLD(CDLNPR)
     A            HDLEAM    R        H      REFFLD(CDLEAM)
     A            HDCLCD    R        H      REFFLD(CDCLCD)
     A            HDDFCD    R        H      REFFLD(CDDFCD)
     A            HDKLDT    R        H      REFFLD(CDKLDT)
     A            HDPDAM    R        H      REFFLD(CDBAAM)
     A            D2OPT          2A  B 11  2CHGINPDFT(UL)
     A  30N65                               DSPATR(PC)
     A  30N65                               DSPATR(RI)
     A N30N90N65
     A  42                                  DSPATR(HI)
     A N30N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2TPCD    R        B 11  5REFFLD(@@TPCD)
     A  31N65
     AO 51N65                               DSPATR(PC)
     A  31N65                               DSPATR(RI)
     A N31N90N65
     A  42                                  DSPATR(HI)
     A N31N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2MVSN    R        B 11  7REFFLD(@@MVSN)
     A                                      EDTCDE(M)
     A  33N65
     AO 53N65                               DSPATR(PC)
     A  33N65                               DSPATR(RI)
     A N33N90N65
     A  42                                  DSPATR(HI)
     A N33N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2HGCD    R        B 11 16REFFLD(HGHGCD)
     A  32N65
     AO 52N65                               DSPATR(PC)
     A  32N65                               DSPATR(RI)
     A N32N90N65
     A  42                                  DSPATR(HI)
     A N32N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2CYFL    R        O 11 24REFFLD(@@FLAG)
     A            D2LVHD    R        B 11 26REFFLD(CDLVHD)
     A                                      EDTCDE(M)
     A  34N65                               DSPATR(PC)
     A  34N65                               DSPATR(RI)
     A N34N90N65
     A  42                                  DSPATR(HI)
     A N34N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2LVLB    R        B 11 33REFFLD(CDLVLB)
     A                                      EDTCDE(M)
     A  35N65                               DSPATR(PC)
     A  35N65                               DSPATR(RI)
     A N35N90N65
     A  42                                  DSPATR(HI)
     A N35N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2DOHD    R        B 11 40REFFLD(CDDOHD)
     A                                      EDTCDE(M)
     A  36N65                               DSPATR(PC)
     A  36N65                               DSPATR(RI)
     A N36N90N65
     A  42                                  DSPATR(HI)
     A N36N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2DOLB    R        B 11 47REFFLD(CDDOLB)
     A                                      EDTCDE(M)
     A  37N65                               DSPATR(PC)
     A  37N65                               DSPATR(RI)
     A N37N90N65
     A  42                                  DSPATR(HI)
     A N37N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2GRAM    R        B 11 54REFFLD(CDGRAM)
     A                                      EDTCDE(K)
     A  38N65                               DSPATR(PC)
     A  38N65                               DSPATR(RI)
     A N38N90N65
     A  42                                  DSPATR(HI)
     A N38N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2KLDTMDY R     Y  B 11 65REFFLD(@@DT06)
     A  39N65                               DSPATR(PC)
     A  39N65                               DSPATR(RI)
     A N39N90N65
     A  42                                  DSPATR(HI)
     A N39N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A                                      EDTWRD('  /  /  -')
     A            D2TANO    R     Y  B 11 75REFFLD(CDTANO)
     A                                      EDTCDE(Z)
     A  40N65                               DSPATR(PC)
     A  40N65                               DSPATR(RI)
     A N40N90N65
     A  42                                  DSPATR(HI)
     A N40N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2CDHD    R        B 12 26REFFLD(CDCDHD)
     A                                      EDTCDE(M)
     A  44N65                               DSPATR(PC)
     A  44N65                               DSPATR(RI)
     A N44N90N65
     A  42                                  DSPATR(HI)
     A N44N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2CDLB    R        B 12 33REFFLD(CDCDLB)
     A                                      EDTCDE(M)
     A  45N65                               DSPATR(PC)
     A  45N65                               DSPATR(RI)
     A N45N90N65
     A  42                                  DSPATR(HI)
     A N45N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2YDHD    R        B 12 40REFFLD(CDYDHD)
     A                                      EDTCDE(M)
     A  46N65                               DSPATR(PC)
     A  46N65                               DSPATR(RI)
     A N46N90N65
     A  42                                  DSPATR(HI)
     A N46N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A            D2YDLB    R        B 12 47REFFLD(CDYDLB)
     A                                      EDTCDE(M)
     A  47N65                               DSPATR(PC)
     A  47N65                               DSPATR(RI)
     A N47N90N65
     A  42                                  DSPATR(HI)
     A N47N90N65
     A  42                                  DSPATR(UL)
     A  65
     AO 90                                  DSPATR(PR)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT 2 - CHECK DETAIL CONTROL RECORD
     A*----------------------------------------------------------------
     A*
     A          R HP136C2                   SFLCTL(HP136S2)
     A*%%TS  SD  20040526  075951  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A N90                                  CF03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A N90                                  CF12(12 'PREVIOUS')
     A N90                                  CF18(18 'TOP OF SUBFILE')
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRR)
     A                                      SFLMODE(&HDFOLD)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A  89                                  SFLDROP(CF16)
     A  88                                  SFLFOLD(CF16)
     A            HDFOLD         1A  H
     A            HDPAG          4S 0H      SFLRCDNBR
     A            HDRR           5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 26'MAINTAIN CHECK DETAIL'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Check number:'
     A            C2CHNO    R        O  4 16REFFLD(HSRREF/CHCHNO *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  4 55'Business Office:'
     A            C2BOCD    R        O  4 72REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A                                  6  2'2=Revise Carcass Data and Premiums-
     A                                      /discounts'
     A                                      COLOR(BLU)
     A                                  6 48'4=Delete Carcass and Prem/Disc'
     A                                      COLOR(BLU)
     A N90                              8  5'?'
     A N90                              8 11'?'
     A N90                              8 19'?'
     A                                  8 27'Head'
     A                                  8 32'Pounds'
     A                                  8 41'Head'
     A                                  8 46'Pounds'
     A                                  9  5'P'
     A                                  9  9'Sales'
     A                                  9 24'C'
     A                                  9 27'*** Live **'
     A                                  9 41'*** DOA ***'
     A                                  9 58'Gross'
     A                                  9 67'Kill'
     A                                 10  1'Opt'
     A                                 10  5'T'
     A                                 10 10'Move'
     A                                 10 16'Group'
     A                                 10 24'Y'
     A                                 10 27'*Condemned* '
     A                                 10 41'*Yard Dead*'
     A                                 10 57'Amount'
     A                                 10 67'Date'
     A                                 10 74'Tattoo'
     A*----------------------------------------------------------------
     A* COMMAND RECORD 2
     A*----------------------------------------------------------------
     A          R HP136K2
     A*%%TS  SD  19980227  133645  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 10'F4=Prompt'
     A N90                             23 20'F5=Refresh'
     A                                 23 32'F9=Accept'
     A                                 23 43'F12=Previous'
     A N90                             23 57'F16=Fold/Unfold'
     A                                 23 73'F18=Top'
     A*
      *********************************************************************
      * ERROR MESSAGE FORMATS
      *********************************************************************
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP136ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP136EC                   SFLCTL(HP136ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
