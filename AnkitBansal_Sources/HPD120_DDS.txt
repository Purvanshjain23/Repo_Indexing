     A*%%TS  SD  20090427  135116  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD120
     A*  TITLE:         MAINTAIN FEED DELIVERY TICKETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/18/94
     A*
     A*****************************************************************
     A* P405   DN  04/03/20 - Bin Code (BNCD) was increased from 5A to
     A*                       6A. Shifted subfile info to the right 1
     A*                       space.
     A*****************************************************************
     A* SDN736 RECOMPILED FOR TICKET NBR INCREASE (TKNO & RTNO 7.0 TO 9.0)
     A*****************************************************************
     A*  INDICATORS:
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  63  RECOST MODE - ALL FIELDS ARE PROTECTED.
     A*  64  PICKUPS EXIST FOR A DETAIL LINE ITEM. PROTECT ALL FIELDS
     A*      EXCEPT POUNDS AND COST FIELDS
     A*  65  PROTECT ALL FIELDS. THIS TICKET HAS EITHER BEEN POSTED OR
     A*      IS ASSOCIATED WITH ONE OR MORE CLOSED GROUPS.
     A*  66  THIS FEED  TICKET IS FOR 'PRODUCED' FEED--NOT
     A*      PURCHASED FEED. SO PROTECT THE SUBFILE AMOUNT FIELDS
     A*  67  THERE ARE PICKUPS AND/OR ADJUSTMENTS AGAINST THIS
     A*      FEED TICKET.  SO, F11-DELETE IS NOT VALID.
     A*  88  TICKET HAS A STATUS OF CO=COSTED; SO, DISPLAY COSTED DATE/TIME/USER
     A*  90  THE TICKET THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90N63N64
     A N65                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90N65                               CF09(09 'MAINTAIN')
     A N90N65N67
     A N63 60                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP120S1                   SFL
     A*%%TS  SD  19980309  145246  ISLRAMS     REL-V3R7M0  5716-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(HGHGSN)
     A            HDPPCD    R        H      REFFLD(@@PPCD)
     A            HDBLCD    R        H      REFFLD(HGBLCD)
     A            HDRMCD    R        H      REFFLD(HGRMCD)
     A            HDDIR     R        H      REFFLD(@@FLAG)
     A            DFPICK    R        O 15  2REFFLD(@@FLAG)
     A  61                                  DSPATR(ND)
     A            DFBNCD    R        B 15  4REFFLD(FDREC/FDBNCD *LIBL/HSP038)
     A  63
     AO 64
     AO 65
     AO 90                                  DSPATR(PR)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N63N64
     A N65N90 42                            DSPATR(HI)
     A N31N63N64
     A N65N90 42                            DSPATR(UL)
P405 A**          DFHGCD    R        B 15 10REFFLD(HGREC/HGHGCD *LIBL/HSP034)
P405 A            DFHGCD    R        B 15 11REFFLD(HGREC/HGHGCD *LIBL/HSP034)
     A  63
     AO 64
     AO 65
     AO 90                                  DSPATR(PR)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N63N64
     A N65N90 42                            DSPATR(HI)
     A N32N63N64
     A N65N90 42                            DSPATR(UL)
P405 A**          DFRNCD    R        B 15 18REFFLD(FDREC/FDRNCD *LIBL/HSP038)
P405 A            DFRNCD    R        B 15 19REFFLD(FDREC/FDRNCD *LIBL/HSP038)
     A  63
     AO 64
     AO 65
     AO 90                                  DSPATR(PR)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N63N64
     A N65N90 42                            DSPATR(HI)
     A N33N63N64
     A N65N90 42                            DSPATR(UL)
P405 A**          DFADCD    R        B 15 24REFFLD(FDREC/FDADCD *LIBL/HSP038)
P405 A            DFADCD    R        B 15 25REFFLD(FDREC/FDADCD *LIBL/HSP038)
     A  63
     AO 64
     AO 65
     AO 90                                  DSPATR(PR)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A N47N63N64
     A N65N90 42                            DSPATR(HI)
     A N47N63N64
     A N65N90 42                            DSPATR(UL)
P405 A**          DFFTCD    R        B 15 30REFFLD(FDFTCD)
P405 A            DFFTCD    R        B 15 31REFFLD(FDFTCD)
     A  63
     AO 64
     AO 65
     AO 90                                  DSPATR(PR)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A N48N63N64
     A N65N90 42                            DSPATR(HI)
     A N48N63N64
     A N65N90 42                            DSPATR(UL)
P405 A**          DFFDLB    R        B 15 32REFFLD(FDREC/FDFDLB *LIBL/HSP038)
P405 A            DFFDLB    R        B 15 33REFFLD(FDREC/FDFDLB *LIBL/HSP038)
     A                                      CHECK(RB)
     A                                      EDTCDE(K)
     A  63
     AO 65
     AO 90                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34N63N65
     A N90 42                               DSPATR(HI)
     A N34N63N65
     A N90 42                               DSPATR(UL)
P405 A**          DFINAM    R        B 15 43REFFLD(FDREC/FDINAM *LIBL/HSP038)
P405 A            DFINAM    R        B 15 44REFFLD(FDREC/FDINAM *LIBL/HSP038)
     A                                      EDTCDE(K)
     A  63
     AO 65
     AO 66
     AO 90                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35N63N65
     A N66N90 42                            DSPATR(HI)
     A N35N63N65
     A N66N90 42                            DSPATR(UL)
     A            DFMDAM    R        B 15 57REFFLD(FDREC/FDMDAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A  63
     AO 65
     AO 66
     AO 90                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40N63N65
     A N66N90 42                            DSPATR(HI)
     A N40N63N65
     A N66N90 42                            DSPATR(UL)
     A            DFMFAM    R        B 15 69REFFLD(FDREC/FDMFAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A  63
     AO 65
     AO 66
     AO 90                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41N63N65
     A N66N90 42                            DSPATR(HI)
     A N41N63N65
     A N66N90 42                            DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP120C1                   SFLCTL(HP120S1)
     A*%%TS  SD  20090427  135116  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0007)
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(30)
     A                                      SETOF(33)
     A                                      SETOF(50)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            C1TKDT    R        H      REFFLD(FHTKDT)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 21'MAINTAIN FEED DELIVERY TICKET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..................'
     A N60 42                           4 31'?'
     A            C1FSCD    R        B  4 33REFFLD(FSREC/FSFSCD *LIBL/HSP018)
     A                                      EDTCDE(M)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N60N63
     A N64N65N90
     A  42                                  DSPATR(HI)
     A N36N60N63
     A N64N65N90
     A  42                                  DSPATR(UL)
     A  60
     AO 63
     AO 64
     AO 65                                  DSPATR(PR)
     A            C1FSNM    R        O  4 44REFFLD(FSFSNM)
     A                                  5  2'Feed mill business office ..'
     A N63N64N65
     A N60 42                           5 22'?'
     A N60 42                           5 31'?'
     A            C1FMBO    R        B  5 33REFFLD(FDFMBO)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N60N63
     A N64N65N90
     A  42                                  DSPATR(HI)
     A N30N60N63
     A N64N65N90
     A  42                                  DSPATR(UL)
     A  60
     AO 63
     AO 64
     AO 65                                  DSPATR(PR)
     A            C1FMDS    R        O  5 44REFFLD(BOREC/BOBODS *LIBL/HSP009)
     A                                  6  2'Ticket number ..............'
     A            C1TKNO    R        B  6 33REFFLD(FHREC/FHTKNO *LIBL/HSP037)
     A  60
     AO 63
     AO 64
     AO 65                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N60N63
     A N64N65N90
     A  42                                  DSPATR(HI)
     A N37N60N63
     A N64N65N90
     A  42                                  DSPATR(UL)
     A  88                              6 64'    Costed      '
     A                                      DSPATR(UL)
     A                                  7  2'Ticket date ................'
     A            C1MDY     R        B  7 33REFFLD(@@DT06 *LIBL/HSPREF)
     A                                      EDTCDE(Y)
     A  63
     AO 64
     AO 65
     AO 67                                  DSPATR(PR)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N63N64
     A N65N67N90
     A  42                                  DSPATR(HI)
     A N38N63N64
     A N65N67N90
     A  42                                  DSPATR(UL)
     A N61                              7 44'Ticket status:'
     A N61        C1TSCD    R        O  7 59REFFLD(FHTSCD)
     A  88                              7 64'Date:'
     A  88        C1COMDY   R        O  7 70REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  8  2'Produced flag .....'
     A N63N64N65
     A N60                              8 22'?'
     A            C1PRFL    R        B  8 24REFFLD(FHREC/FHPRFL *LIBL/HSP037)
     A  60
     AO 63
     AO 64
     AO 65                                  DSPATR(PR)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39N60N63
     A N64N65N90
     A  42                                  DSPATR(HI)
     A N39N60N63
     A N64N65N90
     A  42                                  DSPATR(UL)
     A  88                              8 64'Time:'
     A  88        C1COTM    R        O  8 70REFFLD(HSRREF/@@COTM *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
     A                                  9  2'Cost thru system ..'
     A N60                              9 22'?'
     A            C1CSFL    R        B  9 24REFFLD(HSRREF/@@CSFL *LIBL/HSPREF)
     A  60
     AO 63
     AO 64
     AO 65                                  DSPATR(PR)
     A  45
     AO 55                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A N45N60N63
     A N64N65N90
     A  42                                  DSPATR(HI)
     A N45N60N63
     A N64N65N90
     A  42                                  DSPATR(UL)
     A  88                              9 64'User:'
     A  88        C1COUSR   R        O  9 70REFFLD(HSRREF/@@COUSR *LIBL/HSPREF)
     A                                 10  2'Vendor invoice # ..'
     A            C1VINO    R        B 10 24REFFLD(FHREC/FHVINO *LIBL/HSP037)
     A  63
     AO 64
     AO 65                                  DSPATR(PR)
     A N39N63N64
     A N65N90 42                            DSPATR(HI)
      *
     A N39N63N64
     A N65N90 42                            DSPATR(UL)
     A                                 10 35'------------------- TOTALS --------
     A                                      -----------'
     A                                      DSPATR(HI)
      ** P405-6/26/20-ORIGINAL LINE.
P405 A            C1FDLB         7Y 0O 11 32EDTCDE(K)
     A            C1INAM    R        O 11 43REFFLD(FDREC/FDINAM *LIBL/HSP038)
     A                                      EDTCDE(K)
     A            C1MDAM    R        O 11 57REFFLD(FDREC/FDMDAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A            C1MFAM    R        O 11 69REFFLD(FDREC/FDMFAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A N65 42                          12  5'?'
     A                                      DSPATR(HI)
P405 A*N65 42                          12 12'?'
P405 A N65 42                          12 13'?'
     A                                      DSPATR(HI)
P405 A*N65 42                          12 20'?'
P405 A N65 42                          12 21'?'
     A                                      DSPATR(HI)
P405 A*N65 42                          12 26'?'
P405 A N65 42                          12 27'?'
     A                                      DSPATR(HI)
P405 A*N65 42                          12 30'?'
P405 A N65 42                          12 31'?'
     A                                      DSPATR(HI)
     A N61                             13  2'P'
     A                                      DSPATR(HI)
P405 A**                               13 30'F'
P405 A                                 13 31'F'
     A                                      DSPATR(HI)
     A                                 13 45'Ingredient'
     A                                      DSPATR(HI)
     A                                 13 57'Medication'
     A                                      DSPATR(HI)
     A                                 13 71'Manufact'
     A                                      DSPATR(HI)
     A N61                             14  2'U'
     A                                      DSPATR(HI)
     A                                 14  4'Bin'
     A                                      DSPATR(HI)
P405 A**                               14 10'Group'
P405 A                                 14 11'Group'
     A                                      DSPATR(HI)
P405 A**                               14 18'Ration/Add'
P405 A                                 14 19'Ration/Add'
     A                                      DSPATR(HI)
P405 A**                               14 30'T'
P405 A                                 14 31'T'
     A                                      DSPATR(HI)
P405 A**                               14 35'Pounds'
P405 A                                 14 35'Pounds'
     A                                      DSPATR(HI)
     A                                 14 51'Cost'
     A                                      DSPATR(HI)
     A                                 14 63'Cost'
     A                                      DSPATR(HI)
     A                                 14 75'Cost'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP120K1
     A*%%TS  SD  19940426  131054  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90N63N64
     A N65                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90N65 61                       23 34'F9=Create'
     A N90N65 60                       23 34'F9=Revise'
     A N90N65 63                       23 34'F9=Recost'
     A N90N65N63
     AAN67 60                          23 45'F11=Delete'
     A                                 23 57'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP120ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP120EC                   SFLCTL(HP120ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
