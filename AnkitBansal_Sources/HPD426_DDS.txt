     A*%%TS  SD  19950303  092517  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD426
     A*  TITLE:         WORK WITH USERS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/13/95
     A*
     A*****************************************************************
     A* MODIFICATIONS:
     A* 06/18/97  LEANNE FEDOR
     A*           ADDED A FUNCTION KEY FOR PRINT OPTIONS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP426S1                   SFL
     A*%%TS  SD  19950302  083635  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            DFUSCD    R        O 10  7REFFLD(AUUSCD)
     A            DFUSNM    R        O 10 18REFFLD(@@NM25)
     A            DFSLCD    R        O 10 45REFFLD(AUSLCD)
     A            DFRUFL    R        O 10 53REFFLD(AURUFL)
     A            DFMDFL    R        O 10 60REFFLD(AUMDFL)
     A            DFRMFL    R        O 10 67REFFLD(AURMFL)
     A            DFRVFL    R        O 10 72REFFLD(AURVFL)
     A            DFAFFL    R        O 10 78REFFLD(AUAFFL)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP426C1                   SFLCTL(HP426S1)
     A*%%TS  SD  19950303  092517  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP426'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 29'WORK WITH USERS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'3=Copy Authorized Functions'
     A                                  4 41'4=Delete'
     A                                  4 51'6=Print Authorized Functions'
     A                                  5 12'7=Maintain Authorized Functions'
     A                                  5 51'8=View Authorized Functions'
     A                                  6 47'?'
     A                                      DSPATR(HI)
     A                                  6 53'?'
     A                                      DSPATR(HI)
     A                                  6 59'?'
     A                                      DSPATR(HI)
     A                                  6 67'?'
     A                                      DSPATR(HI)
     A                                  6 72'?'
     A                                      DSPATR(HI)
     A                                  6 78'?'
     A                                      DSPATR(HI)
     A                                  7 45'Secur'
     A                                      DSPATR(HI)
     A                                  7 52'Rstr'
     A                                      DSPATR(HI)
     A                                  7 57'Modify'
     A                                      DSPATR(HI)
     A                                  7 65'Restricted'
     A                                      DSPATR(HI)
     A                                  7 77'Auth'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'User'
     A                                      DSPATR(HI)
     A                                  8 18'Name'
     A                                      DSPATR(HI)
     A                                  8 45'Loc'
     A                                      DSPATR(HI)
     A                                  8 52'User'
     A                                      DSPATR(HI)
     A                                  8 58'Data'
     A                                      DSPATR(HI)
     A                                  8 65'Maint'
     A                                      DSPATR(HI)
     A                                  8 71'View'
     A                                      DSPATR(HI)
     A                                  8 77'Func'
     A                                      DSPATR(HI)
     A            C1USCD    R        B  9  7REFFLD(AUUSCD)
     A                                      DSPATR(HI)
     A            C1SLCD    R        B  9 45REFFLD(AUSLCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1RUFL    R        B  9 53REFFLD(AURUFL)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1MDFL    R        B  9 60REFFLD(AUMDFL)
     A                                      DSPATR(HI)
     A  58                                  DSPATR(PC)
     A            C1RMFL    R        B  9 67REFFLD(AURMFL)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1RVFL    R        B  9 72REFFLD(AURVFL)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A            C1AFFL    R        B  9 78REFFLD(AUAFFL)
     A                                      DSPATR(HI)
     A  58                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP426K1
     A*%%TS  SD  19950216  111320  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F17=Print Options'
     A                                 23 57'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP426ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP426EC                   SFLCTL(HP426ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
