     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *****************  RPG PROGRAM HEADING  ************************
      *                                                              *
      * PROGRAM NUMBER..... - VENDRETFW                              *
      *          DATE....... -  3/19/2020                            *
      *          AUTHOR..... - Brad Baden   SDN465   TFS# 16169      *
      *          DESCRIPTION - THIS PROGRAM IS CALLED FROM THE HPE   *
      *                        (HOG PROCUREMENT) SYSTEM.  IT IS      *
      *                        USED TO RETRIEVE THE NAME AND ADDRESS *
      *                        FOR THE VENDOR NUMBER THAT IS PASSED  *
      *                        IN.  THE CITY, STATE AND ZIP WILL BE  *
      *                        FORMATED ON ONE LINE INSTEAD OF       *
      *                        SEPARATE LINES.                       *
      ****************************************************************
      *  REVISIONS                                                   *
      ****************************************************************
      *                                                              *
      *  REV # - 00     DATE - MM/DD/YY     PROGRAMMER - XXX         *
      *                                                              *
      *  CHANGES MADE -        10/05/95     BARB GUTIERREZ           *
      *                        ADDED COUNTRY CODE                    *
      *                                                              *
      *  CHANGES MADE -        05/09/96     JONI MILLER              *
      *                        JDE RELEASE A7.1 CHANGES              *
      *                        F0101LA IS SPLIT OUT INTO 4 ADDL FILES*
      *                                                              *
      *  3/19/2020 JBB SDN465 - JDE World to JDE Enterprise 1        *
      *                P16169   Copy source from VENDRETF in library *
      *                         JFOODCUST to library E1IDEVGEN and   *
      *                         rename VENDRETFW.                    *
      *                                                              *
      ****************************************************************
      /EJECT
      ****************************************************************
      *   F :             FILE SPECIFICATIONS                        *
      ****************************************************************
      /SPACE 2
     FF0101LA   IF   E           K DISK
      * JDE ADDRESS BOOK  (KEY: ABAN8)
     FF0116     IF   E           K DISK
      * JDE ADDRESS BOOK - ADDRESSES (KEY: ALAN8 ALEFTB)
      ****************************************************************
      *   C :             CALC SPECIFICATIONS                        *
      ****************************************************************
      *
      *   CHAIN TO ADDRESS BOOK CONTROL FILE
      *
     C     NUM           CHAIN     I0101                              50
     C     *IN50         IFEQ      *OFF                                         50 = OFF
     C                   MOVE      *BLANKS       WKRETN            7
     C*
     C                   MOVE      ABALPH        WKNAME           40
      *
      *   CHAIN TO ADDRESS BOOK ADDRESSES FILE
      *
     C     NUM           CHAIN     I0116                              51
     C     *IN51         IFEQ      *OFF                                         51 = OFF
     C*
     C*    CONCATENATE CITY AND STATE WITH A COMMA (,) IN BETWEEN
     C*
     C                   MOVEL(P)  ALCTY1        WKCS             40            WKCITY
     C     WKCS          CAT(P)    ALADDS:1      WKCS                           CITY,STATE
     C     WKCS          CAT(P)    ALADDZ:1      WKCSZ            40            CTY,STATE,ZIP
     C*
     C                   MOVE      ALADD1        WKADR1           40
     C                   MOVE      ALADD2        WKADR2           40
     C                   MOVE      ALADD3        WKADR3           40
     C                   MOVE      ALADD4        WKADR4           40
     C                   MOVE      ALCTR         WKCTR             3
     C*
     C                   ENDIF                                                  51 = OFF
     C*
     C*  IF THE VENDOR PASSED IN DOES NOT EXIST IN JDE, MOVE ERR IN
     C*  RETURN FIELD AND BLANKS OUT PARMS.
     C*
     C                   ELSE                                                   50 = ON
     C                   MOVEL     'ERR'         WKRETN
     C                   MOVE      *BLANKS       WKNAME
     C                   MOVE      *BLANKS       WKADR1
     C                   MOVE      *BLANKS       WKADR2
     C                   MOVE      *BLANKS       WKADR3
     C                   MOVE      *BLANKS       WKADR4
     C                   MOVE      *BLANKS       WKCTR
     C                   ENDIF                                                  50 = OFF
      *
      *  SETUP PARM FIELDS TO PASS BACK TO CALLING PROGRAM.
      *
     C                   MOVEL     WKRETN        RETRN
     C                   MOVEL     WKADR1        ADDR1
     C                   MOVEL     WKADR2        ADDR2
     C                   MOVEL     WKADR3        ADDR3
     C                   MOVEL     WKADR4        ADDR4
     C                   MOVEL(P)  WKNAME        NAME
     C                   MOVE      *BLANKS       CNTRY
      *
     C                   MOVE      *ON           *INLR
      *
      *--------------------------------------------------------------*
      *   *INZSR    INITIALIZATION ROUTINE                           *
      *--------------------------------------------------------------*
      *
     C     *ENTRY        PLIST
     C                   PARM                    RETRN             7
     C                   PARM                    NUM               8 0
     C                   PARM                    NAME             40
     C                   PARM                    ADDR1            40
     C                   PARM                    ADDR2            40
     C                   PARM                    ADDR3            40
     C                   PARM                    ADDR4            40
     C                   PARM                    CNTRY             3
      *
     C                   MOVEL     ','           COMMA             1
      *
