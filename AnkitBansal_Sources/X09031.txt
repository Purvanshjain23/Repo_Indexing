     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
     H/TITLE X09031 - Compute period, fiscal year, G/L date
     H* ----------------------------------------------------------------
     H*
     H*    Copyright (c) 1990
     H*    J. D. Edwards & Company
     H*
     H*        This unpublished material is proprietary to
     H*        J. D. Edwards & Company.  All rights reserved.
     H*        The methods and techniques described herein are
     H*        considered trade secrets and/or confidential.
     H*        Reproduction or distribution, in whole or in part,
     H*        is forbidden except by express written permission
     H*        of J. D. Edwards & Company.
     H*
     H* ----------------------------------------------------------------
     H*                                                                -
     F*
     F*    PROGRAM REVISION LOG
     F*    --------------------
     F*
     F*          Date     Programmer     Nature of Revision
     F*        --------   ----------  ------------------------------------
     F*        07/01/91   FURNAS      SAR # 00349093
     F*        03/24/92   FURNAS      SAR # 00345033
     F*        05/18/92   FURNAS      SAR # 00477853
     F*        07/29/92   FURNAS      SAR # 00506271
     F*        08/04/92   DML         SAR # 00501382
     F*        10/29/92   BUHRER      SAR # 00363522
     F*        03/01/93   BUHRER      SAR # 00583322
     F*        04/16/93   BUSS        SAR # 00595542
     F*        08/05/93   BUHRER      SAR # 00644359
     F*        08/31/93   SW930339    SAR # 00651735
     F*        05/24/95   LB266041    SAR # 00962681
     F*        09/01/95   HP4890303   SAR # 00997171
     F*        03/15/96   HP4890303   SAR # 01132996
     F*        04/30/96   HP4890303   SAR # 01203336
     F*        10/17/96   LB266041    SAR # 01348691
     F*        02/18/97   MAKOOI      SAR # 01458283
     F*        03/12/99   LH5865724   SAR # 02918843
     F*        11/01/99   JP6382337   SAR #  3471254
     F*        04/03/20   BRAD BADEN  SDN465   TFS# 16169
     F*                   Source copied to E1 model and compiled.
     F*
     F*****************************************************************
     F*                                                                     ****
     F*     ---------------------------------------------------------
     F*     X09031 - Compute period number, fiscal year or
     F*              G/L date
     F*            - Edit dates for PYEB,PBCO, PACO, & WACO
     F*     ---------------------------------------------------------
     F*
     F*    PURPOSE
     F*    -------
     F*
     F*        To provide a program common to all programs which
     F*        will calculate period number, fiscal year, or G/L date;
     F*        or which will edit a transaction date for prior year,
     F*        current year, current period, posted after cutoff and
     F*        posted way after cutoff.
     F*
     F*    REMARKS
     F*    -------
     F*
     F*        This program reads the Company Constants File
     F*        (F0010), for the company you are working with, and
     F*        loads the array "@CD" with date pattern, current
     F*        begin fiscal year, current period number, and last
     F*        and next month closed dates with company as the
     F*        index, (element 1000 for company '000'), if not
     F*        already loaded for this company.
     F*
     F*        It also reads the Fiscal Date Pattern File (F0008),
     F*        for the company you are working with's date pattern/
     F*        fiscal year, and loads the array "@PD" with prior,
     F*        current, and next future year, begin fiscal year
     F*        and ending periods if they have not already been
     F*        loaded for this date pattern/fiscal year.
     F*
     F*        The default company's (company 000) data will be used
     F*        if the individual company is not set up. If all three
     F*        fiscal years are not set up, those elements will be
     F*        loaded with zeros.
     F*
     F*        If a '1' is sent to this subroutine in the field '#CALC',
     F*        period number and fiscal year will be computed and the
     F*        company number and G/L date are required.
     F*
     F*        If a '2' is sent to this subroutine in the field '#CALC',
     F*        period number and fiscal year will be computed and the
     F*        G/L date will be edited. The company number and G/L date
     F*        are required.
     F*
     F*        If a '3' is sent to this subroutine in the field '#CALC',
     F*        G/L date will be computed and the company number, period
     F*        number, and fiscal year are required.
     F*
     F*    Input fields:
     F*
     F*        #CALC    Indicator of       1
     F*                 computations
     F*                 to be done
     F*        #CO      Company Number     3
     F*        #DG      G/L Date           6.0   MM/DD/YY Format
     F*        #PN      Period Number      2.0
     F*        #FY      Fiscal Year        2.0
     F*        #CTY     Century            2.0
     F*        #JFL     Date Format Flag   1
     F*
     F*    Output fields:
     F*
     F*        #DG      G/L Date           6.0
     F*        #PN      Period Number      2.0
     F*        #FY      Fiscal Year        2.0
     F*        #CTY     Century            2.0
     F*        #EDT     Result of Edit     1
     F*             1 = #PYEB, prior year
     F*             2 = #PBCO, current year but prior to current
     F*                        period
     F*             3 = #PACO, posted after next period
     F*             4 = #WACO, date is 2 or more years in the future
     F*             5 = trying to post to date pattern/fiscal year
     F*                 not set up
     F*        #BEG     Beginning date     6.0
     F*        #BGCT    Beginning century  2.0
     F*        @DT      Fiscal dates       6.0 (14 elements)
     F*        @CT      Corresponding Cty  2.0 (14 elements)
     F*        #JFLG    Date Format - Input/Output Dates Only
     F*                 '1' - indicates Julian format is being used
     F*                 ' ' - indicates Gregarian format is being used
     F*
     F*        NOTE:    Reference - PASSED PARAMETER DATA STRUCTURE
     F*                             in the S999 subroutine for more
     F*                             information on parameters
     F*
     F*****************************************************************
     F*
     F*    Fiscal date patterns
     F*
     FF0008     IF   E           K DISK
     FF0010     IF   E           K DISK
     D*****************************************************************
     D*    PROGRAM TABLES AND ARRAYS
     D*    -------------------------
     D*
     D @PT             S              9    DIM(45)                              Date DTPN/DTFY
     D @PD             S            120    DIM(45)                              Dates per 1-14   FY
     D @PN             S            120    DIM(45)                              Dates per 1-14   FY
     D @PP             S            120    DIM(45)                              Dates per 1-14   FY
     D*
     D @D              S              2  0 DIM(12) CTDATA PERRCD(12)            Month/Days
     D*
     D**************************************************************
     D*****************************************************************
     D*
     D*    PROGRAM INPUT SPECIFICATIONS AND DATA STRUCTURES
     D*    ------------------------------------------------
     D*-----------------------------------------------------------------
     D*
     D*    Data Structure to re-define arrays
     D*
     D DSSAVE          DS
     D  $$DTPN                 1      1
     D  $$DF                   2      9  0
     D  $$DFR                 10     17  0
     D  $$DFP                 18     25  0
     D*
     D  $$PNC                 26     27  0
     D  $$PNCR                28     29  0
     D  $$PNCP                30     31  0
     D*
     D  $$PRV                 32     39  0
     D  $$PRVR                40     47  0
     D  $$PRVP                48     55  0
     D*
     D  $$NXT                 56     63  0
     D  $$NXTR                64     71  0
     D  $$NXTP                72     79  0
     D*
     D  $$GL                  80     82  0
     D  $$AR                  83     85  0
     D  $$AP                  86     88  0
     D*-----------------------------------------------------------------
     D*
     D*    Data Structure to re-define dates from F0008
     D*
     D DS@CD           DS
     D  @CD                    1    120  0 ASCEND
     D                                     DIM(15)                              Dates per 1-14   FY
     D  $D01#                  1      2  0
     D  CDD01J                 3      8  0
     D  $D02#                  9     10  0
     D  CDD02J                11     16  0
     D  $D03#                 17     18  0
     D  CDD03J                19     24  0
     D  $D04#                 25     26  0
     D  CDD04J                27     32  0
     D  $D05#                 33     34  0
     D  CDD05J                35     40  0
     D  $D06#                 41     42  0
     D  CDD06J                43     48  0
     D  $D07#                 49     50  0
     D  CDD07J                51     56  0
     D  $D08#                 57     58  0
     D  CDD08J                59     64  0
     D  $D09#                 65     66  0
     D  CDD09J                67     72  0
     D  $D10#                 73     74  0
     D  CDD10J                75     80  0
     D  $D11#                 81     82  0
     D  CDD11J                83     88  0
     D  $D12#                 89     90  0
     D  CDD12J                91     96  0
     D  $D13#                 97     98  0
     D  CDD13J                99    104  0
     D  $D14#                105    106  0
     D  CDD14J               107    112  0
     D*
     D*    Consolidate beginning fiscal date (G/L)
     D*
     D  $CD#                 113    114  0
     D  CENFY                115    117  0
     D  CDDFYJ               115    120  0
     D  CDDFY                113    120  0
     D*-----------------------------------------------------------------
     D*
     D PS@DT           DS            84
     D  @DT                    1     84  0
     D                                     DIM(14)                              Dates per 1-14   FY
     D*
     D PS@CT           DS            28
     D  @CT                    1     28  0
     D                                     DIM(14)                              Century for @DT  FY
     D*-----------------------------------------------------------------
     D*    Work date
     D*
     D                 DS
     D  $DATEX                 1      8  0
     D  $DATE                  3      8  0
     D  $DATEC                 1      2  0
     D  $DATE1                 3      4  0
     D  $DATE2                 5      6  0
     D  $DATE3                 7      8  0
     D*-----------------------------------------------------------------
     D*
     D*    G/L date in *YMD format.
     D*
     D                 DS
     D  $XXYMD                 1      8  0
     D  $XXC                   1      2  0
     D  $XXY                   3      4  0
     D  $XXM                   5      6  0
     D  $XXD                   7      8  0
     D*-----------------------------------------------------------------
     D*
     D*    Data Structure for using input servers
     D*
     D*S0010    E DSF0010
     D*-----------------------------------------------------------------
     D*
     D*    Server parameter data structures.
     D*
     D*/COPY JDECPY,I00PS@@
     D**************************************************************
     D*  This data structure constitues the complete parameter list
     D*  for each of the data file server functions used by the
     D*  Data Dictionary Server.
     D*****************************************************************
     D*    PROGRAM INPUT SPECIFICATIONS AND DATA STRUCTURES
     D*    ------------------------------------------------
     D*
     D PS@@            DS
     D*
     D*    Data record key value.
     D  KY@@                   1    100
     D*    Data record key value - various sizes
     D  KY@@01                 1      1
     D  KY@@02                 1      2
     D  KY@@03                 1      3
     D  KY@@04                 1      4
     D  KY@@05                 1      5
     D  KY@@06                 1      6
     D  KY@@07                 1      7
     D  KY@@08                 1      8
     D  KY@@09                 1      9
     D  KY@@10                 1     10
     D  KY@@11                 1     11
     D  KY@@12                 1     12
     D  KY@@13                 1     13
     D  KY@@14                 1     14
     D  KY@@15                 1     15
     D  KY@@16                 1     16
     D  KY@@17                 1     17
     D  KY@@18                 1     18
     D  KY@@19                 1     19
     D  KY@@20                 1     20
     D  KY@@21                 1     21
     D  KY@@22                 1     22
     D  KY@@23                 1     23
     D  KY@@24                 1     24
     D  KY@@25                 1     25
     D  KY@@26                 1     26
     D  KY@@27                 1     27
     D  KY@@28                 1     28
     D  KY@@29                 1     29
     D  KY@@30                 1     30
     D  KY@@31                 1     31
     D  KY@@32                 1     32
     D  KY@@33                 1     33
     D  KY@@34                 1     34
     D  KY@@35                 1     35
     D  KY@@36                 1     36
     D  KY@@37                 1     37
     D  KY@@38                 1     38
     D  KY@@39                 1     39
     D  KY@@40                 1     40
     D  KY@@41                 1     41
     D  KY@@42                 1     42
     D  KY@@43                 1     43
     D  KY@@44                 1     44
     D  KY@@45                 1     45
     D  KY@@46                 1     46
     D  KY@@47                 1     47
     D  KY@@48                 1     48
     D  KY@@49                 1     49
     D  KY@@50                 1     50
     D  KY@@51                 1     51
     D  KY@@52                 1     52
     D  KY@@53                 1     53
     D  KY@@54                 1     54
     D  KY@@55                 1     55
     D  KY@@56                 1     56
     D  KY@@57                 1     57
     D  KY@@58                 1     58
     D  KY@@59                 1     59
     D  KY@@60                 1     60
     D  KY@@61                 1     61
     D  KY@@62                 1     62
     D  KY@@63                 1     63
     D  KY@@64                 1     64
     D  KY@@65                 1     65
     D  KY@@66                 1     66
     D  KY@@67                 1     67
     D  KY@@68                 1     68
     D  KY@@69                 1     69
     D  KY@@70                 1     70
     D  KY@@71                 1     71
     D  KY@@72                 1     72
     D  KY@@73                 1     73
     D  KY@@74                 1     74
     D  KY@@75                 1     75
     D  KY@@76                 1     76
     D  KY@@77                 1     77
     D  KY@@78                 1     78
     D  KY@@79                 1     79
     D  KY@@80                 1     80
     D  KY@@81                 1     81
     D  KY@@82                 1     82
     D  KY@@83                 1     83
     D  KY@@84                 1     84
     D  KY@@85                 1     85
     D  KY@@86                 1     86
     D  KY@@87                 1     87
     D  KY@@88                 1     88
     D  KY@@89                 1     89
     D  KY@@90                 1     90
     D  KY@@91                 1     91
     D  KY@@92                 1     92
     D  KY@@93                 1     93
     D  KY@@94                 1     94
     D  KY@@95                 1     95
     D  KY@@96                 1     96
     D  KY@@97                 1     97
     D  KY@@98                 1     98
     D  KY@@99                 1     99
     D  KY@@00                 1    100
     D*
     D*    Return code (N=Record not found).
     D  RT@@                 101    101
     D*
     D*    Flash message text return.
     D  FLSH                 102    141
     D*
     D*    Number of composite key values.
     D  CKYS                 142    143  0
     D*
     D*    Returned descriptions 1 thru 6.
     D  D1@@                 144    183
     D  D2@@                 184    223
     D  D3@@                 224    263
     D  D4@@                 264    303
     D  D5@@                 304    343
     D  D6@@                 344    383
     D  D7@@                 384    423
     D  D8@@                 424    463
     D  D9@@                 464    503
     D  D0@@                 504    543
     D*----------------------------------------------------------------
     D*    Number of Returned descriptions 1 thru 10.
     D  DX@@                 544    545  0
     D*----------------------------------------------------------------
     D*
     D*    Data Structure to Define Indexes in Common Subroutines
     D*
     D*/COPY JDECPY,I00DSINX
     D*                                                                    00001
     D*    This data strucure provides the field definitions of all
     D*    possible array index field names useable by all common
     D*    subroutines.
     D*
     D DSINX           DS                  INZ                                   00001
     D*
     D*    Indexes of 1 digit 0 decimals
     D*
     D  #A                     1      1  0
     D  #B                     2      2  0
     D  #C                     3      3  0
     D  #D                     4      4  0
     D  #E                     5      5  0
     D  #F                     6      6  0
     D*
     D*    Indexes of 2 digits 0 decimals
     D*
     D  #G                     7      8  0
     D  #H                     9     10  0
     D  #I                    11     12  0
     D  #J                    13     14  0
     D  #K                    15     16  0
     D  #L                    17     18  0
     D*
     D*    Indexes of 3 digits 0 decimals
     D*
     D  #M                    19     21  0
     D  #N                    22     24  0
     D  #O                    25     27  0
     D  #P                    28     30  0
     D  #Q                    31     33  0
     D  #R                    34     36  0
     D*
     D*    Indexes of 4 digits 0 decimals
     D*
     D  #S                    37     40  0
     D  #T                    41     44  0
     D  #U                    45     48  0
     D  #V                    49     52  0
     D  #W                    53     56  0
     D  #X                    57     60  0
     D  #Y                    61     64  0
     D  #Z                    65     68  0
     D*/COPY JDECPY,I00DSPROG
     D*****************************************************************    00003
     D*
     D*    PROGRAM STATUS DATA STRUCTURE
     D*    -----------------------------
     D*
     D*    Portions of this data structure are loaded at the time the
     D*    program is loaded.  Other portions of this data structure
     D*    are loaded as you perform I/O.
     D*
     D*    PURPOSE
     D*    -------
     D*    This common subroutine is set up to be used with C0000
     D*    (Cost Center Security) common subroutine and C0001(Edit
     D*    Action Code) common subroutine.  Those two subroutines
     D*    will retrieve ##USER for the user name.
     D*
     D*    No program calcs are done in this subroutine.
     D*
     D ##PSDS         SDS
     D*
     D*          Program Name
     D  ##PROG                 1     10
     D*          Status Code(09999=I/O Error)
     D  ##STAT                11     15  0
     D*          Previous Status code
     D  ##PSTA                16     20  0
     D*          RPG Source Statement Sequence Number
     D  ##SEQN                21     28
     D*          RPG Routine in Which Exception/Error Occured
     D  ##ROUT                29     36
     D*          Number of Parameters Passed to This Program
     D  ##PARM                37     39  0
     D*          Exception Type(MCH=Machine, CPF=CPF)
     D  ##ETYP                40     42
     D*          Exception Message Number
     D  ##ENBR                43     46
     D*          Machine Instruction/Object Definition Template Number
     D  ##MINO                47     50
     D*          Work Area for Messages
     D  ##MWRK                51     80
     D*          Name of Library in Which Program is Located
     D  ##PLIB                81     90
     D*          Retrieved Exdeption Data.  CPF Messages.
     D  ##MSG                 91    170
     D*          Identification of Exception That Caused RPG9001
     D  ##9001               171    174
     D*          Unused
     D  ##FLR1               175    200
     D*          Name of File for Last I/O(Only Updated if Error)
     D  ##LFIL               201    208
     D*          Status Info on Last File Used(Only on Error)
     D  ##LFST               209    243
     D*          Status Code on Last File Used(Only on Error)
     D  ##LFS5               209    213
     D*          Job Name
     D  ##JOBN               244    253
     D*          User Name From User Profile
     D  ##USER               254    263
     D*          Job Number
     D  ##JOB#               264    269  0
     D*          Date Job Entered the System(MMDDYY)
     D  ##JDT                270    275  0
     D*          Date of Program Execution(MMDDYY)
     D  ##EDT                276    281  0
     D*          Time of Program Execttion(HHMMSS)
     D  ##ETM                282    287  0
     D*          Date Program Was Compiled
     D  ##CDT                288    293  0
     D*          Time Program Was Compiled
     D  ##CTM                294    299  0
     D*          Level of the Compiler
     D  ##LVL                300    303
     D*          Source File Name
     D  ##SRCN               304    313
     D*          Source Library Name
     D  ##SRCL               314    323
     D*          Source File Member Name
     D  ##SRCM               324    333
     D*          Unused
     D  ##FLR2               334    429
     D*/COPY JDECPY,I9800E
     D*
     D*    Data Dictionary Edit Structure
     D*
     D I9800E          DS
     D*
     D*    Data Item
     D  FRDTAI                 1     10
     D*    Data Item Type
     D  FRDTAT                11     11
     D*    Edit Code
     D  FREC                  12     12
     D*    Data Item Size
     D  FRDTAS                13     17  0
     D*    Data File Decimals
     D  FRDTAD                18     19  0
     D*    Install System Code
     D  FRSY                  20     23
     D*    Row Description
     D  FRDSCR                24     63
     D*    User Defined Codes
     D  FRRT                  64     65
     D*    Default Value for Entry
     D  FRDVAL                66    105
     D*    Allowed Values if not Desc. Title
     D  FRVAL                106    145
     D*    Lower Allowed Value
     D  FRLVAL               146    185
     D*    Upper Allowed Value
     D  FRUVAL               186    225
     D*    Data Display Decimals
     D  FRCDEC               226    226
     D*    Next Numbering Index Number
     D  FRNNIX               227    228  0
     D*    Edit Word
     D  FREDWR               229    258
     D*    Justify (R or L)
     D  FRLR                 259    259
     D*    Editing Subroutine Name
     D  FREXTN               260    269
     D*    Reporting System Code
     D  FRSYR                270    273
     D*    Data Item Category
     D  FRDSYN               274    283
     D*    Error Flag (1=Error 0=Okay)
     D  FRERR                284    284
     I****************************************************************
     C****************************************************************
     C*                                                                     ****
     C*    MAINLINE PROGRAM
     C*    ----------------
     C*                                                                     ****
     C*    Exit program if no parameters passed                             ****
     C*                                                                     ****
     C     ##PARM        CABEQ     *ZERO         END
     C*                    -----          ---
     C*
     C*    Initial subroutine
     C*
     C                   EXSR      S999
     C*                    ---- ----
     C*
     C*    Processing subroutine
     C*
     C                   EXSR      S001
     C*                    ---- ----
     C*    Return.
     C*
     C     END           TAG
     C*          ---       ---
     C*
     C     EOJ           TAG
     C*          ---       ---
     C*
     C                   RETURN
     C                   SETON                                        RT
     C*
     C*    END MAINLINE PROGRAM
     C*    --------------------
     C*****************************************************************
     C*
     CSR   S001          BEGSR
     C*          ----      -----
     C*-----------------------------------------------------------------
     C*
     C*    Load previous company data if same company
     C*
B1   CSR   $SCO          IFEQ      PSCO
     CSR   $SCO          ANDNE     *BLANKS
DBN  CSR                 MOVE      $SSAVE        DSSAVE
     CSR                 GOTO      T001
     C*                    ---- ----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Save company number
     C*
     CSR                 MOVE      PSCO          $SCO                           data struct
     C*
     C*    Retrieve company record
     C*
     CSR   $SCO          CHAIN     I0010                              83
     C*
     C*    Use default company if company not set up in F0010
     C*
B1   CSR   *IN83         IFEQ      '1'
     CSR   *ZEROS        CHAIN     I0010                              84
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Convert CCDFY from *JUL to *YMD
     C*
     CSR                 MOVE      CCDFYJ        #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $DATE
     CSR                 MOVE      $DATE1        $XXM
     CSR                 MOVE      $DATE2        $XXD
     CSR                 MOVE      $DATE3        $XXY
     CSR                 MOVE      #CTRY         $XXC
     CSR                 MOVE      #CTRY         $CD#
     CSR                 MOVE      $XXYMD        $BEGGL
     C*
     C*
B1   CSR   CCDFYJ        IFNE      CCARFJ
     CSR                 MOVE      CCARFJ        #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY             2
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $DATE
     CSR                 MOVE      $DATE1        $XXM
     CSR                 MOVE      $DATE2        $XXD
     CSR                 MOVE      $DATE3        $XXY
     CSR                 MOVE      #CTRY         $XXC
     CSR                 MOVE      $XXYMD        $BEGAR
X1   CSR                 ELSE
     CSR                 MOVE      $BEGGL        $BEGAR
E1   CSR                 ENDIF
     C*
B1   CSR   CCDFYJ        IFNE      CCAPFJ
     CSR                 MOVE      CCAPFJ        #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY             2
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $DATE
     CSR                 MOVE      $DATE1        $XXM
     CSR                 MOVE      $DATE2        $XXD
     CSR                 MOVE      $DATE3        $XXY
     CSR                 MOVE      #CTRY         $XXC
     CSR                 MOVE      $XXYMD        $BEGAP
X1   CSR                 ELSE
     CSR                 MOVE      $BEGGL        $BEGAP
E1   CSR                 ENDIF
     C*
     C*    Load company data structure
     C*
     CSR                 CLEAR                   DSSAVE
     C*
     CSR                 MOVE      CCDTPN        $$DTPN                         date pattern
     CSR                 MOVE      $BEGGL        $$DF                           cur beg fis yr
     CSR                 MOVE      CCPNC         $$PNC                          cur period #
     C*
     CSR                 MOVE      $BEGAR        $$DFR                          a/r beg fis yr
     CSR                 MOVE      CCARPN        $$PNCR                         a/r period #
     C*
     CSR                 MOVE      $BEGAP        $$DFP                          a/p beg fis yr
     CSR                 MOVE      CCAPPN        $$PNCP                         a/p period #
     C*
     C*    Default A/R and A/P periods
     C*
B1   CSR   $$DFR         IFLT      $$DF
     CSR   $$DFR         OREQ      $$DF
     CSR   $$PNCR        ANDLT     $$PNC
     CSR                 MOVE      $$DF          $$DFR
     CSR                 MOVE      $$PNC         $$PNCR
E1   CSR                 ENDIF
     C*
B1   CSR   $$DFP         IFLT      $$DF
     CSR   $$DFP         OREQ      $$DF
     CSR   $$PNCP        ANDLT     $$PNC
     CSR                 MOVE      $$DF          $$DFP
     CSR                 MOVE      $$PNC         $$PNCP
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Retrieve date pattern for this year, previous, and next
     C*
     CSR                 MOVE      $$DF          $BEG
     CSR                 EXSR      S008
     C*                    ---- ----
     CSR                 Z-ADD     $X            $$GL                           array position
     C*-----------------------------------------------------------------
     C*
     C*    Retrieve A/R date pattern for this year, previous, and next
     C*
B1   CSR   $$DF          IFEQ      $$DFR
     CSR                 Z-ADD     $$GL          $$AR                           array position
X1   CSR                 ELSE
     CSR                 MOVE      $$DFR         $BEG
     CSR                 EXSR      S008
     C*                    ---- ----
     CSR                 Z-ADD     $X            $$AR                           array position
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Retrieve A/P date pattern for this year, previous, and next
     C*
B1   CSR   $$DF          IFEQ      $$DFP
     CSR                 Z-ADD     $$GL          $$AP                           array position
X1   CSR                 ELSE
     CSR                 MOVE      $$DFP         $BEG
     CSR                 EXSR      S008
     C*                    ---- ----
     CSR                 Z-ADD     $X            $$AP                           array position
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Determine last period closed date
     C*
     CSR                 EXSR      S010
     C*                    ---- ----
     C*-----------------------------------------------------------------
     C*
     C*    Determine next period closed date
     C*
     CSR                 EXSR      S011
     C*                    ---- ----
     C*-----------------------------------------------------------------
     C*
     C*    Save current date pattern/beginning date values
     C*
DBN  CSR                 MOVE      DSSAVE        $SSAVE                         data struct
     C*-----------------------------------------------------------------
     C*
     C*    Begin Computations
     C*
     CSR   T001          TAG
     C*          ----      ---
     C*-----------------------------------------------------------------
     C*
     C*    Load current period
     C*
     CSR                 Z-ADD     $$PNC         $PER
B1   CSR   $TYPE         IFEQ      '1'
     CSR                 Z-ADD     $$PNCR        $PER                           A/R
E1   CSR                 ENDIF
B1   CSR   $TYPE         IFEQ      '2'
     CSR                 Z-ADD     $$PNCP        $PER                           A/P
E1   CSR                 ENDIF
     C*
     C*    Load current begin fiscal year
     C*
     CSR                 Z-ADD     $$DF          $BEG
B1   CSR   $TYPE         IFEQ      '1'
     CSR                 Z-ADD     $$DFR         $BEG
E1   CSR                 ENDIF                                                  A/R
B1   CSR   $TYPE         IFEQ      '2'
     CSR                 Z-ADD     $$DFP         $BEG                           A/P
E1   CSR                 ENDIF
     C*
     C*    Load current array position
     C*
     CSR                 Z-ADD     $$GL          $@
B1   CSR   $TYPE         IFEQ      '1'
     CSR                 Z-ADD     $$AR          $@
E1   CSR                 ENDIF                                                  A/R
B1   CSR   $TYPE         IFEQ      '2'
     CSR                 Z-ADD     $$AP          $@                             A/P
E1   CSR                 ENDIF
     C*
     C*    Load next/prev dates
     C*
     CSR                 Z-ADD     $$PRV         $@PRV
     CSR                 Z-ADD     $$NXT         $@NXT
B1   CSR   $TYPE         IFEQ      '1'
     CSR                 Z-ADD     $$PRVR        $@PRV
     CSR                 Z-ADD     $$NXTR        $@NXT
E1   CSR                 ENDIF                                                  A/R
B1   CSR   $TYPE         IFEQ      '2'
     CSR                 Z-ADD     $$PRVP        $@PRV                          A/P
     CSR                 Z-ADD     $$NXTP        $@NXT
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Compute period number and fiscal year
     C*
B1   CSR   PSCALC        IFEQ      '1'
     CSR   PSCALC        OREQ      '2'
     CSR                 EXSR      S002
     C*                    ---- ----
     CSR   PSERR         CABNE     *BLANKS       E001
     C*                    -----          ----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Edit G/L date for PYEB, PBCO, PACO, & WACO
     C*
B1   CSR   PSCALC        IFEQ      '2'
     CSR                 EXSR      S003
     C*                    ---- ----
     CSR   PSERR         CABNE     *BLANKS       E001
     C*                    -----          ----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Compute G/L date
     C*
B1   CSR   PSCALC        IFEQ      '3'
     CSR                 EXSR      S004
     C*                    ---- ----
     CSR   PSERR         CABNE     *BLANKS       E001
     C*                    -----          ----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E001          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S002   - Calculate Period and Fiscal Year
     C*    ----------------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Load fiscal date patterns.
     C*                 2.  Calculate period and fiscal year
     C*
     C*
     CSR   S002          BEGSR
     C*          ----      -----
     C*
     C*    Clear output parameters
     C*
     CSR                 Z-ADD     *ZERO         PSPN                           period
     CSR                 Z-ADD     *ZERO         PSFY                           fiscal year
     CSR                 Z-ADD     *ZERO         PSCTY                          century
     C*-----------------------------------------------------------------
     C*
     C*    Load date patterns for current fiscal date
     C*
DBN  CSR                 MOVEL     @PD($@)       DS@CD
     C*-----------------------------------------------------------------
     C*
     C*    If G/L date is less than current fiscal start date,
     C*    it is a prior year
     C*
     CSR                 MOVE      $PSDG         $$PSDG            8 0
     C*
B1   CSR   $$PSDG        IFLT      @CD(15)
     C*
     C*    Error if previous fiscal year not set up
     C*
B2   CSR   @PP($@)       IFEQ      *ZEROS
     CSR                 MOVE      '5'           PSERR                          posting to
     CSR                 GOTO      E002                                         fiscal year
     C*                    ---- ----                       not set up
E2   CSR                 ENDIF
     C*
DBN  CSR                 MOVEL     @PP($@)       DS@CD
     C*
     C*    Error if date falls after last date of previous year
     C*
B2   CSR   $$PSDG        IFGT      @CD(14)
     CSR                 MOVE      '5'           PSERR                          posting to
     CSR                 GOTO      E002                                         fiscal year
     C*                    ---- ----                       not set up
E2   CSR                 ENDIF
     C*
     C*    Date is more than 1 fiscal year prior
     C*
B2   CSR   $$PSDG        IFLT      @CD(15)
     CSR                 EXSR      S002A
     C*                    ---- -----
     CSR                 GOTO      T002
     C*                    ---- ----
E2   CSR                 ENDIF
X1   CSR                 ELSE                                                   current
     C*
     C*    If date is in current or future fiscal year,
     C*    current fiscal year dates must be set up
     C*
B2   CSR   @PD($@)       IFEQ      *ZEROS
     CSR                 MOVE      '5'           PSERR                          posting to
     CSR                 GOTO      E002                                         fiscal year
     C*                    ---- ----                       not set up
E2   CSR                 ENDIF
     C*
     C*    If G/L date is greater period 14, it is future year
     C*
B2   CSR   $PSDG         IFGT      @CD(14)
     C*
     C*    Load dates for next fiscal year
     C*
DBN  CSR                 MOVEL     @PN($@)       DS@CD
     C*
     C*    Next fiscal year must be set up
     C*
B3   CSR   @PN($@)       IFEQ      *ZEROS
     CSR                 MOVE      '5'           PSERR                          posting to
     CSR                 GOTO      E002                                         fiscal year
     C*                    ---- ----                       not set up
E3   CSR                 ENDIF
     C*
     C*    Error if date prior to beginning date of next fiscal year
     C*
     CSR                 MOVE      $PSDG         $$PSDG            8 0
     C*
B3   CSR   $$PSDG        IFLT      @CD(15)
     CSR                 MOVE      '5'           PSERR                          posting to
     CSR                 GOTO      E002                                         fiscal year
     C*                    ---- ------                     not set up
E3   CSR                 ENDIF
     C*
     C*    Date is more than 1 fiscal year future
     C*
B3   CSR   $PSDG         IFGT      @CD(14)
     CSR                 EXSR      S002A
     C*                    ---- -----
     CSR                 GOTO      T002
     C*                    ---- ----
E3   CSR                 ENDIF
     C*
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Skip out if any errors have occurred
     C*
     CSR   T002          TAG
     C*          ----      ---
     CSR   PSERR         CABNE     *BLANKS       E002
     C*                    -----          ----
     C*-----------------------------------------------------------------
     C*
     C*    Determine which period and year G/L date falls into.
     C*
     CSR                 MOVE      $PSDG         #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*YMD    '    #FFMT             7
     CSR                 MOVEL     '*YMD    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     C*
     CSR                 Z-ADD     1             #X
     CSR                 MOVEL     #CTRY         $PSDG
     CSR   $PSDG         LOOKUP    @CD(#X)                            81  81
B1   CSR   #X            IFEQ      15
     CSR                 Z-ADD     1             #X
E1   CSR                 ENDIF
     C*
     C*    Period Number
     C*
     CSR                 Z-ADD     #X            PSPN
     C*
     C*    Fiscal Year
     C*
B1   CSR   $#LAST        IFEQ      '1'
     CSR                 MOVE      @CD(14)       $DATEX
     CSR                 MOVE      $DATE1        PSFY
     CSR                 MOVE      $DATEC        PSCTY
X1   CSR                 ELSE
     CSR                 MOVE      @CD(01)       $DATEX
     CSR                 MOVEL     $DATE1        PSFY
     CSR                 MOVE      $DATEC        PSCTY
E1   CSR                 ENDIF
     C*
     C*    Beginning Date
     C*
B1   CSR   $PSBEG        IFEQ      '1'
     CSR   $FLAG         ANDNE     '1'
     CSR                 MOVE      @CD(15)       #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*YMD    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $XXYMD
     CSR                 MOVEL     #CTRY         $XXYMD
     CSR                 MOVEL     #CTRY         $CD#
     CSR                 MOVE      $XXM          $DATE1
     CSR                 MOVE      $XXD          $DATE2
     CSR                 MOVE      $XXY          $DATE3
     CSR                 MOVE      $DATE         PSBEG
     CSR                 MOVE      $XXC          PSBGCT
X1   CSR                 ELSE
B2   CSR   $PSBEG        IFEQ      '1'
     CSR                 MOVE      @CD(15)       PSBEG
     CSR                 MOVE      PSBEG         #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*YMD    '    #FFMT             7
     CSR                 MOVEL     '*JUL    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        PSBEG
     CSR                 MOVE      #CTRY         PSBGCT
     CSR                 MOVE      #CTRY         $CD#
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    14 Ending Dates
     C*
B1   CSR   $@DT          IFEQ      '1'
     CSR   $FLAG         ANDNE     '1'
B2   CSR   1             DO        14            #I
     CSR                 MOVE      @CD(#I)       $XXYMD
     CSR                 MOVE      $XXM          $DATE1
     CSR                 MOVE      $XXD          $DATE2
     CSR                 MOVE      $XXY          $DATE3
     CSR                 MOVE      $DATE         @DT(#I)
     CSR                 MOVEL     @CD(#I)       @CT(#I)
E2   CSR                 ENDDO
X1   CSR                 ELSE
B2   CSR   $@DT          IFEQ      '1'
B3   CSR   1             DO        14            #I
     CSR                 MOVE      @CD(#I)       $XXYMD
     CSR                 MOVE      $XXM          $DATE1
     CSR                 MOVE      $XXD          $DATE2
     CSR                 MOVE      $XXY          $DATE3
     CSR                 MOVE      $DATE         #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*MDY    '    #FFMT             7
     CSR                 MOVEL     '*JUL    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        @DT(#I)
     CSR                 MOVE      #CTRY         @CT(#I)
E3   CSR                 ENDDO
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E002          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S002A  - Load fiscal date pattern
     C*    --------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Load fiscal date pattern for date more
     C*                     than 1 fiscal year away.
     C*
     C*      Input:
     C*        $$DTPN   = Date pattern
     C*
     C*      Output:
     C*        DS@CD    = Fiscal dates in *YMD format
     C*
     CSR   S002A         BEGSR
     C*          -----     -----
     C*-----------------------------------------------------------------
     C*
     C*    Retrieve fiscal date pattern for passed date
     C*
     CSR                 MOVE      $$DTPN        CDDTPN
     CSR                 Z-ADD     $PSDG         $DATEX
     CSR                 MOVE      $DATEX        CDDFYJ
     CSR                 MOVE      CDDFYJ        #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*YMD    '    #FFMT             7
     CSR                 MOVEL     '*JUL    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        CDDFYJ
     CSR                 MOVE      #CTRY         $CD#
     CSR                 MOVE      *BLANKS       CDFQ
     CSR   CDKY05        SETLL     I0008                                  81
B1   CSR   *IN81         IFEQ      '1'
     CSR   CDKY05        READE     I0008                                  81
X1   CSR                 ELSE
     CSR   CDKY01        READPE    I0008                                  81
E1   CSR                 ENDIF
     C*
     C*    No date pattern found
     C*
B1   CSR   *IN81         IFEQ      '1'
     CSR                 MOVE      '5'           PSERR
     CSR                 GOTO      E002A
     C*                    ---- -----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Convert dates from *MDY to *YMD format
     C*
     CSR                 EXSR      S009
     C*                    ---- ----
     C*
     C*    Error if date is after the last date in pattern/year
     C*
B1   CSR   $$PSDG        IFGT      @CD(14)
     CSR                 MOVE      '5'           PSERR
     CSR                 GOTO      E002A
     C*                    ---- -----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E002A         ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S003   - Validate G/L date
     C*    -------------------------------------
     C*
     C*    Processing:
     C*                 1.  Load fiscal date patterns.
     C*                 2.  Validate G/L date.
     C*
     C*
     CSR   S003          BEGSR
     C*          ----      -----
     C*-----------------------------------------------------------------
     C*
     C*    Load fiscal date pattern dates for current fiscal year
     C*
DBN  CSR                 MOVEL     @PD($@)       DS@CD
     C*-----------------------------------------------------------------
     C*
     C*    If transaction date less than fiscal start date - PYEB
     C*
     CSR                 MOVE      $PSDG         $$PSDG            8 0
     C*
B1   CSR   $$PSDG        IFLT      @CD(15)
     CSR                 MOVE      '1'           PSERR
     CSR                 GOTO      E003
     C*                    ---- ----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    If transaction date less than or equal to last period closed
     C*    date - PBCO
     C*
     CSR                 MOVEL     #CTRY         $PSDG
B1   CSR   $PSDG         IFLE      $@PRV
     CSR                 MOVE      '2'           PSERR
     CSR                 GOTO      E003
     C*                    ---- ----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    If transaction date greater than one year in the future
     C*    - WACO
     C*
B1   CSR   $@NXT         IFNE      *ZERO
     CSR   $PSDG         ANDGT     @CD(14)
     CSR   $PSDG         ANDGT     $@NXT
     CSR                 MOVE      '4'           PSERR
     CSR                 GOTO      E003
     C*                    ---- ----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    If transaction date greater than the next period closed
     C*    date - PACO
     C*
B1   CSR   $@NXT         IFNE      *ZERO
     CSR   $PSDG         ANDGT     $@NXT
     CSR                 MOVE      '3'           PSERR
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E003          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S004   - Compute G/L date
     C*    ------------------------------------
     C*
     C*    Processing:
     C*                 1.  Find G/L date.
     C*
     C*
     CSR   S004          BEGSR
     C*          ----      -----
     C*
     C*    Clear output parameter
     C*
     CSR                 Z-ADD     *ZERO         PSDG
     C*-----------------------------------------------------------------
     C*
     C*    Find first date pattern beginning with fiscal year
     C*
     CSR                 MOVE      $$DTPN        CDDTPN
     CSR                 Z-ADD     PSFY          CENFY
     CSR                 Z-ADD     *ZEROS        CENFY1            3 0
     CSR                 MOVE      CENFY1        CDDFY
     CSR                 MOVEL     $PSDG         $HOLD             2 0
B1   CSR   $HOLD         IFEQ      20
B2   CSR   PSFY          IFLT      10
     CSR                 MOVEL     10            CENFY
X2   CSR                 ELSE
     CSR                 MOVEL     1             CENFY
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     CSR                 SUB       1             CENFY
     CSR   CDKY02        SETLL     I0008
B1   CSR   *IN81         DOUEQ     '1'
     CSR   CDKY01        READE     I0008                                  81
     C*
B2   CSR   *IN81         IFEQ      '1'
     CSR                 MOVE      '5'           PSERR
     CSR                 GOTO      E004
     C*                    ---- ----
E2   CSR                 ENDIF
     C*
B2   CSR   $#LAST        IFEQ      '1'
     CSR                 MOVE      CDD14J        #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $FY
     CSR                 MOVE      #CTRY         $FY#
X2   CSR                 ELSE
     CSR                 MOVE      CDD01J        #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $FY
     CSR                 MOVE      #CTRY         $FY#
E2   CSR                 ENDIF
     C*
B2   CSR   PSFY          IFEQ      $FY
     CSR   PSCTY         ANDEQ     $FY#
     CSR                 GOTO      T004
     C*                    ---- ----
E2   CSR                 ENDIF
     C*                    -----          ----
E1   CSR                 ENDDO
     C*
     C*    Load output values
     C*
     CSR   T004          TAG
     C*          ----      ---
     C*
     C*    Output G/L date as *MDY
B1   CSR   $FLAG         IFEQ      '1'
     CSR                 MOVE      @CD(PSPN)     #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     C****                 MOVE #CTRY     PSCTY            DEL 3471254
     CSR                 MOVE      @CD(PSPN)     PSDG
X1   CSR                 ELSE
     CSR                 MOVE      @CD(PSPN)     PSDG
     CSR                 MOVE      @CD(PSPN)     #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        PSDG
     C****                 MOVE #CTRY     $#CTRY           DEL 3471254
     C****       PSCTY     IFEQ 19                         DEL 3471254
     C****       $#CTRY    ANDGTPSCTY                      DEL 3471254
     C****       PSFY      ANDGT20                         DEL 3471254
     C****                 GOTO PASS                       DEL 3471254
     C****                 ENDIF                           DEL 3471254
     C****                 MOVE #CTRY     PSCTY            DEL 3471254
E1   CSR                 ENDIF
     C*
     C****       PASS      TAG                             DEL 3471254
     C*
     C*    Beginning Date
     C*
B1   CSR   $PSBEG        IFEQ      '1'
     CSR   $FLAG         ANDNE     '1'
     CSR                 MOVE      @CD(15)       #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*YMD    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #CTRY         $CD#
     CSR                 MOVE      #SIDAT        $XXYMD
     CSR                 MOVE      $XXM          $DATE1
     CSR                 MOVE      $XXD          $DATE2
     CSR                 MOVE      $XXY          $DATE3
     CSR                 MOVE      $DATE         PSBEG
     CSR                 MOVE      $XXC          PSBGCT
     CSR                 MOVE      $XXC          $CD#
X1   CSR                 ELSE
B2   CSR   $PSBEG        IFEQ      '1'
     C*
     C*    Reference SAR#1203336, @CD has incorrect CTRY when current
     C*    and next fiscal date pattern is in different century.
     C*
     CSR                 MOVE      @CD(15)       #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #CTRY         PSBGCT
     CSR                 MOVE      @CD(15)       PSBEG
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    14 Ending Dates
     C*
B2   CSR   $@DT          IFEQ      '1'
B3   CSR   1             DO        14            #I
     CSR                 MOVE      @CD(#I)       @DT(#I)
     CSR                 MOVEL     @CD(#I)       @CT(#I)
E3   CSR                 ENDDO
E2   CSR                 ENDIF
E1   C*R                   ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E004          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S008   - Load fiscal date pattern
     C*    --------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Load fiscal date pattern.
     C*
     C*      Input:
     C*        $$DTPN   = Date pattern
     C*        $BEG     = Beginning fiscal date
     C*
     C*      Output:
     C*        $X       = Date array position for @PT, @PD
     C*
     CSR   S008          BEGSR
     C*          ----      -----
     C*
     C*    Clear output
     C*
     CSR                 Z-ADD     *ZERO         $X
     C*-----------------------------------------------------------------
     C*
     C*    Default from previous
     C*
B1   CSR   $PT           IFGT      *ZERO
     CSR   $8DTPN        ANDEQ     $$DTPN
     CSR   $8BEG         ANDEQ     $BEG
     CSR                 Z-ADD     $8X           $X
     CSR                 GOTO      E008
     C*                    ---- ----
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Load current fiscal pattern
     C*
B1   CSR   $PT           IFGT      *ZERO
     CSR                 Z-ADD     *ZERO         $X
     CSR                 MOVEL     $$DTPN        $PTKEY
     CSR                 MOVE      $BEG          $PTKEY
     CSR                 Z-ADD     1             #X
     CSR   $PTKEY        LOOKUP    @PT(#X)                                81
B2   CSR   *IN81         IFEQ      '1'
     CSR                 Z-ADD     #X            $X
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     C*
     C*    If found, exit routine
     C*
     CSR   $X            CABNE     *ZERO         T008
     C*                    -----          ----
     C*-----------------------------------------------------------------
     C*
     C*    Determine array position of date pattern to be loaded
     C*
B1   CSR   $PT           IFLT      45
     CSR                 ADD       1             $PT
X1   CSR                 ELSE
     CSR                 Z-ADD     1             $PT
E1   CSR                 ENDIF
     C*
     CSR                 Z-ADD     $PT           $X
     CSR                 MOVEL     $PTKEY        @PT($X)                        pattern/date
     C*
     C*    Read fiscal date record
     C*
     C*    Convert $BEG(Beginning Year) from *YMD to *JUL
     C*
     CSR                 MOVE      $BEG          $SVBEG
     CSR                 MOVE      $SVBEG        #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVEL     $SVBEG        #CTRY
     CSR                 MOVEL     '*YMD    '    #FFMT             7
     CSR                 MOVEL     '*JUL    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $BEG
     C*
     CSR                 MOVE      $$DTPN        CDDTPN
     C*R                   MOVE CCDFYJ    CDDFYJ
     CSR                 MOVE      $BEG          CDDFYJ
     CSR                 MOVE      *BLANKS       CDFQ
     C*
     CSR   CDKY05        SETLL     I0008
     CSR   CDKY04        READE     I0008                                  81
B1   CSR   *IN81         IFEQ      '0'
     CSR                 EXSR      S009
     C*                    ---- ----
DBN  CSR                 MOVE      DS@CD         @PD($X)                        dates
X1   CSR                 ELSE
     CSR                 MOVE      *ZEROS        @PD($X)                        dates
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Load previous fiscal date pattern
     C*
     CSR                 MOVE      $$DTPN        CDDTPN
     C*R                   MOVE CCDFYJ    CDDFYJ
     CSR                 MOVE      $BEG          CDDFYJ
     CSR                 MOVE      *BLANKS       CDFQ
     C*
     CSR   CDKY05        SETLL     I0008
     CSR   CDKY01        READPE    I0008                                  81
     C*
B1   CSR   *IN81         IFEQ      '0'
     CSR                 EXSR      S009
     C*                    ---- ----
DBN  CSR                 MOVE      DS@CD         @PP($X)                        dates
X1   CSR                 ELSE
     CSR                 MOVE      *ZEROS        @PP($X)                        dates
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Load next fiscal date pattern
     C*
     CSR                 MOVE      $$DTPN        CDDTPN
     C*R                   MOVE CCDFYJ    CDDFYJ
     CSR                 MOVE      $BEG          CDDFYJ
     CSR                 MOVE      *BLANKS       CDFQ
     C*
     CSR   CDKY05        SETGT     I0008
     CSR   CDKY01        READE     I0008                                  81
     C*
B1   CSR   *IN81         IFEQ      '0'
     CSR                 EXSR      S009
     C*                    ---- ----
DBN  CSR                 MOVE      DS@CD         @PN($X)                        dates
X1   CSR                 ELSE
     CSR                 MOVE      *ZEROS        @PN($X)                        dates
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR                 MOVE      $SVBEG        $BEG
     C*
     C*    Save values
     C*
     CSR   T008          TAG
     C*          ----      ---
     CSR                 MOVE      $$DTPN        $8DTPN
     CSR                 MOVE      $BEG          $8BEG
     CSR                 MOVE      $X            $8X
     C*-----------------------------------------------------------------
     C*
     CSR   E008          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S009   - Convert Fiscal Dates to *YMD
     C*    ------------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Convert DS@CD dates to *YMD.
     C*
     CSR   S009          BEGSR
     C*          ----      -----
     C*-----------------------------------------------------------------
     C*
     C*    Convert periods 1 through 14 (Beginning already *YMD)
     C*
     CSR                 Z-ADD     *ZEROS        #I
B1   CSR   1             DO        15            #I
     CSR                 MOVE      @CD(#I)       #SIDAT
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $DATEX
     CSR                 MOVEL     #CTRY         @CD(#I)
     CSR                 MOVEL     #CTRY         $DATEX
     CSR                 MOVE      $DATE1        $XXM
     CSR                 MOVE      $DATE2        $XXD
     CSR                 MOVE      $DATE3        $XXY
     CSR                 MOVE      $DATEC        $XXC
     C*
     CSR                 MOVE      $XXYMD        @CD(#I)
     C*
E1   CSR                 ENDDO
     C*-----------------------------------------------------------------
     C*
     CSR   E009          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S010   - Determine previous close dates
     C*    --------------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Determine previous close dates.
     C*
     C*      Input:
     C*        $$PNC    = Current period
     C*        $$DF     = Beginning fiscal date
     C*        $$PNCR   = Current period (A/R)
     C*        $$DFR    = Beginning fiscal date (A/R)
     C*        $$PNCP   = Current period (A/P)
     C*        $$DFP    = Beginning fiscal date (A/P)
     C*
     C*      Output:
     C*        $$PRV    = G/L previous date
     C*        $$PRVR   = A/R previous date
     C*        $$PRVP   = A/P previous date
     C*
     CSR   S010          BEGSR
     C*          ----      -----
     C*-----------------------------------------------------------------
     C*
     C*    G/L period and prior close date
     C*
DBN  CSR                 MOVE      @PD($$GL)     DS@CD
     CSR                 MOVE      $$PNC         $PER
     CSR                 MOVE      $$DF          $BEG
     CSR                 EXSR      S010A
     C*                    ---- -----
     CSR                 MOVE      $@PRV         $$PRV
     C*-----------------------------------------------------------------
     C*
     C*    A/R period and prior close date
     C*
B1   CSR   $$PNC         IFEQ      $$PNCR
     CSR   $$DF          ANDEQ     $$DFR
     CSR                 MOVE      $$PRV         $$PRVR
X1   CSR                 ELSE
DBN  CSR                 MOVE      @PD($$AR)     DS@CD
     CSR                 MOVE      $$PNCR        $PER
     CSR                 MOVE      $$DFR         $BEG
     CSR                 EXSR      S010A
     C*                    ---- -----
     CSR                 MOVE      $@PRV         $$PRVR
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    A/P period and prior close date
     C*
B1   CSR   $$PNC         IFEQ      $$PNCP
     CSR   $$DF          ANDEQ     $$DFP
     CSR                 MOVE      $$PRV         $$PRVP
X1   CSR                 ELSE
DBN  CSR                 MOVE      @PD($$AP)     DS@CD
     CSR                 MOVE      $$PNCP        $PER
     CSR                 MOVE      $$DFP         $BEG
     CSR                 EXSR      S010A
     C*                    ---- -----
     CSR                 MOVE      $@PRV         $$PRVP
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E010          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S010A  - Determine previous close dates
     C*    --------------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Determine previous close dates.
     C*
     C*      Input:
     C*        DS@CD    = Current G/L year date patterns
     C*        $PER     = Current period
     C*        $BEG     = Beginning fiscal date
     C*
     C*      Output:
     C*        $@PRV    = previous date
     C*
     C*
     CSR   S010A         BEGSR
     C*          -----     -----
     C*-----------------------------------------------------------------
     C*
     C*    If current period is 1, use 1 day less than fiscal start
     C*
B1   CSR   $PER          IFLE      1
     CSR                 MOVE      $BEG          $XXYMD
     C*
     C*    Subtract one from fiscal start date
     C*
     CSR                 SUB       1             $XXD
     C*
     C*    If day is less than or equal to zero, calculate
     C*
B2   CSR   $XXD          IFLE      *ZERO
     C*
     C*    Use month prior to fiscal start date
     C*
     CSR                 SUB       1             $XXM
     C*
     C*    Use year prior to fiscal start date
     C*
B3   CSR   $XXM          IFLE      *ZERO
     CSR                 SUB       1             CENFY
     CSR                 Z-ADD     12            $XXM
     C*
     C*    Determine prior year
     C*
B4   CSR   $XXY          IFGT      *ZERO
     CSR                 SUB       1             $XXY
     C*
     C*    Determine prior century, if necessary
     C*
X4   CSR                 ELSE
     CSR                 Z-ADD     99            $XXY
     CSR                 SUB       1             $XXC
E4   CSR                 ENDIF
E3   CSR                 ENDIF
     C*
     C*    Load last day of month
     C*
     CSR                 Z-ADD     @D($XXM)      $XXD
B3   CSR   $XXM          IFEQ      02
     CSR   $XXY          DIV       4             $RESLT            2 0
     CSR                 MVR                     $REM              1 0
B4   CSR   $REM          IFEQ      *ZERO
     CSR                 ADD       1             $XXD
E4   CSR                 ENDIF
E3   CSR                 ENDIF
     C*
E2   CSR                 ENDIF
     C*
     C*    Load previous period close date
     C*
     CSR                 MOVEL     $XXYMD        $@PRV                          last period
E1   CSR                 ENDIF
     C*
     C*    Use prior period ending date for last period closed date
     C*
B1   CSR   $PER          IFGT      1
     CSR   $PER          SUB       1             #X
     CSR                 MOVE      @CD(#X)       $@PRV
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E010A         ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S011   - Determine next close dates
     C*    ----------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Determine next close dates.
     C*
     C*      Input:
     C*        $$PNC    = Current period
     C*        $$PNCR   = Current period (A/R)
     C*        $$PNCP   = Current period (A/P)
     C*        $$GL     = array position
     C*        $$AR     = array position
     C*        $$AP     = array position
     C*
     C*      Output:
     C*        $$PRV    = G/L previous date
     C*        $$PRVR   = A/R previous date
     C*        $$PRVP   = A/P previous date
     C*
     CSR   S011          BEGSR
     C*          ----      -----
     C*-----------------------------------------------------------------
     C*
     C*    G/L period and prior close date
     C*
DBN  CSR                 MOVE      @PD($$GL)     DS@CD
     CSR                 Z-ADD     $$GL          $@
     CSR                 MOVE      $$PNC         $PER
     CSR                 EXSR      S011A
     C*                    ---- -----
     CSR                 MOVE      $@NXT         $$NXT
     C*-----------------------------------------------------------------
     C*
     C*    A/R period and prior close date
     C*
B1   CSR   $$PNC         IFEQ      $$PNCR
     CSR   $$DF          ANDEQ     $$DFR
     CSR                 MOVE      $$NXT         $$NXTR
X1   CSR                 ELSE
DBN  CSR                 MOVE      @PD($$AR)     DS@CD
     CSR                 Z-ADD     $$AR          $@
     CSR                 MOVE      $$PNCR        $PER
     CSR                 EXSR      S011A
     C*                    ---- -----
     CSR                 MOVE      $@NXT         $$NXTR
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    A/P period and prior close date
     C*
B1   CSR   $$PNC         IFEQ      $$PNCP
     CSR   $$DF          ANDEQ     $$DFP
     CSR                 MOVE      $$NXT         $$NXTP
X1   CSR                 ELSE
DBN  CSR                 MOVE      @PD($$AP)     DS@CD
     CSR                 Z-ADD     $$AP          $@
     CSR                 MOVE      $$PNCP        $PER
     CSR                 EXSR      S011A
     C*                    ---- -----
     CSR                 MOVE      $@NXT         $$NXTP
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E011          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S011A  - Determine next close dates
     C*    ----------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Determine next close dates.
     C*
     C*      Input:
     C*        DS@CD    = Current G/L year date patterns
     C*        $PER     = Current period
     C*
     C*      Output:
     C*        $@NXT    = next date
     C*
     C*
     CSR   S011A         BEGSR
     C*          -----     -----
     CSR                 MOVE      *ZEROS        $@NXT
     C*-----------------------------------------------------------------
     C*
     C*    Increment period by one
     C*
     CSR   $PER          ADD       1             #X
     C*-----------------------------------------------------------------
     C*
     C*    If next period close date is same as current period
     C*      -Using less than 14 period accounting
     C*      -Default to period 1 of the next fiscal year
     C*
B1   CSR   #X            IFLE      14
     CSR   @CD(#X)       ANDEQ     @CD($PER)
     CSR                 Z-ADD     1             #X
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    If next period greater than 14
     C*      -Default to period 1 of the next fiscal year
     C*
B1   CSR   #X            IFGT      14
     CSR                 Z-ADD     1             #X
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Determine next period close if first period of next year
     C*
B1   CSR   #X            IFEQ      1
DBN  CSR                 MOVEL     @PN($@)       DS@CD
     CSR                 MOVE      @CD(01)       $@NXT                          next period
X1   CSR                 ELSE                                                    closed date
     C*
     C*    Next period of this year
     C*
     CSR                 MOVE      @CD(#X)       $@NXT
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   E011A         ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S999   - Initial subroutine
     C*    --------------------------------------
     C*
     C*    Processing:
     C*                 1.  Initialize output parameters.
     C*                 2.  Validate input parameters.
     C*                 3.  Retrieve processing options.
     C*                 4.  Initialize variables.
     C*
     CSR   S999          BEGSR
     C*          ----      -----
     C*-----------------------------------------------------------------
     C*
     C*    Passed Parameter Data Structure:
     C*
     C*        1.  Computation to perform
     C*        2.  Company number
     C*        3.  G/L date MM/DD/YY format
     C*        4.  Period number
     C*        5.  Fiscal year
     C*        6.  Century
     C*        7.  Error flag
     C*             1 = #PYEB, prior year
     C*             2 = #PBCO, current year but prior to current
     C*                        period
     C*             3 = #PACO, posted after next period
     C*             4 = #WACO, date is 2 or more years in the future
     C*             5 = Date pattern/fiscal year not set up
     C*        8.  System (optional) = 1-A/R, 2-A/P, default-G/L
     C*        9.  Optional parm indicating date format passed
     C*            No parm is defaulted to ' ' - MDY Format
     C*            Parm passed             '1' - Julian Format
     C*       10.  Beginning date of fiscal year (optional, output)
     C*       11.  Beginning date of fiscal year century portion
     C*       12.  Date array of all fiscal periods for fiscal year
     C*       13.  Century array of all fiscal periods for fiscal year
     C*
     CSR   *ENTRY        PLIST
     CSR                 PARM                    PSCALC            1            calculate
     CSR                 PARM                    PSCO              5            company
     CSR                 PARM                    PSDG              6 0          G/L date
     CSR                 PARM                    PSPN              2 0          period
     CSR                 PARM                    PSFY              2 0          fiscal year
     CSR                 PARM                    PSCTY             2 0          century
     CSR                 PARM                    PSERR             1            error flag
     CSR                 PARM                    PSTYPE            1            system code
     CSR                 PARM                    PSJFLG            1            JUL OR MDY FMT
     CSR                 PARM                    PSBEG             6 0          beginning date
     CSR                 PARM                    PSBGCT            2 0          begin century
     CSR                 PARM                    PS@DT                          14 ending dates
     CSR                 PARM                    PS@CT                          century arry
     C*
     C*    Convert G/L date to *YMD from *MDY
     C*
B1   CSR   PSCALC        IFEQ      '3'
     CSR                 MOVE      PSPN          $XXM
     CSR                 MOVE      PSFY          $XXY
     CSR                 MOVE      PSCTY         $XXC
B2   CSR   ##PARM        IFGE      9
     CSR                 MOVEL     PSJFLG        $FLAG
X2   CSR                 ELSE
     CSR                 MOVEL     *BLANKS       $FLAG
E2   CSR                 ENDIF
X1   CSR                 ELSE
B2   CSR   PSCALC        IFEQ      '1'
     CSR   PSCALC        OREQ      '2'
     CSR                 Z-ADD     *ZEROS        PSCTY                          ADD 3471254
B3   CSR   ##PARM        IFGE      9
     CSR                 MOVEL     PSJFLG        $FLAG
X3   CSR                 ELSE
     CSR                 MOVEL     *BLANKS       $FLAG
E3   CSR                 ENDIF
B3   CSR   $FLAG         IFEQ      '1'
     CSR                 MOVEL     PSJFLG        $FLAG
     CSR                 MOVE      PSDG          #SIDAT            6
     CSR                 MOVE      *BLANK        #CTRY
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 PARM                    #CTRY
     CSR                 MOVE      #SIDAT        $DATE
     CSR                 MOVE      $DATE1        $XXM
     CSR                 MOVE      $DATE2        $XXD
     CSR                 MOVE      $DATE3        $XXY
     CSR                 MOVE      #CTRY         $XXC
     C*
X3   CSR                 ELSE
     CSR                 MOVE      PSDG          $DATE
     CSR                 MOVE      $DATE1        $XXM
     CSR                 MOVE      $DATE2        $XXD
     CSR                 MOVE      $DATE3        $XXY
     CSR                 MOVE      PSCTY         $XXC
E3   CSR                 ENDIF
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     C*
B1   CSR   PSCTY         IFEQ      *ZEROS
B2   CSR   $XXY          IFGT      $$#CYR
     CSR                 Z-ADD     19            $XXC
X2   CSR                 ELSE
     CSR                 Z-ADD     20            $XXC
E2   CSR                 ENDIF
     CSR                 MOVE      $XXC          PSCTY
E1   CSR                 ENDIF
     CSR                 MOVE      $XXYMD        $PSDG
     C*
     C*    Check if type provided.
     C*
     CSR                 MOVE      *BLANKS       $TYPE
B1   CSR   ##PARM        IFGE      8
     CSR                 MOVEL     PSTYPE        $TYPE
E1   CSR                 ENDIF
     C*
     C*    Check if beginning of year requested.
     C*
     CSR                 MOVE      *BLANKS       $PSBEG            1
B1   CSR   ##PARM        IFGE      10
     CSR                 MOVEL     '1'           $PSBEG
     CSR                 CLEAR                   PSBEG
E1   CSR                 ENDIF
     C*
     C*    Check if 14 ending dates requested.
     C*
     CSR                 MOVE      *BLANKS       $@DT              1
B1   CSR   ##PARM        IFGE      12
     CSR                 MOVEL     '1'           $@DT
     CSR                 CLEAR                   @DT
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     C*    Initialize output parameters
     C*
     CSR                 MOVE      ' '           PSERR             1
     C*-----------------------------------------------------------------
     C*
     C*    Set flag to look at beginning date for fiscal year
     C*
     CSR                 MOVEL     '0'           $#LAST            1
     C*-----------------------------------------------------------------
     C*
     C*    Define key lists
     C*
     C*
     C*    KLIST over F0008
     C*
     CSR   CDKY05        KLIST
     CSR                 KFLD                    CDDTPN
     CSR                 KFLD                    CDDFYJ
     CSR                 KFLD                    CDFQ
     C*
     CSR   CDKY04        KLIST
     CSR                 KFLD                    CDDTPN
     CSR                 KFLD                    CDDFYJ
     C*
     CSR   CDKY01        KLIST
     CSR                 KFLD                    CDDTPN
     C*
     CSR   CDKY02        KLIST
     CSR                 KFLD                    CDDTPN
     CSR                 KFLD                    CDDFYJ
     C*-----------------------------------------------------------------
     C*
     C*    Define work fields
     C*
     C*
     CSR   *LIKE         DEFINE    DSSAVE        $SSAVE
     CSR   *LIKE         DEFINE    DS@CD         $S@CD
     CSR   *LIKE         DEFINE    PSCO          $SCO
     CSR   *LIKE         DEFINE    PSTYPE        $TYPE
     CSR   *LIKE         DEFINE    PSJFLG        $FLAG
     CSR   *LIKE         DEFINE    @PT           $PTKEY
     CSR   *LIKE         DEFINE    #X            $X
     CSR   *LIKE         DEFINE    #X            $@
     CSR   *LIKE         DEFINE    #X            $PT
     CSR   *LIKE         DEFINE    PSFY          $FY
     CSR   *LIKE         DEFINE    PSCTY         $FY#
     CSR   *LIKE         DEFINE    $$PNC         $PER
     CSR   *LIKE         DEFINE    $$PRV         $@PRV
     CSR   *LIKE         DEFINE    $$NXT         $@NXT
     CSR   *LIKE         DEFINE    $$DF          $PSDG
     CSR   *LIKE         DEFINE    $$DF          $BEG
     CSR   *LIKE         DEFINE    $$DF          $SVBEG
     CSR   *LIKE         DEFINE    #X            $8X
     CSR   *LIKE         DEFINE    $$DF          $8BEG
     CSR   *LIKE         DEFINE    $$DTPN        $8DTPN
     CSR   *LIKE         DEFINE    $$DF          $BEGGL
     CSR   *LIKE         DEFINE    $$DF          $BEGAR
     CSR   *LIKE         DEFINE    $$DF          $BEGAP
     CSR   *LIKE         DEFINE    PSCTY         $#CTRY
     C*-----------------------------------------------------------------
     C*
     CSR   E999          ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE *INZSR - Initial subroutine - first time only
     C*    --------------------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Open database files
     C*                 2.  Retrieve constants
     C*
     CSR   *INZSR        BEGSR
     C*          ------    -----
     C*----------------------------------------------------------------
     C*
     C*    Clear data structure
     C*
     CSR                 CLEAR                   DS@CD
     CSR                 CLEAR                   $DATEX
     CSR                 CLEAR                   $XXYMD
     C*----------------------------------------------------------------
     CSR                 Z-ADD     10            $$#CYR            2 0
B1   CSR   *IN81         IFEQ      '0'
     CSR                 MOVEL     *BLANKS       FRDTAI
     CSR                 MOVEL     '#CYR'        FRDTAI
     CSR                 CALL      'X9800E'                             81
     C*                    ---- --------
     CSR                 PARM                    I9800E
     CSR                 MOVE      FRERR         *IN81
B2   CSR   *IN81         IFEQ      '0'
     CSR   FRDVAL        ANDNE     *BLANKS
DBN  CSR                 MOVEL     FRDVAL        $WORK             3
DBN  CSR                 MOVE      $WORK         $WRK2             2
     CSR                 TESTN                   $WRK2                81
B3   CSR   *IN81         IFEQ      '1'
     CSR                 MOVE      $WRK2         $$#CYR
E3   CSR                 ENDIF
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   EINZSR        ENDSR
     C*****************************************************************
** TABLE OF DAYS
312831303130313130313031
