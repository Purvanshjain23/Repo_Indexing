     A*%%TS  SD  20021202  154526  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5950- SELECT OPEN GROUP ON NURSERY OR GROW FINISH
     A*                          FARM
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/05/01
     A*
     A*****************************************************************
     A*%%EC
     A*                                     DSPSIZ(24 80 *DS3)
     A                                      DSPSIZ(*DS3 *DS4)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP5950R1
     A*%%TS  SD  19991110  083626  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 20 60)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5950R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP5950R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5950S1                  SFL
     A*%%TS  SD  20021202  154029  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFBLCD    R        O  8  1REFFLD(@@BLCD)
     A            DFRMCD    R        O  8 11REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A            DFHGCD    R        O  8 18REFFLD(HGHGCD)
     A            DFPTCD    R        O  8 27REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFPPCD    R        O  8 34REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFGTCD    R        O  8 43REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFOPDTMDY R        O  8 48REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            DFGSCD    R        O  8 58REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5950C1                  SFLCTL(HP5950S1)
     A*%%TS  SD  20021202  154526  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A                                      WINDOW(HP5950R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 16'SELECT OPEN GROUP ON A FARM'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Farm site:'
     A            C1FSCD    R        O  4 12REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            C1FSNM    R        O  4 19REFFLD(FSFSNM)
     A                                  5 27'-Production-'
     A                                      DSPATR(HI)
     A                                  5 41'Group'
     A                                      DSPATR(HI)
     A                                  5 50'Date'
     A                                      DSPATR(HI)
     A                                  6  1'Building'
     A                                      DSPATR(HI)
     A                                  6 11'Room'
     A                                      DSPATR(HI)
     A                                  6 18'Group'
     A                                      DSPATR(HI)
     A                                  6 27'Type'
     A                                      DSPATR(HI)
     A                                  6 34'Phase'
     A                                      DSPATR(HI)
     A                                  6 42'Type'
     A                                      DSPATR(HI)
     A                                  6 49'Opened'
     A                                      DSPATR(HI)
     A                                  6 58'St'
     A                                      DSPATR(HI)
     A            C1BLCD    R        B  7  1REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1RMCD    R        B  7 11REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND LINE
     A*-------------------------------------------------------------------------
     A          R HP5950K1
     A                                      OVERLAY
     A                                      WINDOW(HP5950R1)
     A                                 19  1'F12=Previous'
