/***************************************************************** */
/*                                                                 */
/*  ENVIRONMENT -----> PORK                                        */
/*  SYSTEM ----------> ACCOUNTS RECEIVABLE SYSTEM                  */
/*  PROGRAM NUMBER --> AR410CL                                     */
/*  PROGRAM NAME ----> FOREIGN CORPORATE TAX SAVINGS               */
/*                     YEARLY STATEMENT OF ACCOUNT REPORTS         */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 10/23/97                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> GENERATE A ONCE-A-YEAR REPORT FOR TAX.      */
/*                                                                 */
/***************************************************************** */
/* MODIFICATIONS                                                   */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(60)
             DCL        VAR(&LDCCYY) TYPE(*DEC) LEN(4)
             DCL        VAR(&LD0YMD) TYPE(*DEC) LEN(7)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDCCYY) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LD0YMD) VALUE(%SST(*LDA 5 7))

/*-----------------------------------------------------------------*/
/*  SET UP QUERY FOR EXPORT SALES                                  */
/*-----------------------------------------------------------------*/

/* ONLY SELECT RECORDS WITH AN ACCOUNTING YEAR EQUAL TO SELECTED YEAR */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('F4ESNB *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDCCYY)


/* ONLY SELECT RECORDS WITH AN EXPORT STATUS OF YES                   */

             CHGVAR     VAR(%SST(&QDATA 17 19)) VALUE('*AND F4U2ST +
                          *EQ "Y"')

/* ONLY SELECT RECORDS WITH AN INVOICE SUFFIX OF 'IN'            */

             CHGVAR     VAR(%SST(&QDATA 37 20)) VALUE('*AND F4CEST +
                          *EQ "IN"')


/*------------------------------------------------------------------*/
/*  RUN QUERY OVER OMS SALES HISTORY FILE                           */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(OMHSTPLA) SHARE(*YES)
             OPNQRYF    FILE((OMHSTPLA)) QRYSLT(&QDATA) +
                          KEYFLD((F4ANC7)) SEQONLY(*YES)


/*-----------------------------------------------------------------*/
/*  SET UP QUERY FOR DOMESTIC SALES                                */
/*-----------------------------------------------------------------*/

/* ONLY SELECT RECORDS WITH AN ACCOUNTING YEAR EQUAL TO SELECTED YEAR */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('F4ESNB *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDCCYY)


/* ONLY SELECT RECORDS WHERE EXPORT STATUS IS NOT YES                 */

             CHGVAR     VAR(%SST(&QDATA 17 19)) VALUE('*AND F4U2ST +
                          *NE "Y"')

/* ONLY SELECT RECORDS WITH AN INVOICE SUFFIX OF 'IN'            */

             CHGVAR     VAR(%SST(&QDATA 37 20)) VALUE('*AND F4CEST +
                          *EQ "IN"')


/*------------------------------------------------------------------*/
/*  RUN QUERY OVER OMS SALES HISTORY FILE                           */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(OMHSTPLP) SHARE(*YES)
             OPNQRYF    FILE((OMHSTPLP)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  CREATE WORKFILES                                                */
/*------------------------------------------------------------------*/


             CRTDUPOBJL OBJ(ARP310) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(ARP311) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*------------------------------------------------------------------*/
/* CALL PROGRAM TO:                                                 */
/*------------------------------------------------------------------*/
/*       1) BUILD WORKFILES                                         */
/*       2) CALCULATE OUTSTANDING BALANCE FOR EACH CUSTOMER         */
/*       3) CALCULATE TOTAL SALES FOR EACH CUSTOMER                 */

             CALL AR310


/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE FOR INDIVIDUAL CUSTOMER REPORTS              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS PRINTS ON 8.5 BY 11; PORTRAIT ON A LASER PRINTER  */
/* FORCE IT TO DRAWER '1' FOR LETTER PAPER AND 'PORTRAIT'           */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(82) LANDSCAPE(*NO) DRAWER(1) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE FOR SUMMARY LISTING                          */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT PRINTS ON 8.5 X 11, PORTRAIT, ON A LASER PRINTER.    */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT2) OUTQ(&OUTQ) +
                          WIDTH(90) LANDSCAPE(*NO) DRAWER(1) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/*  CALL PROGRAM TO PRINT CUSTOMER REPORT AND SUMMARY LISTING       */
/*------------------------------------------------------------------*/

             CALL       PGM(AR610)

             CLOF       OPNID(OMHSTPLA)
             CLOF       OPNID(OMHSTPLP)
             DLTOVR     FILE(*ALL)

             ENDPGM

