     A*%%TS  SD  20070920  152828  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4024
     A*  TITLE:         SPECIFY NUR/FIN TARGETS TO COPY
     A*  PROGRAMMER:    SHARON ZURN
     A*  DATE:          01/10/07
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4024EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4024R1
     A*%%TS  SD  20070920  144232  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(HP4024EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(39)
     A                                      OVERLAY
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SPECIFY NUR/FIN TARGETS TO COPY'
     A                                      DSPATR(HI)
     A                                  3 32'COPY'
     A                                      DSPATR(HI)
     A                                  3 42'COPY'
     A                                      DSPATR(HI)
     A                                  4 32'FROM'
     A                                      DSPATR(HI)
     A                                  4 43'TO'
     A                                      DSPATR(HI)
     A                                  5  3'Required:'
     A                                      COLOR(BLU)
     A                                  6 12'Year .............'
     A            D1FCDYR   R        B  6 32REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1TCDYR   R        B  6 42REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  7  3'Optional:'
     A                                      COLOR(BLU)
     A                                  8 12'Origin BGF cell ..'
     A            D1FOBCELL R        B  8 34REFFLD(HSRREF/@@OBCELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A            D1TOBCELL R        B  8 44REFFLD(HSRREF/@@OBCELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  9 12'Origin BGF pod ...'
     A            D1FOBPOD  R        B  9 33REFFLD(HSRREF/@@OBPOD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A            D1TOBPOD  R        B  9 43REFFLD(HSRREF/@@OBPOD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A  39                             11  2'Your copy will overlay existing da-
     A                                      ta; take F8 to continue.'
     A                                      COLOR(RED)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4024K1
     A*%%TS  SD  20070920  144232  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(HP4024EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             13  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 14  1'F3=Exit'
     A                                 14 10'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4024ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(15)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4024EC                  SFLCTL(HP4024ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 16 60)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
