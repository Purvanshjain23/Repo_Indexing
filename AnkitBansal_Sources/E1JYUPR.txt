     H OPTION(*SRCSTMT : *NODEBUGIO)
     H*DFTACTGRP(*NO)
     H BNDDIR('RDB2000')

      *****************************************************************
     H* ‚Company       : Seaboard Foods
     H* ‚Function      : JD Edwards Upgrade
     H* ‚                Insert F0011 Record - copied from marine smlsynsrc/qrpglesrc/jeu056w9
     H* ‚Programmer    : Rose Centonze
     H* ‚Date          : 2020/05/21
      *****************************************************************
     H* ‚Date          Changed by    Ticket    Description
     H* ‚----------    ----------    -------   -----------------------------------------------------
     H* ‚2020-05-21    R Centonze    P16169    Original version
     H*                                        not passing: icupmj,icuptm
     H*                                        not passing: icamt icdocn
     H*                 ‚TO COMPILE: ADDLIBLE RDB40 , CRTPGMMOD  WITH BNDDIR(RDB2000)
     H*                             ‚CRTPGM PGM(E1IDEVGEN/e1jyupr) MODULE(*PGM)
      *****************************************************************

      * ‚Entry parameters ....
     dE1JYUPR          PR                  extpgm('E1JYUPR')
     d @p_ICICUT                      2a                                        Batch Type
     d @p_ICICU                       8  0                                      Batch Number
     d @p_ICIST                       1a                                        Batch Status
     d @p_ICIAPP                      1a                                        Batch Approval for
     d @p_ICAICU                     15  0                                      postin   Type. . .
wg   d @p_ICUSER                     10a                                        Input Total
     d @p_ICDICJ                      6  0                                      User ID
     d @p_ICNDO                       5  0                                      Batch Date
     d @p_ICBAL                       1a                                        Number of Docs
     d @p_ICBALJ                      1a                                        Journal Entries Bal
     d @p_ICAME                      15p 0                                      Amount entered
     d @p_ICDOCN                     15p 0                                      Documents Entered
     d @p_ICAUSR                     10a                                        Authorized User ID
     d @p_ICPOB                       1a                                        Post Out of Balanc
     d @p_ICIBOI                      1a                                        Include Batch Integr
     d @p_ICAIPT                     15  0                                      Amount - input total
     d @p_ICOFFP                      1a                                        Auto Offset Mode AP
     d @p_ICPID                      10a                                        Program ID
     d @p_ICJOBN                     10a                                        Work Station ID
     d @p_ICUPMJ                      6  0                                      Date - Updated
     d @p_ICUPTM                      6  0                                      Time Last Updated
     d @p_ICDRSP                      1a                                        Detailed Restatement
     d @p_IC52PP                      1a                                        52 Period Processed
     d @p_ICCBP                       1a                                        Cash Basis Processed
     d @p_Error                       1a                                        error sts
     d @p_ICO6TX                      7a                                        E1 Process Error ID
     d @p_ico7tx                     25a                                        E1 Process Error
     d @p_xxco                        5a                                        company

     dE1JYUPR          PI
     d @p_ICICUT                      2a                                        Batch Type
     d @p_ICICU                       8  0                                      Batch Number
     d @p_ICIST                       1a                                        Batch Status
     d @p_ICIAPP                      1a                                        Batch Approval
     d @p_ICAICU                     15  0                                      Input total
wg   d @p_ICUSER                     10a                                        User id
     d @p_ICDICJ                      6  0                                      Batch Date
     d @p_ICNDO                       5  0                                      Number of doc exp
     d @p_ICBAL                       1a                                        Balanced-Docs and
     d @p_ICBALJ                      1a                                        Journal Entries Bal
     d @p_ICAME                      15p 0                                      Amount Entered
     d @p_ICDOCN                     15p 0                                      Documents Entered
     d @p_ICAUSR                     10a                                        Authorized User ID
     d @p_ICPOB                       1a                                        Post out of bal y/n
     d @p_ICIBOI                      1a                                        Include Batch Integr
     d @p_ICAIPT                     15  0                                      Amount - input total
     d @p_ICOFFP                      1a                                        Auto Offset Mode AP
     d @p_ICPID                      10a                                        Program ID
     d @p_ICJOBN                     10a                                        Work Station ID
     d @p_ICUPMJ                      6  0                                      Date - Updated
     d @p_ICUPTM                      6  0                                      Time Last Updated
     d @p_ICDRSP                      1a                                        Detailed Restatement
     d @p_IC52PP                      1a                                        52 Period Processed
     d @p_ICCBP                       1a                                        Cash Basis Processed
     d @p_Error                       1a                                        error sts
     d @p_ICO6TX                      7a                                        E1 Process Error ID
     d @p_ico7tx                     25a                                        E1 Process Error
     d @p_xxco                        5a                                        company

      * ‚RTV JDE RDB Connection
     d*rtvRDBCfg       PR                  ExtPgm('LHXMXFR')    (marine version)
     d rtvRDBCfg       PR                  ExtPgm('E1IEXFR')
     d  @rtnCode                      7a                                        Return Code
     d  @companyId                    3a
     d* @jdeCompany                   5a
     d  @prmConnect                  10a
     d  @prmSchema                   20a
     d  @secConnect                  10a
     d  @secSchema                   20a
     d  @postJDEVer                   1a

      * ‚Copy Book ....
      /COPY *LIBL/RDBSRC,RDBPROTO

      * ‚Variables ....
     d Id              S             10I 0 inz
     d rc              S             10I 0 inz
     d @ErrId          S              7a   inz
     d @ErrText        S            100a   inz
     d @sFileSchema    S             30a   inz
     d @sql            S          10000A   inz

     d rtnCode         s              7a   inz                                  Return Code
     d companyId       s              3a   inz
     d jdeCompany      s              5a   inz

     d pCon            s             10a   inz
     d pSchm           s             20a   inz

     d sCon            s             10a   inz
     d sSchm           s             20a   inz

     d @Con            s             10a   inz
     d @Schm           s             20a   inz

     d pJDEVer         s              1a   inz

      /FREE

   ‚   // Process Primary Schema ....
       exSr $writeF0011Sr;

       // EoJ ....
       *Inlr = *On;

     ‚* ----------------------------------------------------------- *
     ‚* Setup Primary Schema for processing                         *
     ‚* ----------------------------------------------------------- *

       begSr $writeF0011Sr;

   ‚   // Remote connection - Getting Connection ID ....
       Id = RdbConnect(@Con);

   ‚   // Connection failed ....
       If Id < 0;
          exSr $errorSr;
       endIf;

   ‚   // Process Insert/Update  ....
       exSr $insertSr;

   ‚   // Closing Primary Connection ....
       RdbFreeStmt(Id);
       RdbClose(Id);

       endSr;

     ‚* ----------------------------------------------------------- *
     ‚* Insert Record into Schema                                   *
     ‚* ----------------------------------------------------------- *

       begSr $insertSr;

   ‚    // Set Schema ....
        @sFileSchema = '[' + %trimr(@Schm) + '].[F0011]';

   ‚    // Setup SQL stmt - SQL Server ....
        @sql =  'Insert into ' + %trimr(@sFileSchema) + ' (' +
        'ICICUT, ICICU,ICIST,ICIAPP,ICAICU,ICUSER,ICDICJ,ICNDO,' +
        'ICBAL,ICBALJ,ICAUSR,ICPOB,ICIBOI,'  +
        'ICAIPT,ICOFFP,ICPID,ICJOBN,ICDRSP,IC52PP,ICCBP)' +
        'Values(?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?)';

   ‚      // Prepared Stmt ....
          rc = RdbPrepStmt(Id: @sql);

   ‚      // Parameters for Prepared Stmt ...
          rc = RdbSetStr(ID: 1:   @p_ICICUT);
          rc = RdbSetNum(ID: 2:   @p_ICICU );
          rc = RdbSetStr(ID: 3:   @p_ICIST );
          rc = RdbSetStr(ID: 4:   @p_ICIAPP);
          rc = RdbSetNum(ID: 5:   @p_ICAICU);
          rc = RdbSetStr(ID: 6:   @p_ICUSER);
          rc = RdbSetNum(ID: 7:   @p_ICDICJ);
          rc = RdbSetNum(ID: 8:   @p_ICNDO );
          rc = RdbSetStr(ID: 9:   @p_ICBAL);
          rc = RdbSetStr(ID: 10:  @p_ICBALJ);
          rc = RdbSetStr(ID: 11:  @p_ICAUSR);
          rc = RdbSetStr(ID: 12:  @p_ICPOB );
          rc = RdbSetStr(ID: 13:  @p_ICIBOI);
          rc = RdbSetNum(ID: 14:  @p_ICAIPT);
          rc = RdbSetStr(ID: 15:  @p_ICOFFP);
          rc = RdbSetStr(ID: 16:  @p_ICPID );
          rc = RdbSetStr(ID: 17:  @p_ICJOBN);
          rc = RdbSetStr(ID: 18:  @p_ICDRSP);
          rc = RdbSetStr(ID: 19:  @p_IC52PP);
          rc = RdbSetStr(ID: 20:  @p_ICCBP );
      //  rc = RdbSetNum(ID: 11:  @p_ICAME );
      //  rc = RdbSetNum(ID: 12:  @p_ICDOCN);
      //  rc = RdbSetNum(ID: 20:  @p_ICUPMJ);
      //  rc = RdbSetNum(ID: 21:  @p_ICUPTM);

   ‚      // Executing Prepared Stmt ...
          rc = RdbPrepExec(Id);

   ‚      // Prepared Stmt was not successful ....
          if rc < 0;
             exSr $errorSr;
          endif;

       endSr;

     ‚* ----------------------------------------------------------- *
     ‚* Error Routine ....                                          *
     ‚* ----------------------------------------------------------- *
       begsr $errorSr;

   ‚   // Error - Job is ended ....
       @p_Error='Y';
       RdbError(@ErrId: @ErrText);

       if @Errid = *blank;
          @Errid='SCHEMNF';
          @ErrText=companyId;
       endif;

       @p_ico6tx=@Errid;
       @p_ico7tx=@ErrText;

       if Id >= 0;
          RdbClose(Id);
       endif;

       *inLr = *on;
       return;

       endSr;

     ‚* ----------------------------------------------------------- *
     ‚* Initial Routine ....                                        *
     ‚* ----------------------------------------------------------- *
       begSr *InzSr;

                 companyId=%subst(@p_xxco:3:3);

   ‚   // Retrieving RDB Connection ....
       rtvRDBCfg(rtnCode: companyId:  pCon:
                 pSchm:   scon:      sSchm:      pJDEVer);

   ‚   // Defaulting error flag ....
       @p_Error='N';

   ‚   // It works only for MS SQL ....
       select;
         when pJDEVer = 'C' or pJDEVer = 'S';   // Primary
              @con  = pCon;
              @schm = pSchm;
         when pJDEVer = 'B';                    // Secondary
              @con  = sCon;
              @schm = sSchm;
         Other;
              exSr $errorSr;                    // Iseries no valid
       endSl;


       endSr;
      /END-FREE
