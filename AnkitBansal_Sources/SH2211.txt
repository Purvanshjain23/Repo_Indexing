      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH2211 - Datamart Sales: Delete Accrual Expense
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     10/22/08
      *
      * FUNCTION: For each run of the Datamart build, we will delete/rewrite
      *           records for the system "year".
      *
      *           At the time this program was written, only Dailys data was
      *           being processed and put into our new Accrual Expense file.
      *
      *           So, the delete logic in this program is simply by "year".
      *           If we eventually put "seaboard" data into the file, we may
      *           need to rethink the delete logic.
      *
      ****************************************************************
      * Date      Programmer
      *
      * 08/06/09  LeAnne Ramsey
      *           Recompile only. New fields were added to the file.
      *
      * 04/22/10  LeAnne Ramsey
      *           Recompile only. New fields were added to the file:
      *             Total Market Accrual Amt
      *             Non-Ledgered Market Accrual Amt
      *             Ledgered Market Accrual Amt
      *             Weight Billed
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fshl211a   uf   e           k disk
      *  Datamart: Accrual expense
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Parms
      *
     D xxfromdt        s              8  0
     D xxtodt          s              8  0
      *
      *
      * Workfields for date manipulation
      *
     D wkyear          s              4  0
     D wkccyymmdd      s              8  0
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Delete all Datamart records for the "system" year and forward.
      *
     C     wkyear        setll     shl211a
      *
     C                   dou       *in91 = *on                                  Do delete
     C                   read      shl211a                                91
     C                   if        *in91 = *off and
     C                             aeacyr >= wkyear
     C                   delete    aerec
     C                   endif
     C                   enddo                                                  Do delete
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      *  Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm list
     C     *entry        plist
     C                   parm                    xxfromdt
     C                   parm                    xxtodt
      *
      * Key lists
      *  None
      *
      * In a later step we will want to process records out of files in Dailys
      * libraries that 1) do not have year and 2) are not keyed by date.
      * So, we will calc the Beginning/Ending dates for the System Year and
      * pass them back out for later processing.
      *
     C                   move      *year         wkyear
     C     10000         mult      wkyear        wkccyymmdd
      *
      * January 1
     C     wkccyymmdd    add       0101          xxfromdt
      *
      * December 31
     C     wkccyymmdd    add       1231          xxtodt
      *
     C                   endsr
      /EJECT
