     A*%%TS  SD  20170606  153319  ISLBUSE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD400
     A*  TITLE:         WORK WITH FEED MILL
     A*  PROGRAMMER:    LARA BUSER
     A*  DATE:          06/01/2017
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP400S1                   SFL
     A*%%TS  SD  20170606  153319  ISLBUSE     REL-V7R1M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1FMBO    R        O  9  6REFFLD(HSRREF/@@FMBO *LIBL/HSPREF)
     A            D1FMALNM      20A  O  9 14
     A            D1MLTCD   R        O  9 35REFFLD(HSRREF/@@MLTCD *LIBL/HSPREF)
     A            D1MLTDS   R        O  9 40REFFLD(HSRREF/@@MLTDS *LIBL/HSPREF)
     A            D1MMNO    R        O  9 62REFFLD(HSRREF/@@MMNO *LIBL/HSPREF)
     A            D12SUB    R        O  9 68REFFLD(HSRREF/@@2SUB *LIBL/HSPREF)
     A            D1FTCD    R        O  9 73REFFLD(HSRREF/@@FTCD *LIBL/HSPREF)
     A            D1AIST    R        O  9 77REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP400C1                   SFLCTL(HP400S1)
     A*%%TS  SD  20170606  153319  ISLBUSE     REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 31'WORK WITH FEED MILL'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Inactivate'
     A                                  6  7'Feed'
     A                                      DSPATR(HI)
     A                                  6 14'Feed Mill'
     A                                      DSPATR(HI)
     A                                  6 34'Mill'
     A                                      DSPATR(HI)
     A                                  6 40'Mill'
     A                                      DSPATR(HI)
     A                                  6 45'Type'
     A                                      DSPATR(HI)
     A                                  6 62'Map'
     A                                      DSPATR(HI)
     A                                  6 68'G/L'
     A                                      DSPATR(HI)
     A                                  6 72'Mnf'
     A                                      DSPATR(HI)
     A                                  6 77'A'
     A                                      DSPATR(HI)
     A***********
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  7'Mill'
     A                                      DSPATR(HI)
     A                                  7 14'Alias'
     A                                      DSPATR(HI)
     A                                  7 34'Type'
     A                                      DSPATR(HI)
     A                                  7 40'Description'
     A                                      DSPATR(HI)
     A                                  7 61'Mill#'
     A                                      DSPATR(HI)
     A                                  7 68'SUB'
     A                                      DSPATR(HI)
     A                                  7 72'Type'
     A                                      DSPATR(HI)
     A                                  7 77'I'
     A                                      DSPATR(HI)
     A***********
     A            C1FMBO    R        B  8  6REFFLD(HSRREF/@@FMBO *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1MLTCD   R        B  8 35REFFLD(HSRREF/@@MLTCD *LIBL/HSPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A            C1AIST    R        B  8 77REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP400K1
     A*%%TS  SD                    ISLBUSE     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F5=Refresh'
     A                                 22 23'F6=Create'
     A                                 22 34'F7=Listing'
     A                                 22 45'F17=Print Options'
     A                                 22 64'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP400ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP400EC                   SFLCTL(HP400ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
