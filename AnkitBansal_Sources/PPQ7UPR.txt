/***************************************************************** */
/*  PROGRAM NUMBER:  PPQ7UPR                                       */
/*  PROGRAM NAME:    RECREATE DOWNLOAD FILE OBJECTS                */
/*  PROGRAMMER:      PURVA DROGE                                   */
/*  CREATE DATE:     06/28/05                                      */
/*                                                                 */
/*  FUNCTION:                                                      */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*******************************************************************/
/* Modification History                                            */
/*                                                                 */
/* MM/DD/YY PGMR EXPLANATION OF CHANGE                             */
/* -------- ---- ------------------------------------------------- */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&FILE &LIB)

/*-----------------------------------------------------------------*/
/* PARAMETER                                                       */
/*-----------------------------------------------------------------*/
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)

/*-----------------------------------------------------------------*/
/* PROGRAM VARIABLES                                               */
             DCL        VAR(&DWNFILE1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DWNFILE2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SNTFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&STSFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DWN1RCDS) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&DWN2RCDS) TYPE(*DEC) LEN(10 0)

/*-----------------------------------------------------------------*/
/* INITIALIZE                                                      */
/*-----------------------------------------------------------------*/
             MONMSG     MSGID(CPF0000)
             CHGJOB     LOG(4 00 *SECLVL)

             CHGVAR     VAR(&DWNFILE1) VALUE(&FILE *TCAT 'GUY')
             CHGVAR     VAR(&DWNFILE2) VALUE(&FILE *TCAT 'STJ')
             CHGVAR     VAR(&SNTFILE) VALUE(&FILE *TCAT 'SNT')
             CHGVAR     VAR(&STSFILE) VALUE(&FILE *TCAT 'STS')

/*-----------------------------------------------------------------*/
/* MAIN PROCESS                                                    */
/*-----------------------------------------------------------------*/
/* RETRIEVE NUMBER OF RECORDS IN DOWNLOAD FILE */
             RTVMBRD    FILE(&LIB/&DWNFILE1) NBRCURRCD(&DWN1RCDS)
             RTVMBRD    FILE(&LIB/&DWNFILE2) NBRCURRCD(&DWN2RCDS)

/* SAVE SNT AND STS FILE RECORDS INTO QTEMP    */
             CPYF       FROMFILE(&LIB/&SNTFILE) +
                          TOFILE(QTEMP/&SNTFILE) MBROPT(*REPLACE) +
                          CRTFILE(*YES) FMTOPT(*MAP *DROP)
             CPYF       FROMFILE(&LIB/&STSFILE) +
                          TOFILE(QTEMP/&STSFILE) MBROPT(*REPLACE) +
                          CRTFILE(*YES) FMTOPT(*MAP *DROP)

/* DELETE EXISTING OBJECT AND RECREATE FILES */
             IF         COND(&DWN1RCDS *EQ 0 *AND &DWN2RCDS *EQ 0) +
                          THEN(DO)
             /* DELETE */
             DLTF       FILE(&LIB/&DWNFILE1)
             DLTF       FILE(&LIB/&DWNFILE2)
             DLTF       FILE(&LIB/&SNTFILE)
             DLTF       FILE(&LIB/&STSFILE)

             /* CREATE FILES */
             CRTDUPOBJ  OBJ(&FILE) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          NEWOBJ(&DWNFILE1)
             CRTDUPOBJ  OBJ(&FILE) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          NEWOBJ(&DWNFILE2)
             CRTDUPOBJ  OBJ(&FILE) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          NEWOBJ(&SNTFILE)
             CRTDUPOBJ  OBJ(&FILE) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          NEWOBJ(&STSFILE)

             /* COPY DATA BACK INTO SNT AND STS FILE */
             CPYF       FROMFILE(QTEMP/&SNTFILE) +
                          TOFILE(&LIB/&SNTFILE) MBROPT(*REPLACE) +
                          CRTFILE(*NO) FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2869)
             MONMSG     MSGID(CPF2817)

             CPYF       FROMFILE(QTEMP/&STSFILE) +
                          TOFILE(&LIB/&STSFILE) MBROPT(*REPLACE) +
                          CRTFILE(*NO) FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2869)
             MONMSG     MSGID(CPF2817)

             /* START JOURNALS IN TEST */
             IF COND(&LIB *EQ 'PRKTESTF') THEN(DO)
             STRJRNPF   FILE(&LIB/&DWNFILE1) JRN(TESTJRN/PORKTEST) +
                          OMTJRNE(*OPNCLO)
             STRJRNPF   FILE(&LIB/&DWNFILE2) JRN(TESTJRN/PORKTEST) +
                          OMTJRNE(*OPNCLO)
             STRJRNPF   FILE(&LIB/&STSFILE) JRN(TESTJRN/PORKTEST) +
                          OMTJRNE(*OPNCLO)
             ENDDO
             ENDDO
             ELSE DO
               SNDPGMMSG  MSG('Data in Download File. Cannot recreate +
                          file objects for download for file' *BCAT +
                          &FILE)
             ENDDO
/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:
             ENDPGM
