     h option(*SRCSTMT:*NODEBUGIO)
      *
      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF215 - Margin: Delete Adjustment Records
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/22/04
      *
      * Function:    When the Margin Close function is run, we call this program
      *              first to delete any records that may already exist for the
      *              'week' and, conditionally, "period" in the files.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/15/06  LeAnne Ramsey
      *           We have gone to separate 'summary' files for MIX and VOLUME.
      *
      * 10/11/06  LeAnne Ramsey
      *           Recompile only. More fields added to TFP014 file.
      *
      * 11/09/06  LeAnne Ramsey
      *           We now do both "weekly" and "period" margin processing. So, we
      *           have more files to "clear". And, we changed LDA positions.
      *
      * 02/14/07  LeAnne Ramsey
      *           Recompile only. New "other cost" fields were added to files.
      *
      * 03/22/07  LeAnne Ramsey
      *           Added our 2 new files:
      *              Volume Summary Extension--Weekly
      *              Volume Summary Extension--Period
      *
      * 04/16/07  LeAnne Ramsey
      *           Recompile only.
      *           We renamed the following flag fields:
      *             1) Exclude from Mix Flag    is now Mix Flag
      *             2) Exclude from Volume Flag is now Volume Flag
      *           Before this change Y=Yes meant EXCLUDE the product from processing
      *           After this change  Y=Yes means INCLUDE the product in processing
      *
      * 06/20/07  LeAnne Ramsey
      *           Recompile only.
      *           We added new field 'Item Type' to the Margin Adjustment Detail file.
      *
      * 08/27/08  LeAnne Ramsey
      *           Removed the "period" logic and files.
      *
      * 11/20/08  LeAnne Ramsey
      *           As a prep for synchronizing the TFS and Meat Cost LDAs, we
      *           removed the unused LDA fields.
      *
      * 11/26/08  LeAnne Ramsey
      *           We are now writing Volume Adjustment records for Neckbones (NVA)
      *           to Meat Costing file ROP125. So, added logic here to delete
      *           records from ROP125 that were written using program TF233. (We
      *           will not delete the BP6 records written to ROP125 in the Meat
      *           Costing program RO233.)
      *
      * 12/02/08  LeAnne Ramsey
      *           Recompile only.
      *
      * 05/02/12  LeAnne Ramsey (E2077)
      *           Recompile only.
      *           We added a new field to ROP125-TF ByProduct Volume:
      *              YVT72APR-Special Trim Price/LB
      *
      * 09/05/12  LeAnne Ramsey (E2243)
      *           Recompile only.
      *           Per Damon G. we must now write more/special records to TFP014 for the
      *           Items that receive lbs/dollars from Skirt Meat Items!! So, I had to add
      *           a new field to TFP014:
      *                         ADSMSFL-Skirt Meat Split Flag
      *
      * 02/08/22  Danny Nguyen   - DO2484 - WI479 STF Variance Reporting
      *           Recompile only due to TFP014 DBF change. Added the following fields:
      *             ADXPULB  - STF PRODUCED LBS
      *             ADXPUSLB - STF PRODUCED START WEIGHT
      *             ADXYPC   - STF STD YIELD %
      *             ADXPMPPC - STF PUMP %
      *             ADXSLLB  - STF SOLD LBS
      *             ADXSLSLB - STF SOLD START WEIGHT
      *
      * 10/10/22  R Centonze   W105621 - Increase price fields to handle > 999.99
      *         COPY @@SPU1MG TO  @@SPU1MG10 SIZE 10.6  -> FIELD TFP014.ADSPU1MG
      *         COPY @@TPU1MG TO  @@TPU1MG10 SIZE 10.6  -> FIELD TFP014.ADTPU1MG
      *         COPY @@SSL1MG TO  @@SSL1MG10 SIZE 10.6  -> FIELD TFP014.ADSSL1MG
      *         COPY @@SSL2MG TO  @@SSL2MG10 SIZE 10.6  -> FIELD TFP014.ADSSL2MG
      *         COPY @@TSL1MG TO  @@TSL1MG10 SIZE 10.6  -> FIELD TFP014.ADTSL1MG
      *         COPY @@TSL2MG TO  @@TSL2MG10 SIZE 10.6  -> FIELD TFP014.ADTSL2MG
      * 05/07/24  Santosh Patil P310149 - Field length is increased
      *           in TFP014, TFP015. Function is recompiled only.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl014a   uf   e           k disk
      *  Product margin adjustment transaction detail
      *
      *
     Ftfl015a   uf   e           k disk
      *  Mix summary--weekly
      *
      *
     Ftfl018a   uf   e           k disk
      *  Volume summary--weekly
      *
      *
     Ftfl031a   uf   e           k disk
      *  Volume summary extension--weekly
      *
      *
     Frop125    uf   e           k disk
      *  TF ByProduct and NVA Volume
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
      *
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *************************************************************************************
      * Mainline
      *************************************************************************************
      *
      * Perform "weekly" deletions from the Product Margin Adjustment Transaction
      * Detail file.
      *
      *
     C     key01         setll     tfl014a
      *
     C                   dou       *in90 = *on
     C     key01         reade     tfl014a                                90
     C                   if        *in90 = *off
     C                   delete    adrec
     C                   endif
     C                   enddo
      *
      * Mix summary--weekly
      *
     C     key01         setll     tfl015a
     C                   dou       *in90 = *on
     C     key01         reade     tfl015a                                90
     C                   if        *in90 = *off
     C                   delete    mwrec
     C                   endif
     C                   enddo
      *
      * Volume summary--weekly
      *
     C     key01         setll     tfl018a
     C                   dou       *in90 = *on
     C     key01         reade     tfl018a                                90
     C                   if        *in90 = *off
     C                   delete    vwrec
     C                   endif
     C                   enddo
      *
      * Volume summary extension--weekly
      *
     C     key01         setll     tfl031a
     C                   dou       *in90 = *on
     C     key01         reade     tfl031a                                90
     C                   if        *in90 = *off
     C                   delete    ewrec
     C                   endif
     C                   enddo
      *
      *
      * TF ByProduct and NVA Volume
      *
     C     ldwedt        setll     rop125
     C                   dou       *in90 = *on
     C     ldwedt        reade     rop125                                 90
     C                   if        *in90 = *off and yvpgm = 'TF233     '
     C                   delete    yvrec
     C                   endif
     C                   enddo
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldyr
     C                   kfld                    ldwk
      *
     C                   endsr
      /eject
