     A*%%TS  SD  20111209  084319  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD475
     A*  TITLE:         SPECIFY SCHEDULED MOVEMENT LOG OPTIONS
     A*  PROGRAMMER:    SHARON ZURN
     A*  DATE:          06/08/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP475R1
     A*%%TS  SD  20111209  084319  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(42)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 18'SPECIFY SCHEDULED MOVEMENT LOG OPT-
     A                                      IONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ................'
     A                                  4 35'?'
     A            DFFSBO    R        B  4 37REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 46REFFLD(BOBODS)
     A                                  5  2'Report .........................'
     A                                  5 35'?'
     A            DFMRCD    R        B  5 37REFFLD(MRMRCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 55                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFMRDS    R        O  5 46REFFLD(MRMRDS)
     A                                  6  2'Page break on farm .............'
     A                                  6 35'?'
     A            DFPBFL    R        B  6 37REFFLD(@@FLAG)
     A N47                                  DSPATR(HI)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Transfers across a state line ..'
     A                                  7 35'?'
     A            DFASLFL   R        B  7 37REFFLD(@@FLAG)
     A N48                                  DSPATR(HI)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFASLDS   R        O  7 46REFFLD(@@DS30)
     A                                  9  2'Enter a From/To Scheduled Date Ran-
     A                                      ge or an Update Date--DO NOT Enter -
     A                                      Both:'
     A                                      DSPATR(HI)
     A                                 11  2'From scheduled date ..'
     A            DFFMDY    R     Y  B 11 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 12  2'To scheduled date ....'
     A            DFTMDY    R     Y  B 12 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13  8'--OR--'
     A                                      DSPATR(HI)
     A                                 14  2'Update date ..........'
     A            DFUMDY    R     Y  B 14 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 16  2'Production phase .....'
     A                                 16 25'?'
     A            DFPPCD    R        B 16 27REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 53                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O 16 37REFFLD(PPPPDS)
     A                                 16 68'(Blank = All)'
     A                                 17  2'Farm site ............'
     A                                 17 25'?'
     A            DFFSCD    R        B 17 27REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 54                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 17 37REFFLD(FSFSNM)
     A                                 17 68'(Blank = All)'
     A                                 18  2'Customer number ......'
     A                                 18 25'?'
     A            DFCVNO    R        B 18 27REFFLD(CDCVNO)
     A N42                                  DSPATR(HI)
     A  42
     AO 59                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFCNAM        23A  O 18 37
     A                                 18 68'(Blank = All)'
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP475K1
     A*%%TS  SD  19941101  163520  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP475ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP475EC                   SFLCTL(HP475ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
