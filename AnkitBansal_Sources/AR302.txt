      *
      * SYSTEM:      ACCOUNT RECEIVABLE SYSTEM (PORK)
      * PROGRAM:     AR302
      * TITLE:       A/R CUSTOMER SUMMARY BY AGED AMOUNT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/03/99
      *
      *  FUNCTION:   THIS PROGRAM CREATES A WORKFILE THAT IS FTP'D TO
      *              A NETWORK DRIVE. DENNIS HUMMEL THEN ACCESSES THE
      *              NETWORK DRIVE AND PULLS THE DATA INTO HIS LOTUS
      *              SPREAD SHEET.
      *
      *              IT RUNS OVER WORKFILES CREATED IN RPG PROGRAM
      *              AR300 WHICH IS SUBMITTED THROUGH CLP PROGRAM
      *              POA9UPC IN THE PORK SYNON OMS MODEL.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 09/29/99  LEANNE FEDOR
      *           AS REQUESTED BY DENNIS HUMMEL, ADDED PARENT CUSTOMER NAME
      *           AND NUMBER TO FTP FILE.
      * 11/08/02  R CENTONZE .. RECOMPILE FOR ARP300 FILE CHANGE
      * 05/16/08  Barb Gutierrez
      *           Recompile only due to new HPB environment.
      * 05/16/08  Barb Gutierrez
      *           Recompile only due to new HPB environment.
      * 06/18/09  Lara Buser
      *           Replace ARP305 with Synon file PNFMCPL0.
      *           Remove half rounding
      * 10/27/09  Lara Buser E00477
      *           Change fields to 11.2 to accommodate changes to PNFMCPP
      *           and for the new reports for Tim (removed $total sr)
      * 06/29/23  Eric L        (3000027)
      *           ARP300/$CBOCD (Type Code) populates PNFMCPP/FMBOCD
      /EJECT
      *******************************************************************
      * FILE SPECIFICTIONS
      *******************************************************************
      *
     FARP300    IF   E           K DISK
      *  WORKFILE: CUSTOMER HEADINGS
      *
      *
     FARL302B   IF   E           K DISK
      *  WORKFILE: CUSTOMER TOTALS
      *
      *
     FPNFMCPL0  O    E             DISK
      *  A/R Customer Totals WF
      *
      *******************************************************************
      * INPUT SPECS
      *******************************************************************
      * NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SUBDSC          C                   CONST('Subtotal')
     D RPTDSC          C                   CONST('Report total')
      *
      *
      *---------------------------------------------------------------
      *  DATA STRUCTURES
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      *
      /EJECT
      *******************************************************************
      * MAINLINE PROCESSING
      *******************************************************************
      * PROCESS ALL CUSTOMER RECORDS IN THE CUSTOMER TOTALS FILE
      * (THIS LOGICAL SELECTS ONLY THE TYPE '1' RECORDS)
      *
     C     *IN90         DOUEQ     *ON
     C                   READ      ARL302B                                90
     C     *IN90         IFEQ      *OFF
      *
     C                   MOVE      NO            FIRST
      *
      * CLEAR WORKFILE RECORD
      *
     C                   CLEAR                   @FMCPCB
      *
      * POPULATE WORKFILE RECORD
      *
     C                   Z-ADD     $TANC7        FMBTC7
     C                   Z-ADD     $TC1AM        FMK9VL
     C                   Z-ADD     $TC2AM        FMLAVL
     C                   Z-ADD     $TC3AM        FMLBVL
     C                   Z-ADD     $TC4AM        FMLCVL
     C                   Z-ADD     $TC5AM        FMLDVL
     C                   Z-ADD     $TC6AM        FMLEVL
     C                   Z-ADD     $TOAAM        FMLFVL
      *
      * CALCULATE TOTAL AMOUNT DUE.
      *
     C                   Z-ADD     FMK9VL        FMLGVL
     C                   ADD       FMLAVL        FMLGVL
     C                   ADD       FMLBVL        FMLGVL
     C                   ADD       FMLCVL        FMLGVL
     C                   ADD       FMLDVL        FMLGVL
     C                   ADD       FMLEVL        FMLGVL
     C                   ADD       FMLFVL        FMLGVL
      *
      * RETRIEVE CUSTOMER INFO FROM THE CUSTOMER HEADING WORKFILE
      *
     C                   EXSR      $CUSTH
      *
      * Retrieve Customer Watch Flag from Customer EXT file
      *
     C                   EXSR      $CUSTW
      *
      * OUTPUT CUSTOMER WORKFLIE RECORD
      *
     C                   WRITE     @FMCPCB
      *
     C                   ENDIF
     C                   ENDDO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      * Exit program
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $CUSTH - RETRIEVE INFO FROM CUSTOMER HEADING WORKFILE
      *----------------------------------------------------------------
     C     $CUSTH        BEGSR
      *
     C     KEY01         CHAIN     ARP300                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     $CAIC3        FMAIC3
     C                   MOVEL     $CPNAM        FMSYTX
     C                   Z-ADD     $CBTC7        FMBTC7
     C                   Z-ADD     $CANC7        FMANC7
     C                   MOVEL     $CAYTX        FMAQTX
     C                   Z-ADD     $CS2NB        FMASVA
     C                   Z-ADD     $CYADP        FMGUPC
     C                   Z-ADD     $CVPVA        FMLHVL
     C                   MOVEL     $CBOCD        FMBOCD
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CUSTW - Retrieve Customer Watch Flag from Customer EXT file
      *----------------------------------------------------------------
     C     $CUSTW        BEGSR
      *
     C                   CALL      'PNJ3XFR'
     C                   parm      *Blank        P0RTN             7
     C                   parm                    FMBTC7
     C     FMP4SX        parm                    FMP4SX
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
     C     *INZSR        BEGSR
      *
      * PARMS
     C     *ENTRY        PLIST
     C                   PARM                    XXAIC3
      *
     C     *LIKE         DEFINE    $CAIC3        XXAIC3
      *
      * KEY LISTS
      *
      *
     C     KEY01         KLIST
     C                   KFLD                    XXAIC3
     C                   KFLD                    $TANC7
      *
     C     KEY02         KLIST
     C                   KFLD                    P0RTN             7
     C                   KFLD                    $TANC7
     C                   KFLD                    FMP4SX
      *
      * FIELDS TO CONTROL PROCESSING
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     NO            PRTSUB            1
     C                   Z-ADD     0             X                 1 0
      *
     C                   ENDSR
      /EJECT
