     A*%%TS  SD  20040206  093046  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1706
     A*  TITLE:         SELECT OPTIONS FOR MAINTAINING FARM LINKS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/05/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'AUTO')
     A                                      CF10(10 'MANUAL')
     A                                      CF15(15 'VIEW')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1706R1
     A*%%TS  SD  20040206  093046  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(39)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2  9'SELECT OPTION 1 OR OPTION 2 FOR MA-
     A                                      INTAINING FARM LINKS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Budget template:'
     A                                      DSPATR(HI)
     A            D1BTCD    R        O  4 20REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            D1BTDS    R        O  4 28REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                  6  2'Option 1: Manual'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  4'This option takes you to the Farm -
     A                                      Link Maintenance screen where'
     A                                  8  4'you can manually add/remove Farms -
     A                                      for the Budget Template.'
     A                                  9  4'To take this option, simply press'
     A                                  9 38'F10=Manual.'
     A                                      DSPATR(HI)
     A                                 12  2'Option 2: Automatic'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13  4'This option automatically adds or -
     A                                      removes Farm Links for you.'
     A                                 14  4'You may limit the farms that are a-
     A                                      dded/removed by entering values bel-
     A                                      ow.'
     A                                 15  4'After making your selections below-
     A                                      , press'
     A                                 15 46'F8=Automatic.'
     A                                      DSPATR(HI)
     A                                 17  2'Add or remove .....'
     A            D1ARFL    R        B 17 24REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 17 63'(A=Add, R=Remove)'
     A                                 18  2'Production type ...'
     A                                 18 22'?'
     A            D1PTCD    R        B 18 24REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A N32                                  DSPATR(HI)
     A  52
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1PTDS    R        O 18 31REFFLD(PTREC/PTPTDS *LIBL/HSP010)
     A                                 18 63'(Blank = All)'
     A                                 19  2'Production phase ..'
     A                                 19 22'?'
     A            D1PPCD    R        B 19 24REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A N33                                  DSPATR(HI)
     A  53
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1PPDS    R        O 19 31REFFLD(PPREC/PPPPDS *LIBL/HSP011)
     A                                 19 63'(Blank = All)'
     A                                 20  2'Type of farm ......'
     A                                 20 22'?'
     A            D1TFCD    R        B 20 24REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A N39                                  DSPATR(HI)
     A  59
     AO 39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A            D1TFDS    R        O 20 31REFFLD(HSRREF/@@TFDS *LIBL/HSPREF)
     A                                 20 63'(Blank = All)'
     A                                 21  2'Cell ..............'
     A            D1CELL    R        B 21 24REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                 21 63'(Blank = All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1706K1
     A*%%TS  SD  20040206  093046  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Automatic'
     A                                 23 36'F10=Manual'
     A                                 23 48'F15=View Farm Links'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1706ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP1706EC                  SFLCTL(HP1706ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
