     A*%%TS  SD  20091002  081720  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD705
     A*  TITLE:         VIEW CASH DISTRIBUTION HEADER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       05/18/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF10(10 'VIEW DEPOSITS')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R TF705R1
     A*%%TS  SD  20091002  081720  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'VIEW CASH DISTRIBUTION HEADER'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Daily date ..'
     A            D1MDY     R        O  3 16REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A                                  3 30'Batch number:'
     A                                      COLOR(PNK)
     A            D1BTSN    R        O  3 44REFFLD(TFREF/@@BTSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  3 52'Status:'
     A                                      COLOR(PNK)
     A            D1STDS    R        O  3 60REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A                                  4  2'Deposit-Prior day total cash colle-
     A                                      cted .:'
     A                                      COLOR(PNK)
     A            D1DPDCCAM R        O  4 44REFFLD(TFREF/@@DPDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  5  2'Deposit-Current day total cash col-
     A                                      lected:'
     A                                      COLOR(PNK)
     A            D1DCDCCAM R        O  5 44REFFLD(TFREF/@@DCDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  6  2'Bank report-Prior day total cash c-
     A                                      ollected .............'
     A            D1BPDCCAM R        O  6 60REFFLD(TFREF/@@BPDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  7  2'Bank report-Current day total cash-
     A                                       collected ...........'
     A            D1BCDCCAM R        O  7 60REFFLD(TFREF/@@BCDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  9  2'Bank report-Opening available amou-
     A                                      nt ...................'
     A            D1BOAAM   R        O  9 60REFFLD(TFREF/@@BOAAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  9 78'+'
     A                                 10  2'Bank report-Current day immediate -
     A                                      availability amount ..'
     A            D1BCDIAAM R        O 10 60REFFLD(TFREF/@@BCDIAAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 10 78'+'
     A                                 11  2'Unapplied wire transfer pre-paymen-
     A                                      ts ...................'
     A            D1WTPPAM  R        O 11 60REFFLD(TFREF/@@WTPPAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 11 78'-'
     A                                 12  2'Required balance in account ......-
     A                                      ......................'
     A            D1RBAM    R        O 12 60REFFLD(TFREF/@@RBAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 12 78'-'
     A                                 13 38'Cash payment amount:'
     A                                      DSPATR(HI)
     A            D1CPAM    R        O 13 60REFFLD(TFREF/@@CPAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 13 78'='
     A                                 15  2'TF distribution adjustment amount -
     A                                      .......'
     A            D1TDAAM   R        O 15 44REFFLD(TFREF/@@TDAAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 16  2'TF distribution adjustment comment-
     A                                       ......'
     A            D1TDACOM  R        O 16 44REFFLD(TFREF/@@TDACOM *LIBL/TFPREF)
     A                                 17  2'AR adjustment payout amount ......-
     A                                      ......:'
     A                                      COLOR(PNK)
     A            D1ARAPAM  R        O 17 44REFFLD(TFREF/@@TCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 18  2'Sales expense cash distributed amo-
     A                                      unt ..:'
     A                                      COLOR(PNK)
     A            D1ASXCDAM R        O 18 44REFFLD(TFREF/@@ASXCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 18 65'Exempt Amounts:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 19  2'SBF cash distributed amount ......-
     A                                      ......:'
     A                                      COLOR(PNK)
     A            D1SCDAM   R        O 19 44REFFLD(TFREF/@@SCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 19 60'SBF:'
     A                                      DSPATR(HI)
     A            D1SEXAM   R        O 19 65REFFLD(TFREF/@@SEXAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 20  2'TF cash distributed amount .......-
     A                                      ......:'
     A                                      COLOR(PNK)
     A            D1TCDAM   R        O 20 44REFFLD(TFREF/@@TCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 20 61'TF:'
     A                                      DSPATR(HI)
     A            D1TEXAM   R        O 20 65REFFLD(TFREF/@@TEXAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 21  2'TF total payment amount ..........-
     A                                      ......:'
     A                                      COLOR(PNK)
     A            D1TTPAM   R        O 21 44REFFLD(TFREF/@@TTPAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 21 60'AGG:'
     A                                      DSPATR(HI)
     A            D1AEXAM   R        O 21 65REFFLD(TFREF/@@AEXAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R TF705K1
     A*%%TS  SD  20050519  090654  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 52'F10=View Assigned Deposits'
     A                                      COLOR(PNK)
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R TF705ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R TF705EC                   SFLCTL(TF705ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
