     A*%%TS  SD  20140306  103914  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD703
     A* DESCRIPTION:  MP2: VIEW INVOICES FOR A RECEIPT
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         04/02/02
     A*
     A*----------------------------------------------------------------
     A* MODIFICATIONS LOG:
     A*----------------------------------------------------------------
     A* MODIFIED BY:  BARB GUTIERREZ 03/30/2011
     A*               QTY AND UNIT COST INCREASED.  NO CHANGES WERE NECESSARY
     A*               TO THE SCREEN.  EVERYTHING STILL FITS.  RECOMPILED
     A*               ONLY.  E001398
     A*
     A* 05/26/21 S17068 Danny Nguyen
     A*          Unit Cost Per Item field length changed from 9.4 to 11.4.
     A*          Shifted 'Unit Cost' right 3 positions.
     A*          Shifted 'Applied Amount' left 2 positions.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP703S1                   SFL
     A*%%TS  SD  20020502  110506  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1INNO    R        O 16  2REFFLD(@@INNO)
     A            D1INMDY   R        O 16 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1INQT    R        O 16 37REFFLD(@@INQT)
     A                                      EDTCDE(K)
     A            D1OVFL    R        O 16 54REFFLD(@@OVFL)
17068A**          D1APAM    R        O 16 63REFFLD(@@APAM)
17068A            D1APAM    R        O 16 61REFFLD(@@APAM)
     A                                      EDTCDE(K)
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP703C1                   SFLCTL(MP703S1)
     A*%%TS  SD  20140306  103914  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 29'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 26'VIEW INVOICES FOR A RECEIPT'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'Receipt number ............:'
     A                                      DSPATR(HI)
     A            C1RCNO    R        O  4 31REFFLD(@@RCNO)
     A                                      EDTCDE(Z)
     A                                  4 58'JDE vendor:'
     A                                      DSPATR(HI)
     A            C1JDVNNO  R        O  4 70REFFLD(@@JDVNNO)
     A                                      EDTCDE(M)
     A                                  5  2'Serial number .............:'
     A                                      DSPATR(HI)
     A            C1SERNO   R        O  5 31REFFLD(@@SERNO)
     A                                      EDTCDE(Z)
     A                                  5 54'Purchase order:'
     A                                      DSPATR(HI)
     A            C1PONO    R        O  5 70REFFLD(@@PONO)
     A                                  6  2'Transaction code ..........:'
     A                                      DSPATR(HI)
     A            C1TTCD    R        O  6 31REFFLD(@@TTCD)
     A                                  6 51'Purchasing center:'
     A                                      DSPATR(HI)
     A            C1PCID    R        O  6 70REFFLD(@@PCID)
     A                                  7  2'Transaction quantity ......:'
     A                                      DSPATR(HI)
     A            C1TRQT    R        O  7 31REFFLD(@@TRQT)
     A                                      EDTCDE(K)
     A                                  7 50'Accounting company:'
     A                                      DSPATR(HI)
     A            C1ACONO   R        O  7 70REFFLD(@@ACONO)
     A                                      EDTCDE(Z)
     A                                  8  2'Unit cost .................:'
     A                                      DSPATR(HI)
17068A**          C1ITEMRT  R        O  8 31REFFLD(@@ITEMRT)
17068A            C1ITEMRT  R        O  8 34REFFLD(@@ITEMRT)
     A                                      EDTCDE(K)
     A                                  9  2'Cost center/account obj/sub:'
     A                                      DSPATR(HI)
     A            C1MCU     R        O  9 31REFFLD(@@MCU)
     A            C1OBJ     R        O  9 44REFFLD(@@OBJ)
     A            C1SUB     R        O  9 51REFFLD(MPREC/@@SUB *LIBL/MP2REF)
     A                                 10  2'Item number ....:'
     A                                      DSPATR(HI)
     A            C1ITEMNO  R        O 10 20REFFLD(@@ITEMNO)
     A                                 11  2'Item description:'
     A                                      DSPATR(HI)
     A            C1ITEMDS  R        O 11 20REFFLD(@@ITEMDS)
     A                                 13 51'Override'
     A                                      DSPATR(HI)
     A                                 14 26'Invoice'
     A                                      DSPATR(HI)
     A                                 14 38'Invoiced'
     A                                      DSPATR(HI)
     A                                 14 50'Calculated'
     A                                      DSPATR(HI)
     A                                 14 68'Applied'
     A                                      DSPATR(HI)
     A                                 15  2'Invoice Number'
     A                                      DSPATR(HI)
     A                                 15 28'Date'
     A                                      DSPATR(HI)
     A                                 15 38'Quantity'
     A                                      DSPATR(HI)
     A                                 15 52'Amount'
     A                                      DSPATR(HI)
     A                                 15 69'Amount'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP703K1
     A*%%TS  SD  20011024  165925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP703ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R MP703EC                   SFLCTL(MP703ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
