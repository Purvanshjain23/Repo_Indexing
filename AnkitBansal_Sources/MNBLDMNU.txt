/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  MNBLDMNU                                      */
/*  PROGRAM NAME:    BUILD MENU DATAMART FILES                     */
/*  PROGRAMMER:      BARB GUTIERREZ                                */
/*  CREATE DATE:     04/27/05                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED NIGHTLY FROM THE JOB     */
/*                   SCHEDULER ON BIGBYTE UNDER JOB BIBLDMNU.      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:    BARB GUTIERREZ   2/16/2006                    */
/*                   ADDED THE PROCESSING OF DAILY FOODS (DF)      */
/* MODIFICATIONS:    BARB GUTIERREZ   10/17/2007                   */
/*                   ADDED THE PROCESSING OF THE NEW DAILY FOODS   */
/*                   ENVIRONMENT (DPM) AND HIGH PLAINS BIODIESEL   */
/*                   (HPB)                                         */
/*                                                                 */
/* 08/14/09  ALICE BROWNFIELD                                      */
/*           E00472 - Data Mart Build changes for DR Test          */
/*           Change CPYF from PRKFLIB to PRKDMFLIB to be CRTDUPOBJ */
/*           ----  this works better for MIMIX                     */
/*                                                                 */
/* 09/28/09  BARB GUTIERREZ                                        */
/*           E00472 - Removed the check of the error processing    */
/*           flag on the crtdupobj.                                */
/*                                                                 */
/* 04/28/10  LeAnne Ramsey                                         */
/*           We now allocate/deallocate the files that are         */
/*           deleted/created in PRKDMFLIB.                         */
/*           Also, we now use ESEND instead of MPLUS/MPLUS.        */
/*                                                                 */
/* 04/29/10  LeAnne Ramsey                                         */
/*           To eliminate some of the hardcoding of PRKDMFLIB,     */
/*           we will now add it to the library list.               */
/*           Also, since the users cannot call this from a Menu,   */
/*           I removed the AS400 SNDMSG to the user.               */
/*           Note: Leave the MONMSG on the CRTDUPOBJ of the LFs    */
/*           monitoring for CPF0000 without setting the error flag.*/
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA)    TYPE(*CHAR) LEN(247)
             DCL        VAR(&ERR)      TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&EMAIL)    TYPE(*CHAR) LEN(38)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SUBJECT)  TYPE(*CHAR) LEN(60) +
                          VALUE('MNBLDMNU-Datamart MENU: Build +
                          Files....DIST LIST: prk cognos')

/*------------------------------------------------------------------*/
/* Set up the Email Recipient (either Test or Production)           */
/*------------------------------------------------------------------*/

/* Retrieve the Data Area that lets us know whether we are in TEST  */
/* or PRODUCTION. (Always default to the 'TEST' Distribution List.) */

             CHGVAR     VAR(&EMAIL) VALUE('prk cognos test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)
             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('prk cognos'))

/*-----------------------------------------------------------------*/
/* Add PRKDMFLIB to the top of the library list.                   */
/* We do not expect PRKDMFLIB to already be in the library list;   */
/* but, since we want it at the top of the list, we will remove/add*/
/* it.                                                             */
/*-----------------------------------------------------------------*/
             RMVLIBLE   LIB(PRKDMFLIB)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(PRKDMFLIB)

/*-----------------------------------------------------------------*/
/* Allocate all files that will be deleted/recreated in PRKDMFLIB  */
/*-----------------------------------------------------------------*/

/* Menu User Access file  */

             ALCOBJ     OBJ((*LIBL/MNP001 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&ERR) VALUE('Y')
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) MSG('No +
                          Datamart files were rebuilt because the +
                          program was unable to allocate file +
                          MNP001. Check the physical (and logical +
                          files over it) to see who has the file(s) +
                          locked.') IMPORTANCE(*HIGH)
                        ENDDO

             IF         COND(&ERR = 'Y') THEN(GOTO CMDLBL(EXIT))

/*-----------------------------------------------------------------*/
/* DO NOT PROCESS COMPANIES ALM, CHF, JPI, SML, SSM, TEST, OR TOOLS*/
/*-----------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA 1 8)) VALUE('COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 10 7)) VALUE('"COR  "')
             CHGVAR     VAR(%SST(&QDATA 18 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 31 7)) VALUE('"GUY  "')
             CHGVAR     VAR(%SST(&QDATA 39 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 52 7)) VALUE('"IS   "')
             CHGVAR     VAR(%SST(&QDATA 60 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 73 7)) VALUE('"PDV  "')
             CHGVAR     VAR(%SST(&QDATA 81 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 94 7)) VALUE('"PRK  "')
             CHGVAR     VAR(%SST(&QDATA 102 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 115 7)) VALUE('"SOL  "')
             CHGVAR     VAR(%SST(&QDATA 123 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 136 7)) VALUE('"STC  "')
             CHGVAR     VAR(%SST(&QDATA 144 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 157 7)) VALUE('"STS  "')
             CHGVAR     VAR(%SST(&QDATA 165 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 178 7)) VALUE('"TF   "')
             CHGVAR     VAR(%SST(&QDATA 186 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 199 7)) VALUE('"DF   "')
             CHGVAR     VAR(%SST(&QDATA 207 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 220 7)) VALUE('"DPM  "')
             CHGVAR     VAR(%SST(&QDATA 228 12)) VALUE('*OR COID *EQ')
             CHGVAR     VAR(%SST(&QDATA 241 7)) VALUE('"HPB  "')

             OVRDBF     FILE(WMAUTH) SHARE(*YES)
             OPNQRYF    FILE((WMAUTH)) QRYSLT(&QDATA) KEYFLD(*FILE)

/*-----------------------------------------------------------------*/
/* POPULATE DATAMART FILES IN PRKFLIB                              */
/*-----------------------------------------------------------------*/

             CALL       PGM(MN200) /*BUILD AS/400 MENUS */

             CLOF       OPNID(WMAUTH)
             DLTOVR     FILE(WMAUTH)

/*-----------------------------------------------------------------*/
/* ALWAYS DELETE ALL LOGICALS & THE PHYSICAL FILE IN PRKDMFLIB     */
/*-----------------------------------------------------------------*/

             DLTF       FILE(*LIBL/MNL001*)
             MONMSG     MSGID(CPF0600 CPF2100 CPF3200)

             DLTF       FILE(*LIBL/MNP001)
             MONMSG     MSGID(CPF0600 CPF2100 CPF3200)

/*---------------------------------------------------------------------*/
/* CREATE THE FILES BACK INTO PRKDMFLIB FROM PRKFLIB                   */
/*---------------------------------------------------------------------*/

             CRTDUPOBJ  OBJ(MNP001) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*YES)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

             CRTDUPOBJ  OBJ(MNL001*) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB)
             MONMSG     MSGID(CPF0000)

/*---------------------------------------------------------------------*/
/* SEND COMPLETION MESSAGE TO I.S. FOLKS                               */
/*---------------------------------------------------------------------*/
             IF         COND(&ERR = 'Y') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('Some MNPxxx or MNLxxx files did not +
                          get recreated in PRKDMFLIB. If the preceeding +
                          DELETEs were successful, you may be +
                          missing files in PRKDMFLIB. Investigate +
                          and rerun the build.') IMPORTANCE(*HIGH))

             IF         COND(&ERR = 'N') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('All files were successfully created +
                          in PRKDMFLIB; the build completed +
                          normally.') IMPORTANCE(*HIGH))

/*---------------------------------------------------------------------*/
/* Deallocate files                                                    */
/*---------------------------------------------------------------------*/

 EXIT:
             DLCOBJ     OBJ((*LIBL/MNP001 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)

             ENDPGM

