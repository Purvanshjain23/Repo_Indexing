     A*%%TS  SD  20030227  112231  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4077
     A*  TITLE:         SPECIFY OPTIONS FOR IDLE FARMS REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       06/11/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4077R1
     A*%%TS  SD  20030227  112231  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 17'SPECIFY OPTIONS FOR IDLE FARMS REP-
     A                                      ORT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'From date .........'
     A            D1FMDY    R     Y  B  4 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  5  2'To date ...........'
     A            D1TMDY    R     Y  B  5 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'Business office ...'
     A N99                              6 22'?'
     A N99        D1FSBO    R        B  6 24REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A N33                                  DSPATR(HI)
     A  53
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1BODS    R        O  6 34REFFLD(BOBODS)
     A                                  6 65'(Blank for All)'
     A                                  7  2'Production type ...'
     A N99                              7 22'?'
     A N99        D1PTCD    R        B  7 24REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A N30                                  DSPATR(HI)
     A  50
     AO 30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A            D1PTDS    R        O  7 34REFFLD(PTREC/PTPTDS *LIBL/HSP010)
     A                                  7 65'(Blank for All)'
     A                                  8  2'Production phase ..'
     A N99                              8 22'?'
     A N99        D1PPCD    R        B  8 24REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  51
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1PPDS    R        O  8 34REFFLD(PPREC/PPPPDS *LIBL/HSP011)
     A                                  8 65'(Blank for All)'
     A                                  9  2'Cell ..............'
     A N99        D1CELL    R        B  9 24REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                  9 65'(Blank for All)'
     A                                 10  2'Active/inactive ...'
     A N99                             10 22'?'
     A N99        D1AIST    R        B 10 24REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A N32                                  DSPATR(HI)
     A  52
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 10 65'(Blank for All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4077K1
     A*%%TS  SD  19980601  163600  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit Listing'
     A N99                             23 41'F17=Print Options'
     A N99                             23 60'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4077ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4077EC                  SFLCTL(HP4077ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
