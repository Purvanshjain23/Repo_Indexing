/*                                                                 */
/*  PROGRAM NUMBER:  HP419CL                                       */
/*  PROGRAM NAME:    HPE/HPS COMPARISON LISTING                    */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     09/15/00                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP419-SPECIFY HPE/HPS COMPARISON LISTING.     */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*  03/03/08  LEANNE RAMSEY                                        */
/*            SEABOARD IS NOW SELLING THEIR PIGS TO OUTSIDE PLANTS.*/
/*            SO, ADDED LOGIC TO SELECT ONLY RECORDS FOR CUSTOMER  */
/*                                                                 */
/*  11/08/17  BRAD BADEN   E010812                                 */
/*            ADDED BUSINESS OFFICE AS A REQUIRED SELECTION.       */
/*            THE VALUE IS STORED IN THE LDA POSITIONS 17 - 21.    */
/*            IF BUSINESS OFFICE IS 'OKLIV', ONLY PROCESS CUSTOMER */
/*            360516.                                              */
/*                                                                 */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(100)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 9 8))
             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 17 5))


/*  ALWAYS SELECT RECORDS WITH KILL DATE IN THE RANGE              */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('CDKLDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND CDKLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  Always select records for the Business Office selected       */
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND HGFSBO +
                          *EQ ''')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('''')

/*  IF BUSINESS OFFICE IS 'OKLIV', ONLY PROCESS CUSTOMER 360516  */
             IF         COND(&LDFSBO = 'OKLIV') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 70 22)) VALUE('*AND CDCVNO +
                          *EQ 360516')
             ENDDO

/*  PERFORM THE OPNQRY ON THE CHECK DETAIL FILE                     */

             OVRDBF     FILE(HSJ064J) SHARE(*YES)
             OPNQRYF    FILE((HSJ064J)) QRYSLT(&QDATA) +
                          KEYFLD((CDTPCD) (CDKLDT) (CDCVNO) +
                          (CDTANO)) SEQONLY(*YES)

/* SET OUTPUT PARMS FROM LDA                       */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT OF 15CPI ON 198 COLUMNS OF PRINT  */
/* FORCE IT TO PRINT OUT OF DRAWER 2 ON LANDSCAPE                   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE LISTING                                                 */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP619)

             CLOF       OPNID(HSJ064J)
             DLTOVR     FILE(*ALL)

             ENDPGM

