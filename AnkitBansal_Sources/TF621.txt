      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF621 - Eopfees: Verification for Freight Variance
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     04/25/06
      *
      * Function:    This program prints for a specific Year/Period:
      *                TFP017-Freight Variance records
      *                TFP028-Freight Variance Total records
      *
      *              Printing is done by the "Method" used to allocate the Freight Variance
      *              to SBF and Triumph.
      *
      *              There are 3 "methods".
      *
      ****************************************************************************************
      * MODIFICATIONS:
      ****************************************************************************************
      * DATE      PROGRAMMER
      *
      * 04/16/07  LeAnne Ramsey
      *           Recompile only. Field "Exclude from Mix Flag' was renamed to
      *           Mix Flag in the Freight Variance file.
      *
      * 05/14/07  LeAnne Ramsey
      *           Added 'Mix Flag' to the Item Structure Heading.
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Fombyrel0  if   e           k disk
      *    Item default
      *
      *
     Fome8rel0  if   e           k disk
      *    OM Product type
      *
      *
     Fome9rel0  if   e           k disk
      *    OM Product group
      *
      *
     Fomfarel1  if   e           k disk
      *    OM Product class
      *
      *
     Ftfl017b   if   e           k disk
      *    Freight Variance
      *
      *
     Ftfl017c   if   e           k disk    rename(fvrec:fvrecc)
      *    Freight Variance
      *
      *
     Ftfp028    if   e           k disk
      *    Freight Variance Totals
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     d aralb           s             11  2 dim(1)
     d arslb           s             11  2 dim(1)
     d artlb           s             11  2 dim(1)
      *
     d arevam          s             11  2 dim(1)
     d arevampc        s              7  4 dim(1)
      *
     d arafvam         s             11  2 dim(1)
     d arsfvam         s             11  2 dim(1)
     d artfvam         s             11  2 dim(1)
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d dash198         s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d first           s              1    inz('Y')
     d printfl         s              1    inz('N')
     d count           s              3  0
      *
      *
      * Workfields
      *
     D wkmethod        s                   like(ftmethod)
      *
     D wkistycd        s              3
     D wkistyds        s                   like(e8dctx)
     D wkisgrcd        s              3
     D wkisgrds        s                   like(e9ddtx)
     D wkisclcd        s              3
     D wkisclds        s                   like(fadetx)
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      *
      * Print fields
      *
     d h1demand        s             26
     d h1desc          s              8
     d h1fbam          s                   like(fvfbam)
     d h1aevam         s                   like(fvaevam)
     d h1afoam         s                   like(fvafoam)
     d h1fvam          s                   like(fvfvam)
      *
     d h2desc          s            160
      *
     d l1prds          s                   like(byavna)
      *
     d t1spflbpc       s                   like(fvspflbpc)
     d t1tpflbpc       s                   like(fvtpflbpc)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldpe                   6      7  0
      *
     D  ldcbmdy               16     21  0
      *
     D  ldcedt                29     36  0
     D  ldcemdy               37     42  0
      *
     D  ldrdfl               300    300
     D  ldemail              350    399
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Retrieve/Print data based on Freight Allocation Method 1:
      *
     C                   seton                                        8081
     C                   z-add     1             wkmethod
     C                   eval      h1desc = 'Method 1'
     C                   exsr      $method
      *
      * Retrieve/Print data based on Freight Allocation Method 2:
      *
     C                   setoff                                       8081
     C                   seton                                        82
     C                   z-add     2             wkmethod
     C                   eval      h1desc = 'Method 2'
     C                   exsr      $method
      *
      * Retrieve/Print data based on Freight Allocation Method 3:
      *
     C                   setoff                                       82
     C                   seton                                        80
     C                   z-add     3             wkmethod
     C                   eval      h1desc = 'Method 3'
     C                   exsr      $method
      *
      *
      * EOP processing
      *   1) Print Report Total
      *
     C                   if        printfl = yes
     C                   exsr      $rpttot
     C                   else
     C                   exsr      $h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Logic for printing each "Method"
      *---------------------------------------------------------------
      *
     C     $method       begsr
      *
     C                   move      yes           first
      *
     C     key01         setll     tfp028
      *
     C                   dou       *in90 = *on                                  Do method
     C     key01         reade     tfp028                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        first = yes
     C                   exsr      $h1hdr
     C                   move      no            first
     C                   endif
      *
      * Print headings
     C                   if        wkmethod = 2
     C                   exsr      $desc
     C                   endif
     C                   exsr      $h2hdr
      *
      * Retrieve/print detail
      *
     C                   exsr      $l1dtl
      *
      * Print total line
      *
     C                   exsr      $t1tot
      *
      * Accumulate for Report Total
      *
     C                   if        ftmethod = 1
     C                   add       ftapfslb      aralb
     C                   add       ftspfslb      arslb
     C                   add       fttpfslb      artlb
     C                   else
     C                   add       ftapflb       aralb
     C                   add       ftspflb       arslb
     C                   add       fttpflb       artlb
     C                   endif
      *
     C                   add       ftevam        arevam
     C                   add       ftevampc      arevampc
      *
     C                   add       ftafvam       arafvam
     C                   add       ftsfvam       arsfvam
     C                   add       fttfvam       artfvam
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do method
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve "Descriptions" for the Method 2 headings.
      *---------------------------------------------------------------
      *
     C     $desc         begsr
      *
      * Item structure type code
      *
     C     ftistycd      chain     ome8rel0                           92
     C                   if        *in92 = *off
     C                   move      e8dctx        wkistyds
     C                   else
     C                   move      *blank        wkistyds
     C                   endif
      *
      * Item structure group code
      *
     C     ftisgrcd      chain     ome9rel0                           92
     C                   if        *in92 = *off
     C                   move      e9ddtx        wkisgrds
     C                   else
     C                   move      *blank        wkisgrds
     C                   endif
      *
      * Item structure class code
      *
     C     ftisclcd      chain     omfarel1                           92
     C                   if        *in92 = *off
     C                   move      fadetx        wkisclds
     C                   else
     C                   move      *blank        wkisclds
     C                   endif
      *
      * Now, concatentate the 3 codes and their descriptions into 1 long
      * print field and tack on the Mix Flag.
      *
      * Move numeric "code" fields to alpha fields.
      *
     C                   move      ftistycd      wkistycd
     C                   move      ftisgrcd      wkisgrcd
     C                   move      ftisclcd      wkisclcd
      *
     C                   eval      h2desc = 'ITEM STRUCTURE:  '  +
     C                             'Type:  ' + %trim(wkistycd) + '  '
     C                                       + %trim(wkistyds) + '  ' +
     C                             'Group: ' + %trim(wkisgrcd) + '  '
     C                                       + %trim(wkisgrds) + '  ' +
     C                             'Class: ' + %trim(wkisclcd) + '  '
     C                                       + %trim(wkisclds) + '   ' +
     C                             'Mix Flag: ' + ftmixfl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print structure headings
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    h2hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C   82key02         setll     tfl017c
     C  n82key03         setll     tfl017b
      *
     C                   dou       *in91 = *on                                  Do detail
     C   82key02         reade     tfl017c                                91
     C  n82key03         reade     tfl017b                                91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Retrieve the Product Description
      *
     C     fvprcd        chain     ombyrel0                           92
     C                   if        *in92 = *off
     C                   movel(p)  byavna        l1prds
     C                   else
     C                   move      *blank        l1prds
     C                   endif
      *
      * Print headings
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
      * Print detail line
     C                   except    l1dtl
     C                   move      yes           printfl
      *
     C                   if        wkmethod = 3
     C                   add       1             count
     C                   endif

     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print total line for Classification
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   z-add     1             x
      *
     C                   if        wkmethod = 3 and count = 1                   If only 1
     C                   except    blank
     C                   else
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
      * Print Total line
      *
     C                   except    t1tot
     C                   endif                                                  If only 1
      *
      * Initialize workfield.
      *
     C                   z-add     0             count
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report total line
      *---------------------------------------------------------------
      *
     C     $rpttot       begsr
      *
     C                   if        *inof = *on
     C                   move      *blank        h1desc
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   z-add     1             x
      *
      * Calculate % of Prepaid Freight Pounds
      *
     C                   exsr      $lbpc
      *
     C                   seton                                        87
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Calculate % of Prepaid Freight Pounds
      *------------------------------------------------------------------------------------
      *
     C     $lbpc         begsr
      *
     C                   select
      * 100% to Seaboard
     C                   when      artlb(x) = 0
     C                   z-add     100           t1spflbpc
      * 100% to Triumph
     C                   when      arslb(x) = 0
     C                   z-add     100           t1tpflbpc
      * Both SBF and TF
     C                   when      aralb(x) <> 0
     C                   eval      t1spflbpc = (arslb(x) / aralb(x)) * 100
     C                   eval      t1tpflbpc = 100 - t1spflbpc
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcedt
     C                   kfld                    wkmethod
      *
     C     key02         klist
     C                   kfld                    ldcedt
     C                   kfld                    wkmethod
     C                   kfld                    ftistycd
     C                   kfld                    ftisgrcd
     C                   kfld                    ftisclcd
      *
     C     key03         klist
     C                   kfld                    ldcedt
     C                   kfld                    wkmethod
     C                   kfld                    fttfcgcd
     C                   kfld                    fttfclcd
     C                   kfld                    ftistycd
      *
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
     C     ldcedt        chain     tfl017c                            92
     C                   if        *in92 = *off
     C                   z-add     fvfbam        h1fbam
     C                   z-add     fvaevam       h1aevam
     C                   z-add     fvafoam       h1afoam
     C                   z-add     fvfvam        h1fvam
     C                   endif
     C
      *
      * This report is generated on-demand and also from the submitted
      * from the EOP Close function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      h1demand = '        On-Demand        '
     C                   seton                                        91
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1demand = '   Final EOP Fees Close  '
      *
     C                   when      ldpfcd = 'P'
     C                   eval      h1demand = 'Preliminary EOP Fees Close'
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report headings
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           97 'VERIFICATION FOR FREIGHT'
     O                                          106 ' VARIANCE'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           102
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           18 'Year/Period .....:'
     O                       ldyr          z     24
     O                       ldpe          z     28
      *
     O          e            h1hdr          1
     O                                           18 'Period begin date:'
     O                       ldcbmdy             28 '  /  /  '
     O                                           69 'Total freight payable '
     O                                           77 'balance:'
     O                       h1fbam        k     93
      *
     O                                          135 'Total estimated variance:'
     O                       h1aevam       k    151
      *
     O          e            h1hdr          1
     O                                           18 'Period end date .:'
     O                       ldcemdy             28 '  /  /  '
     O                                           49 'Total estimated '
     O                                           69 'outstanding freight '
     O                                           77 'payable:'
     O                       h1afoam       k     93
      *
      *
     O          e            h1hdr          1
     O                                           92 '--------------'
      *
     O          e            h1hdr          2
     O                       h1desc               8
     O                                           60 'Total freight'
     O                                           77 ' variance amount:'
     O                       h1fvam        k     93
      *
      *-------------------------------------------------------------
      * Column headings for Section 2: Freight Variance records
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
      *
     O              n81                          77 '----- Prepaid Freight '
     O              n81                          99 'Pounds from the Weekly'
     O              n81                         121 ' Revenue Records -----'
      *
     O               81                          77 '-- Prepaid Freight Sta'
     O               81                          99 'rt Pounds from the Wee'
     O               81                         121 'kly Revenue Records --'
      *
     O                                          149 '% of Tot'
      *
     O          e            h1hdr       0  1
     O                                            8 'Product'
     O               81                          52 'Yield'
      *
     O                                           69 'Aggregate'
     O                                           85 'SBF'
     O                                           95 'SBF'
     O                                          111 'TF'
     O                                          121 'TF'
      *
     O                                          138 'Estimated'
     O                                          149 'Estimated'
      *
     O                                          162 '---------- '
     O                                          186 'Freight Variance Amount'
     O                                          198 '-----------'
      *
     O          e            h1hdr       0  1
     O                                            8 'Code'
     O                                           29 'Product Description'
     O               81                          52 'Percent'
      *
     O                                           69 'Pounds'
     O                                           85 'Pounds'
     O                                           95 'Percent'
     O                                          111 'Pounds'
     O                                          121 'Percent'
      *
     O                                          138 'Variance'
     O                                          149 'Variance'
      *
     O                                          166 'Aggregate'
     O                                          181 'Seaboard'
     O                                          197 'Triumph'
      *
     O          e            h1hdr          1
     O                       dash198            198
      *
      *-------------------------------------------------------------
      * Structure headings
      *-------------------------------------------------------------
      *
     O          e    80      h2hdr       1  0
     O                                           20 'TF Class Group Code:'
     O                       fttfcgcd            24
     O                                           50 'TF Classification Code:'
     O                       fttfclcd            55
     O                                           84 'Item Structure Type Code:'
     O                       ftistycd      z     89
     O                                          102 'Mix Flag:'
     O                       ftmixfl            104
      *
      *
     O          e    80      h2hdr          1
     O                                           20 'TF Class Group Code:'
     O                       fttfcgcd            24
     O                                           50 'TF Classification Code:'
     O                       fttfclcd            55
     O                                           84 'Item Structure Type Code:'
     O                       ftistycd      z     89
     O                                          102 'Mix Flag:'
     O                       ftmixfl            104
      *
      *
     O          e    82      h2hdr       1  0
     O                       h2desc             160
      *
     O          e    82      h2hdr          1
     O                       h2desc             160
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
      *
     O                       fvprcd        z      8
     O                       l1prds              40
     O               81      fvypc         k     53
      *
     O              n81      fvapflb       k     70
     O               81      fvapfslb      k     70
     O              n81      fvspflb       k     86
     O               81      fvspfslb      k     86
     O                       fvspflbpc     k     96
     O              n81      fvtpflb       k    112
     O               81      fvtpfslb      k    112
     O                       fvtpflbpc     k    122
      *
     O                       fvevam        k    139
     O                       fvevampc      k    150
      *
     O                       fvafvam       k    167
     O                       fvsfvam       k    182
     O                       fvtfvam       k    198
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       1  1
     O              n87                          69 '--------------'
     O              n87                          85 '--------------'
     O              n87                          95 '--------'
     O              n87                         111 '--------------'
     O              n87                         121 '--------'
      *
     O              n87                         138 '--------------'
     O              n87                         149 '--------'
      *
     O              n87                         166 '--------------'
     O              n87                         181 '--------------'
     O              n87                         197 '--------------'
      *
     O          e   n87      t1tot       0  1
     O              n81      ftapflb       k     70
     O               81      ftapfslb      k     70
     O              n81      ftspflb       k     86
     O               81      ftspfslb      k     86
     O                       ftspflbpc     k     96
     O              n81      fttpflb       k    112
     O               81      fttpfslb      k    112
     O                       fttpflbpc     k    122
      *
     O                       ftevam        k    139
     O                       ftevampc      k    150
      *
     O                       ftafvam       k    167
     O                       ftsfvam       k    182
     O                       fttfvam       k    198
      *
     O          e    87      t1tot       0  1
     O                                           50 'Report Total:'
     O                       aralb  (x)    kb    70
     O                       arslb  (x)    kb    86
     O                       t1spflbpc     kb    96
     O                       artlb  (x)    kb   112
     O                       t1tpflbpc     kb   122
      *
     O                       arevam (x)    kb   139
     O                       arevampc(x)   kb   150
      *
     O                       arafvam(x)    kb   167
     O                       arsfvam(x)    kb   182
     O                       artfvam(x)    kb   198
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       2
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           20 'No freight data was'
     O                                           31 ' retrieved.'
      /eject
