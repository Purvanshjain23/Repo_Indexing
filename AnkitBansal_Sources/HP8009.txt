      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP8009 - Calculations Used in Wean Pig Predictions
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/06/02
      *
      * Function:  This function calculates various factors used in Wean Pig Predictions
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields for calculations
      *
     D wkfld155        s             15  5
     D wkfld152        s             15  2
      *
      *
      * Parm fields
      *
     D xxrsgrsv        s              9  0
     D xxrpgrhd        s              9  0
     D xxfsgrhd        s              9  0
     D xxfpgrhd        s              9  0
     D xxrfgrhd        s              9  0
     D xxtolt          s              9  0
     D xxbahd          s              9  0
     D xxpmhd          s              9  0
     D xxnrmhd         s              9  0
     D xxnajtihd       s              9  0
     D xxfrmhd         s              9  0
     D xxfajtihd       s              9  0
      *
     D xxltbahd        s              5  2
     D xxcrpr          s              5  2
     D xxfrpr          s              5  2
     D xxfopr          s              5  2
     D xxpmpr          s              5  2
     D xxnrmpr         s              5  2
     D xxfrmpr         s              5  2
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      *
      * Conception rate:
      *      Report PCP head group / Report PCP services group
      *
     C                   if        xxrsgrsv <> 0
     C     xxrpgrhd      div       xxrsgrsv      wkfld155
     C     wkfld155      mult(h)   100           xxcrpr
     C                   endif
      *
      *
      * Farrowing rate:
      *      Report PCP head group / Report PCP services group
      *
     C                   if        xxfsgrhd <> 0
     C     xxrfgrhd      div       xxfsgrhd      wkfld155
     C     wkfld155      mult(h)   100           xxfrpr
     C                   endif
      *
      * Fall-out rate:
      *      (Report farrowed PCP head group - report farrowed head group) /
      *       Report farrowed PCP head group
      *
     C                   if        xxfpgrhd <> 0
     C                   eval      wkfld155 =(xxfpgrhd - xxrfgrhd) / xxfpgrhd
     C     wkfld155      mult(h)   100           xxfopr
     C                   endif
      *
      *
      * Born alive per litter
      *
     C                   if        xxtolt <> 0
     C     xxbahd        div(h)    xxtolt        wkfld152
     C                   z-add     wkfld152      xxltbahd
     C                   endif
      *
      *
      * Piglet Mortality Rate %:
      *    (total piglet mortality / total born alive) * 100
      *                where total piglet mortality is:
      *    total prewean losses + rejected head from transfers + quality head from transfers +
      *    live head from sales - total weaned head
      *
     C                   if        xxbahd <> 0
     C     xxpmhd        div       xxbahd        wkfld155
     C     wkfld155      mult(h)   100           xxpmpr
     C                   endif
      *
      *
      * Nursery removal rate:
      *      removal head / adjusted transfer in head
      *
     C                   if        xxnajtihd <> 0
     C     xxnrmhd       div       xxnajtihd     wkfld155
     C     wkfld155      mult(h)   100           xxnrmpr
     C                   endif
      *
      *
      * Finsher removal rate:
      *      removal head / adjusted transfer in head
      *
     C                   if        xxfajtihd <> 0
     C     xxfrmhd       div       xxfajtihd     wkfld155
     C     wkfld155      mult(h)   100           xxfrmpr
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxrsgrsv
     C                   parm                    xxrpgrhd
     C                   parm                    xxfsgrhd
     C                   parm                    xxfpgrhd
     C                   parm                    xxrfgrhd
     C                   parm                    xxtolt
     C                   parm                    xxbahd
     C                   parm                    xxpmhd
     C                   parm                    xxnrmhd
     C                   parm                    xxnajtihd
     C                   parm                    xxfrmhd
     C                   parm                    xxfajtihd
      * Output
     C                   parm                    xxltbahd
     C                   parm                    xxcrpr
     C                   parm                    xxfrpr
     C                   parm                    xxfopr
     C                   parm                    xxpmpr
     C                   parm                    xxnrmpr
     C                   parm                    xxfrmpr
      *
     C                   endsr
      /eject
