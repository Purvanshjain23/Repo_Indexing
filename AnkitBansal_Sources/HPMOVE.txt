      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPMOVE
      * TITLE:       GENERIC GROUP HOG MOVEMENTS CALCULATIONS
      * PROGRAMMER:  ALICE BROWNFIELD
      * CREATED:     6/02/94
      *
      * NOTE:        THIS PROGRAM DOES NOT SET ON LR--IT USES RETURN.
      *              SO, IF YOUR PROGRAM CALLS THIS PROGRAM OR A
      *              PROGRAM THAT CALLS THIS PROGRAM, THEN YOU MUST
      *              CALL THE CLP TO RECLAIM RESOURCES AS YOU EXIT THE
      *              CALLING PROGRAM.
      *
      * FUNCTION: GENERIC PROGRAM TO BE CALLED FROM MULTIPLE PROGRAMS
      *           TO DETERMINE THE INVENTORY OF A GROUP.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 09/27/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED THE FOLLOWING UNUSED
      *           DATABASE FIELDS FROM THE CHECK FILES:
      *             HSP063-CHECK HEADER        CHCHBO CHHRDT
      *             HSP064-CHECK DETAIL        CDCHBO CDCSAM
      *             HSP065-CHECK ADJUSTMENTS   CJCHBO
      *
      * 02/07/95  LEANNE FEDOR
      *           CHANGED PROGRAM TO 'RETURN' INSTEAD OF SETTING ON THE
      *           LAST RECORD INDICATOR.
      *
      * 04/18/95  LEANNE FEDOR
      *           RECOMPILE ONLY.  FARM SITE CHANGED TO NUMERIC FIELD.
      *
      * 07/05/95  LEANNE FEDOR
      *           REWRITTEN FOR MOVEMENT REWRITE.
      *
      * 02/06/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO SALES MOVEMENT HEADER FILE.
      *
      * 02/13/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO TRANSFER MOVEMENT HEADER FILE.
      *
      * 08/07/96  LEANNE FEDOR
      *           INCREASED THE SIZE OF THE 'POUNDS' PARMS FOR HPMOVE
      *           FROM 5,0 TO 9,2.
      *
      * 03/18/97  LEANNE FEDOR
      *           RECOMPILE ONLY. UPDATE TIME STAMP ADDED TO MOVEMENT
      *           EVENT FILE.
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      * 04/27/06  LeAnne Fedor
      *           Changed the "NC" (not-complete sales movement) logic. We
      *           now handle NC like "FP" (fully paid) instead of like
      *           "SH" (shipped).
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL058D   IF   E           K DISK
      *  MOVEMENT EVENT               (KEY: MEHGSN MEMEDT)
      *
     FHSP068    IF   E           K DISK
      *  KILLED/DEAD HOGS             (KEY: KDMVSN)
      *
     FHSP069    IF   E           K DISK
      *  INVENTORY ADJUSTMENT         (KEY: IAMVSN)
      *
     FHSP074    IF   E           K DISK
      *  TRANSFER HEADER             (KEY: MHMVSN)
      *
     FHSP075    IF   E           K DISK
      *  TRANSFER DETAIL              (KEY: MDMVSN MDLNNO)
      *
     FHSP084    IF   E           K DISK
      *  SALE HEADER                 (KEY: SHMVSN)
      *
     FHSP085    IF   E           K DISK
      *  SALE DETAIL                 (KEY: SGMVSN SGHGSN)
      *
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
     D                 DS
     D  DUMMYD                 1      1
      *
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
     I/EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * SINCE THIS PROGRAM USES 'RETURN' INSTEAD OF SETTING ON LR,
      * YOU MUST FORCE THE PROGRAM TO GET THE INCOMING PARMS AND CLEAR
      * THE OUTGOING PARM FIELDS EACH TIME THIS PROGRAM IS CALLED.
      *
     C                   EXSR      $CLEAR
      *
      *---------------------------------------------------------------
      * PROCESS ALL MOVEMENTS FOR A HOG GROUP THROUGH THE CUTOFF DATE
      *---------------------------------------------------------------
      *
     C     XXHGSN        SETLL     HSL058D
      *
      * PROCESS THE MOVEMENT EVENT FILE
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     XXHGSN        READE     HSL058D                                92
     C     *IN92         IFEQ      *OFF                                         IF IN RANGE
     C     MEMEDT        ANDLE     XXCYMD
      *
      * RETRIEVE HEAD AND STATUS FOR THE APPROPRIATE MOVEMENT TYPE
      *
     C                   SELECT
     C     MEMTCD        WHENEQ    'T'
     C                   EXSR      $TRAN
      *
     C     MEMTCD        WHENEQ    'S'
     C                   EXSR      $SALE
      *
     C     MEMTCD        WHENEQ    'A'
     C                   EXSR      $ADJ
      *
     C     MEMTCD        WHENEQ    'D'
     C                   EXSR      $DEAD
     C                   ENDSL
      *
     C                   ENDIF                                                  IF IN RANGE
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      * RETURN TO CALLER
     C                   RETURN
      /EJECT
      *---------------------------------------------------------------
      * $TRAN - RETRIEVE TRANSFER MOVEMENT
      *---------------------------------------------------------------
      *
     C     $TRAN         BEGSR
      *
      * RETRIEVE STATUS FROM TRANSFER MOVEMENT HEADER
      *
     C     MEMVSN        CHAIN     HSP074                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     MHMSCD        WKMSCD
     C                   ELSE
     C                   MOVE      *BLANK        WKMSCD
     C                   ENDIF
      *
      * NEVER PROCESS SCHEDULED TRANSFER MOVEMENTS
      *
     C     WKMSCD        IFNE      'SC'                                         IF NOT SCHED
     C     WKMSCD        ANDNE     *BLANK
      *
      * RETREIVE HEAD FROM TRANSFER MOVEMENT DETAIL FOR THIS
      * MOVEMENT AND LINE NUMBER. THE FOLLOWING RULES APPLY:
      *
      * IF THE GROUP IS AN ORIGIN GROUP
      *       DECREASE GROUP INVENTORIES BY RECEIVED VALUES
      *
      * IF THE GROUP IS A DESTINATION GROUP
      *       INCREASE GROUP INVENTORIES BY RECEIVED VALUES
      *
     C     K1P075        CHAIN     HSP075                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
      *
     C                   SELECT
      *---------------------------------------------------
      * ORIGIN GROUP ON A TRANSFER
      *---------------------------------------------------
      *
     C     MEODFL        WHENEQ    'O'
     C                   SUB       MDQLHD        GPIHD
     C                   SUB       MDRJHD        GPIHD
     C                   SUB       MDDOHD        GPIHD
      *
      * REJECTS OUT
     C                   ADD       MDRJHD        ROUHD
     C                   ADD       MDRJLB        ROULB
      *
      * QUALITY OUT
     C                   ADD       MDQLHD        QOUHD
     C                   ADD       MDQLLB        QOULB
      *
      * DOA ON ARRIVAL AT THE DESTINATION GROUP
      *
     C                   ADD       MDDOHD        MORHD
     C                   ADD       MDDOLB        MORLB
      * PIGS OUT
     C                   ADD       MDQLHD        POUHD
     C                   ADD       MDRJHD        POUHD
      *
     C                   ADD       MDQLLB        POULB
     C                   ADD       MDRJLB        POULB
      *
      * TRANSFERRED OUT
     C                   ADD       MDQLHD        TOUHD
     C                   ADD       MDRJHD        TOUHD
      *
     C                   ADD       MDQLLB        TOULB
     C                   ADD       MDRJLB        TOULB
      *
      * TRANSFERRED OUT TO A GROUP IN THE SAME PHASE
      *
     C     MHORPP        IFEQ      MHDNPP
     C                   ADD       MDQLHD        TOSPHD
     C                   ADD       MDRJHD        TOSPHD
      *
     C                   ADD       MDQLLB        TOSPLB
     C                   ADD       MDRJLB        TOSPLB
     C                   ELSE
      *
      * TRANSFERRED OUT TO A GROUP IN A DIFFERENT PHASE
      *
     C                   ADD       MDQLHD        TODPHD
     C                   ADD       MDRJHD        TODPHD
      *
     C                   ADD       MDQLLB        TODPLB
     C                   ADD       MDRJLB        TODPLB
     C                   ENDIF
      *
      *---------------------------------------------------
      * DESTINATION GROUP ON A TRANSFER
      *---------------------------------------------------
      *
     C     MEODFL        WHENEQ    'D'
     C                   ADD       MDQLHD        GPIHD
     C                   ADD       MDRJHD        GPIHD
      *
      * REJECTS IN
     C                   ADD       MDRJHD        RINHD
     C                   ADD       MDRJLB        RINLB
      *
      * QUALITY IN
     C                   ADD       MDQLHD        QINHD
     C                   ADD       MDQLLB        QINLB
      * PIGS IN
     C                   ADD       MDQLHD        PINHD
     C                   ADD       MDRJHD        PINHD
      *
     C                   ADD       MDQLLB        PINLB
     C                   ADD       MDRJLB        PINLB
      *
      * TRANSFERRED IN
     C                   ADD       MDQLHD        TINHD
     C                   ADD       MDRJHD        TINHD
      *
     C                   ADD       MDQLLB        TINLB
     C                   ADD       MDRJLB        TINLB
      *
      * TRANSFERRED IN FROM A GROUP IN THE SAME PHASE
      *
     C     MHORPP        IFEQ      MHDNPP
     C                   ADD       MDQLHD        TISPHD
     C                   ADD       MDRJHD        TISPHD
      *
     C                   ADD       MDQLLB        TISPLB
     C                   ADD       MDRJLB        TISPLB
     C                   ELSE
      *
      * TRANSFERRED IN FROM A GROUP IN A  DIFFERENT PHASE
      *
     C                   ADD       MDQLHD        TIDPHD
     C                   ADD       MDRJHD        TIDPHD
      *
     C                   ADD       MDQLLB        TIDPLB
     C                   ADD       MDRJLB        TIDPLB
     C                   ENDIF
      *
     C                   ENDSL
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF NOT SCHED
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SALE - RETRIEVE SALES MOVEMENT
      *---------------------------------------------------------------
      *
     C     $SALE         BEGSR
      *
      * RETRIEVE THE STATUS FROM THE SALES MOVEMENT HEADER
      *
     C     MEMVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     SHMSCD        WKMSCD
     C                   ELSE
     C                   MOVE      *BLANK        WKMSCD
     C                   ENDIF
      *
      * RETREIVE HEAD FROM THE SALES MOVEMENT DETAIL FOR
      * THIS MOVEMENT/HOG GROUP. DO NOT PROCESS ANY SCHEDULED SALES.
      *
     C     WKMSCD        IFNE      'SC'                                         IF NOT SCHED
     C     WKMSCD        ANDNE     *BLANK
      *
     C     K2P085        CHAIN     HSP085                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
      *
     C                   SELECT
     C     WKMSCD        WHENEQ    'SH'
     C                   SUB       SGSHHD        GPIHD
     C                   ADD       SGSHHD        POUHD
     C                   ADD       SGSHLB        POULB
      *
     C     WKMSCD        WHENEQ    'FP'
     C     WKMSCD        OREQ      'PP'
     C     WKMSCD        OREQ      'PG'
     C     WKMSCD        OREQ      'NC'
     C                   SUB       SGLVHD        GPIHD
     C                   SUB       SGDOHD        GPIHD
     C                   ADD       SGLVHD        POUHD
     C                   ADD       SGLVLB        POULB
     C                   ENDSL
      *
      * DOA ON ARRIVAL AT THE CUSTOMER SITE
      *
     C                   ADD       SGDOHD        MORHD
     C                   ADD       SGDOLB        MORLB
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF NOT SCHED
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DEAD - RETRIEVE DAILY DEAD MOVEMENT
      *---------------------------------------------------------------
      *
     C     $DEAD         BEGSR
      *
      * RETRIEVE STATUS AND HEAD. FOR DAILY DEADS, THERE WILL ONLY BE
      * ONE RECORD PER MOVEMENT.
      *
     C     MEMVSN        CHAIN     HSP068                             92
     C     *IN92         IFEQ      *OFF
     C                   SUB       KDKDHD        GPIHD
     C                   ADD       KDKDHD        MORHD
     C                   ADD       KDKDLB        MORLB
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ADJ - RETRIEVE ADJUSTMENT MOVEMENT
      *---------------------------------------------------------------
      *
     C     $ADJ          BEGSR
      *
      * RETRIEVE STATUS AND HEAD. FOR ADJUSTMENTS, THERE WILL ONLY BE
      * ONE RECORD PER MOVEMENT.
      *
     C     MEMVSN        CHAIN     HSP069                             92
     C     *IN92         IFEQ      *OFF
     C                   ADD       IAIAHD        GPIHD
     C                   ADD       IAIAHD        INAHD
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLEAR - CLEAR ALL PARMS, ETC.
      *---------------------------------------------------------------
      *
     C     $CLEAR        BEGSR
      *
     C     *ENTRY        PLIST
      *
      * INPUT PARMS
     C                   PARM                    XXHGSN                         GROUP #
     C                   PARM                    XXCYMD                         CUT-OFF DATE
      * OUTPUT PARMS
     C                   PARM                    GPIHD                          GROUP INV
     C                   PARM                    PINHD                          PIG IN
     C                   PARM                    TINHD                          TRSF IN
     C                   PARM                    TISPHD                         SAME PHASE
     C                   PARM                    TIDPHD                         DIFF PHASE
     C                   PARM                    POUHD                          PIGS OUT
     C                   PARM                    TOUHD                          TRSF OUT
     C                   PARM                    TOSPHD                         SAME PHASE
     C                   PARM                    TODPHD                         DIFF PHASE
     C                   PARM                    RINHD                          REJECT IN
     C                   PARM                    ROUHD                          REJECT OUT
     C                   PARM                    QINHD                          QUALITY IN
     C                   PARM                    QOUHD                          QUALITY OUT
     C                   PARM                    MORHD                          MORTALITY
     C                   PARM                    INAHD                          INV ADJ
      * POUNDS
     C                   PARM                    PINLB                          PIG IN
     C                   PARM                    TINLB                          TRSF IN
     C                   PARM                    TISPLB                         SAME PHASE
     C                   PARM                    TIDPLB                         DIFF PHASE
     C                   PARM                    POULB                          PIG OUT
     C                   PARM                    TOULB                          TRSF OUT
     C                   PARM                    TOSPLB                         SAME PHASE
     C                   PARM                    TODPLB                         DIFF PHASE
     C                   PARM                    RINLB                          REJECT IN
     C                   PARM                    ROULB                          REJECT OUT
     C                   PARM                    QINLB                          QUALITY IN
     C                   PARM                    QOULB                          QUALITY OUT
     C                   PARM                    MORLB                          MORTALITY
      *
     C                   Z-ADD     0             GPIHD
     C                   Z-ADD     0             PINHD
     C                   Z-ADD     0             TINHD
     C                   Z-ADD     0             TISPHD
     C                   Z-ADD     0             TIDPHD
     C                   Z-ADD     0             POUHD
     C                   Z-ADD     0             TOUHD
     C                   Z-ADD     0             TOSPHD
     C                   Z-ADD     0             TODPHD
     C                   Z-ADD     0             RINHD
     C                   Z-ADD     0             ROUHD
     C                   Z-ADD     0             QINHD
     C                   Z-ADD     0             QOUHD
     C                   Z-ADD     0             MORHD
     C                   Z-ADD     0             INAHD
      * POUNDS
     C                   Z-ADD     0             PINLB
     C                   Z-ADD     0             TINLB
     C                   Z-ADD     0             TISPLB
     C                   Z-ADD     0             TIDPLB
     C                   Z-ADD     0             POULB
     C                   Z-ADD     0             TOULB
     C                   Z-ADD     0             TOSPLB
     C                   Z-ADD     0             TODPLB
     C                   Z-ADD     0             RINLB
     C                   Z-ADD     0             ROULB
     C                   Z-ADD     0             QINLB
     C                   Z-ADD     0             QOULB
     C                   Z-ADD     0             MORLB
      *
     C                   MOVE      *OFF          *IN
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *
     C     K1P075        KLIST
     C                   KFLD                    MEMVSN
     C                   KFLD                    MELNNO
      *
     C     K2P085        KLIST
     C                   KFLD                    MEMVSN
     C                   KFLD                    MEHGSN
      *
      * DEFINE INPUT PARAMETERS
      *
     C     *LIKE         DEFINE    MEHGSN        XXHGSN                         GROUP
     C     *LIKE         DEFINE    MEMEDT        XXCYMD                         CUT-OFF DATE
      *
      * DEFINE OUTPUT PARAMETERS
      *
      * HEAD
     C                   Z-ADD     0             @@NU50            5 0
     C     *LIKE         DEFINE    @@NU50        GPIHD
     C     *LIKE         DEFINE    @@NU50        PINHD
     C     *LIKE         DEFINE    @@NU50        TINHD
     C     *LIKE         DEFINE    @@NU50        TISPHD
     C     *LIKE         DEFINE    @@NU50        TIDPHD
     C     *LIKE         DEFINE    @@NU50        POUHD
     C     *LIKE         DEFINE    @@NU50        TOUHD
     C     *LIKE         DEFINE    @@NU50        TOSPHD
     C     *LIKE         DEFINE    @@NU50        TODPHD
     C     *LIKE         DEFINE    @@NU50        RINHD
     C     *LIKE         DEFINE    @@NU50        ROUHD
     C     *LIKE         DEFINE    @@NU50        QINHD
     C     *LIKE         DEFINE    @@NU50        QOUHD
     C     *LIKE         DEFINE    @@NU50        MORHD
     C     *LIKE         DEFINE    @@NU50        INAHD
      * POUNDS
     C                   Z-ADD     0             @@NU92            9 2
     C     *LIKE         DEFINE    @@NU92        PINLB
     C     *LIKE         DEFINE    @@NU92        TINLB
     C     *LIKE         DEFINE    @@NU92        TISPLB
     C     *LIKE         DEFINE    @@NU92        TIDPLB
     C     *LIKE         DEFINE    @@NU92        POULB
     C     *LIKE         DEFINE    @@NU92        TOULB
     C     *LIKE         DEFINE    @@NU92        TOSPLB
     C     *LIKE         DEFINE    @@NU92        TODPLB
     C     *LIKE         DEFINE    @@NU92        RINLB
     C     *LIKE         DEFINE    @@NU92        ROULB
     C     *LIKE         DEFINE    @@NU92        QINLB
     C     *LIKE         DEFINE    @@NU92        QOULB
     C     *LIKE         DEFINE    @@NU92        MORLB
      *
      * DEFINE WORK FIELDS
      *
     C     *LIKE         DEFINE    MHMSCD        WKMSCD
      *
     C                   ENDSR
      *
