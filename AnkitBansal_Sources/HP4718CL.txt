/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4718CL                                      */
/*  PROGRAM NAME:    LISTINGS FROM WORK WITH REQUESTS              */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     03/23/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4718-WORK WITH REQUESTS.                    */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(18)

             DCL        VAR(&LDVER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDRQSN) TYPE(*DEC) LEN(7)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDVER) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDRQSN) VALUE(%SST(*LDA 142 7))

/*------------------------------------------------------------------*/
/* E-MAIL                                                           */
/*------------------------------------------------------------------*/

             IF         COND(&LDVER *EQ '1' *OR &LDVER *EQ '2') +
                          THEN(DO)
             CALL       PGM(HP6709CL)
             ENDDO

/*------------------------------------------------------------------*/
/* PRINT PICK LIST FOR A REQUEST                                    */
/*------------------------------------------------------------------*/

             IF         COND(&LDVER *EQ 'P') THEN(DO)

/*  SET UP PRINT FILE OVERRIDES                                      */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)


             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('QDRQSN *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 7)) VALUE(&LDRQSN)

             OVRDBF     FILE(HSJ181E) SHARE(*YES)
             OPNQRYF    FILE((HSJ181E)) QRYSLT(&QDATA) KEYFLD(*FILE)

             CALL       PGM(HP6718)

             CLOF       OPNID(HSJ181E)
             DLTOVR     FILE(HSJ181E)
             ENDDO


             DLTOVR     FILE(*ALL)

             ENDPGM

