     A*%%TS  SD  20030113  095705  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD551 - SELECT FARM SITE WITH FARM POSITIONER
     A*                          (OMIT BGF FARMS)
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/06/95
     A*
     A*****************************************************************
     A* MODIFICATIONS
     A*
     A* 05/10/95  LEANNE FEDOR
     A*           ADD A SELECTOR ON PRODUCTION TYPE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*---------------------------------------------------------------
     A          R HP551R1
     A*%%TS  SD  20010521  132245  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 65)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP551R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*---------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*---------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*---------------------------------------------------------------
     A          R HP551R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP551S1                   SFL
     A*%%TS  SD  20030113  095705  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFSCD    R        O  6  2REFFLD(HSRREF/FSFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFFSNM    R        O  6 10REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A            DFCELL    R        O  6 37REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            DFPPCD    R        O  6 42REFFLD(FSPPCD)
     A            DFPTCD    R        O  6 50REFFLD(FSPTCD)
     A            DFFSBO    R        O  6 59REFFLD(FSFSBO)
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP551C1                   SFLCTL(HP551S1)
     A*%%TS  SD  20030113  095705  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP551R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 12'SELECT GROW FINISH OR NURSERY FARM-
     A                                       SITE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  3  3'Farm'
     A                                      DSPATR(HI)
     A                                  3 42'-Production-'
     A                                      DSPATR(HI)
     A                                  3 58'Business'
     A                                      DSPATR(HI)
     A                                  4  3'Site'
     A                                      DSPATR(HI)
     A                                  4 10'Name'
     A                                      DSPATR(HI)
     A                                  4 36'Cell'
     A                                      DSPATR(HI)
     A                                  4 42'Phase'
     A                                      DSPATR(HI)
     A                                  4 50'Type'
     A                                      DSPATR(HI)
     A                                  4 59'Office'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  5  2REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  5 37REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1PPCD    R        B  5 42REFFLD(FSPPCD)
     A                                      DSPATR(HI)
     A            C1PTCD    R        B  5 50REFFLD(FSPTCD)
     A                                      DSPATR(HI)
     A            C1FSBO    R        B  5 59REFFLD(FSFSBO)
     A                                      DSPATR(HI)
     A          R HP551K1
     A*%%TS  SD  19940316  155439  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      WINDOW(HP551R1)
     A                                 16  1'F12=Previous'
