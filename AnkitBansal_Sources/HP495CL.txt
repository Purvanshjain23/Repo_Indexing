/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP495CL                                       */
/*  PROGRAM NAME:    SPECIFY INVENTORY RECAP BY GROUP OPTIONS      */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     06/24/03                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED FROM HP495.                 */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(279)

             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDSEQCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCRCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 9 8))
             CHGVAR     VAR(&LDSEQCD) VALUE(%SST(*LDA 77 1))

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 87 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 92 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 97 5))
             CHGVAR     VAR(&LDCRCD) VALUE(%SST(*LDA 104 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 109 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 114 1))


/*-------------------------------------------------------------------*/
/*  SETUP OPNQRY STATEMENT FOR GROUPS                                */
/*-------------------------------------------------------------------*/

/*         1) OMIT ALL "BGF" PRODUCTION PHASE GROUPS                 */
/*         2) OMIT ALL HOG GROUPS WITH A STATUS OF VOIDED            */
/*         3) SELECT ONLY GROUPS WHERE:                              */
/*            A) CLOSE DATE IS ZERO OR AFTER BEGINNING PERIOD DATE   */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('HGPPCD +
                          *NE "BGF  "')

             CHGVAR     VAR(%SST(&QDATA 20 20)) VALUE('*AND HGGSCD +
                          *NE "VD"')

             CHGVAR     VAR(%SST(&QDATA 41 16)) VALUE('*AND (HGCLDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 67 15)) VALUE(' *OR HGCLDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 83 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 91 1)) VALUE(')')


/*  IF BUSINESS OFFICE                                            */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 143 17)) VALUE('*AND HGFSBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 160 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 165 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                            */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 167 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 184 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 189 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                            */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 191 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 208 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 213 1)) VALUE('"')
             ENDDO

/*  IF GROUP TYPE                                                  */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 215 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 232 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 233 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 235 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 251 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF COMPANY RELATIONSHIP                                      */

             IF         COND(&LDCRCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 257 17)) VALUE('*AND HGCRCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 274 5)) VALUE(&LDCRCD)
             CHGVAR     VAR(%SST(&QDATA 279 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) +
                        KEYFLD((HGPPCD) (HGFSCD) (HGHGCD)) +
                          SEQONLY(*YES)

/*--------------------------------------------------------------------*/
/* CREATE/BUILD WORKFILE                                              */
/*--------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP395) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP395)

/*-------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*-------------------------------------------------------------------*/
/* PRINT THE 'PHASE' VERSION OF THE INVENTORY RECAP REPORT           */
/*-------------------------------------------------------------------*/

             IF         COND(&LDSEQCD *EQ 'P') THEN(DO)
             CALL       PGM(HP695) PARM('PHASE')
             ENDDO

/*-------------------------------------------------------------------*/
/*  INVENTORY RECAP---STAGE VERSION (GROW FINISH GROUPS ONLY)        */
/*-------------------------------------------------------------------*/

             IF         COND(&LDSEQCD *EQ 'S') THEN(DO)

             OVRDBF     FILE(HSP395) SHARE(*YES)
             OPNQRYF    FILE((HSP395)) QRYSLT('WGPPCD = "GF   "') +
                          KEYFLD((WGNUMSTG) (WGTOTDAY)) +
                          SEQONLY(*YES)
             CALL       PGM(HP695) PARM('STAGE')

             CLOF       OPNID(HSP395)
             DLTOVR     FILE(HSP395)

             ENDDO


/*******************************************************************/
/*  DELETE REMAINING FILE OVERRIDES                                */
/*******************************************************************/

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(HSP034)

             DLTOVR     FILE(QPRINT)

             ENDPGM

