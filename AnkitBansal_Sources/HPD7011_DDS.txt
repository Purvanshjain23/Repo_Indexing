     A*%%TS  SD  20011112  080003  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7011
     A*  TITLE:         VIEW SIRE LINE DOSES FOR ALL FARMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/25/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7011S1                  SFL
     A*%%TS  SD  20011112  080003  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O 10  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1CELL    R        O 10 12REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1CDYR    R        O 10 18REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1CDWK    R        O 10 25REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1SSCD    R        O 10 33REFFLD(HSRREF/@@SSCD *LIBL/HSPREF)
     A            D1SICD    R        O 10 43REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A            D1DONO    R        O 10 52REFFLD(HSRREF/@@DONO *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1MDYWEDT R        O 10 63REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7011C1                  SFLCTL(HP7011S1)
     A*%%TS  SD  20011112  080003  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'VIEW SIRE LINE DOSES FOR ALL FARMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Show only the most recent week for-
     A                                       each farm ..'
     A                                  4 50'?'
     A            C1YNFL    R        B  4 52REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  6  6'?'
     A                                      DSPATR(HI)
     A                                  6 35'?'
     A                                      DSPATR(HI)
     A                                  6 45'?'
     A                                      DSPATR(HI)
     A                                  7 18'-Calendar-'
     A                                      DSPATR(HI)
     A                                  7 31'Sire Line'
     A                                      DSPATR(HI)
     A                                  7 43'Sire'
     A                                      DSPATR(HI)
     A                                  7 52'Number'
     A                                      DSPATR(HI)
     A                                  7 61'Week-ending'
     A                                      DSPATR(HI)
     A                                  8  4'Farm'
     A                                      DSPATR(HI)
     A                                  8 11'Cell'
     A                                      DSPATR(HI)
     A                                  8 18'Year'
     A                                      DSPATR(HI)
     A                                  8 24'Week'
     A                                      DSPATR(HI)
     A                                  8 33'Source'
     A                                      DSPATR(HI)
     A                                  8 43'Line'
     A                                      DSPATR(HI)
     A                                  8 50'of Doses'
     A                                      DSPATR(HI)
     A                                  8 65'Date'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  9  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A N51N53N54
     A N31
     AO 52                                  DSPATR(PC)
     A            C1CELL    R        B  9 12REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1CDYR    R        B  9 18REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CDWK    R        B  9 25REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1SSCD    R        B  9 33REFFLD(HSRREF/@@TTBCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1SICD    R        B  9 43REFFLD(HSRREF/@@TTBCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7011K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7011ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7011EC                  SFLCTL(HP7011ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
