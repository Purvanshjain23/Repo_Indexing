      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF631
      * TITLE:       Claim Expense Distribution Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/28/05
      *
      * FUNCTION:  Batch program to print the backup info for an Invoice Charge
      *            created in the Weekly Revenue Close function for "CLAIMPAY".
      *
      *            This listing is generated:
      *                2) on-demand from a menu
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/03/09  LeAnne Ramsey
      *           We removed the automatic generation of this report from
      *           the TF Revenue Close function. The users have never used
      *           the "CLAIMPAY" feature; so, the report is always blank.
      *           We have left the submission screen/report in case we use it
      *           some time in the future.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fppa1cpl1  if   e           k disk
      *  AR Claim expense  (file keyed in open query)
      *
      *
     Fpdmkcpl1  if   e           k disk
      *    AR claim
      *
      *
     Ftfp020    if   e           k disk
      *    Invoice header
      *
      *
     Ftfp021    if   e           k disk
      *    Invoice charges  (records selected in open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D countchg        s              5  0
     D countclm        s              5  0
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wksyndt         s              7  0
      *
      *
      * Print fields
      *
     D h1fmdy          s              6  0
     D h1tmdy          s              6  0
     D h1demand        s             25
      *
     D l1indtmdy       s              6  0
     D l1podtmdy       s              6  0
     D l1icdtmdy       s              6  0
      *
      *
      * Array index
      *
     D x               s              3  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D aricam          s             11  2 dim(2)
     D arexam          s             11  2 dim(2)
     D artfam          s             11  2 dim(2)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
      * This report is generated from the Revenue Close function as well as on-demand
      * (as are several other reports). So, BEFORE you change/add any fields, check
      * what positions are available (see TF410CL).
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                37     37
     D  lddemand             300    300
      *
     D  ldfwedt              320    327  0
     D  ldtwedt              328    335  0
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read each Invoice Charge record selected by the open query.
      *
     C     *loval        setll     tfp021
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp021                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      no            first
      *
      *
      * Retrieve Invoice info for this Invoice Charge and print Invoice
      * Charge line.
     C                   exsr      $charge
      *
      * Retrieve Claims for this Invoice Charge.
      *
     C                   exsr      $claims
      *
      * Print total line for Invoice Charge
      *
     C                   exsr      $t1tot
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
      *
     C                   when      countchg > 1
     C                   exsr      $rpttot
     C                   endsl
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Invoice Charge
      *---------------------------------------------------------------
      *
     C     $charge       begsr
      *
      * Count the number of Invoice Charges on this report.
      *
     C                   add       1             countchg
      *
      * Flip Invoice Charge Date from CCYYMMDD to MMDDYY
      *
     C                   if        icicdt <> 0
     C     *iso          test(d)                 icicdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      icicdt        wkisodate
     C     *mdy          move      wkisodate     l1icdtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Accumulate into arrays for totals:
      *  1) Invoice charge amount
      *
     C                   add       icicam        aricam
      *
      * Retrieve Invoice Info---if the Charge record has an Invoice Number.
      *
     C                   if        icinsn <> 0
     C                   exsr      $invoice
     C                   endif
      *
      * Print Invoice Charge Record
      *
     C                   exsr      $l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Claims
      *---------------------------------------------------------------
      *
     C     $claims       begsr
      *
      * Convert the Invoice Charge date into Synon format of CYYMMDD.
      *
     C     *iso          move      icicdt        wkisodate
     C     *cymd         move      wkisodate     wksyndt
      *
      *
      * Read each record in the AR Claim Expense for this Invoice Charge.
      *   (ie: AR Claim Records where TFS Batch Number = Invoice Charge Date)
      *
     C     wksyndt       setll     ppa1cpl1
      *
     C                   dou       *in91 = *on                                  Do claims
     C     wksyndt       reade     ppa1cpl1                               91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Retrieve the AR Claim record for this Claim Number.
      *
     C     a1c0c7        chain     pdmkcpl1                           92
     C                   if        *in92 = *off                                 If hit
      *
      * Accumulate into arrays for totasl:
      *  1) Expense amount
      *  2) TFS claim expense amount
      *
     C                   add       a1emvl        arexam
     C                   add       a1e1vl        artfam
      * Count claims
     C                   add       1             countclm
      *
     C                   exsr      $l2dtl
      *
     C                   endif                                                  If hit
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do claims
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Invoice Charge line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Claim line for Invoice Charge
      *---------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   except    l2dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line for Invoice Charge
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   z-add     1             x
      *
     C                   if        countclm > 1
     C                   except    t1tot
     C                   else
     C                   except    blank
     C                   endif
      *
     C                   z-add     0             aricam(x)
     C                   z-add     0             arexam(x)
     C                   z-add     0             artfam(x)
     C                   z-add     0             countclm
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Report Total
      *---------------------------------------------------------------
      *
     C     $rpttot       begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    blank
     C                   z-add     2             x
     C                   seton                                        87
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Invoice Info
      *---------------------------------------------------------------
      *
     C     $invoice      begsr
      *
      * Initialize print fields for Invoice dates.
      *
     C                   z-add     0             l1indtmdy
     C                   z-add     0             l1podtmdy
      *
      * Retrieve Invoice record.
      *
     C     icinsn        chain     tfp020                             92
     C                   if        *in92 = *off
      *
      * Flip Invoice Date from CCYYMMDD to MMDDYY
      *
     C                   if        ihindt <> 0
     C     *iso          test(d)                 ihindt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihindt        wkisodate
     C     *mdy          move      wkisodate     l1indtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip Posted Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        ihpodt <> 0
     C     *iso          test(d)                 ihpodt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihpodt        wkisodate
     C     *mdy          move      wkisodate     l1podtmdy
     C                   endif                                                  If OK date
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Flip From/To Week-ending dates to MMDDYY format.
      *
     C     *iso          test(d)                 ldfwedt                92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ldfwedt       wkisodate
     C     *mdy          move      wkisodate     h1fmdy
     C                   endif                                                  If OK date
      *
     C     *iso          test(d)                 ldtwedt                92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ldtwedt       wkisodate
     C     *mdy          move      wkisodate     h1tmdy
     C                   endif                                                  If OK date
      *
      *
      * This report is generated on-demand and also from the submitted
      * Weekly Revenue Close function when the run is a 'Final'.
      *
     C                   select
     C                   when      lddemand = yes
     C                   eval      h1demand = '        On-Demand        '
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1demand = '   Final Revenue Close   '
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'CLAIM EXPENSE DISTRIBUTION'
     O                                          107 ' REPORT'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           105
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           24 'From week-ending date ..'
     O                       h1fmdy              34 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           24 'To week-ending date ....'
     O                       h1tmdy              34 '  /  /  '
      *
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           80 'Claim'
     O                                           95 'Average TF'
     O                                          155 'TFS Claim'
      *
     O          e            h1hdr       0  1
     O                                            7 'Charge'
     O                                           16 'Charge'
     O                                           32 'Charge'
      *
     O                                           42 'Invoice'
     O                                           52 'Invoice'
     O                                           62 'Posted'
     O                                           72 'Invoice'
      *
     O                                           81 'Company'
     O                                           97 'Responsibility'
     O                                          106 'Claim'
     O                                          122 'Expense'
     O                                          136 'Expense'
     O                                          155 'Expense'
      *
     O          e            h1hdr       0  1
     O                                            7 'Number'
     O                                           15 'Date'
     O                                           32 'Amount'
      *
     O                                           42 'Number'
     O                                           51 'Date'
     O                                           61 'Date'
     O                                           72 'Status'
      *
     O                                           81 'Number'
     O                                           94 'Percent'
     O                                          106 'Number'
     O                                          122 'Amount'
     O                                          139 'Responsibility'
     O                                          155 'Amount'
      *
     O          e            h1hdr          2
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Detail line for Invoice Charge
      *-------------------------------------------------------------
      *
     O          e            l1dtl          0
     O                       icicsn        z      7
     O                       l1icdtmdy           17 '  /  /  '
     O                       icicam        k     33
     O                       icinsn        z     42
     O                       l1indtmdy           52 '  /  /  '
     O                       l1podtmdy           63 '  /  /  '
     O                       icinstcd            69
      *
      *
      *-------------------------------------------------------------
      * Detail line for the Claims for each Invoice Charge
      *-------------------------------------------------------------
      *
     O          e            l2dtl          1
     O                       mkbic3        z     79
     O                       mkf7pc        k     95
     O                       a1c0c7        z    106
     O                       a1emvl        k    123
     O                       a1txsx             132
     O                       a1e1vl        k    156
      *
      *
      *-------------------------------------------------------------
      * Total line for Report
      *-------------------------------------------------------------
      *
     O          e   n87      t1tot          1
     O                                           32 '--------------'
     O                                          122 '--------------'
     O                                          155 '--------------'
     O          e            t1tot          1
     O               87                          17 'Report Total:'
     O                       aricam(x)     k     33
     O                       arexam(x)     k    123
     O                       artfam(x)     k    156
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1  1
      *
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
