      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF220 - Eopfees: Delete EOP Fees Records
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     04/13/05
      *
      * Function:    When the EOP Fees Close function is run, we call this program
      *              first to delete any "fees" records that may already exist for the
      *              "period" in the files.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/27/06  LeAnne Fedor
      *           Recompile only. New field "Product Code" added to Freight Variance file.
      *
      * 01/04/07  LeAnne Ramsey
      *           Added logic for new file Freight Variance Totals.
      *
      * 04/16/07  LeAnne Ramsey
      *           Recompile only. Field "Exclude from Mix Flag' was renamed to
      *           Mix Flag in the Freight Variance file.
      *
      * 05/14/07  LeAnne Ramsey
      *           Recompile only. Field "Mix Flag' was added to TFP028-Freight
      *           Variance Totals file.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl017a   uf   e           k disk
      *  Freight variance
      *
      *
     Ftfl028a   uf   e           k disk
      *  Freight variance totals
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldcyr                  2      5  0
     D  ldcpe                  6      7  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Freight variance
      *
     C     key01         setll     tfl017a
      *
     C                   dou       *in90 = *on                                  Do freight
     C     key01         reade     tfl017a                                90
     C                   if        *in90 = *off                                 If not EOF
     C                   delete    fvrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do freight
      *
      * Freight variance totals
      *
     C     key01         setll     tfl028a
      *
     C                   dou       *in90 = *on                                  Do totals
     C     key01         reade     tfl028a                                90
     C                   if        *in90 = *off                                 If not EOF
     C                   delete    ftrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do totals
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcyr
     C                   kfld                    ldcpe
      *
     C                   endsr
      /eject
