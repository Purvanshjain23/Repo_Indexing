/********************************************************************/
/*  PROGRAM  - PLGZUPC                                              */
/*  TITLE    - AUTO SUBMIT PURCHASE JOURNAL BY DATE REPORT          */
/*  SYNOPSIS - CL PROGRAM TO BE RUN AT NIGHT                        */
/*                PRINT PURCHASE JOURNAL BY DATE REPORT             */
/*                THIS WILL MONDAY NIGHT FOR THE PRIOR MONDAY       */
/*                  THROUGH THE PRIOR SUNDAY                        */
/*                THE COMPANY NUMBER IS HARDCODED TO 960 AS         */
/*                  REQUESTED                                       */
/*                THIS NEEDS TO RUN BEFORE MIDNIGHT EACH NIGHT      */
/*                                                                  */
/*  SYSTEM   - HPE                                                  */
/*  DATE     - 02/15/2006                                           */
/*  BY       - LARA BUSER                                           */
/*------------------------------------------------------------------*/
/* MAINTENANCE LOG                                                  */
/*------------------------------------------------------------------*/
/*  MODIFIED: NAME                DATE                              */
/*            MODIFICATION DESCRIPTION                              */
/*  SUSAN MAWSON                  6/3/2008                          */
/*                                PASS IN NEW PARMS TO SELECT       */
/*                                DOWNLOAD FILE AND EMAIL           */
/*                                WILL NOT BE SENDING OUT EMAIL     */
/*                                IN THIS ROUTINE                   */
/********************************************************************/
 PLGZUPC:    PGM        PARM(&OUTQ &HOLD &DIST)

        DCL    &FDATE     *CHAR   7              /* FROM DATE       */
        DCL    &TDATE     *CHAR   7              /* TO DATE         */
        DCL    &SUNDAY    *DEC    7 0            /* PRIOR SUNDAY    */
        DCL    &MONDAY    *DEC    7 0            /* PRIOR MONDAY    */
        DCL    &TODAY     *DEC    7 0            /* TODAY'S DATE    */
        DCL    &COUNT     *DEC    8 0            /* DATE INCREMENT  */
        DCL    &RETURN    *CHAR   7              /* RETURN CODE     */
        DCL    &COMPA     *CHAR   3              /* COMPANY ALPHA   */
        DCL    &XCOPIES   *CHAR   15             /* # OF COPIES     */
        DCL    &SRCE      *CHAR   1              /* SOURCE CODE     */
        DCL    &PAYM      *CHAR   1              /* PAYMENT TYPE    */
        DCL    &PROD      *CHAR   6              /* PRODUCER CODE   */
        DCL    &HOLD      *CHAR   4              /* HOLD SPOOL      */
        DCL    &SAVE      *CHAR   4              /* SAVE SPOOL      */
        DCL    &OUTQ      *CHAR   10             /* OUTPUT QUEUE    */

        DCL    VAR(&JOBDATE) TYPE(*CHAR) LEN(6)
        DCL    VAR(&JOBDT) TYPE(*CHAR) LEN(7)

/* ADD NEW PARM TO SELECT SPREAD SHEET VIA EMAIL         */
/* THIS CL IS PERFORM FROM JS AND WILL NOT BE SEND       */
/* THE DOWN LOAD FILE                                    */
             DCL        VAR(&DTAXLS) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50) +
                          VALUE('                                     -
             ')
/* ****************************************************  */

/* PARAMETERS FOR THE VAL APPLICATION/USER IF - STR ORDER           */
/*   MANAGEMENT SYSTEM PROGRAM*. IN OMS THIS SYNON FUNCTION IS      */
/*   CALLED VAL SET INT ACC EXTNL XF - /UT FUNCTIONS*               */
/*   THE ONLY PARMS NEEDED BACK ARE IS THE TITLE.                   */

        DCL    &WHSE      *CHAR   3              /* WAREHOUSE CODE  */
        DCL    &COMP      *DEC    3 0            /* COMPANY NUMBER  */
        DCL    &SYSCODE   *CHAR   6              /* SYSTEM CODE     */
        DCL    &SLSPRSN   *CHAR   3              /* SALESPERSON CD  */
        DCL    &ALLOWM    *CHAR   1              /* ALLOW MULTI CO  */
        DCL    &PRINTER   *CHAR   10             /* PRINTER         */
        DCL    &NAME      *CHAR   30             /* NAME            */
        DCL    &HPETITLE  *CHAR   40             /* HPE DESCRIPTION */
        DCL    &ACCESS    *CHAR   1              /* ACCESS DENIED   */
        DCL    &OMSTITLE  *CHAR   33             /* HPE DESCRIPTION */

/* SEND PROGRAM MESSAGE PARAMETERS                                  */

        DCL    &DIST      *CHAR   10             /* MESSAGE DIST    */
        DCL    &USER      *CHAR   10             /* USER ID         */
        DCL    &JOBD      *CHAR   10             /* JOB DESCRIPTION */
        DCL    &JOBNAME   *CHAR   10             /* JOB NAME        */
        DCL    &MSG       *CHAR   (512)          /* MESSAGE         */
        DCL    &MSG1      *CHAR   (132)          /* MSG FOR PAGER   */
        DCL    &MSGID     *CHAR   (7)            /* MESSAGE ID      */
        DCL    &MSGL      *DEC    (5 0)          /* MESSAGE LENGTH  */

/*------------------------------------------------------------------*/
/* INITIALIZE PARAMETERS WITH DEFAULT VALUES                        */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COMPA) VALUE('960') /* Change to alpha */
             CHGVAR     VAR(&SAVE) VALUE('*YES')
             CHGVAR     VAR(&COUNT) VALUE(1)
             CHGVAR     VAR(&XCOPIES) VALUE(1)
             CHGVAR     VAR(&SRCE) VALUE(' ')
             CHGVAR     VAR(&PAYM) VALUE(' ')
             CHGVAR     VAR(&PROD) VALUE('      ')
             CHGVAR     VAR(&WHSE) VALUE('   ')
             CHGVAR     VAR(&COMP) VALUE(960)
             CHGVAR     VAR(&SYSCODE) VALUE(HPE)
             CHGVAR     VAR(&SLSPRSN) VALUE('   ')
             CHGVAR     VAR(&ALLOWM) VALUE(' ')
             CHGVAR     VAR(&PRINTER) VALUE('          ')
             CHGVAR     VAR(&ACCESS) VALUE(' ')
/* CLEAR ANY POSSIBLE GARBAGE                                       */
             CHGVAR     VAR(&NAME) +
                          VALUE('                              ')
             CHGVAR     VAR(&HPETITLE) +
                          VALUE('                                     -
   ')

/*------------------------------------------------------------------*/
/* RETRIEVE JOB INFO FOR SNDPGRMSG, PUT ALPHA DATE INTO NUMERIC     */
/*------------------------------------------------------------------*/
             RTVJOBA    JOB(&JOBNAME) USER(&USER)
             RTVUSRPRF  USRPRF(&USER) JOBD(&JOBD)
             RTVJOBA    DATE(&JOBDATE)
             CVTDAT     DATE(&JOBDATE) TOVAR(&JOBDT) FROMFMT(*MDY) +
                          TOFMT(*CYMD) TOSEP(*NONE)
             CHGVAR     VAR(&TODAY) VALUE(&JOBDT)

/*------------------------------------------------------------------*/
/* RETRIEVE LAST SUNDAY'S DATE                                      */
/*------------------------------------------------------------------*/
             CALL       PGM(PDLNXFR) PARM(&RETURN &COMP &TODAY +
                          &SUNDAY)
             CHGVAR     VAR(&TDATE) VALUE(&SUNDAY)

/*------------------------------------------------------------------*/
/* TRAP FOR RETURN CODE AND FOR CPF MESSAGES                        */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF9999) EXEC(DO)
             CHGVAR     VAR(&MSG1) VALUE('The Retrieve Prior Sunday +
                          for the Print Purchase Journal Report on')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &JOBDT)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

             IF         COND(&RETURN *NE '       ') THEN(DO)
             CHGVAR     VAR(&MSG1) VALUE('The Retrieve Prior Sunday +
                          for the Print Purchase Journal Report on')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &JOBDT)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

/*------------------------------------------------------------------*/
/* SUBTRACT 6 DAYS TO GET THE PRIOR MONDAY'S DATE                   */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&COUNT) VALUE(-6)
             CALL       PGM(PDNKXFR) PARM(&RETURN &MONDAY &COMP +
                          &SUNDAY &COUNT)
             CHGVAR     VAR(&FDATE) VALUE(&MONDAY)

/*------------------------------------------------------------------*/
/* TRAP FOR RETURN CODE AND FOR CPF MESSAGES                        */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF9999) EXEC(DO)
             CHGVAR     VAR(&MSG1) VALUE('The Calculate Prior +
                          Mondays date for Print Purchase Journal +
                          Report for')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &JOBDT)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

             IF         COND(&RETURN *NE '       ') THEN(DO)
             CHGVAR     VAR(&MSG1) VALUE('The Calculate Prior +
                          Mondays date for Print Purchase Journal +
                          Report for')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &JOBDT)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

/*------------------------------------------------------------------*/
/* GET TITLE                                                        */
/*------------------------------------------------------------------*/
             CALL       PGM(PDGXXFR) PARM(&RETURN &WHSE &COMP +
                          &SYSCODE &SLSPRSN &ALLOWM &PRINTER &NAME +
                          &HPETITLE &ACCESS)
             CHGVAR     VAR(&OMSTITLE) VALUE(&HPETITLE)

/*------------------------------------------------------------------*/
/* TRAP FOR RETURN CODE AND FOR CPF MESSAGES                        */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF9999) EXEC(DO)
             CHGVAR     VAR(&MSG1) VALUE('The VAL Application/User +
                          IF to retrieve HPE Title for')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &JOBDT)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

             IF         COND(&RETURN *NE '       ') THEN(DO)
             CHGVAR     VAR(&MSG1) VALUE('The VAL Application/User +
                          IF to retrieve HPE Title for')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &JOBDT)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO
/*------------------------------------------------------------------*/
/*********************************************************************/
/* ON MONDAY NIGHT THIS REPORT IS TO BE RUN FOR THE PRIOR WEEK.     */
/*        REGARDLESS OF THE RUN DATE, THE REPORT WILL ALWAYS BE     */
/*        FOR THE PRIOR WEEK.                                       */
/*********************************************************************/

/* RUN THE PURCHASE JOURNAL BY DATE REPORT                          */

             CALL       PGM(PKUEFPR) PARM(&RETURN &COMPA &SRCE &PAYM +
                          &PROD &FDATE &TDATE &OMSTITLE &OUTQ &HOLD +
                          &SAVE &XCOPIES &DTAXLS &EMAIL)

/*------------------------------------------------------------------*/
/* TRAP FOR CPF MESSAGES                                            */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF9999 RPG9999) EXEC(DO)
             CHGVAR     VAR(&MSG1) VALUE('Program: PKUEFPR - Print +
                          Purchase Journals Report for')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &JOBDT)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

/*------------------------------------------------------------------*/
/* SEND A REPORT COMPLETED NORMALLY MESSAGE FOR EACH REPORT DATE    */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&MSG1) VALUE('Program: PKUEFPR - Print +
                          Purchase Journals Report for')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &FDATE)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'through')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &TDATE)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'completed +
                          normally')
             SNDPGRMSG  TOPGR(&DIST) MSG(&MSG1) JOB(&JOBD/&JOBNAME)

 ENDREPORT:  GOTO       CMDLBL(ENDPGM)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000)

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

             SNDPGRMSG  TOPGR(&DIST) MSG(&MSG1) JOB(&JOBD/&JOBNAME)

 ENDPGM:     ENDPGM
