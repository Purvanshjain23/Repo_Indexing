     A*%%TS  SD  20010626  110909  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD482
     A*  TITLE:         SPECIFY CLOSED HOG GROUP PERFORMANCE
     A*  PROGRAMMER:    SHARON ZURN
     A*  DATE:          07/29/94
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP482R1
     A*%%TS  SD  20010626  110909  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(39)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(70)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 15'SPECIFY CLOSED GROUP PERFORMANCE R-
     A                                      EPORT OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .....'
     A                                  4 24'?'
     A            DFFSBO    R        B  4 26REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 34REFFLD(BOBODS)
     A                                  4 65'(Blank for all)'
     A                                  4 65'(Required)'
     A                                  5  2'From closed date ....'
     A            DFFMDY    R     Y  B  5 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To closed date ......'
     A            DFTMDY    R     Y  B  6 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Production type .....'
     A                                  7 24'?'
     A            DFPTCD    R        B  7 26REFFLD(PTPTCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 52                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  7 34REFFLD(PTPTDS)
     A                                  7 65'(Blank for all)'
     A                                  8  2'Production phase ....'
     A                                  8 24'?'
     A            DFPPCD    R        B  8 26REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 53                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  8 34REFFLD(PPPPDS)
     A                                  8 65'(Blank for all) '
     A                                 10  2'Cell ................'
     A            DFCELL    R     Y  B 10 26REFFLD(FSCELL)
     A                                      EDTCDE(K)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 10 65'(Blank for all)'
     A                                 11  2'Farm site ...........'
     A                                 11 24'?'
     A            DFFSCD    R        B 11 26REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 54                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 11 34REFFLD(FSFSNM)
     A                                 11 65'(Blank for all) '
     A                                 12  2'Group type ..........'
     A                                 12 24'?'
     A            DFGTCD    R        B 12 26REFFLD(@@GTCD)
     A N49                                  DSPATR(HI)
     A  49
     AO 59                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFGTDS    R        O 12 34REFFLD(GTGTDS)
     A                                 12 65'(Blank for all)'
     A                                 13  2'Breed stock .........'
     A                                 13 24'?'
     A            DFBSCD    R        B 13 26REFFLD(@@BSCD)
     A N70                                  DSPATR(HI)
     A  70
     AO 50                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBSDS    R        O 13 34REFFLD(BSBSDS)
     A                                 13 65'(Blank for all)'
     A                                 15  2'Ranking .............'
     A                                 15 24'?'
     A            DFOPTN    R        B 15 26REFFLD(@@FLAG)
     A N39                                  DSPATR(HI)
     A  39
     AO 58                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFOPDS    R        O 15 34REFFLD(@@NM25)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP482K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP482ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP482EC                   SFLCTL(HP482ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
