     A*%%TS  SD  20050308  104728  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD513 - SELECT CHARGE CODE--ACTIVE/INACTIVE OPTIONS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/08/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      PRINT
     A                                      CF04(04 'PROMPT')
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF513S1                   SFL
     A*%%TS  SD  20050308  090440  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1CCCD    R        O  8  1REFFLD(@@CCCD)
     A            D1CCDS    R        O  8 13REFFLD(TFREF/@@CCDS *LIBL/TFPREF)
     A            D1GLDBCD  R        O  8 39REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A            D1GLCRCD  R        O  8 48REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A            D1CFQCD   R        O  8 58REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A            D1UCFL    R        O  8 61REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A            D1INFL    R        O  8 65REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A            D1SMCD    R        O  8 69REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A            D1AIST    R        O  8 74REFFLD(@@AIST)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF513C1                   SFLCTL(TF513S1)
     A*%%TS  SD  20050308  104728  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(TF513EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 13'SELECT CHARGE CODE'
     A                                      DSPATR(HI)
     A            C1AIDESC      30A  O  1 39DSPATR(HI)
     A            C1UCDESC      32A  O  2 39DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  4 42'?'
     A                                      DSPATR(HI)
     A                                  4 51'?'
     A                                      DSPATR(HI)
     A                                  4 58'?'
     A                                      DSPATR(HI)
     A N63                              4 61'?'
     A                                      DSPATR(HI)
     A                                  4 65'?'
     A                                      DSPATR(HI)
     A                                  4 70'?'
     A                                      DSPATR(HI)
     A N62                              4 74'?'
     A                                      DSPATR(HI)
     A                                  5  1'Charge'
     A                                      DSPATR(HI)
     A                                  5 39'--- GL Codes ----'
     A                                      DSPATR(HI)
     A                                  5 61'U'
     A                                      DSPATR(HI)
     A                                  5 64'Inc'
     A                                      DSPATR(HI)
     A                                  5 69'Sys'
     A                                      DSPATR(HI)
     A                                  5 74'A/'
     A                                      DSPATR(HI)
     A                                  6  1'Code'
     A                                      DSPATR(HI)
     A                                  6 13'Description'
     A                                      DSPATR(HI)
     A                                  6 39'Debit'
     A                                      DSPATR(HI)
     A                                  6 48'Credit'
     A                                      DSPATR(HI)
     A                                  6 57'Fq'
     A                                      DSPATR(HI)
     A                                  6 61'C'
     A                                      DSPATR(HI)
     A                                  6 64'Inv'
     A                                      DSPATR(HI)
     A                                  6 69'Mod'
     A                                      DSPATR(HI)
     A                                  6 74'I'
     A                                      DSPATR(HI)
     A            C1CCCD    R        B  7  1REFFLD(@@CCCD)
     A                                      DSPATR(HI)
     A            C1CCDS    R        B  7 13REFFLD(@@CCDS)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1GLDBCD  R        B  7 39REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1GLCRCD  R        B  7 48REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1CFQCD   R        B  7 58REFFLD(@@CFQCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A N63        C1UCFL    R        B  7 61REFFLD(@@UCFL)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A            C1INFL    R        B  7 65REFFLD(@@INFL)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1SMCD    R        B  7 69REFFLD(@@SMCD)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A N62        C1AIST    R        B  7 74REFFLD(@@AIST)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R TF513K1
     A                                      WINDOW(TF513EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF513ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R TF513EC                   SFLCTL(TF513ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
