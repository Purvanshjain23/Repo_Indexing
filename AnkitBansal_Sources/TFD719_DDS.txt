     A*%%TS  SD  20081231  121210  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD719
     A*  TITLE:         VIEW PRODUCT EXCEPTION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/08/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R TF719R1
     A*%%TS  SD  20081231  121210  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'VIEW PRODUCT EXCEPTION'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  3 31'Year'
     A                                      DSPATR(UL)
     A                                  3 37'Week'
     A                                      DSPATR(UL)
     A                                  4  2'Week-ending date:'
     A                                      DSPATR(HI)
     A            D1WEDTMDY R        O  4 20REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A            D1YR      R        O  4 31REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A            D1WK      R        O  4 38REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                  4 49'System-generated:'
     A                                      DSPATR(HI)
     A            D1SYSFL   R        O  4 67REFFLD(TFREF/@@SYSFL *LIBL/TFPREF)
     A                                  5 58'Program:'
     A                                      DSPATR(HI)
     A            D1PGM     R        O  5 67REFFLD(TFREF/@@PGM *LIBL/TFPREF)
     A                                  6  3'Product code:'
     A                                      DSPATR(HI)
     A            D1PRCD    R        O  6 17REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1PRDS    R        O  6 26REFFLD(TFREF/@@PRDS *LIBL/TFPREF)
     A                                  7  8'Comment:'
     A                                      DSPATR(HI)
     A            D1EXCOM   R        O  7 17REFFLD(TFREF/@@EXCOM *LIBL/TFPREF)
     A                                  7 56'Item type:'
     A                                      COLOR(PNK)
     A            D1ITYCD   R        O  7 67REFFLD(TFREF/@@ITYCD *LIBL/TFPREF)
     A                                  8  2'Exception Nbr:'
     A                                      DSPATR(HI)
     A            D1PESN    R        O  8 17REFFLD(TFREF/@@PESN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  8 50'Meat cost group:'
     A                                      COLOR(PNK)
     A            D1CGCD    R        O  8 67REFFLD(TFREF/@@CGCD *LIBL/TFPREF)
     A                                  9 47'Meat cost subgroup:'
     A                                      COLOR(PNK)
     A            D1SGCD    R        O  9 67REFFLD(TFREF/@@SGCD *LIBL/TFPREF)
     A                                 10 56'Mix group:'
     A                                      COLOR(PNK)
     A            D1MIXGRP  R        O 10 67REFFLD(TFREF/@@MIXGRP *LIBL/TFPREF)
     A                                 11 51'TF Class group:'
     A                                      COLOR(PNK)
     A            D1TFCGCD  R        O 11 67REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A                                 12  2'FLAGS:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 12 48'TF Classification:'
     A                                      COLOR(PNK)
     A            D1TFCLCD  R        O 12 67REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A                                 13  2'Mix flag ..:'
     A                                      DSPATR(HI)
     A            D1MIXFL   R        O 13 15REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A                                 13 34'Item structure type/group/class:'
     A                                      COLOR(PNK)
     A            D1ISTYCD  R        O 13 67REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A            D1ISGRCD  R        O 13 71REFFLD(TFREF/@@ISGRCD *LIBL/TFPREF)
     A            D1ISCLCD  R        O 13 75REFFLD(TFREF/@@ISCLCD *LIBL/TFPREF)
     A                                 14  2'Volume flag:'
     A                                      DSPATR(HI)
     A            D1VOLFL   R        O 14 15REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A                                 14 34'IS Class desc:'
     A                                      COLOR(PNK)
     A            D1ISCLDS  R        O 14 49REFFLD(TFREF/@@ISCLDS *LIBL/TFPREF)
     A                                 16  2'ADJUST VALUES:             '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 16 31'      SEABOARD     '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 16 53'       TRIUMPH     '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 17  2'Sold pounds ..............:'
     A                                      DSPATR(HI)
     A            D1SEXSLLB R        O 17 31REFFLD(TFREF/@@SEXSLLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TEXSLLB R        O 17 53REFFLD(TFREF/@@TEXSLLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 18  2'Net product revenue amount:'
     A                                      DSPATR(HI)
     A            D1SEXNPRAMR        O 18 32REFFLD(@@SEXNPRAM)
     A                                      EDTCDE(K)
     A            D1TEXNPRAMR        O 18 54REFFLD(@@TEXNPRAM)
     A                                      EDTCDE(K)
     A                                 19  2'Produced pounds ..........:'
     A                                      DSPATR(HI)
     A            D1SEXPULB R        O 19 31REFFLD(TFREF/@@SEXPULB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TEXPULB R        O 19 53REFFLD(TFREF/@@TEXPULB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 20  2'Produced amount ..........:'
     A                                      DSPATR(HI)
     A            D1SEXPUAM R        O 20 32REFFLD(TFREF/@@SEXPUAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TEXPUAM R        O 20 54REFFLD(TFREF/@@TEXPUAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R TF719K1
     A*%%TS  SD  20060911  095744  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                       user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF719ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF719EC                   SFLCTL(TF719ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
