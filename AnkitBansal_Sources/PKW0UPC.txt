/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - CL PROGRAM FOR BUILDING AND DOWNLOADING SWINE        */
/*             DAILY REPORT FILES - AFTERNOON REPORTING TIME        */
/*      LS119A SWINE DAILY SUMMARY                                */
/*      LS119B SWINE DAILY DETAIL                                 */
/*                                                                  */
/*  NOTE  ***** MUST BE COMPILED USER PROFILE(*OWNER)   *****       */
/*                                                                  */
/*  SYSTEM    - HOG PROCUREMENT AND EVALUATION                      */
/*  DATE      - 01/26/00                                            */
/*  DEVELOPER - PURVA DROGE                                         */
/*                                                                  */
/********************************************************************/
/* MODIFICATIONS:                                                   */
/* RMC  3/15/01  CALL SETHPELIB                                     */
/* RMC  3/19/01  CHANGE PCFILE FORMAT TO NEW *COMMA FORMAT          */
/* RMC  3/29/01  CHANGE SOME FIELDS TO NULL IF ZERO              */
/* RMC  3/29/01  DONT SEND LS119A,B IF NO DATA EXISTS            */
/* RMC  4/10/01  SEND SQL REPORT TO GUPPRT201                    */
/* RMC  4/12/01  SEND SQL REPORT TO PRKPRT04                     */
/* RMC  4/18/01  SET LIBLIST IF NOT USER CLASS *PGMR             */
/* RMC  5/16/02  DONT PRINT SQL TO PRKPRT04                      */
/* RMC  9/11/02  DONT CALL SETHPELIB                             */
/* RMC  11/16/5  CHANGED THE FTP PROGRAM CALLED. THE NEW PROGRAM */
/*               USES THE PLANT ESTABLISHMENT NUMBER TO DETERMINE*/
/*               THE FOLDER TO DOWNLOAD THE FILES INTO.          */
/*               PASS THE &PLTCO, &JOBD                          */
/* RMC  04/25/06 PRINT 960 SQL TO ST JO PRINTER TFPRT04A              */
/* RMC  05/11/06 PASS COMPANY IN TO XF                               */
/* RMC  05/17/6  CHG &DOC TO HAVE THE &PLTCO ALSO-1ST DIGIT OF COMPANY */
/* RMC  06/05/06 PRINT 960 SQL TO ST JO PRINTER TFPRT04               */
/* RMC  06/20/08 E00175 -  ADDED PARM TO FTPMPRPLT FOR FTP TO NEW FOLDER Y/N */
/*               IT WILL BE "N" IN THIS PGM                        */
/********************************************************************/
             PGM        PARM(&PLTCO &JOBD)

/* INPUT PARMS */
             DCL        VAR(&RPTTME) TYPE(*DEC) LEN(1 0) VALUE(2) +
                          /*REPORTING TIME    */

/* WORK FIELDS */
             DCL        VAR(&PLTEST) TYPE(*CHAR) LEN(5)   /*PLT ESTABLISHMENT#*/
             DCL        VAR(&RPTDTE) TYPE(*DEC ) LEN(7 0) /*REPORTING DATE    */
             DCL        VAR(&RPTDTEX) TYPE(*CHAR) LEN(7) /*REPORTING DATE    */
             DCL        VAR(&RPTTMEX) TYPE(*CHAR) LEN(1) /*REPORTING TIME    */
             DCL        VAR(&RPTDAY) TYPE(*CHAR) LEN(1)   /*REPORTING DAY STS */
             DCL        VAR(&LS119A) TYPE(*CHAR) LEN(1)   /*REPORTING DAY STS */
             DCL        VAR(&LS119B) TYPE(*CHAR) LEN(1)   /*REPORTING DAY STS */
             DCL        VAR(&YYMD)   TYPE(*CHAR) LEN(8)   /*YYYYMMDD          */
             DCL        VAR(&YYYY)   TYPE(*CHAR) LEN(4)   /*YEAR              */
             DCL        VAR(&MM)     TYPE(*CHAR) LEN(2)   /*MONTH             */
             DCL        VAR(&DD)     TYPE(*CHAR) LEN(2)   /*DAY               */
             DCL        VAR(&TIME)   TYPE(*CHAR) LEN(6)   /*SYSTEM TIME       */
             DCL        VAR(&JOBD) TYPE(*CHAR) LEN(10) /* JOBD FOR +
                          SETLIB */
             DCL        VAR(&PLTCO) TYPE(*CHAR) LEN(3) /* PLANT +
                          COMPANY */
             DCL        VAR(&PLTNO) TYPE(*CHAR) LEN(1) /* PLANT 1ST DIGIT */
             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0) /* PLANT */
             DCL        VAR(&NEWFLD) TYPE(*CHAR) LEN(1) VALUE('N') +
                          /* FTP TO OLD FOLDER */
             DCL        VAR(&USRCLS) TYPE(*CHAR) LEN(10)  /*USER CLASS        */

             DCL        VAR(&DOC)   TYPE(*CHAR) LEN(10)   /*DOCUMENT NAME     */
             DCL        VAR(&PCFILE) TYPE(*CHAR) LEN(32) /*PC FILE           */
                        /* FORMAT: LS118A_PPPPP_YYYYMMDDHHMMSS.MPR */

             DCL        VAR(&RMTCMD) TYPE(*CHAR) LEN(256) /*REMOTE COMMAND*/

             DCL        VAR(&SEL)   TYPE(*CHAR) LEN(512) /*SELECT STATEMENT  */
/*-----------------------------------------------------------------*/
/* INITILIZATION                                                   */
/*-----------------------------------------------------------------*/

/* GET USER CLASS   */
             RTVUSRPRF  USRCLS(&USRCLS)
/* SET LIBRARY LIST  11.18.05    */

             SETLIBL    JOBD(&JOBD)
/*-----------------------------------------------------------------*/
/* MAINLINE                                                        */
/*-----------------------------------------------------------------*/
             CHGVAR     VAR(&COMP) VALUE(&PLTCO)
PLTNO:       CHGVAR     VAR(&PLTNO) VALUE(%SST(&PLTCO 1 1))
             CALL       PGM(PKWEXFR) PARM('' &RPTTME &COMP &PLTEST &RPTDTE  +
                          &RPTDAY &LS119A &LS119B)
             IF         COND(&RPTDAY *EQ 'Y') THEN(DO)
             /* SET UP DATE AND TIME */
             CHGVAR     VAR(&RPTDTEX) VALUE(&RPTDTE)
             CHGVAR     VAR(&RPTTMEX) VALUE(&RPTTME)
             CVTDAT     DATE(&RPTDTEX) TOVAR(&YYMD) FROMFMT(*CYMD) +
                          TOFMT(*YYMD) TOSEP(*NONE)
             CHGVAR     VAR(&YYYY) VALUE(%sst(&YYMD 1 4))
             CHGVAR     VAR(&MM)   VALUE(%sst(&YYMD 5 2))
             CHGVAR     VAR(&DD)   VALUE(%sst(&YYMD 7 2))

             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)

 /******* SEND FILE A: LS119A MPR DAILY PURCHASE SUM  (PKE8CPP)   **** ***/

 IF COND(&LS119A *EQ 'N') THEN(GOTO LS119B)

             CHGVAR     VAR(&DOC)  VALUE('PKE8CPP' *CAT &PLTNO)

             CHGVAR     VAR(&PCFILE) VALUE('ls119a_' *CAT &PLTEST +
                          *CAT '_' *CAT &YYYY *CAT &MM *CAT &DD +
                          *CAT &TIME *CAT '.MPR')

             CHGVAR     VAR(&SEL) VALUE('SELECT E8H8CD, +
                          CHAR(CVTDATE(E8ETDT,CYMD),USA) LEN(10) +
                          NAME(RPTDTE), E8C2A1, E8D1A1, E8D2A1, +
                          E8D3A1, E8D4A1, E8D5A1, E8D6A1, E8D7A1, +
                          E8D8A1, E8D9A1, E8EAA1, E8EBA1, E8ECA1, +
                          E8EDA1, E8EEA1, E8EFA1, E8EGA1 FROM +
                          PKE8CPP WHERE E8H8CD="' *CAT &PLTEST *CAT +
                          '" AND E8ETDT=' *CAT &RPTDTEX *BCAT 'AND +
                          E8C2A1=' *CAT &RPTTMEX)

             EXECUTE    SQL(&SEL) TOFLR(GUY.IS) TODOC(&DOC) +
                          PCFMT(*COMMA) REPLACE(*YES)

             CALL       PGM(FTPMPRPLT) PARM(&PLTCO &DOC &PCFILE &NEWFLD)

/******* SEND FILE B: LS119B MPR DAILY PURCHASE DTL    (PKE9CPP) ****  ***/

 LS119B:     IF         COND(&LS119B *EQ 'N') THEN(GOTO CMDLBL(PRINT))

             CHGVAR     VAR(&DOC)  VALUE('PKE9CPP' *CAT &PLTNO)

             CHGVAR     VAR(&PCFILE) VALUE('ls119B_' *CAT &PLTEST +
                          *CAT '_' *CAT &YYYY *CAT &MM *CAT &DD +
                          *CAT &TIME *CAT '.MPR')

             CHGVAR     VAR(&SEL) VALUE('SELECT E9H8CD, +
                          CHAR(CVTDATE(E9ETDT,CYMD),USA) LEN(10) +
                          NAME(RPTDTE), E9C2A1, E9IBCD, E9EIA1, +
CASE E9EJA1 WHEN 0 THEN " " ELSE CHAR(E9EJA1) END NAME(EJA1), E9EKA1, +
CASE E9ELA1  WHEN 0 THEN " " ELSE CHAR(E9ELA1) END  NAME(ELA1), +
CASE E9U0PR  WHEN 0 THEN " " ELSE CHAR(E9U0PR) END NAME(U0PR), +
                          E9IDCD +
                          FROM PKE9CPP WHERE E9H8CD="' *CAT &PLTEST +
                          *CAT '" AND E9ETDT=' *CAT &RPTDTEX *BCAT +
                          'AND E9C2A1=' *CAT &RPTTMEX)

             EXECUTE    SQL(&SEL) +
                          TOFLR(GUY.IS) +
                          TODOC(&DOC) PCFMT(*COMMA) +
                          REPLACE(*YES)

            CALL       PGM(FTPMPRPLT) PARM(&PLTCO &DOC &PCFILE &NEWFLD)

         /******* SEND SQL REPORT TO GUPPRT201                  ****   ***/
PRINT:       IF COND(&USRCLS *NE '*PGMR') THEN(DO)
 IF COND(&PLTCO = '360') THEN(DO)
             OVRPRTF    FILE(SQLPRT1) OUTQ(GUPPRT201) HOLD(*NO)
             ENDDO
 IF COND(&PLTCO = '960') THEN(DO)
             OVRPRTF    FILE(SQLPRT1) OUTQ(TFPRT04) HOLD(*NO)
             ENDDO
             REPORT     REPORT(SQPKE8CPPR) VIEW(SQPKE8CPPP) +
                          HOLD(*SAME) SETVAR((&C2A1 &RPTTMEX) (&ETDT +
                          &RPTDTEX))
             REPORT     REPORT(SQPKE9CPPR) VIEW(SQPKE9CPPP) +
                          HOLD(*SAME) SETVAR((&C2A1 &RPTTMEX) (&ETDT +
                          &RPTDTEX))
             ENDDO
 /*          OVRPRTF    FILE(SQLPRT1) OUTQ(PRKPRT04) HOLD(*NO)     +
             REPORT     REPORT(SQPKE8CPPR) VIEW(SQPKE8CPPP) +
                          HOLD(*SAME) SETVAR((&C2A1 &RPTTMEX) (&ETDT +
                          &RPTDTEX))                               +
             REPORT     REPORT(SQPKE9CPPR) VIEW(SQPKE9CPPP) +
                          HOLD(*SAME) SETVAR((&C2A1 &RPTTMEX) +
                          (&ETDT &RPTDTEX))                        */

             ENDDO

/*-----------------------------------------------------------------*/
/* END PROGRAM                                                     */
/*-----------------------------------------------------------------*/
ENDPGM:
             DLTOVR     FILE(*ALL)

             ENDPGM
