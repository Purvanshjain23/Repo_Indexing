     A*%%TS  SD  20001012  154447  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4049
     A*  TITLE:         WORK WITH LOAD TYPES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/12/00
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4049S1                  SFL
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  8  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            D1LTCD    R        O  8  9REFFLD(@@LTCD)
     A            D1LTDS    R        O  8 18REFFLD(@@LTDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4049C1                  SFLCTL(HP4049S1)
     A*%%TS  SD  20001012  154447  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0014)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R           1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH LOAD TYPES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  6  2'Opt'
     A                                      DSPATR(HI)
     A                                  6  8'Load     '
     A                                      DSPATR(HI)
     A                                  7  8'Type'
     A                                      DSPATR(HI)
     A                                  7 18'Description'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4049K1
     A*%%TS  SD  19950220  141328  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F5=Refresh'
     A N99                             23 23'F6=Create'
     A N99                             23 34'F7=Listing'
     A N99                             23 46'F17=Print Options'
     A N99                             23 64'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4049ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4049EC                  SFLCTL(HP4049ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
