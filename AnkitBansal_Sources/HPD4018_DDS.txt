     A*%%TS  SD  20030217  112802  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4018
     A*  TITLE:         WORK WITH FARM HEALTH STATUS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/13/03
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4018S1                  SFL
     A*%%TS  SD  20030213  140505  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            HDEFDT    R        H      REFFLD(@@DT08)
     A            D1OPT     R        B 12  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1FSCD    R        O 12  8REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1HCCD    R        O 12 17REFFLD(HSRREF/@@HCCD *LIBL/HSPREF)
     A            D1HSFL    R        O 12 28REFFLD(HSRREF/@@HSFL *LIBL/HSPREF)
     A            D1MDYEFDT R        O 12 36REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CELL    R        O 12 50REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4018C1                  SFLCTL(HP4018S1)
     A*%%TS  SD  20030217  112802  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 25'WORK WITH FARM HEALTH STATUS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Show only the last status for each-
     A                                       farm/health code ..'
     A                                  4 57'?'
     A            C1YNFL    R        B  4 59REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  6  2'2=Revise'
     A                                  6 12'4=Delete'
     A                                  8 11'?'
     A                                      DSPATR(HI)
     A                                  8 19'?'
     A                                      DSPATR(HI)
     A                                  8 28'?'
     A                                      DSPATR(HI)
     A                                  9  9'Farm'
     A                                      DSPATR(HI)
     A                                  9 17'Health'
     A                                      DSPATR(HI)
     A                                  9 26'Health'
     A                                      DSPATR(HI)
     A                                  9 36'Effective'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  9'Site'
     A                                      DSPATR(HI)
     A                                 10 17'Code'
     A                                      DSPATR(HI)
     A                                 10 26'Status'
     A                                      DSPATR(HI)
     A                                 10 39'Date'
     A                                      DSPATR(HI)
     A                                 10 49'Cell'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B 11  8REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N51N53N54                            DSPATR(PC)
     A            C1HCCD    R        B 11 17REFFLD(HSRREF/@@HCCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1HSFL    R        B 11 28REFFLD(HSRREF/@@HSFL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1MDYEFDT R        B 11 36REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1CELL    R        B 11 50REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP4018K1
     A*%%TS  SD  20011025  145441  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F17-Print Options'
     A                                 23 57'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4018ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4018EC                  SFLCTL(HP4018ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
