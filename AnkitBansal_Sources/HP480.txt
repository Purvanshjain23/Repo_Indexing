      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP480
      * TITLE:       SPECIFY HOG SALES ACTIVITY REPORT OPTIONS
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     06/16/94
      *
      * FUNCTION: USES A SPECIFY RECORD FORMAT TO ALLOW USER TO
      *           ENTER A BUSINESS OFFICE, FROM DATE, TO DATE,
      *           PRODUCTION TYPE, PHASE, SALES TYPE, FARM SITE,
      *           CUSTOMER A/R NO, AND HOG GROUP. THE USER MUST SELECT
      *           WHICH REPORT TO SUBMIT:
      *
      *                HOG SALES ACTIVITY SUMMARY REPORT
      *                HOG SALES ACTIVITY DETAIL REPORT
      *                HOG SALES REPROT WITH ANIMAL AGE
      *
      *           THE OPTIONS ARE LOADED INTO THE *LDA FOR SUBMISSION.
      *           THIS PROGRAM IS CALLED FROM A MENU.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/07/00  LeAnne Fedor
      *           Removed print options (hold, copies, outque, etc.) from screen
      *           and replaced them with a function key that calls the pop-up
      *           window of print options.
      *
      * 02/14/01  LeAnne Fedor
      *           In preparation for allowing groups on a single farm to have different
      *           production types, we changed the edit for a farm.  The production type
      *           of a selected farm no longer has to "match" the production type that
      *           the user may have keyed on the screen.
      *
      * 07/22/02  LeAnne Fedor
      *           In preparation for allowing groups on a single farm to have different
      *           production phases, we changed the edit for a farm.  The production phase
      *           of a selected farm no longer has to "match" the production phase that
      *           the user may have keyed on the screen.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD480    CF   E             WORKSTN
     F                                     INFDS(IOFEED)
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS                 (KEY: FNFNCD)
      *
     FHSP009    IF   E           K DISK
      * BUSINESS OFFICE              (KEY: BOBOCD)
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE           (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE          (KEY: PPPPCD)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                 (KEY: FSFSCD)
      *
     FHSP031    IF   E           K DISK
      *    BREED STOCK               (KEY: BSBSCD)
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP                 (KEY: HGHGCD)
      *
     FHSP066    IF   E           K DISK
      *    SALE TYPE CODES           (KEY: STSTCD)
      *
     FHSL079A   IF   E           K DISK
      *  SALE TYPE REPORT CODES      (KEY: SRTPCD SRSTCD)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      *  COMPILE TIME ARRAYS FOR SUBMIT JOB COMMAND USED IN QCMDEXC
      *---------------------------------------------------------------
     D CMD             S              1    DIM(80) CTDATA PERRCD(80)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAAPER'
      *---------------------------------------------------------------
     D                 DS
     D  DAAPER                 1     24
     D  DACCYY                 1      4
     D  DAPER                  5      6
     D  DABPDT                 7     14  0
     D  DAEPDT                15     22  0
     D  DAPGFL                23     23
     D  DAPPFL                24     24
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D ALL             C                   CONST('A')
     D UPDATE          C                   CONST('UPDATE')
      *
     D SUM             C                   CONST('HOG SALES ACTIVITY -
     D                                     SUMMARY')
      *
     D DETAIL          C                   CONST('HOG SALES ACTIVITY -
     D                                     DETAIL')
      *
     D CARC            C                   CONST('CARCASS PAYMENT')
     D LIVE            C                   CONST('LIVE PAYMENT')
      *
      *---------------------------------------------------------------
      *  LDA - SALES ACTIVITY REPORT OPTIONS
      *---------------------------------------------------------------
      *
     D LDA            UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDSTCD                32     36
     D  LDFSCD                37     41  0
     D  LDCVNO                42     49  0
     D  LDHGSN                50     56  0
     D  LDRPFL                57     57
     D  LDTPCD                58     58
      *
     D  LDBODS                59     94
     D  LDFSNM                95    119
     D  LDHGCD               120    126
     D  LDCNAM               127    156
     D  LDSTDS               157    176
     D  LDPPDS               177    206
     D  LDFMDY               207    212  0
     D  LDTMDY               213    218  0
     D  LDPTDS               219    228
     D  LDTPDS               229    248
     D  LDBLCD               249    253
     D  LDRMCD               254    258
     D  LDRPDS               259    265
     D  LDBSCD               266    270
     D  LDBSDS               271    290
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412  0
     D  LDOUTQ               413    422
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      * RETRIEVE DEFAULT FROM AND TO DATES
      *
     C                   Z-ADD     WKFMDY        DFFMDY
     C                   Z-ADD     WKTMDY        DFTMDY
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * IF THE USER FAILED THE INITIAL SECURITY CHECKS, SETON 99
      * TO PROTECT INPUT FIELDS AND INVALIDATE FUNCTION KEYS.
      *
     C     TERMFL        IFEQ      YES
     C                   SETON                                        99
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP480K1
     C                   WRITE     HP480EC
     C                   EXFMT     HP480R1
      *
      * CLEAR THE MESSAGES FROM THE MESSAGE DATA STRUCTURES AND PROGRAM
      * MESSAGE QUEUE.
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND SET THE NEXT ROUTINE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C     TERMFL        OREQ      YES                                          BAD USER
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN17         WHENEQ    *ON                                          F17-PRINT OPT
     C                   MOVEL     SCRN1         ROUTNE
     C                   CALL      'HP451'
     C                   PARM                    SDPGM
     C     LDCOPY        PARM      LDCOPY        XXCOPY
     C                   PARM                    LDHOLD
     C                   PARM                    LDOUTQ
     C                   PARM                    XXNITE
     C                   PARM                    XXJOBQ
      *
     C     *IN20         WHENEQ    *ON                                          F20-WRKSPLF
     C                   CALL      'QEZOUTPT'
     C                   MOVEL     SCRN1         ROUTNE
      *
      *
      * CHECK FOR A USER PROMPT FIRST. THE USER MAY WANT
      * TO SELECT A NEW BUSINESS OFFICE.
      *
     C     *IN04         WHENEQ    *ON                                          WH PROMPT
     C                   EXSR      $F4S1
     C                   MOVEL     SCRN1         ROUTNE
     C                   OTHER
      *
      * ALWAYS CHECK SECURITY FOR BUSINESS OFFICE
      *
     C                   MOVEL(P)  'HP480'       WKFNCD
     C                   EXSR      $CHKBO
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * VERIFY THAT A FROM DATE IS ENTERED
      *
     C     DFFMDY        IFEQ      0                                            IF NO DATE
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
      * VERIFY THAT THE DATE IS VALID
      *
     C                   Z-ADD     DFFMDY        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   EXSR      $DATE
      *
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          LDFYMD
     C                   ENDIF                                                  IF INVALID
     C                   ENDIF                                                  IF NO DATE
      *
      * VERIFY THAT A TO DATE IS ENTERED
      *
     C     DFTMDY        IFEQ      0                                            IF NO TO
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
      * VERIFY THAT THE DATE IS VALID
      *
     C                   Z-ADD     DFTMDY        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   EXSR      $DATE
      *
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          LDTYMD
     C                   ENDIF                                                  IF INVALID
     C                   ENDIF                                                  IF NO TO
      *
      * VERIFY THAT THE "TO" DATE IS NOT EARLIER THAN THE "FROM" DATE
      *
     C     LDTYMD        IFLT      LDFYMD                                       IF EARLIER
     C     *IN32         ANDEQ     *OFF
     C     *IN33         ANDEQ     *OFF
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09009'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF EARLIER
      *
      * IF PRODUCTION TYPE KEYED, CHECK TO SEE IF IT IS VALID
      *
     C                   MOVE      *BLANK        DFPTDS
     C     DFPTCD        IFNE      *BLANK                                       IF TYPE
     C     DFPTCD        CHAIN     HSP010                             90
     C     *IN90         IFEQ      *OFF                                         IF GOOD TYPE
     C                   MOVEL     PTPTDS        DFPTDS
     C                   ELSE
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00180'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFPTCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD TYPE
     C                   ENDIF                                                  IF TYPE
      *
      * IF PRODUCTION PHASE KEYED, CHECK TO SEE IF IT IS VALID
      *
     C                   MOVE      *BLANK        DFPPDS
     C     DFPPCD        IFNE      *BLANK                                       IF PHASE
     C     DFPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF GOOD PHAS
     C                   MOVEL     PPPPDS        DFPPDS
     C                   ELSE
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00170'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFPPCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD PHAS
     C                   ENDIF                                                  IF PHASE
      *
      * IF SALE TYPE KEYED, VALIDATE
      *
     C                   MOVE      *BLANK        DFSTDS
     C     DFSTCD        IFNE      *BLANK                                       IF SALE TYPE
     C     DFSTCD        CHAIN     HSP066                             37
     C     *IN37         IFEQ      *OFF                                         IF GOOD
     C                   MOVEL     STSTDS        DFSTDS
     C                   ELSE
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00456'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFSTCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD
     C                   ENDIF                                                  IF SALE TYPE
      *
      * IF ENTERED, EDIT TYPE OF PAYMENT
      *
     C                   MOVE      *BLANK        DFTPDS
     C     DFTPCD        IFNE      *BLANK                                       IF TYPE PAY
     C     DFTPCD        CHAIN     HSL079A                            92
     C     *IN92         IFEQ      *ON                                          IF BAD PAY
     C                   SETON                                        40
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00651'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   SELECT
     C     DFTPCD        WHENEQ    'L'
     C                   MOVEL     LIVE          DFTPDS
     C     DFTPCD        WHENEQ    'C'
     C                   MOVEL     CARC          DFTPDS
     C                   ENDSL
      *
      * IF THE USER ENTERED A VALID SALES TYPE, CHECK THAT THE
      * TYPE OF PAYEMENT SPECIFIED MATCHES THE SALES TYPE
      *
     C     DFSTCD        IFNE      *BLANK                                       IF ONE SALE
     C     *IN37         ANDEQ     *OFF
     C     K1L079        CHAIN     HSL079A                            92
     C     *IN92         IFEQ      *ON                                          IF MISMATCH
     C                   SETON                                        40
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00664'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFTPCD        MGW(1)
     C                   MOVEA     DFSTCD        MGW(2)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF MISMATCH
     C                   ENDIF                                                  IF ONE SALE
     C                   ENDIF                                                  IF BAD PAY
     C                   ENDIF                                                  IF TYPE PAY
      *
      * IF FARM SITE KEYED,
      *    VALIDATE AND RETREIVE NAME
      *    CHECK FOR A MATCH BETWEEN FARM'S B.O. AND SCREEN B.O.
      *
     C                   MOVE      *BLANK        DFFSNM
     C     DFFSCD        IFNE      0                                            IF FARM
     C     DFFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD FARM
     C                   MOVEL     FSFSNM        DFFSNM
      *
     C     FSFSBO        IFNE      DFFSBO                                       IF MISMATCH
     C     DFFSBO        ANDNE     *BLANK
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00158'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     FSFSBO        MGW(6)
     C                   MOVEA     DFFSBO        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF MISMATCH
     C                   ELSE
      * INVALID FARM
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD FARM
     C                   ENDIF                                                  IF FARM
      *
      * VALIDATE THE CUSTOMER A/R NUMBER
      *
     C                   MOVE      *BLANK        DFCNAM
     C                   MOVE      *BLANK        LDCNAM
     C     DFCVNO        IFNE      0                                            IF CUST
     C                   Z-ADD     DFCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
      *
      * IF THERE IS AN ERROR WITH THE CUST A/R NUMBER
      *
     C     JDEFL         IFEQ      YES
     C                   SETON                                        38
     C                   ELSE
     C                   MOVEL     OUALPH        DFCNAM
     C                   MOVEL     OUALPH        LDCNAM
     C                   ENDIF
     C                   ENDIF                                                  IF CUST
      *
      *
      * IF BREED STOCK KEYED, VALIDATE
      *
     C                   MOVE      *BLANK        DFBSDS
     C     DFBSCD        IFNE      *BLANK                                       IF BREED
     C     DFBSCD        CHAIN     HSP031                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD
     C                   MOVEL     BSBSDS        DFBSDS
     C                   ELSE
     C                   SETON                                        30
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00251'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFBSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD
     C                   ENDIF                                                  IF BREED
      *
      *
      * IF A HOG GROUP IS KEYED
      *    VALIDATE
      *    CHECK FOR MATCH ON FARM SITE
      *    CHECK FOR MATCH ON BUSINESS OFFICE
      *    CHECK FOR MATCH ON BREED STOCK
      *
     C                   Z-ADD     0             LDHGSN
      *
     C     DFHGCD        IFNE      *BLANK                                       IF GROUP
      *
     C     DFRPFL        IFEQ      'S'                                          IF SUMMARY
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00467'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     DFHGCD        CHAIN     HSP034                             93
     C     *IN93         IFEQ      *OFF                                         IF GOOD GRP
      *
      * VOIDED GROUPS CANNOT BE SELECTED FOR THIS REPORT
      *
     C     HGGSCD        IFEQ      'VD'                                         IF VOIDED
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00477'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
      * 'CREATED' GROUPS CANNOT BE SELECTED FOR THIS REPORT
      *
     C     HGGSCD        IFEQ      'CR'                                         IF CREATED
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00478'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF CREATED
     C                   ENDIF                                                  IF VOIDED
      *
      * THE GROUP'S BUSINESS OFFICE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGFSBO        IFNE      DFFSBO                                       IF DIFF B/O
     C     DFFSBO        ANDNE     *BLANK
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00459'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGFSBO        MGW(8)
     C                   MOVEA     DFFSBO        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF B/O
      *
      * THE GROUP'S PRODUCTION TYPE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGPTCD        IFNE      DFPTCD                                       IF DIFF PT
     C     DFPTCD        ANDNE     *BLANK
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00463'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGPTCD        MGW(8)
     C                   MOVEA     DFPTCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF PT
      *
      * THE GROUP'S PRODUCTION PHASE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGPPCD        IFNE      DFPPCD                                       IF DIFF PP
     C     DFPPCD        ANDNE     *BLANK
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00462'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGPPCD        MGW(8)
     C                   MOVEA     DFPPCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF PP
      *
      * THE GROUP'S FARM SITE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGFSCD        IFNE      DFFSCD                                       IF DIFF FARM
     C     DFFSCD        ANDNE     0
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00468'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVE      HGFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(8)
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF FARM
      *
      *
      * THE GROUP'S BREED STOCK MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGBSCD        IFNE      DFBSCD                                       IF DIFF BRED
     C     DFBSCD        ANDNE     *BLANK
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00461'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGBSCD        MGW(8)
     C                   MOVEA     DFBSCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF BRED
      *
     C                   Z-ADD     HGHGSN        LDHGSN
     C                   MOVEL     HGBLCD        LDBLCD
     C                   MOVEL     HGRMCD        LDRMCD
     C                   ELSE
      *
      * INVALID GROUP
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00261'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD GRP
     C                   ENDIF                                                  IF SUMMARY
     C                   ENDIF                                                  IF GROUP
      *
      * SUMMARY, DETAIL OR ANIMAL AGE REPORT MUST BE SELECTED
      *
     C                   MOVE      *BLANK        DFRPDS
      *
     C     DFRPFL        IFNE      'S'                                          IF WRONG
     C     DFRPFL        ANDNE     'D'
     C                   SETON                                        41
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00454'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   SELECT
     C     DFRPFL        WHENEQ    'S'
     C                   MOVEL(P)  SUM           DFRPDS
      *
     C     DFRPFL        WHENEQ    'D'
     C                   MOVEL(P)  DETAIL        DFRPDS
     C                   ENDSL
     C                   ENDIF                                                  IF WRONG
      *
      * IF THERE ARE NO ERRORS, EXECUTE THE SUBROUTINE TO PROCESS THE
      * USERS REQUEST
      *
     C     ERROR         IFEQ      0
     C     *IN44         IFEQ      *OFF
     C                   SETON                                        44
     C                   ELSE
     C                   EXSR      $PROC1
     C                   ENDIF
     C                   ELSE
     C                   SETOFF                                       44
     C                   ENDIF
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CHKBO - CHECK THE BUSINESS OFFICE ENTERED AS A RESTRICTOR
      *----------------------------------------------------------------
      *
      * THE BUSINESS OFFICE ENTERED MUST:
      *    BE A VALID OFFICE IN THE BUSINESS OFFICE MASTER
      *    HAVE A SECURITY LOCATION TO WHICH THE USER IS AUTHORIZED
      *
     C     $CHKBO        BEGSR
      *
      * SET THE 'VALIDATE B.O.' FLAG DEPENDING ON WHETHER THE
      * USER LEFT B.O. BLANK TO GET ALL BUSINESS OFFICES OR ENTERED
      * A SPECIFIC OFFICE.
      *
     C     DFFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           INBOFL
     C                   ELSE
     C                   MOVEL     YES           INBOFL
     C                   ENDIF
      *
      * CHECK THE USER'S SECURITY TO THIS BUSINESS OFFICE.
      * SET INDVFL TO YES TO TELL SECURITY PROGRAM TO COMPARE THE
      * FUNCTION DIVISION CODE TO THE B.O. DIVISION CODE.
      *
     C                   MOVEL     YES           INDVFL
     C                   EXSR      $SEC
     C                   MOVEL     NO            INDVFL
      *
     C                   MOVEL     OUBODS        DFBODS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SEC   - CALL SECURITY PROGRAM
      *---------------------------------------------------------------
      *
     C     $SEC          BEGSR
      *
      * SETUP THE FIELDS TO SEND INTO THE SECURITY PROGRAM
      *
     C                   MOVEL     WKFNCD        INFNCD                         PGM NAME
     C                   MOVEL     DFFSBO        INBOBO                         SCREEN BO
      *
     C                   MOVE      *BLANK        OUBOBO                         BO CODE
     C                   MOVE      *BLANK        OUBODS                         BO DESC
     C                   MOVE      *BLANK        OUDVCD                         FUNCTION DIV
     C                   MOVEL     NO            TERMFL            1            TERMINAL FLG
      *
     C                   CALL      'HPSEC  '
      **** IN
     C                   PARM                    MGID                           MSGID ARRAY
     C                   PARM                    MGDT                           MSGDTA DATA
     C                   PARM                    ERROR                          # OF ERRORS
     C                   PARM                    INFNCD           10            FUNC CODE
     C                   PARM                    INBOBO            5            BO CODE
     C                   PARM                    INDVFL            1            CHECK DIV?
     C                   PARM                    INBOFL            1            CHECK B.O.?
      **** OUT
     C                   PARM                    OUBOBO            5            BO CODE
     C                   PARM                    OUBODS           30            BO DESC.
     C                   PARM                    OUDVCD            5            DIV CODE
     C                   PARM                    SECFL             1            SEC FLAG
     C                   PARM                    OUBOFL            1            BO ERRFLG
     C                   PARM                    OURMFL            1
      *
     C     OUBOFL        IFEQ      YES
     C                   SETON                                        31
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO PROCESS THE USER'S REQUEST
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
      *  IF USER TOOK F8=SUBMIT REPORT
      *
     C     *IN08         IFEQ      *ON                                          IF F8=SBMJOB
      *
      * MOVE DISPLAY FILE FIELDS INTO LDA
      *
     C                   Z-ADD     DFFMDY        LDFMDY
     C                   Z-ADD     DFTMDY        LDTMDY
     C                   MOVEL     DFFSBO        LDFSBO
     C                   MOVEL     DFBODS        LDBODS
     C                   MOVEL     DFPTCD        LDPTCD
     C                   MOVEL     DFPTDS        LDPTDS
     C                   MOVEL     DFPPCD        LDPPCD
     C                   MOVEL     DFPPDS        LDPPDS
     C                   MOVEL     DFSTCD        LDSTCD
     C                   MOVEL     DFSTDS        LDSTDS
     C                   Z-ADD     DFFSCD        LDFSCD
     C                   MOVEL     DFFSNM        LDFSNM
     C                   MOVEL     DFCVNO        LDCVNO
     C                   MOVEL     DFRPFL        LDRPFL
     C                   MOVEL     DFBSCD        LDBSCD
     C                   MOVEL     DFBSDS        LDBSDS
      *
     C                   SELECT
     C     DFRPFL        WHENEQ    'D'
     C                   MOVEL(P)  'DETAIL'      LDRPDS
     C     DFRPFL        WHENEQ    'S'
     C                   MOVEL(P)  'SUMMARY'     LDRPDS
     C                   ENDSL
      *
     C                   MOVEL     DFTPCD        LDTPCD
     C                   MOVEL     DFTPDS        LDTPDS
     C                   MOVEL     DFHGCD        LDHGCD
      *
     C                   OUT       LDA
      *
      * MOVE THE 80-CHARACTER COMPILE TIME ARRAY 'CMD' TO A
      * 80-CHARACTER FIELD FOR SUBMITTING TO QCMDEXC.
      *
     C                   MOVEA     XXJOBQ        CMD(52)
     C                   MOVEA     CMD           CMDSBM
      *
     C                   CALL      'QCMDEXC'
     C                   PARM                    CMDSBM
     C                   PARM                    LENGTH
      *
      * ISSUE STANDARD REQUEST SUBMISSION MESSAGE
      *
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09003'     MGID(E)
     C                   ENDIF                                                  IF F8=SBMJOB
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *---------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZERO         ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *----------------------------------------------------------------
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $F4S1 - F4 PROMPT ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      * BUSINESS OFFICE
      *
     C     FLD           WHENEQ    'DFFSBO'
     C                   MOVE      *BLANK        XXFSBO
     C                   MOVE      *BLANK        XXBODS
     C                   CALL      'HP503'
     C                   PARM                    XXFSBO
     C                   PARM                    XXBODS
     C                   PARM                    SVDVCD
     C                   SETON                                        51
     C     XXFSBO        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXFSBO        DFFSBO
     C                   MOVEL     XXBODS        DFBODS
     C                   ENDIF                                                  IF SELECTED
      *
      * PRODUCTION TYPE
      *
     C     FLD           WHENEQ    'DFPTCD'
     C                   MOVE      *BLANK        XXPTCD
     C                   MOVE      *BLANK        XXPTDS
     C                   CALL      'HP504'
     C                   PARM                    XXPTCD
     C                   PARM                    XXPTDS
     C                   SETON                                        52
     C     XXPTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPTCD        DFPTCD
     C                   MOVEL     XXPTDS        DFPTDS
     C                   ENDIF                                                  IF SELECTED
      *
      * PRODUCTION PHASE
      *
     C     FLD           WHENEQ    'DFPPCD'
     C                   MOVE      *BLANK        XXPPCD
     C                   MOVE      *BLANK        XXPPDS
     C                   CALL      'HP507'
     C                   PARM                    XXPPCD
     C                   PARM                    XXPPDS
     C                   SETON                                        53
     C     XXPPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPPCD        DFPPCD
     C                   MOVEL     XXPPDS        DFPPDS
     C                   ENDIF                                                  IF SELECTED
      *
      * SALES TYPE
      *
     C     FLD           WHENEQ    'DFSTCD'
     C                   MOVE      *BLANK        XXSTCD
     C                   MOVE      *BLANK        XXSTDS
     C                   CALL      'HP544'
     C                   PARM                    XXSTCD
     C                   PARM                    XXSTDS
     C                   SETON                                        55
     C     XXSTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXSTCD        DFSTCD
     C                   MOVEL     XXSTDS        DFSTDS
     C                   ENDIF                                                  IF SELECTED
      *
      * TYPE OF PAYMENT
      *
     C     FLD           WHENEQ    'DFTPCD'
     C                   MOVE      *BLANK        XXTPCD
     C                   MOVE      *BLANK        XXTPDS
     C                   CALL      'HP537'
     C                   PARM                    XXTPCD
     C                   PARM                    XXTPDS
     C*                    SETON                     50
     C     XXTPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXTPCD        DFTPCD
     C                   ENDIF                                                  IF SELECTED
      *
      * FARM SITE
      *
     C     FLD           WHENEQ    'DFFSCD'
     C                   CALL      'HP5040'
     C                   PARM      *blank        xxaist
     C                   PARM      0             WKFSCD
     C                   PARM      *blank        XXFSNM
     C                   SETON                                        54
     C     WKFSCD        IFNE      0                                            IF SELECTED
     C                   Z-ADD     WKFSCD        DFFSCD
     C                   MOVEL     XXFSNM        DFFSNM
     C                   ENDIF                                                  IF SELECTED
      *
      * JDE CUSTOMER NUMBER
      *
     C     FLD           WHENEQ    'DFCVNO'
     C                   MOVEL(P)  'C'           INAT1                          SEARCH TYPE
     C                   CALL      'HP513'
     C                   PARM                    BOCOCD                         COMPANY
     C                   PARM                    INAT1                          SEARCH TYPE
     C                   PARM      *ZERO         OUAJD                          JDE NUMBER
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   SETON                                        56
     C     OUAJD         IFNE      0                                            IF SELECTED
     C                   Z-ADD     OUAJD         DFCVNO
     C                   MOVEL     OUALPH        DFCNAM
     C                   ENDIF                                                  IF SELECTED
      *
      * BREED STOCK
      *
     C     FLD           WHENEQ    'DFBSCD'
     C                   CALL      'HP516'
     C                   PARM      *BLANK        XXBSCD
     C                   PARM      *BLANK        XXBSDS
     C                   SETON                                        70
     C     XXBSCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXBSCD        DFBSCD
     C                   MOVEL     XXBSDS        DFBSDS
     C                   ENDIF                                                  IF SELECTED
      *
      * HOG GROUP
      *
     C     FLD           WHENEQ    'DFHGCD'
     C                   MOVEL     DFFSBO        XXFSBO
     C                   Z-ADD     DFFSCD        WKFSCD
     C                   MOVE      *BLANK        XXHGCD
     C                   CALL      'HP566'
     C                   PARM                    XXFSBO
     C                   PARM                    WKFSCD
     C                   PARM                    XXHGCD
     C                   SETON                                        57
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        DFHGCD
     C                   ENDIF                                                  IF SELECTED
      *
      * REPORT FLAG
      *
     C     FLD           WHENEQ    'DFRPFL'
     C                   MOVE      *BLANK        XXRPFL
     C                   MOVE      *BLANK        XXRPDS
     C                   CALL      'HP565'
     C                   PARM                    XXRPFL
     C                   PARM                    XXRPDS
     C                   SETON                                        58
     C     XXRPFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXRPFL        DFRPFL
     C                   MOVEL     XXRPDS        DFRPDS
     C                   ENDIF                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
      *
     C                   ENDSL
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *
     C     K1L079        KLIST
     C                   KFLD                    DFTPCD
     C                   KFLD                    DFSTCD
      *
      * PARM LISTS
      *     NONE
      *
      * DEFINE LDA
     C     *DTAARA       DEFINE    *LDA          LDA
      *
      * INITIALIZE LDA FIELDS
      *
     C                   MOVE      *BLANK        LDFSBO
     C                   Z-ADD     0             LDFYMD
     C                   Z-ADD     0             LDTYMD
     C                   MOVE      *BLANK        LDPTCD
     C                   MOVE      *BLANK        LDPPCD
     C                   MOVE      *BLANK        LDSTCD
     C                   MOVE      *BLANK        LDFSCD
     C                   Z-ADD     0             LDCVNO
     C                   Z-ADD     0             LDHGSN
     C                   MOVE      *BLANK        LDRPFL
     C                   MOVE      *BLANK        LDTPCD
     C                   MOVE      *BLANK        LDBLCD
     C                   MOVE      *BLANK        LDRMCD
     C                   MOVE      *BLANK        LDBSCD
      *
     C                   MOVE      *BLANK        LDBODS
     C                   MOVE      *BLANK        LDFSNM
     C                   MOVE      *BLANK        LDHGCD
     C                   MOVE      *BLANK        LDCNAM
     C                   MOVE      *BLANK        LDSTDS
     C                   MOVE      *BLANK        LDPTDS
     C                   MOVE      *BLANK        LDPPDS
     C                   MOVE      *BLANK        LDTPDS
     C                   MOVE      *BLANK        LDBSDS
      *
      * Initialize LDA fields for printing
      *
     C                   MOVE      *BLANK        LDHOLD
     C                   Z-ADD     0             LDCOPY
     C                   MOVEL     LDOUTU        LDOUTQ
     C                   MOVEL     '*JOBD'       XXJOBQ           10
     C                   MOVEL     NO            XXNITE            1
     C                   Z-ADD     0             XXCOPY            1 0
      *
      * GET THIS FUNCTION'S NUMBER OF COPIES, HOLD YES/NO
      *
     C     SDPGM         CHAIN     HSP002                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     FNCOPY        LDCOPY
     C                   MOVEL     FNHOLD        LDHOLD
     C                   ENDIF
      *
     C     LDCOPY        IFEQ      0
     C                   Z-ADD     1             LDCOPY
     C                   ENDIF
      *
     C     LDHOLD        IFEQ      *BLANK
     C                   MOVEL     NO            LDHOLD
     C                   ENDIF
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
      *
      * NUMERIC PARM FIELDS
      *
     C     *LIKE         DEFINE    DFFSCD        WKFSCD
      *
      * ALPHA PARM FIELDS
      *
     C     *LIKE         DEFINE    DFTPCD        XXTPCD
     C     *LIKE         DEFINE    DFTPDS        XXTPDS
     C                   MOVE      *BLANK        XXAIST            1
     C                   MOVE      *BLANK        XXFSCD            5
     C     *LIKE         DEFINE    DFFSBO        XXFSBO
     C     *LIKE         DEFINE    DFBODS        XXBODS
     C     *LIKE         DEFINE    DFFSNM        XXFSNM
     C     *LIKE         DEFINE    DFPTCD        XXPTCD
     C     *LIKE         DEFINE    DFPTDS        XXPTDS
     C     *LIKE         DEFINE    DFPPCD        XXPPCD
     C     *LIKE         DEFINE    DFPPDS        XXPPDS
     C     *LIKE         DEFINE    DFSTCD        XXSTCD
     C     *LIKE         DEFINE    DFSTDS        XXSTDS
     C     *LIKE         DEFINE    DFRPFL        XXRPFL
     C     *LIKE         DEFINE    DFRPDS        XXRPDS
     C     *LIKE         DEFINE    DFBSCD        XXBSCD
     C     *LIKE         DEFINE    DFBSDS        XXBSDS
     C     *LIKE         DEFINE    DFHGCD        XXHGCD
     C     *LIKE         DEFINE    DFFMDY        WKFMDY
     C     *LIKE         DEFINE    DFTMDY        WKTMDY
      *
      * JDE FIELDS
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE SEARCH
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    DFCVNO        INAJD                          JDE ADDR IN
     C     *LIKE         DEFINE    DFCVNO        OUAJD                          JDE ADDR OUT
      *
      *
      *  PARMS FOR USE WITH QCMDEXC.  'LENGTH' IS THE LENGTH OF THE
      *  COMMAND THAT WILL BE ISSUED WITH QCMDEXC.  QCMDEXC REQUIRES
      *  THE FIELD CONTAINING LENGTH TO BE DEFINED AS 15 WITH 5 DECIMAL
      *  POSITIONS.  THE FIELD 'CMDSBM' WILL HOLD THE VALUES OF THE
      *  THE SUBMIT JOB COMMAND WHEN PASSED AS A PARM ON THE CALL TO
      *  QCMDEXC.
      *
     C                   Z-ADD     80            LENGTH           15 5          QCMDEXC PARM
     C                   MOVE      *BLANK        CMDSBM           80            QCMDEXC PARM
      *
      * BRING IN THE DATA AREA THAT CONTAINS THE CURRENT ACCOUNTING
      * PERIOD AND SET UP SCREEN1 DEFAULT FROM AND TO DATES
      *
     C     *DTAARA       DEFINE                  DAAPER
     C                   IN        daaper
      *
     C     DABPDT        IFNE      0                                            IF BEGINING
     C                   MOVEL     DABPDT        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'MDY '        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   EXSR      $DATE
      *
     C     PRTRN         IFEQ      *BLANK                                       IF OK
     C                   Z-ADD     PTO8          WKFMDY
     C                   ENDIF                                                  IF OK
     C                   ENDIF                                                  IF BEGINING
      *
     C     DAEPDT        IFNE      0                                            IF ENDING
     C                   MOVEL     DAEPDT        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'MDY '        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   EXSR      $DATE
      *
     C     PRTRN         IFEQ      *BLANK                                       IF GOOD
     C                   Z-ADD     PTO8          WKTMDY
     C                   ENDIF                                                  IF GOOD
     C                   ENDIF                                                  IF ENDING
      *
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
      * EXECUTE THE SECURITY SUBROUTINE TO DETERMINE USER
      * AUTHORIZATIONS.
      *
     C                   MOVEL(P)  SDPGM         WKFNCD           10
     C                   MOVEL     NO            INBOFL
     C                   EXSR      $SEC
     C                   MOVEL     YES           INBOFL
      *
      * IF ERRORS ON INITIAL CALL, SET TERMINAL FLAG TO YES.
      *
     C     SECFL         IFEQ      YES
     C                   MOVEL     YES           TERMFL
     C                   ENDIF
      *
      * MOVE RETURNED FIELDS TO SCREEN
      *
     C                   MOVEL     OUBOBO        DFFSBO
     C                   MOVEL     OUBODS        DFBODS
      *
      * SAVE THIS FUNCTION'S DIVISION CODE
      *
     C     *LIKE         DEFINE    OUDVCD        SVDVCD
     C                   MOVEL     OUDVCD        SVDVCD
      *
      * RETRIEVE COMPANY CODE FOR J.D. EDWARDS VERIFICATION
      *
     C     DFFSBO        CHAIN     HSP009                             80
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * COMPILE TIME ARRAY NAMED 'CMD' USED IN QCMDEXC
      *****************************************************************
** CMD TABLE
SBMJOB CMD(CALL PGM(HP480CL)) JOB(SALEREPORT) JOBQ(__________)
