/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF477CL                                       */
/*  PROGRAM NAME:    WEEKLY YIELD REPORTS                          */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     12/03/08                                      */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 02/12/09  LeAnne Ramsey                                         */
/*           We now have 2 sets of Weekly Yield Reports.           */
/*           So, added new LDA field and logic for generating:     */
/*             1) Primal or 2) ByProduct or 3) Both                */
/*                                                                 */
/* 03/06/09  LeAnne Ramsey                                         */
/*           This used to be TF476CL. But, we are pulling TF676-   */
/*           Weekly Converting % Report out of this and giving     */
/*           it a separate kick-off screen/cl. Since the report    */
/*           was and will remain TF676, we wanted it to have the   */
/*           '476' names.                                          */
/*                                                                 */
/* 05/06/09  LeAnne Ramsey                                         */
/*           Changed print logic to match Meat Costing.            */
/*                                                                 */
/* 02/09/22  Danny Nguyen  - DO2484 - WI479 STF Variance Reporting */
/*           Added call to TF6810 right after TF681.               */
/*           Added call to TF6800 right after TF680.               */
/*                                                                 */
/* 02/21/23  Danny Nguyen  - W121675 - Added call to TF6790 to     */
/*           generate STF Weekly Yield Report (Recap).             */
/*           As a result of adding a new report, changed TF415CL   */
/*           to up the hard code 'spool file #' to send the report */
/*           via ESNDMAIL.                                         */
/*                                                                 */
/* 07/31/23  Danny Nguyen  - WI593 - Added call to TF6770 to       */
/*           generate STF Primal Weekly Yield Report (Detail).     */
/*           Added call to TF6780 to generate STF Primal Weekly    */
/*           Yield Report (Summary).                               */
/*           As a result of adding 2 new reports, changed TF415CL  */
/*           to up the hard code 'spool file #' (by +2) to send the*/
/*           reports via ESNDMAIL.                                 */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&CLPARM)

             DCL        VAR(&CLPARM)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&QDATA)   TYPE(*CHAR) LEN(117)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDWEDT) TYPE(*DEC) LEN(8)

             DCL        VAR(&LDDTL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSUM)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCAP)  TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDISTYCD) TYPE(*DEC)  LEN(3)
             DCL        VAR(&LDTFCLCD) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LDTFCGCD) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDPRIMBY) TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDWEDT) VALUE(%SST(*LDA 29 8))

             CHGVAR     VAR(&LDDTL)     VALUE(%SST(*LDA 397 1))
             CHGVAR     VAR(&LDSUM)     VALUE(%SST(*LDA 398 1))
             CHGVAR     VAR(&LDCAP)     VALUE(%SST(*LDA 399 1))
             CHGVAR     VAR(&LDPRIMBY)  VALUE(%SST(*LDA 400 1))

             CHGVAR     VAR(&LDTFCGCD) VALUE(%SST(*LDA 139 2))
             CHGVAR     VAR(&LDTFCLCD) VALUE(%SST(*LDA 156 3))
             CHGVAR     VAR(&LDISTYCD) VALUE(%SST(*LDA 189 3))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*---------------------------------------------------------------------*/
/* EXTRACT WEEKLY YIELD RECORDS                                        */
/*---------------------------------------------------------------------*/

/* WEEKENDING DATE                                                        */

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('YWWEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDWEDT)


/* ITEM STRUCTURE TYPE                                           */

             IF         COND(&LDISTYCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 44 17)) VALUE('*AND YWISTYCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&LDISTYCD)
             ENDDO

/*  TF CLASS GROUP                                            */

             IF         COND(&LDTFCGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 66 19)) VALUE('*AND YWTFCGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 85 2)) VALUE(&LDTFCGCD)
             CHGVAR     VAR(%SST(&QDATA 87 1)) VALUE('"')
             ENDDO

/* TF CLASSIFICATION                                         */

             IF         COND(&LDTFCLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 89 19)) VALUE('*AND YWTFCLCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 108 3)) VALUE(&LDTFCLCD)
             CHGVAR     VAR(%SST(&QDATA 111 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(TFL076A) SHARE(*YES)
             OPNQRYF    FILE((TFL076A)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*---------------------------------------------------------------------*/
/* Primal Recap                                                        */
/*---------------------------------------------------------------------*/
/* Note: Both Primal data and ByProduct data are printed on the Recap. */
/*       So, we don't have separate Primal/ByProduct Recap reports.    */

             IF         COND(&LDCAP *NE ' ') THEN(DO)
             CALL       PGM(TF679) PARM(&CLPARM)

    /* W121675-Weekly Yield Report (Recap) for Company STF Only. */
             CALL       PGM(TF6790) PARM(&CLPARM)
             ENDDO

/*---------------------------------------------------------------------*/
/* Print Primal Yield Reports                                          */
/*   1) Weekly Yield Detail  (Item)                                    */
/*   2) Weekly Yield Summary (Item Structure Type/TF Classification)   */
/*---------------------------------------------------------------------*/

             IF         COND(&LDPRIMBY *EQ 'P' *OR &LDPRIMBY = ' ') +
                          THEN(DO) /* Primal */

/* Primal Detail */

             IF         COND(&LDDTL *NE ' ') THEN(DO)
             CALL       PGM(TF677) PARM(&CLPARM)

    /* WI593-Weekly Primal Yield Report (Detail) for Company STF Only. */
             CALL       PGM(TF6770) PARM(&CLPARM)
             ENDDO

/* Primal Summary */

             IF         COND(&LDSUM *NE ' ') THEN(DO)
             CALL       PGM(TF678) PARM(&CLPARM)

    /* WI593-Weekly Primal Yield Report (Summary) for Company STF Only. */
             CALL       PGM(TF6780) PARM(&CLPARM)
             ENDDO

             ENDDO      /* Primal */


/*---------------------------------------------------------------------*/
/* Print ByProduct Yield Reports                                       */
/*   1) Weekly Yield Detail  (Item)                                    */
/*   2) Weekly Yield Summary (Item Structure Type/TF Classification)   */
/*---------------------------------------------------------------------*/

             IF         COND(&LDPRIMBY *EQ 'B' *OR &LDPRIMBY = ' ') +
                          THEN(DO) /* ByProduct */
/* ByProduct Detail */

             IF         COND(&LDDTL *NE ' ') THEN(DO)
             CALL       PGM(TF680) PARM(&CLPARM)
    /* ByProduct Weekly Yield Report Detail for Company STF Only */
             CALL       PGM(TF6800) PARM(&CLPARM)
             ENDDO

/* ByProduct Summary */

             IF         COND(&LDSUM *NE ' ') THEN(DO)
             CALL       PGM(TF681) PARM(&CLPARM)
    /* ByProduct Weekly Yield Report Summary for Company STF Only */
             CALL       PGM(TF6810) PARM(&CLPARM)
             ENDDO
             ENDDO      /* ByProduct */

             CLOF       OPNID(TFL076A)
             DLTOVR     FILE(TFL076A)

             ENDPGM

