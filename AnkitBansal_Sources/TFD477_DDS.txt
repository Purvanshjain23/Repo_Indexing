     A*%%TS  SD  20090305  155213  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD477
     A*  TITLE:         SPECIFY WEEKLY YIELD REPORTS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       03/06/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF477R1
     A*%%TS  SD  20090305  155213  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 20'SPECIFY OPTIONS FOR WEEKLY YIELD R-
     A                                      EPORTS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 27'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 48'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 54'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Date ..'
     A            D1MDY     R        B  5 10REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O  5 31REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  5 48REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  5 55REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A  31                                  DSPATR(RI)
     A                                  8  2'Please X each of the following rep-
     A                                      orts that you wish to generate:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            D1DTL          1A  B  9  5
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                  9  8'Weekly Yield Detail   (Item Level)'
     A            D1SUM          1A  B 10  5
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                 10  8'Weekly Yield Summary  (Item Struct-
     A                                      ure Type/TF Classification Level)'
     A            D1CAP          1A  B 11  5
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A                                 11  8'Weekly Yield Recap    (Item Struct-
     A                                      ure Type Level)'
     A                                 13  2'For the Detail and Summary Reports-
     A                                      , you may choose Primal, ByProduct -
     A                                      or Both:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 14  2'Primal/ByProduct/Both .....'
     A                                 14 30'?'
     A            D1PRIMBY  R        B 14 32REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A                                 14 38'(Blank=Both)'
     A                                 16  2'Optional selections        '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 17  2'Item structure type code ..'
     A                                 17 30'?'
     A            D1ISTYCD  R        B 17 32REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1ISTYDS  R        O 17 38REFFLD(TFREF/@@ISTYDS *LIBL/TFPREF)
     A                                 17 69'(Blank=All)'
     A                                 18  2'TF class group code .......'
     A                                 18 30'?'
     A            D1TFCGCD  R        B 18 32REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A N32                                  DSPATR(HI)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1TFCGDS  R        O 18 38REFFLD(TFREF/@@TFCGDS *LIBL/TFPREF)
     A                                 18 69'(Blank=All)'
     A                                 19  2'TF classification code ....'
     A                                 19 30'?'
     A            D1TFCLCD  R        B 19 32REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1TFCLDS  R        O 19 38REFFLD(TFREF/@@TFCLDS *LIBL/TFPREF)
     A                                 19 69'(Blank=All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF477K1
     A*%%TS  SD  20040928  155440  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF477ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF477EC                   SFLCTL(TF477ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
