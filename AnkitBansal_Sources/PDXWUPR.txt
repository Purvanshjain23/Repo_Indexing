/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              INVENTORY REVALUATION. CAUSE PRINTER OVERRIDE       */
/*              PROMPTING AND PASSES COMPANY, WAREHOUSE, ITEM,      */
/*              ITEM TYPE, ITEM LEVEL, QUALITY LEVEL,               */
/*              ITEM STR TYPE, ITEM STR GROUP,                      */
/*              ITEM STR CLASS, NON-ZERO QUANTITIES ONLY AND AS OF  */
/*              DATE.                                               */
/*              INVENTORY REVALUATION                               */
/*                                                                  */
/*  SYSTEM    - INVENTORY CONTROL                                   */
/*  PROGRAM   - INVENTORY REVALUATION                               */
/*  DATE      - 06/22/95                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &CLP        - CL PROGRAM TO SUBMIT                      */
/*     I    &PGM        - RPG PROGRAM TO SUBMIT                     */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &ASDATE     - AS OF DATE     (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &WHSE       - WAREHOUSE CODE (PASSED)                   */
/*     B    &ITEM       - ITEM CODE      (PASSED)                   */
/*     B    &ITMTYP     - ITEM STR TYPE  (PASSED)                   */
/*     B    &ITMGRP     - ITEM STR GROUP (PASSED)                   */
/*     B    &ITMCLS     - ITEM STR CLASS (PASSED)                   */
/*     B    &ITEMTP     - ITEM TYPE      (PASSED)                   */
/*     B    &NONZER     - NON-ZERO QTY   (PASSED)                   */
/*     B    &ITEMLV     - ITEM LEVLE     (PASSED)                   */
/*     B    &QLTYLV     - QUALITY LEVEL  (PASSED)                   */
/*                                                                  */
/********************************************************************/

 PDRZUPR:    PGM        PARM(&RETURN &APPLIC &CLP &PGM &DESC &OUTQ +
                         &HOLD &SAVE &NQSTAT &NIGHTQ &COMPNYN &ASDATE +
                         &COPYN &WHSE &ITEM &ITMCLS &ITMGRP &ITMTYP +
                         &ITEMTP &NONZER &ITEMLV &QLTYLV)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &CLP        *CHAR   (10)          /* CL to Submit              */
        DCL    &PGM        *CHAR   (10)          /* RPG TO SUBMIT             */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &ASDATE     *DEC                  /* AS OF DATE     (Passed)   */
        DCL    &WHSE       *CHAR   (3)           /* WARHOUSE       (Passed)   */
        DCL    &ITEM        *DEC                 /* ITEM           (Passed)   */
        DCL    &ITMTYP      *DEC                 /* ITEM STR TYPE  (Passed)   */
        DCL    &ITMGRP      *DEC                 /* ITEM STR GROUP (Passed)   */
        DCL    &ITMCLS      *DEC                 /* ITEM STR CLASS (Passed)   */
        DCL    &ITEMTP      *CHAR   3           /*  ITEM TYPE       */
        DCL    &NONZER      *CHAR  (1)           /* NON ZERO QTY   (Passed)   */
        DCL    &ITEMLV      *CHAR   2           /*  ITEM LEVEL      */
        DCL    &QLTYLV      *CHAR   2           /*  QUALITY LEVEL   */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

/*           IF         COND(&OUTQ *NE '   ') THEN(GOTO CMDLBL(SUBMIT))*/

             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     SBMJOB     CMD(CALL PGM(&CLP) PARM(&PGM &COMPNYN +
                          &WHSE &ITEM &ITEMTP &ITEMLV &QLTYLV +
                          &ITMTYP &ITMGRP &ITMCLS &ASDATE &NONZER +
                          &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ)) JOB(&PGM)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
