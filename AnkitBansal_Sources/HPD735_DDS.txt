     A*%%TS  SD  19950622  084407  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD735
     A*  TITLE:         VIEW CHECK DEDUCTIONS AND ADD-ONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       06/22/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP735S1                   SFL
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFDAAM    R        O  9 21REFFLD(CJCJAM)
     A                                      EDTCDE(K)
     A            DFDACD    R        O  9  8REFFLD(DADACD)
     A            DFDADS    R        O  9 35REFFLD(DADADS)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP735C1                   SFLCTL(HP735S1)
     A*%%TS  SD  19950622  084407  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 22'VIEW CHECK DEDUCTIONS AND ADD-ONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4 13'TOTAL:'
     A            C1DAAM    R     Y  O  4 21REFFLD(CJCJAM)
     A                                      EDTCDE(K)
     A                                  6  3'Deduction/Add-on'
     A                                      DSPATR(HI)
     A                                  7  9'Code'
     A                                      DSPATR(HI)
     A                                  7 24'Amount'
     A                                      DSPATR(HI)
     A                                  7 35'Description'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP735K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*----------------------------------------------------------------
     A          R HP735ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A          R HP735EC                   SFLCTL(HP735ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
