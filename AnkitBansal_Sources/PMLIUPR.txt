     H/TITLE EUP Bld Buyer Grp Accrual Execute user program
     H DATFMT(*YMD) DATEDIT(*YMD) DEBUG(*YES)
     Y* ADDLIBLE LIB(COMMON) POSITION(*LAST)
     Y* ADDLIBLE LIB(DFDATA) POSITION(*LAST)
     Y* ADDLIBLE LIB(DLY029) POSITION(*LAST)
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *
     FREBATBGX  if   e           k disk
      *  JRD Buyer Group Rebate Masterfile
      *
     FCSTMSTL1  if   e           k disk
      *  Active Customer Conversion - By DLYCST
      *
     FOMHOREL1  uf a e           k disk
      *  Customer Accrual          Update index
      *
     FPOC8REL0  uf a e           k disk
      *  Customer Accrual Str      Update index
      *
     FITMDFTL1  if   e           k disk
      *  Uploaded Item Definition By Item Structure Group Code
      *
      * Data structures:
     D PGMDS         ESDS                  EXTNAME(Y2PGDSP)
      * Modified Program data structure
     D JBDTTM          DS
      * Job date/time
     D  ##JDT                  1      7  0
     D  ##JCC                  1      1  0
     D  ##JYY                  2      3  0
     D  ##JMM                  4      5  0
     D  ##JDD                  6      7  0
     D  ##JTM                  8     13  0
     D  ##JHH                  8      9  0
     D  ##JNN                 10     11  0
     D  ##JSS                 12     13  0
      *
      * Standalone fields
     Dcontinue                        1A                                        Continue processing
      *
     D ITMGRP          DS
      * Convert Item Group
     D  SSRHNB                 1      3S 0                                      From POC8REP
     D  PGROUP                 2      3A                                        From REBATBGX
      *
      /EJECT
      *****************************************************************
      * Entry parameters
     C     *ENTRY        PLIST
     C                   PARM                    p1ojcd            3            Accrual Code
     C                   PARM                    library          10            Library Name
      *****************************************************************
      * Initialize
     C                   EXSR      ZZINIT
      *
      * Process REBATBGX - JRD Buyer Group Rebate Masterfile
     C                   read      rebateg                                95
     C                   dow       *in95 = *off                                 do loop
      *
      * Get Ship to Customer
     C                   exsr      $cstmst
      *
      * Continue processing record?
     C                   if        continue = 'Y'                               continue = Y
      *
      * Create Customer Accrual
     C                   exsr      $cusacc
      *
      * Create Customer Accrual Str
     C                   exsr      $cusaccstr
      *
     C                   endif                                                  continue = Y
      * Next record
     C                   read      rebateg                                95
     C                   enddo                                                  do loop
      *
      *----------------------------------------------------------------
      * Terminate program
     C                   SETON                                        LR
      *
      * Exit program
     C                   RETURN
      *
      *================================================================
     C/EJECT
      *---------------------------------------------------------------
      * Get Ship to Customer
      *---------------------------------------------------------------
      *
     C     $cstmst       begsr
      *
      * Set default values for flags
     C                   eval      continue = 'N'
      *
     C     cust          chain     cstmst                             91
      *
      * If record found
     C                   if        *in91 = *off                                 91 = off
     C                   eval      hobkc7 = shipto                              Ship to Customer
     C                   eval      continue = 'Y'
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Create Customer Accrual
      *---------------------------------------------------------------
      *
     C     $cusacc       begsr
      *
      * Populate fields
     C                   eval      hobkc7 = shipto                              Ship to Customer
     C                   eval      hoojcd = p1ojcd                              Accural Code
     C                   eval      hoguny = 1                                   CAH Sequence No
     C                   eval      hobfdt = ##JDT                               Period Beginning Dte
     C                   eval      hoa2dt = 1991231                             Period Ending Date
     C                   eval      hobkpr = *zeros                              Accrual Rate
     C                   eval      hottst = 'N'                                 All Products (Y/N)
     C                   eval      hoshsx = 'W'                                 Accrual Rate Type
     C                   eval      hoqdsx = *blanks                             Comm Threshold Prd
     C                   eval      hopjdt = *zeros                              CAH Unused Date 1
     C                   eval      hopkdt = *zeros                              CAH Unused Date 2
     C                   eval      hoecvl = *zeros                              Comm Min Amt /Period
     C                   eval      hoqesx = *blanks                             Comm Retroactive sts
     C                   eval      hoqfsx = 'S'                                 Accrual Dtl Search
      * Set value for Broker Code
     C                   select
     C                   when      vendor = 'W3683'
     C                   eval      hohhaa = 'GOLBON'                            Broker Code
     C                   when      vendor = 'W9505'
     C                   eval      hohhaa = 'UNIPRO'                            Broker Code
     C                   when      vendor = 'W2030'
     C                   eval      hohhaa = 'DONSON'                            Broker Code
     C                   when      vendor = 'W4795'
     C                   eval      hohhaa = 'KNMEAT'                            Broker Code
     C                   when      vendor = 'W9198'
     C                   eval      hohhaa = 'SYSCO '                            Broker Code
     C                   endsl
      *
     C                   eval      hohqvl = *zeros                              Max Accrual Amount
     C                   eval      hoqgsx = '1'                                 Commission/Not Comm
     C                   eval      hoqhsx = *blanks                             CAH Unused sts 3
     C                   eval      hoqisx = *blanks                             CAH Unused sts 4
     C                   eval      hoqjsx = *blanks                             CAH Unused sts 5
     C                   eval      houvst = *blanks                             EDI status 1
     C                   eval      houwst = *blanks                             EDI status 2
     C                   eval      houxst = *blanks                             EDI status 3
     C                   eval      hoaatm = ##JTM                               Job Time
     C                   eval      hoayna = ##USR                               User Id
     C                   eval      hoa0na = ##JOB                               Job Name
     C                   eval      hoaxdt = ##JDT                               Job Date
      *
      * Check for duplicate record
      * This prevents duplicate records from DFDATA and JRDATA
     C     key01         setll     @HORELB                                90
      * If record not found, add
     C                   if        *in90 = *off
     C                   write     @HORELB
      *
     C                   endif
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Create Customer Accrual Str
      *---------------------------------------------------------------
      *
     C     $cusaccstr    begsr
      *
      * Get Item Default
     C                   exsr      $itmdft
      * Populate fields
     C                   eval      c8bkc7 = hobkc7                              Ship to Customer
     C                   eval      c8ojcd = hoojcd                              Accural Code
     C                   eval      c8guny = hoguny                              CAH Sequence No
     C                   eval      c8rgnb = byrgnb                              Item Struc Type Code
     C                   eval      c8rhnb = ssrhnb                              Item Struc Group Cde
     C                   eval      c8rinb = *zeros                              Item Struc Class Cde
     C                   eval      c8bkpr = rebate                              Accrual Rate
     C                   eval      c8shsx = 'W'                                 Accrual Rate Type
     C                   eval      c8uvst = *blanks                             EDI Status 1
     C                   eval      c8uwst = *blanks                             EDI Status 2
     C                   eval      c8uxst = *blanks                             EDI Status 3
     C                   eval      c8vsst = 'A'                                 Record Status
     C                   eval      c8aatm = ##JTM                               Job Time
     C                   eval      c8ayna = ##USR                               User Id
     C                   eval      c8a0na = ##JOB                               Job Name
     C                   eval      c8axdt = ##JDT                               Job Date
      *
      * Check for duplicate record
      * This prevents duplicate records from DFDATA and JRDATA
     C     key02         setll     @C8RES0                                90
      * If record not found, add
     C                   if        *in90 = *off
     C                   write     @C8RES0
      *
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Get Item Default
      *---------------------------------------------------------------
      *
     C     $itmdft       begsr
      *
      * Field ssrhnb maps to field prgroup in DS ITMGRP
     C                   eval      byrhnb = ssrhnb                              Item Struc Type Code
     C     byrhnb        chain     @byreap                            91
      *
      * If record found
     C                   if        *in91 = *off                                 91 = off
     C                   eval      c8rgnb = byrgnb                              Item Struc Type Code
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
     C/EJECT
     CSR   ZZINIT        BEGSR
      *================================================================
      * Initialisation
      *================================================================
      * Retrieve job attributes
     C                   CALL      'Y2RTJCR'
     C                   PARM                    PGMDS
      * Setup job date/time
     C                   Z-ADD     ##SD7         ##JDT
     C                   TIME                    ##JTM
      *
      * Klist for Customer Accrual          Retrieval index
      * Check for duplicate record before create
     C     KEY01         klist
     C                   kfld                    hobkc7
     C                   kfld                    hoojcd
     C                   kfld                    hoguny
      *
      * Klist for Customer Accrual Str      Retrieval index
     C     key02         klist
     C                   kfld                    c8bkc7
     C                   kfld                    c8ojcd
     C                   kfld                    c8guny
     C                   kfld                    c8rgnb
     C                   kfld                    c8rhnb
     C                   kfld                    c8rinb
      *
      *================================================================
     CSR   ZZEXIT        ENDSR
