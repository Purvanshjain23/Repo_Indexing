/*****************************************************************   */
/*  PROGRAM NUMBER --> HP218CL                                       */
/*  PROGRAM NAME ----> DATAMART FIN: UPDATE GROUP HEADER             */
/*                     (DISPOSED AND CLOSED GROUPS ONLY)             */
/*  PROGRAMMER ------> LEANNE FEDOR                                  */
/*  DATE ------------> 04/09/01                                      */
/*                                                                   */
/*  PROGRAM PURPOSE -> WE ARE ATTEMPTING TO USE EXISTING CODE AS     */
/*                     MUCH AS POSSIBLE FOR THIS DATMART FUNCTION.   */
/*                                                                   */
/*                     STEP 1:                                       */
/*                       CREATE A DUPLICATE OBJECT OF THE CLOSED     */
/*                       GROUP FILE (WITH DATA). WE WILL ADD THE     */
/*                       DISPOSED GROUP DATA LATER.                  */
/*                                                                   */
/*                     STEP 2:                                       */
/*                       CREATE A WORKFILE FOR THE DISPOSED GROUP    */
/*                       DATA. POPULATE THE FILE USING EXISTING      */
/*                       PROGRAM. THEN READ THE FILE AND WRITE THE   */
/*                       RECORDS TO THE CLOSED GROUP FILE IN STEP 1. */
/*                                                                   */
/*                     STEP 3:                                       */
/*                     RUN THE DATAMART PROGRAM TO RETRIEVE DATA     */
/*                     FROM THE WORKFILE CREATED IN STEP 1 AND       */
/*                     UPDATE THE DATAMART GROUP MASTER FILE.        */
/*                                                                   */
/*********************************************************************/
/* MODIFICATIONS                                                     */
/* DATE      PROGRAMMER                                              */
/*                                                                   */
/*******************************************************************/

             PGM        PARM(&PROCFL &CURBEGDT)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(15)
             DCL        VAR(&PROCFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CURBEGDT) TYPE(*DEC) LEN(8)

/*-------------------------------------------------------------------*/
/* STEP 1: CLOSED GROUPS                                             */
/*-------------------------------------------------------------------*/

/*  CREATE A WORKFILE THAT IS A DUPLICATE OF THE HPS CLOSED GROUP    */
/*  FILE.....DUPLICATE THE DATA AS YOU CREATE THE FILE.              */

             CRTDUPOBJL OBJ(HSP033) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*YES)


/*-------------------------------------------------------------------*/
/* STEP 2 : DISPOSED GROUPS                                          */
/*-------------------------------------------------------------------*/

/*  CREATE A WORKFILE FOR THE DISPOSED GROUP DATA                    */

             CRTDUPOBJL OBJ(HSP390) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*  POPULATE THE DISPOSED GROUP WORKFILE USING EXISTING PROGRAM. */
/*  FOR THE EXISTING PROGRAM, YOU NEED TO SET 'CELL' VALUE IN    */
/*  THE LDA TO ZERO. AND, LIMIT THE GROUPS IN THE PRIMARY FILE   */
/*  TO THOSE WITH A DISPOSED STATUS.                             */

             CHGDTAARA  DTAARA(*LDA (179 2)) VALUE('00')

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *EQ "DS"')
             OVRDBF     FILE(HSJ018G) SHARE(*YES)
             OPNQRYF    FILE((HSJ018G)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP390)

             CLOF       OPNID(HSJ018G)
             DLTOVR     FILE(HSJ018G)


/* CALL A PROGRAM TO COPY THE DISPOSED GROUP RECORDS TO THE      */
/* CLOSED GROUP WORKFILE IN QTEMP THAT WAS CREATED IN STEP 1.    */

             CALL       PGM(HP219)


/*-------------------------------------------------------------------*/
/* STEP 3: UPDATE DATAMART GROUP MASTER WITH WORKFILE DATA           */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP218) PARM(&PROCFL &CURBEGDT)

             ENDPGM

