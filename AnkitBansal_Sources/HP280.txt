      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP280
      * TITLE:       Update HPS with Actual Kill Date from Delivery Tracking File
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/10/03
      *
      * FUNCTION:  This program needs to receive the incoming Actual kill date and movement
      *            number and update the Scheduled Kill Date in the Sales Movement Header
      *            record in HPS with this incoming date.
      *
      *            If the movement is in a "SH" status, we are using the Scheduled Kill Date
      *            as the Received Date; so, the Received Date will need to be updated also
      *            when the status is SH.
      *
      *            And, since we are using the Scheduled Kill Date as the Event Date in the
      *            Movement Event file, the Event file record will also need to be updated.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp058    uf   e           k disk
      *   Movement event
      *
      *
     Fhsp084    uf   e           k disk
      *   Sales movement header
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkstatus        s              1
      *
      *
      * Workfields for date manipulation
      *
     D wkudate         s                   like(shscdt)
     D wkcymdiso       s               D   datfmt(*iso)
      *
      *
      * Parms
      *
     D xxmvsn          s                   like(shmvsn)
     D xxnkilldt       s              7  0
     D xxccyymmdd      s              8  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *----------------------------------------------------------------------
      * Definition for external data area 'DAAPER-Current Accounting Period'
      *----------------------------------------------------------------------
     D
     D daaper          ds                  dtaara(daaper)
     D  daccyy                 1      4  0
     D  daper                  5      6  0
     D  dabpdt                 7     14  0
     D  daepdt                15     22  0
     D  dapgfl                23     23
     D  dappfl                24     24
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Retrieve the movement from the HPS Sales Movement Header file.
      *
     C     xxmvsn        chain     hsp084                             92
     C                   if        *in92 = *off and                             If hit
     C                             (shmscd = 'SC' or shmscd = 'SH')
      *
      * Validate the NEW Kill Date sent in by Synon pgm
      *
     C                   exsr      $nckldt
      *
      * If there are no errors, makes updates in HPS.
      *
     C                   if        wkstatus = ' '
     C                   exsr      $upd084
     C                   exsr      $upd058
     C                   endif
      *
     C                   endif                                                  If hit
      *
      * EOF processing
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Validate NEW Kill Date
      *---------------------------------------------------------------
      *
     C     $nckldt       begsr
      *
     C                   if        xxnkilldt = 0                                If zero date
     C                   move      'E'           wkstatus
     C                   else
     C                   exsr      $dates
     C                   endif                                                  If zero date
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate dates
      *---------------------------------------------------------------
      *
     C     $dates        begsr
      *
      * Get Synon date into CCYYMMDD format
      *
     C                   call      'HP8011'
     C                   parm                    xxnkilldt
     C                   parm      0             xxccyymmdd
      *
      * Date validations:
      *   1) date must be a valid date
      *   2) date cannot be in a closed period
      *   3) if the date is in the current period, the period close programs
      *      cannot be running
      *
     C     *iso          test(d)                 xxccyymmdd             92
     C                   if        *in92 = *on                                  If bad date
     C                   move      'E'           wkstatus
     C                   else
      *
     C                   in        daaper
      *
     C                   if        xxccyymmdd < dabpdt or
     C                             (xxccyymmdd >= dabpdt and
     C                              xxccyymmdd <= daepdt and
     C                              dappfl <> *blank)
     C                   move      'E'           wkstatus
     C                   endif
      *
     C                   endif                                                  If bad date
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Update the Movement Header
      *---------------------------------------------------------------
      *
     C     $upd084       begsr
      *
     C                   z-add     wkudate       shupdt
     C                   time                    shuptm
     C                   movel     sdpgm         shpgm
      *
      * Move the Synon date into the Scheduled Kill Date field.
      *
     C                   z-add     xxccyymmdd    shsckldt
      *
      * If the Scheduled Kill Date is now earlier than Scheduled Ship Date, then
      * overlay the existing Scheduled Ship Date with the new Scheduled Kill Date.
      *
     C                   if        shsckldt < shscdt
     C                   z-add     shsckldt      shscdt
     C                   endif
      *
      * If the movement already has a Shipped status, update the Received Date
      * with the new Scheduled Kill Date.
      *
     C                   if        shmscd = 'SH'                                If Shipped Status
     C                   z-add     shsckldt      shrcdt
      *
      * If Received Date is now earlier than Shipped date, then overlay the
      * existing Shipped Date with the new Received Date.
      *
     C                   if        shrcdt < shshdt
     C                   z-add     shrcdt        shshdt
     C                   endif
     C                   endif                                                  If Shipped Status
      *
     C                   update    shrec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Update the Movement Event Records
      *---------------------------------------------------------------
      *
     C     $upd058       begsr
      *
     C     xxmvsn        setll     hsp058
      *
     C                   dou       *in91 = *on                                  Do events
     C     xxmvsn        reade     hsp058                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   z-add     wkudate       meupdt
     C                   time                    meuptm
     C                   movel     sdusr         meuscd
     C                   movel     sdpgm         mepgm
      *
     C                   z-add     xxccyymmdd    memedt
      *
     C                   update    merec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do events
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxnkilldt                      new kill date
     C                   parm                    xxmvsn
      *
      *
      * Flip system date to CCYYMMDD for populating update dates in HPS records.
      *
     C     *mdy          move      udate         wkcymdiso
     C                   move      wkcymdiso     wkudate
      *
     C                   endsr
      /EJECT
