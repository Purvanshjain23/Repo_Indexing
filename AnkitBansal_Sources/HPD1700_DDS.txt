     A*%%TS  SD  20040212  095217  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1700
     A*  TITLE:         REVISE BUDGET TEMPLATE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       01/30/04
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A  60N62                               CF13(13 'REMOVE')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1700R1
     A*%%TS  SD  20040212  095217  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 27'REVISE BUDGET TEMPLATE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Budget template code .........'
     A            D1BTCD    R        B  4 35REFFLD(@@BTCD)
     A N60N62N35                            DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A                                  5  2'Budget template description ..'
     A            D1BTDS    R        B  5 35REFFLD(@@BTDS)
     A                                      CHECK(LC)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N34                               DSPATR(HI)
     A                                  6  2'Creation schedule code .......'
     A N62N64                           6 33'?'
     A            D1CSCD    R        B  6 35REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A N62N64N32                            DSPATR(HI)
     A            D1CSDS    R        O  6 43REFFLD(HSRREF/@@CSDS *LIBL/HSPREF)
     A                                  7  2'Budget template weeks ........'
     A            D1BTWK    R        B  7 35REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N37                               DSPATR(HI)
     A                                  8  2'Budget availability code .....'
     A N62N64                           8 33'?'
     A            D1BACD    R        B  8 35REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A  62
     AO 64                                  DSPATR(PR)
     A N62N64                               DSPATR(UL)
     A N62N64N31                            DSPATR(HI)
     A            D1BADS    R        O  8 43REFFLD(HSRREF/@@BADS *LIBL/HSPREF)
     A                                  9  2'Production phase code ........'
     A N62                              9 33'?'
     A            D1PPCD    R        B  9 35REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N33                               DSPATR(HI)
     A            D1PPDS    R        O  9 43REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A                                 10  2'Type of farm code ............'
     A N62                             10 33'?'
     A            D1TFCD    R        B 10 35REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N36                               DSPATR(HI)
     A            D1TFDS    R        O 10 43REFFLD(HSRREF/@@TFDS *LIBL/HSPREF)
     A                                 11  2'Cell .........................'
     A            D1CELL    R        B 11 35REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N38                               DSPATR(HI)
     A                                 12  2'Size .........................'
     A            D1SIZE    R        B 12 35REFFLD(HSRREF/@@SIZE *LIBL/HSPREF)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N39                               DSPATR(HI)
     A                                 13  2'Active/inactive status .......'
     A N62                             13 33'?'
     A            D1AIST    R        B 13 35REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  30                                  DSPATR(RI)
     A  30
     AO 50                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N30                               DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1700K1
     A*%%TS  SD  20040205  085247  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61                          23 34'F11=Delete'
     A N62N61                          23 46'F13=Remove Farm Links'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1700ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1700EC                  SFLCTL(HP1700ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
