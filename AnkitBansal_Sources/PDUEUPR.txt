/*********************************************************************/
/*     Program: PDUEUPR                                              */
/*       Title: Retrieve information about a file member             */
/*        Date: 11/06/98                                             */
/* Description: Retrieve information about a file member             */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    I     &FILE       File Name                                    */
/*    I     &FROMLIB    From Library                                 */
/*                                                                   */
/*********************************************************************/
/* Modifications:                                                    */
/*                                                                   */
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/*                                                                   */
/*********************************************************************/
             PGM   PARM(&FILE &LIBRARY &MEMBER +
                        &NBRRCD +
                        &CHGDTE &CHGTME +
                        &SAVDTE &SAVTME +
                        &RSTDTE &RSTTME +
                        &USEDTE)

   /*-------------------*/
   /* Declare Variables */
   /*-------------------*/

             /* Parameter Fields */
             DCL  VAR(&FILE)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&LIBRARY) TYPE(*CHAR) LEN(10)
             DCL  VAR(&MEMBER)  TYPE(*CHAR) LEN(10)
             DCL  VAR(&NBRRCD)  TYPE(*DEC)  LEN(10 0)
             DCL  VAR(&CHGDTE)  TYPE(*DEC)  LEN(7 0)
             DCL  VAR(&CHGTME)  TYPE(*DEC)  LEN(6 0)
             DCL  VAR(&SAVDTE)  TYPE(*DEC)  LEN(7 0)
             DCL  VAR(&SAVTME)  TYPE(*DEC)  LEN(6 0)
             DCL  VAR(&RSTDTE)  TYPE(*DEC)  LEN(7 0)
             DCL  VAR(&RSTTME)  TYPE(*DEC)  LEN(6 0)
             DCL  VAR(&USEDTE)  TYPE(*DEC)  LEN(7 0)

             /* Internal Fields */
             DCL VAR(&CHGDATE)   TYPE(*CHAR) LEN(13)
             DCL VAR(&SAVDATE)   TYPE(*CHAR) LEN(13)
             DCL VAR(&RSTDATE)   TYPE(*CHAR) LEN(13)
             DCL VAR(&USEDATE)   TYPE(*CHAR) LEN(7)

   /*-----------------------------*/
   /* Initialize Output Variables */
   /*-----------------------------*/
             CHGVAR VAR(&NBRRCD) VALUE(0)
             CHGVAR VAR(&CHGDTE) VALUE(0)
             CHGVAR VAR(&CHGTME) VALUE(0)
             CHGVAR VAR(&SAVDTE) VALUE(0)
             CHGVAR VAR(&SAVTME) VALUE(0)
             CHGVAR VAR(&RSTDTE) VALUE(0)
             CHGVAR VAR(&RSTTME) VALUE(0)
             CHGVAR VAR(&USEDTE) VALUE(0)

   /*-----------------------------*/
   /* Get File Member Description */
   /*-----------------------------*/
             RTVMBRD FILE(&LIBRARY/&FILE) MBR(&MEMBER) +
                     NBRCURRCD(&NBRRCD) +
                     CHGDATE(&CHGDATE) +
                     SAVDATE(&SAVDATE) +
                     RSTDATE(&RSTDATE) +
                     USEDATE(&USEDATE)

   /*-----------------------*/
   /* Breakup Dates & Times */
   /*-----------------------*/
             IF COND(&CHGDATE *NE ' ') THEN(DO)
                CHGVAR VAR(&CHGDTE) VALUE(%SST(&CHGDATE 1 7))
                CHGVAR VAR(&CHGTME) VALUE(%SST(&CHGDATE 8 6))
             ENDDO
             IF COND(&SAVDATE *NE ' ') THEN(DO)
                CHGVAR VAR(&SAVDTE) VALUE(%SST(&SAVDATE 1 7))
                CHGVAR VAR(&SAVTME) VALUE(%SST(&SAVDATE 8 6))
             ENDDO
             IF COND(&RSTDATE *NE ' ') THEN(DO)
                CHGVAR VAR(&RSTDTE) VALUE(%SST(&RSTDATE 1 7))
                CHGVAR VAR(&RSTTME) VALUE(%SST(&RSTDATE 8 6))
             ENDDO
             IF COND(&USEDATE *NE ' ') THEN(DO)
                CHGVAR VAR(&USEDTE) VALUE(&USEDATE)
             ENDDO

ENDPGM:
             ENDPGM
