/***************************************************************** */
/*                                                                 */
/* ˆWATCH OUT FOR THE EMAIL LOGIC.....WE HARDCODED SPOOL FILE      */
/* ˆNUMBERS!!!!!!                                                  */
/*                                                                 */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF415CL                                       */
/*  PROGRAM NAME:    MARGIN ADJUSTMENT CLOSE                       */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     12/22/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS IS THE PRIMARY CL FOR THE CLOSE--        */
/*                   WHICH RUNS WEEKLY.                            */
/*                                                                 */
/*  AS OF SEPTEMBER 2008 BOTH MIX AND VOLUME ARE "WEEKLY" AGAIN.   */
/*                                                                 */
/*  HISTORICALLY:                                                  */
/*  THE USERS HAVE SWITCHED SEVERAL TIMES ON THIS CLOSE.           */
/*  FIRST, IT WAS A "PERIOD" FUNCTION.                             */
/*  THEN,  IT WAS A "WEEKLY" FUNCTION.                             */
/*  THEN,  "MIX" WAS "WEEKLY"; "VOLUME" WAS "PERIOD" */
/*  SO, TRYING TO COVER ALL THE BASES, WE CREATED BOTH             */
/*      'WEEKLY' AND 'PERIOD' FILES FOR BOTH 'MIX' AND 'VOLUME'.   */
/*      BUT, FOR PRINTING (SO WE DIDN'T CONFUSE THEM):             */
/*         WE PRINTED 'MIX' REPORTS FOR THE WEEKLY CLOSES.         */
/*         WHEN IT WAS THE 'LAST WEEK IN A PERIOD' WE ALSO         */
/*         PRINTED THE 'VOLUME' REPORTS.                           */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 01/11/07  LEANNE RAMSEY                                         */
/*           ADDED E-MAIL OF REPORTS ON A FINAL                    */
/*                                                                 */
/* 03/21/07  LEANNE RAMSEY                                         */
/*           THE USERS ARE CHANGING THE VOLUME LOGIC. NOW, WE NEED */
/*           TO CALCULATE/STORE SEPARATELY THE COST/MARGIN DATA FOR*/
/*           ALL THE NVA PRODUCTS ASSOCIATED WITH EACH CV PRODUCT. */
/*           TO DO THIS WE HAVE TAKEN THE SAME APPROACH THAT IS    */
/*           ALREADY IN PLACE FOR THE VOLUME PROCESSING. WE WILL   */
/*           HAVE 3 NEW FILES TO DO THIS:                          */
/*             TFP031-VOLUME SUMMARY EXTENSION--WEEKLY             */
/*             TFP131-VOLUME SUMMARY EXTENSION--PERIOD             */
/*             TFP331-WORKFILE: VOLUME SUMMARY EXTENSION           */
/*                                                                 */
/* 04/16/07  LeAnne Ramsey                                         */
/*           We changed the following flag field:                  */
/*             1) Exclude from Mix Flag    is now Mix Flag         */
/*           Before this change Y=Yes meant EXCLUDE the product    */
/*           After this change  Y=Yes means INCLUDE the product    */
/*                                                                 */
/* 05/04/07  LeAnne Ramsey                                         */
/*           We are now excluding TF Class Group Code of           */
/*               BP=Byproduct as well as OT=Other.                 */
/*           Also, we have added the call to the TF Byproduct      */
/*           Mix Reports--but it is commented out. When Damon is   */
/*           ready, we need to "turn on" the CALL.                 */
/*                                                                 */
/* 05/21/07  LeAnne Ramsey                                         */
/*           Added a new data area (DATSTPRD) to control E-Mails.  */
/*                                                                 */
/* 06/20/07  LeAnne Ramsey                                         */
/*           The MIX stuff will now include the Work in Process    */
/*           items that became CoOwned Items.                      */
/*           The VOLUME logic continues to be Finished Goods only. */
/*                                                                 */
/* 08/01/07  LeAnne Ramsey                                         */
/*           Added logic to blank out a new LDA position that is   */
/*           used in the Missing Cost report.                      */
/*                                                                 */
/* 10/01/07  LeAnne Ramsey                                         */
/*           Added a call to TF206 to update the Revenue Detail    */
/*           file with Inventory Values.                           */
/*                                                                 */
/* 10/12/07  LeAnne Ramsey                                         */
/*           The change of 08/01/07 should have populated the new  */
/*           LDA position with N=NonByProduct. Made this change.   */
/*                                                                 */
/* 02/11/08  LeAnne Ramsey                                         */
/*           Removed call to TF246-Margin: Update Prime Pork       */
/*           "Other" cost in POAXCPP. We never used this "other"   */
/*           cost approach for Prime Pork. So, all files/programs  */
/*           have been deleted.                                    */
/*                                                                 */
/* 02/13/08  LeAnne Ramsey                                         */
/*           Removed all reference to Byproduct Mix Reports. We    */
/*           initially thought they would run from this CL. But,   */
/*           now, it seems they will run from Meat Costing.        */
/*                                                                 */
/* 07/07/08  LeAnne Ramsey                                         */
/*           Removed call to TF620-Product Exception/Product Split */
/*           Listing. Since the "Exception" file is now holding    */
/*           all of our system-generated exceptions, this dual     */
/*           listing is no longer short. We thought about calling  */
/*           the 2 existing on-demand reports (Exceptions and      */
/*           Splits) from here. BUT, the 3 LDAs were all different.*/
/*           (ie: the Margin Close LDA, the Exception Listing LDA, */
/*           the Split Listing LDA). So, the users will have to    */
/*           run the on-demand reports if they want to see this.   */
/*                                                                 */
/* 07/14/08  LeAnne Ramsey                                         */
/*           Tom Dye wanted the Exception Listing to be generated  */
/*           from this CL...he did not want to run it on-demand.   */
/*                                                                 */
/* 07/22/08  LeAnne Ramsey                                         */
/*           Moved the programs that update the TFP010 file to     */
/*           RO410CL-Meat Costing. (TF206, TF242, TF243)           */
/*                                                                 */
/* 07/24/08  LeAnne Ramsey                                         */
/*           Temporarily put TF206, TF242, TF243 back in; then,    */
/*           removed them again.                                   */
/*                                                                 */
/* 07/30/08  LeAnne Ramsey                                         */
/*           We had  to put TF243-Update Standard Costs/Yields     */
/*           back here permanently. When Tom Dye "finalizes" a     */
/*           Meat Cost run, it updates the Standard Cost file.     */
/*           We have to pull those "new" costs into the Margin     */
/*           close function. Also, changed TF243 to use parms      */
/*           instead of LDA values.                                */
/*                                                                 */
/* 08/27/08  LeAnne Ramsey                                         */
/*           We removed the "period" logic....all processing is,   */
/*           once again, "weekly".                                 */
/*                                                                 */
/* 11/21/08  LeAnne Ramsey                                         */
/*           As part of synchronizing the LDAs between the         */
/*           TFS Margin Adjustment Close and the Meat Costing,     */
/*           the LDA positions were changed.                       */
/*                                                                 */
/* 11/25/08  Alice Brownfield                                      */
/*           Added call to program that creates the NVA Volume     */
/*           Adjustments transactions in ROP125.                   */
/*                                                                 */
/* 11/28/08  LeAnne Ramsey                                         */
/*           Added call to TF422CL to print 2 reports:             */
/*           TF618 - ByProduct and NVA Volume Adjustment Report    */
/*           TF622 - ByProduct Mix Adjustment Report               */
/*                                                                 */
/* 12/03/08  LeAnne Ramsey                                         */
/*           Added call to TF423CL to print:                       */
/*           TF623 - ByProduct Mix Adjustment Recap Report         */
/*                                                                 */
/* 01/05/09  LeAnne Ramsey                                         */
/*           Added Weekly Yield processing.                        */
/*           For the new Weekly Yield Reporting, we call both the  */
/*           Meat Costing and Margin Close CLs behind-the-scenes.  */
/*           To control printing in the CLs, we have added a new   */
/*           incoming parm. We pass in:                            */
/*            1) a blank from the RO410 and TF415 submission       */
/*               screens                                           */
/*            2) an 'N' when the CL is called from Nathan's        */
/*               Inventory Stock Closing. So, I guess "N" can      */
/*               stand for 1) Nathan or 2) Not Normal              */
/*                                                                 */
/* 02/10/09  LeAnne Ramsey                                         */
/*           We have added another file/program for the Weekly     */
/*           Yield logic...it is for ByProduct Weekly Yield        */
/*           reporting.                                            */
/*                                                                 */
/* 03/09/09  LeAnne Ramsey                                         */
/*           We have pulled the TF676-Converting % Report out of   */
/*           the Weekly Yield logic. It now has its own kick-off   */
/*           screen and CL. Going forward, the Weekly Yield Reports*/
/*           will be generated from TF477CL; the Converting %      */
/*           Report will be generated from TF476Cl.                */
/*                                                                 */
/* 08/06/09  LeAnne Ramsey                                         */
/*           Tom Dye wanted the "Yield Reports" sent to a dozen    */
/*           more people...but, he did not want these new folks to */
/*           get the "Margin Reports". So, we:                     */
/*            1) created a new/separate distribution list for      */
/*               Yield Reports                                     */
/*            2) changed this CL to send 2 separate emails...      */
/*               one of Yield Reports and one for Margin Reports   */
/*            3) changed the ESEND logic to use "SPOOL FILE NBRs"  */
/*               This one could REALLY bite us! We send the        */
/*               first 6 to the Yield Distribution List and the    */
/*               last 9 to the Margin Distribution List. Soooo,    */
/*               if we add/take away reports or generate them      */
/*               in a DIFFERENT sequence, we must review/revise    */
/*               the ESENDs.                                       */
/*                                                                 */
/*               Also, hardcoded HOLD=YES on the spoolfiles.       */
/*                                                                 */
/* 02/10/10  LeAnne Ramsey                                         */
/*           The esend to Distribution List "TF ISC Weekly Yield'  */
/*           had quit working due to a 01/31/10 upgrade of ESEND.  */
/*           A "subject" greater than 60 characters was no longer  */
/*           supported; so, the email quit sending. I have         */
/*           shortened the "subject" to meet the new ESEND rule of */
/*           60 characters or less.                                */
/*                                                                 */
/* 03/25/22  Danny Nguyen  - DO2484 - WI 479 STF Variance Reporting*/
/*           Added new report TF6810 & TF6800 hence increase spool */
/*           file number by +2 when executing ESNDMAIL command.    */
/*                                                                 */
/* 02/21/23  Danny Nguyen  - W121675 - Added new report TF6790     */
/*           in TF477CL hence increase spool file # by +1 for the  */
/*           weekly yield reports.                                 */
/*                                                                 */
/* 07/31/23  Danny Nguyen  - WI593 - Added new reports TF6770 &    */
/*           TF6780 for STF in TF477CL hence increase spool file # */
/*           by +2 for the Primal Weekly Yield Detail & Summary    */
/*           reports.                                              */
/*‚09/26/24  Jagdish Mistry (JM-P310149),SR number-3014062         */
/*‚          Include WP items to be printed in Vol adjustment rept */
/*‚          This will match weights between volume adjustment     */
/*‚          detail and summary report.                            */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&CLPARM)

             DCL        VAR(&CLPARM)  TYPE(*CHAR) LEN(1)
/*‚JM-P310149 START */
/*           DCL        VAR(&QDATA)   TYPE(*CHAR) LEN(100)  */
             DCL        VAR(&QDATA)   TYPE(*CHAR) LEN(130)
/*‚JM-P310149 END   */

             DCL        VAR(&LDPFCD)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDYR)    TYPE(*DEC) LEN(4)
             DCL        VAR(&LDWK)    TYPE(*DEC) LEN(2)
             DCL        VAR(&LDWEDT)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDWEMDY) TYPE(*DEC) LEN(6)
             DCL        VAR(&LDWESYN) TYPE(*DEC)  LEN(7)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&USER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNAME)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&EMAIL)    TYPE(*CHAR) LEN(38)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)
/*‚JM-P310149 START */
             DCL        VAR(&WK_WIP) TYPE(*CHAR) LEN(1)
/*‚JM-P310149 END   */

             CHGVAR     VAR(&LDPFCD)   VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDYR)     VALUE(%SST(*LDA 2 4))
             CHGVAR     VAR(&LDWK)     VALUE(%SST(*LDA 6 2))
             CHGVAR     VAR(&LDWEDT)   VALUE(%SST(*LDA 29 8))
             CHGVAR     VAR(&LDWEMDY)  VALUE(%SST(*LDA 44 6))
             CHGVAR     VAR(&LDWESYN)  VALUE(%SST(*LDA 37 7))

/*----------------------------------------------------------------*/
/* FOR SENDING EMAILS:                                            */
/* RETRIEVE DATA AREA THAT LETS US KNOW WHETHER WE ARE IN TEST    */
/* OR PRODUCTION (ALWAYS DEFAULT TO THE "TEST" DISTRIBUTION LIST.)*/
/*----------------------------------------------------------------*/

             CHGVAR     VAR(&EMAIL) VALUE('TF Test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)

/*---------------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                                */
/*---------------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGDTAARA  DTAARA(*LDA (411 4)) VALUE('*YES')
             CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))


/*-------------------------------------------------------------------*/
/* ALWAYS DELETE ANY/ALL EXISTING RECORDS FOR THE YEAR/WEEK          */
/*-------------------------------------------------------------------*/

             CALL       PGM(TF217)

/*--------------------------------------------------------------------*/
/* UPDATE STANDARD COSTS/YIELDS IN THE REVENUE DETAIL RECORDS         */
/*--------------------------------------------------------------------*/

             CALL       PGM(TF243) PARM(&LDWEDT &LDWESYN)

/*---------------------------------------------------------------------*/
/* EXTRACT DATA FOR THE MARGIN ADJUSTMENT DETAIL FILE                  */
/*---------------------------------------------------------------------*/

             CALL       PGM(TF216)

/*---------------------------------------------------------------------*/
/* RETRIEVE "PRODUCT SPLITS" AND UPDATE MARGIN ADJUSTMENT DETAIL FILE  */
/*---------------------------------------------------------------------*/

             CALL       PGM(TF245)

/*---------------------------------------------------------------------*/
/* WEEKLY MIX SUMMARY                                                  */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 8)) VALUE('ADYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 10 4)) VALUE(&LDYR)
             CHGVAR     VAR(%SST(&QDATA 15 13)) VALUE('*AND ADWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 29 2)) VALUE(&LDWK)

             CHGVAR     VAR(%SST(&QDATA 32 45)) VALUE('*AND ADTFCGCD +
                          *NE "OT" *AND ADTFCGCD *NE "BP"')

/*‚JM-P310149 START */
/*           CHGVAR     VAR(%SST(&QDATA 78 20)) VALUE('*AND ADMIXFL +*/
/*                        *EQ "Y"')                                  */
             CHGVAR     VAR(%SST(&QDATA 78 22)) VALUE('*AND (ADITYCD +
                          *EQ "FG"')
             CHGVAR     VAR(%SST(&QDATA 101 24)) VALUE(' *OR ADITYCD +
                          *EQ "WP")')
/*‚JM-P310149 END   */

             OVRDBF     FILE(TFL014B) SHARE(*YES)
             OPNQRYF    FILE((TFL014B)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

             CRTDUPOBJ  OBJ(TFP315) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(TFL315A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(TF215) PARM('W')

             CPYF       FROMFILE(QTEMP/TFP315) TOFILE(TFP015) +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF2817)

             CLOF       OPNID(TFL014B)
             DLTOVR     FILE(TFL014B)
             DLTF       FILE(QTEMP/TFL315A)
             DLTF       FILE(QTEMP/TFP315)

/*---------------------------------------------------------------------*/
/* WEEKLY VOLUME SUMMARY AND EXTENSION FILES                           */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 8)) VALUE('ADYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 10 4)) VALUE(&LDYR)
             CHGVAR     VAR(%SST(&QDATA 15 13)) VALUE('*AND ADWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 29 2)) VALUE(&LDWK)

             CHGVAR     VAR(%SST(&QDATA 32 45)) VALUE('*AND ADTFCGCD +
                          *NE "OT" *AND ADTFCGCD *NE "BP"')

/*‚JM-P310149 START */
/*           CHGVAR     VAR(%SST(&QDATA 78 22)) VALUE('*AND ADITYCD +*/
/*                        *EQ "FG "')                                */
             CHGVAR     VAR(%SST(&QDATA 78 22)) VALUE('*AND (ADITYCD +
                          *EQ "FG"')
             CHGVAR     VAR(%SST(&QDATA 101 24)) VALUE(' *OR ADITYCD +
                          *EQ "WP")')
/*‚JM-P310149 END   */

             OVRDBF     FILE(TFL014C) SHARE(*YES)
             OPNQRYF    FILE((TFL014C)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* WEEKLY VOLUME SUMMARY AND WEEKLY VOLUME SUMMARY EXTENSION           */
/*‚JM-P310149 START */
             DLTF       FILE(QTEMP/TFL318A)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFP318)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFL331A)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/TFP331)
             MONMSG     MSGID(CPF0000)
/*‚JM-P310149 END   */

             CRTDUPOBJ  OBJ(TFP318) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Summary */
/*‚JM-P310149 START */
             CRTDUPOBJ  OBJ(TFL318A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Summary */
/*‚JM-P310149 END   */

             CRTDUPOBJ  OBJ(TFP331) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Summary Extension */
/*‚JM-P310149 START */
             CRTDUPOBJ  OBJ(TFL331A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) /* Summary Extension */
/*‚JM-P310149 END   */

             CALL       PGM(TF218) PARM('W')
             CALL       PGM(TF231) PARM('W')

             CPYF       FROMFILE(QTEMP/TFP318) TOFILE(TFP018) +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF2817)

             CPYF       FROMFILE(QTEMP/TFP331) TOFILE(TFP031) +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF2817)

             CLOF       OPNID(TFL014C)
             DLTOVR     FILE(TFL014C)
/*‚JM-P310149 START */
             DLTF       FILE(QTEMP/TFL318A)
             MONMSG     MSGID(CPF0000)
/*‚JM-P310149 END   */
             DLTF       FILE(QTEMP/TFP318)
/*‚JM-P310149 START */
             DLTF       FILE(QTEMP/TFL331A)
             MONMSG     MSGID(CPF0000)
/*‚JM-P310149 END   */
             DLTF       FILE(QTEMP/TFP331)

/*---------------------------------------------------------------------*/
/* CREATE NVA VOLUME ADJUSTMENT TRANSACTIONS                           */
/*---------------------------------------------------------------------*/

             CALL       PGM(TF233)

/*-------------------------------------------------------------------*/
/* WEEKLY YIELD PROCESSING AND CONVERTING % REPORT                   */
/*-------------------------------------------------------------------*/
/* Delete weekly yield records for the week from the files           */
/* Build weekly yield records for the week in the files              */
/* Run reports (We will always run these reports--even when these    */
/*              CLs are called from the Inventory Stock Closing.)    */
/* When this has been called from the Inventory Stock Closing,       */
/*      we will email the reports.                                   */

             CALL       PGM(TF275)                 /* delete  */
             CALL       PGM(TF276)                 /* build weekly yield     */
             CALL       PGM(TF277)                 /* build byproduct backup */
             CALL       PGM(TF477CL) PARM(&CLPARM) /* weekly yield reports   */
             CALL       PGM(TF476CL) PARM(&CLPARM) /* converting % report    */

             IF         COND(&CLPARM *EQ 'N') THEN(DO) /* If Stock Closing */

             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('TF ISC Weekly Yield'))

             RTVJOBA    JOB(&JOBNAME) USER(&USER) NBR(&JOBNBR)
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Wkly Yield & +
                          Converting % Rpts from Inventory Stock +
                          Closing') MSG('Distribution List: TF ISC +
                          Weekly Yield') ATTLIST((* *PDF *N *ALL +
                          &JOBNBR/&USER/&JOBNAME *ALL *ALL))
             MONMSG     MSGID(CPF0000)

             ENDDO                                     /* If Stock Closing */

/******************************************************************************/
/* ONLY PRINT THE REMAINING REPORTS WHEN THE INCOMING PARM IS BLANK           */
/* (ie: not called from Inventory Stock Closing)                              */
/******************************************************************************/

             IF         COND(&CLPARM *EQ ' ') THEN(DO) /* If reports */

/* Print an FYI Listing of Products Missing Standard Yield/Cost Data   */

             CHGDTAARA  DTAARA(*LDA (113 1))  VALUE('N')
             CALL       PGM(TF414CL)
             CHGDTAARA  DTAARA(*LDA (113 1))  VALUE(' ')

/*-----------------*/
/* Weekly Reports  */
/*-----------------*/

       /* Mix Adjustment */

             CHGDTAARA  DTAARA(*LDA (113 1))  VALUE('M')
/*‚JM-P310149 START */
/*           CALL       PGM(TF416CL)                               */
             CHGVAR     VAR(&WK_WIP) VALUE(' ')
             CALL       PGM(TF416CL) PARM((&WK_WIP))
/*‚JM-P310149 END   */

       /* Volume Adjustment */

             CHGDTAARA  DTAARA(*LDA (113 1))  VALUE('V')
/*‚JM-P310149 START */
/*           CALL       PGM(TF416CL)                               */
             CALL       PGM(TF416CL) PARM((&WK_WIP))
/*‚JM-P310149 END   */

/*---------------------------------*/
/* Margin Adjustment Recap Report  */
/*---------------------------------*/

             CALL       PGM(TF446CL)

/*------------------------------------------------------*/
/* ByProduct Adjustment Reports                         */
/*   TF618 - ByProduct and NVA Volume Adjustment Report */
/*   TF622 - ByProduct Mix Adjustment REport            */
/*------------------------------------------------------*/

             CHGDTAARA  DTAARA(*LDA (113 1))  VALUE('B')
             CALL       PGM(TF422CL)

/*---------------------------------------*/
/* ByProduct Mix Adjustment Recap Report */
/*---------------------------------------*/

             CALL       PGM(TF423CL)

/*---------------------------*/
/* Product Exception Listing */
/*---------------------------*/

             CALL       PGM(TF4190CL)
             ENDDO


/*---------------------------------------------------------------------*/
/* IF IT IS A "FINAL" RUN:                                             */
/*  1) write invoice charge records                                    */
/* ˆ2) e-mail reports....BE CAREFUL of the SPOOL FILE NUMBERS!!!       */
/*---------------------------------------------------------------------*/

             IF         COND(&LDPFCD *EQ 'F') THEN(DO)      /* If FINAL */
             CALL       PGM(TF241) /* Invoice Charge */

             RTVJOBA    JOB(&JOBNAME) USER(&USER) NBR(&JOBNBR)

/* Send the "Yield" Reports; they will be spool files 1 thru 7         */
/* Send the "Yield" Reports; they will be spool files 1 thru 9 ; W121675 */
/* Send the "Yield" Reports; they will be spool files 1 thru 11 ; WI593 */

             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('TF Margin Adj +
                          Close-Weekly Yld'))

/* 03/25/22 DN DO2484 - Added new report TF6810 & TF6800 hence increase +
     spool file number by +2 */
/* 02/21/23 DN W121675 - Added new report TF6790 hence increase spool file +
     number by +1 */
/* 07/31/23 DN WI593 - Added new reports TF6770 & TF6780 hence increase +
     spool file number by +2 */
    /*       ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: Margin +
                          Adjustment Close Reports') +
                          MSG('Distribution List: TF Margin Adj +
                          Close-Weekly Yld') ATTLIST((* *PDF *N +
                          *ALL &JOBNBR/&USER/&JOBNAME 1 6))          */
    /*       ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: Margin +
                          Adjustment Close Reports') +
                          MSG('Distribution List: TF Margin Adj +
                          Close-Weekly Yld') ATTLIST((* *PDF *N +
                          *ALL &JOBNBR/&USER/&JOBNAME 1 8))          */
    /*       ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: Margin +
                          Adjustment Close Reports') +
                          MSG('Distribution List: TF Margin Adj +
                          Close-Weekly Yld') ATTLIST((* *PDF *N +
                          *ALL &JOBNBR/&USER/&JOBNAME 1 9))          */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: Margin +
                          Adjustment Close Reports') +
                          MSG('Distribution List: TF Margin Adj +
                          Close-Weekly Yld') ATTLIST((* *PDF *N +
                          *ALL &JOBNBR/&USER/&JOBNAME 1 11))
             MONMSG     MSGID(CPF0000)

/* Send the "Margin" Reports; they will be spool files 9 thru 17        */
/* Send the "Margin" Reports; they will be spool files 10 thru 18 ; W121675 */
/* Send the "Margin" Reports; they will be spool files 12 thru 20 ; WI593 */

             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('TF Margin Adj Close-Admin'))

/* 03/25/22 DN DO2484 - Added new report TF6810 hence increase spool +
     file number by +1 */
/* 02/21/23 DN W121675 - Added new report TF6790 hence increase spool +
     file number by +1 */
/* 07/31/23 DN WI593 - Added new reports TF6770 & TF6780 hence increase +
     spool file number by +2 */
    /*       ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: Margin +
                          Adjustment Close Reports') +
                          MSG('Distribution List: TF Margin Adj +
                          Close-Admin') ATTLIST((* *PDF *N *ALL +
                          &JOBNBR/&USER/&JOBNAME 7 9))               */
    /*       ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: Margin +
                          Adjustment Close Reports') +
                          MSG('Distribution List: TF Margin Adj +
                          Close-Admin') ATTLIST((* *PDF *N *ALL +
                          &JOBNBR/&USER/&JOBNAME 9 11))              */
    /*       ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: Margin +
                          Adjustment Close Reports') +
                          MSG('Distribution List: TF Margin Adj +
                          Close-Admin') ATTLIST((* *PDF *N *ALL +
                          &JOBNBR/&USER/&JOBNAME 10 12))             */
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: Margin +
                          Adjustment Close Reports') +
                          MSG('Distribution List: TF Margin Adj +
                          Close-Admin') ATTLIST((* *PDF *N *ALL +
                          &JOBNBR/&USER/&JOBNAME 12 14))
             MONMSG     MSGID(CPF0000)

             ENDDO                                          /* If FINAL */

/*-------------------------------------------------------------------*/
/* ALWAYS UPDATE THE FUNCTION CONTROL FILE                           */
/*-------------------------------------------------------------------*/

             CALL       PGM(TF219) PARM(&LDPFCD)

             ENDPGM

