      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6075
      * TITLE:       Movement Verification Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/06/00
      *
      *
      * FUNCTION: This program prints an 'origin' page of movements out of a farm followed by
      *           a 'destination' page of movements into the  farm.
      *
      *           The layout is a dup of the HP676-Actual Movement Log; and, some of the
      *           workfile logic is also the same as the Actual Movement Log.
      *
      *           This program is submitted by HP4075CL which is called from HP4075 through
      *           QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/08/02  LeAnne Fedor
      *           Added a new 'KOA head' column. To make room, I changed the
      *           Quality column to be 'Quality and Reject'---and then put
      *           the new KOA column where the Reject column used to be.
      *
      * 05/23/11  LeAnne Ramsey (E1556)
      *           Recompile only.
      *           Added Sex Code for the Destination Group in both workfiles.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm site (file keyed in CLP)
      *
      *
     Fhsl018a   if   e           k disk    rename(fsrec:fsreca) prefix(p1)
      *    Farm site
      *
      *
     Fhsp376    if   e           k disk    rename(wmrec:wmreco)
      *   Origin workfile
      *
      *
     Fhsp377    if   e           k disk    rename(wmrec:wmrecd)
      *   Destination workfile
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d first           s              1    inz('Y')
     d firstorig       s              1    inz('Y')
     d firstdest       s              1    inz('Y')
      *
      *
      *
      *
      * Header print fields
      *
     d h1cell          s              2  0
     d h1fsnm          s                   like(fsfsnm)
     d h1fscd          s                   like(fsfscd)
      *
      * Detail line print fields
      *
     d d1mdyrcdt       s              6  0
     d d1orfsnm        s             20
     d d1dnfsnm        s             14
     d d1cvnm          s             25
     d d1aslb          s              5  2
     d d1arlb          s              5  2
     d d1rchd          s              5  0
     d d1rclb          s              5  0
     d d1qlrjhd        s              5  0
      *
      * Total line print fields
      *
     d t1schd          s             15  0
     d t1shlb          s             15  0
     d t1rclb          s             15  0
     d t1shhd          s             15  0
     d t1rchd          s             15  0
     d t1doahd         s             15  0
     d t1koahd         s             15  0
     d t1qlrjhd        s             15  0
      *
      *
      * Work fields for date manipulation
      *
     D wkmdy           s               D   datfmt(*mdy)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfmdy                 1      6  0
     D  ldtmdy                 7     12  0
     D  ldfcymd               13     20  0
     D  ldtcymd               21     28  0
      *
     D  ldorfsbo              29     33
     D  ldorptcd              34     38
     D  ldorppcd              39     43
     D  ldorcell              44     45  0
     D  ldorfscd              46     50  0
      *
     D  lddnfsbo              51     55
     D  lddnptcd              56     60
     D  lddnppcd              61     65
     D  lddncell              66     67  0
     D  lddnfscd              68     72  0
      *
     D  ldorbods              73    102
     D  ldorptds             103    112
     D  ldorppds             113    142
     D  ldorfsnm             143    167
      *
     D  lddnbods             168    197
     D  lddnptds             198    207
     D  lddnppds             208    237
     D  lddnfsnm             238    262
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read the farm master and then print the 'origin' data followed by
      * the 'destination' data for each farm that has data in either/both of
      * the workfiles.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   z-add     fsfscd        h1fscd
     C                   movel     fsfsnm        h1fsnm
     C                   z-add     fscell        h1cell
      *
     C                   exsr      $origin
     C                   exsr      $destination
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        first = yes
     C                   exsr      $h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Calculate print values
      *---------------------------------------------------------------
      *
     C     $calc         begsr
      *
      *
      * Populate either a) the destination farm site name for transfers
      * or b) the customer name for sales movements
      *
     C                   select
     C                   when      wmmtcd = 'T'
     C                   setoff                                       61
     C                   movel(p)  wmdnnm        d1dnfsnm
      *
     C                   when      wmmtcd = 'S'
     C                   seton                                        61
     C                   movel(p)  wmdnnm        d1cvnm
     C                   endsl
      *
      * Retrieve the origin farm site name
      *
     C     key01         chain     hsl018A                            92
     C                   if        *in92 = *off
     C                   movel     p1fsfsnm      d1orfsnm
     C                   else
     C                   movel     'Unknown'     d1orfsnm
     C                   endif
      *
      * Calculate received values as: quality + reject
      *
     C     wmqlhd        add       wmrjhd        d1rchd
     C     wmqllb        add       wmrjlb        d1rclb
      *
      * Calculate average received pounds
      *
     C                   z-add     0             d1arlb
     C                   if        d1rchd <> 0
     C     d1rclb        div(h)    d1rchd        d1arlb
     C                   endif
      *
      * Calculate average shipped pounds
      *
     C                   z-add     0             d1aslb
     C                   if        wmshhd <> 0
     C     wmshlb        div(h)    wmshhd        d1aslb
     C                   endif
      *
      * Add quality and reject head together into a single print field
      *
     C     wmqlhd        add       wmrjhd        d1qlrjhd
      *
      * Accumulate quantites for totals
      *
     C                   add       wmschd        t1schd
     C                   add       wmshhd        t1shhd
     C                   add       wmshlb        t1shlb
     C                   add       d1rchd        t1rchd
     C                   add       d1rclb        t1rclb
     C                   add       wmdoahd       t1doahd
     C                   add       wmkoahd       t1koahd
     C                   add       wmrjhd        t1qlrjhd
     C                   add       wmqlhd        t1qlrjhd
      *
      * Flip receive date to MMDDYY for printing
      *
     C
     C     *iso          test(d)                 wmrcdt                 92
     C                   if        *in92 = *off                                 If good date
     C     *iso          movel     wmrcdt        wkmdy
     C                   move      wkmdy         d1mdyrcdt
     C                   endif                                                  If good date
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print origin data
      *---------------------------------------------------------------
      *
     C     $origin       begsr
      *
     C                   setoff                                       86
     C                   seton                                        88
     C                   move      yes           firstorig
     C                   movel     fsfsnm        d1orfsnm
      *
     C     fsfscd        setll     hsp376
      *
     C                   dou       *in91 = *on                                  Do origin
     C     fsfscd        reade     hsp376                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   if        firstorig  = yes
     C                   seton                                        of
     C                   move      no            firstorig
     C                   endif
      *
     C                   exsr      $calc
     C                   exsr      $d1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do origin
      *
      * If you had any 'origin' data, print totals
      *
     C                   if        firstorig = no
     C                   exsr      $t1tot
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print destination data
      *---------------------------------------------------------------
      *
     C     $destination  begsr
      *
     C                   setoff                                       88
     C                   seton                                        86
      *
     C                   move      yes           firstdest
     C                   movel     fsfsnm        d1dnfsnm
      *
     C     fsfscd        setll     hsp377
      *
     C                   dou       *in91 = *on                                  Do dest
     C     fsfscd        reade     hsp377                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   if        firstdest  = yes
     C                   seton                                        of
     C                   move      no            firstdest
     C                   endif
      *
     C                   exsr      $calc
     C                   exsr      $d1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do dest
      *
      * If you had any 'destination' data, print totals
      *
     C                   if        firstdest = no
     C                   exsr      $t1tot
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print header
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail
      *---------------------------------------------------------------
      *
     C     $d1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    d1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
     C                   move      no            first
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    wmorbo
     C                   kfld                    wmorfs
      *
      *
      * Rerieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      *  Setup the user selections in the headings.
      *
      * Origin selections
     C                   if        ldorfsbo = *blank
     C                   movel     'ALL'         ldorfsbo
     C                   endif
      *
     C                   if        ldorptcd = *blank
     C                   movel     'ALL'         ldorptcd
     C                   endif
      *
     C                   if        ldorppcd = *blank
     C                   movel     'ALL'         ldorppcd
     C                   endif
      *
     C                   if        ldorcell = 0
     C                   seton                                        80
     C                   endif
      *
     C                   if        ldorfscd = 0
     C                   seton                                        81
     C                   endif
      *
      * Destination selections
      *
     C                   if        lddnfsbo = *blank
     C                   movel     'ALL'         lddnfsbo
     C                   endif
      *
     C                   if        lddnptcd = *blank
     C                   movel     'ALL'         lddnptcd
     C                   endif
      *
     C                   if        lddnppcd = *blank
     C                   movel     'ALL'         lddnppcd
     C                   endif
      *
     C                   if        lddncell = 0
     C                   seton                                        82
     C                   endif
      *
     C                   if        lddnfscd = 0
     C                   seton                                        83
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                          106 'HOG PRODUCTION SYSTEM'
     O                                          185 'DATE'
     O                       udate         Y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          102 'MOVEMENT VERIFICATION'
     O                                          109 'REPORT'
     O                                          185 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          185 'PAGE'
     O                       page          Z    198
      *
      *
     O          e            h1hdr          1
     O                                           12 'FROM DATE ..'
     O                       ldfmdy        Y     22
     O                                           53 'ORIGIN BUSINESS OFFICE ...'
     O                       ldorfsbo            60
     O                       ldorbods            92
     O                                          119 'DESTINATION BUSINESS '
     O                                          129 'OFFICE ...'
     O                       lddnfsbo           136
     O                       lddnbods           168
      *
     O          e            h1hdr          1
     O                                           12 'TO DATE ....'
     O                       ldtmdy        y     22
     O                                           53 'ORIGIN PRODUCTION TYPE ...'
     O                       ldorptcd            60
     O                       ldorptds            72
     O                                          120 'DESTINATION PRODUCTION'
     O                                          129 ' TYPE ...'
     O                       lddnptcd           136
     O                       lddnptds           148
      *
      *
     O          e            h1hdr          1
     O                                           53 'ORIGIN PRODUCTION PHASE ..'
     O                       ldorppcd            60
     O                       ldorppds            92
     O                                          120 'DESTINATION PRODUCTION'
     O                                          129 ' PHASE ..'
     O                       lddnppcd           136
     O                       lddnppds           168
      *
      *
     O          e            h1hdr          1
     O                                           53 'ORIGIN CELL ..............'
     O              n80      ldorcell      z     57
     O               80                          58 'ALL'
     O                                          114 'DESTINATION CELL'
     O                                          129 ' ..............'
     O              n82      lddncell      z    133
     O               82                         134 'ALL'
      *
      *
     O          e            h1hdr          1
     O                                           53 'ORIGIN FARM SITE .........'
     O              n81      ldorfscd      z     60
     O               81                          58 'ALL'
     O                       ldorfsnm            87
     O                                          119 'DESTINATION FARM SITE'
     O                                          129 ' .........'
     O              n83      lddnfscd      z    136
     O               83                         134 'ALL'
     O                       lddnfsnm           163
      *
     O          e            h1hdr          2
     O                                            5 'CELL:'
     O                       h1cell        z      9
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          E            h1hdr          1
     O                                          144 'AVERAGE'
     O                                          172 'AVERAGE'
     O                                          198 'REJECT &'
      *
     O          E            h1hdr          1
     O                                            8 'MOVEMENT'
     O                                           17 'RECEIVED'
     O                                           34 '---------------'
     O                                           42 'ORIGIN'
     O                                           59 '---------------'
      *
      * Only for transfers
      *
     O               86                          72 '----------  '
     O               86                          83 'DESTINATION'
     O               86                          95 '  ----------'
      *
      * For all movements
     O                                          100 'MOVE'
     O                                          109 'SCHED'
      *
     O                                          128 'SHIPPED'
     O                                          136 'SHIPPED'
     O                                          144 'SHIPPED'
     O                                          154 'RECEIVED'
     O                                          164 'RECEIVED'
     O                                          173 'RECEIVED'
     O                                          178 'KOA'
     O                                          186 'DOA'
     O                                          198 'QUALITY'
     O          e            h1hdr          1
     O                                            6 'NUMBER'
     O                                           15 'DATE'
     O                                           28 'FARM SITE'
     O                                           49 'BLDG/ROOM'
     O                                           57 'GROUP'
      * Both sales and transfers
      *
     O               88                          72 '----------  '
     O               88                          83 'DESTINATION'
     O               88                          95 '  ----------'
      *
      * Only transrers
      *
     O               86                          69 'FARM SITE'
     O               86                          84 'BLDG/ROOM'
     O               86                          93 'GROUP'
     O                                          100 'TYPE'
     O                                          108 'HEAD'
     O                                          118 'TRUCKER'
      *
     O                                          127 'HEAD'
     O                                          135 'POUNDS'
     O                                          144 'POUNDS '
     O                                          152 'HEAD'
     O                                          162 'POUNDS'
     O                                          171 'POUNDS'
     O                                          178 'HEAD'
     O                                          186 'HEAD'
     O                                          198 'HEAD IN'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
     O          e            h1hdr          2
     O               88                          17 'ORIGIN FARM:'
     O               86                          17 'DESTINATION FARM:'
     O                       h1fscd        z     23
     O                       h1fsnm              50
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            d1dtl          1
     O                       wmmvsn        z      7
     O                       d1mdyrcdt           17 '  /  /  '
      * Origin info
     O                       d1orfsnm            39
     O                       wmorbl              45
     O                                           46 '/'
     O                       wmorrm              51
     O                       wmorhg              59
      *
      * Destination info for a transfer
      *
     O              n61      d1dnfsnm            74
     O              n61      wmdnbl              80
     O              n61                          81 '/'
     O              n61      wmdnrm              86
     O              n61      wmdnhg              95
      * For a sale
     O               61      d1cvnm              85
     O               61      wmcvno        z     94
      *
     O                       wmcode             101
     O                       wmschd        2    109
     O                       wmticd             117
      *
     O                       wmshhd        2    128
     O                       wmshlb        2    136
     O                       d1aslb        2    144
      *
     O                       d1rchd        2    153
     O                       d1rclb        2    163
     O                       d1arlb        2    172
     O                       wmkoahd       2    178
     O                       wmdoahd       2    185
     O                       d1qlrjhd      2    198
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          E            t1tot       1
      *
     O                                           88 'TOTALS:'
     O                       t1schd        2b   109
     O                       t1shlb        2b   136
     O                       t1rclb        2b   163
     O                       t1doahd       2b   185
      *
     O          E            t1tot       1  1
      *
     O                       t1shhd        2b   128
     O                       t1rchd        2b   153
     O                       t1koahd       2b   178
     O                       t1qlrjhd      2b   198
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
