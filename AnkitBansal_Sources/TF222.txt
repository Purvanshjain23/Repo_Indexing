      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF222--Eopfees: Edit with Error Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     04/13/05
      *
      * Function:    This function checks that other functions have been completed.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp001    if   e           k disk
      *    Preliminary/final
      *
      *
     Ftfp099    if   e           k disk
      *    Function control
      *
      *
     Fprint80   o    f   80        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *----------------------------------------------------------------
      * ARRAYS AND TABLES
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s             80    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d first           s              1    inz('Y')
     d warnfl          s              1    inz('N')
      *
      *
      * Print fields
      *
     d h1pfds          s                   like(pfpfds)
      *
      *
      * Parm fields
      *
     d xxerror         s              1
     d xxpfcd          s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldcyr                  2      5  0
     D  ldcpe                  6      7  0
      *
     D  ldcbdt                 8     15  0
     D  ldcbmdy               16     21  0
     D  ldcbsyn               22     28  0
      *
     D  ldcedt                29     36  0
     D  ldcemdy               37     42  0
     D  ldcesyn               43     49  0
      *
     D  ldledt                50     57  0
     D  ldlemdy               58     63  0
     D  ldlesyn               64     70  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * The only 'error' that we check for is:
      *     1) The last Weekly Revenue Close must have been completed for
      *        the period--if this is a FINAL.
      *
     C     'REVENUE   '  chain     tfp099                             92
     C                   if        *in92 = *on or                               If not all
     C                             fnledt < ldcedt
      *
     C                   select
     C                   when      xxpfcd = 'F'
     C                   move      yes           xxerror
     C                   except    termrev
      *
     C                   when      xxpfcd = 'P'
     C                   move      yes           warnfl
     C                   except    warnrev
     C                   endsl
     C                   endif                                                  If not all
      *
      * If there were no errors or warnings, print "All OK" message.
      *
     C                   if        xxerror = no and warnfl = no
     C                   except    noerrmsg
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------
      * Print headings
      *-----------------------------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxerror
     C                   parm                    xxpfcd
      *
      * The "error" parm above should come in as NO. But, just in case, set it
      * to NO here.
      *
     C                   move      no            xxerror
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Retrieve 'preliminary/final' description
      *
     C     xxpfcd        chain     tfp001                             92
     C                   if        *in92 = *off
     C                   move      pfpfds        h1pfds
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint80   e            h1hdr          1 01
     O                       sdpgm               10
     O                                           46 'EOP Fees Close'
     O                                           70 'Date'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1pfds              45
     O                                           70 'Time'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'Page'
     O                       page          z     80
      *
     O          e            h1hdr       0  1
     O                       dash                80
      *
      *
      *-------------------------------------------------------------
      * There were no errors.
      *-------------------------------------------------------------
      *
     O          e            noerrmsg       2
     O                                           20 'There are no errors.'
      *-----------------------------------------------------------------------------
      * Weekly Revenue Closes were not Complete--and this is a FINAL
      *-----------------------------------------------------------------------------
      *
     O          e            termrev        2
     O                                           26 'Cannot run. Weekly Revenue'
     O                                           52 ' Closes have not yet been '
     O                                           78 'completed for this period.'
      *
      *-----------------------------------------------------------------------------
      * Weekly Revenue Closes are not Complete--but this is just a PRELIMINARY
      *-----------------------------------------------------------------------------
      *
     O          e            warnrev        2
     O                                           26 'FYI: Revenue has not been '
     O                                           50 'closed yet for all weeks'
     O                                           65 ' in the period.'
      /eject
