     A*%%TS  SD  20020715  144021  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD533 - SELECT HOG GROUP FOR FEED
     A*  RPG PROGRAM:   HP533  - SELECT HOG GROUP FOR FEED
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          04/29/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP533R1
     A*%%TS  SD  19950118  093426  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 47)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP533R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP533R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP533S1                   SFL
     A*%%TS  SD  20020715  144021  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFHGCD    R        O  5  1REFFLD(HGHGCD)
     A            DFGSCD    R        O  5 12REFFLD(HGGSCD)
     A            DFPPCD    R        O  5 19REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFCMDY    R        O  5 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFOMDY    R        O  5 38REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP533C1                   SFLCTL(HP533S1)
     A*%%TS  SD  20020715  144021  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP533R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  8'SELECT HOG GROUP FOR FEED'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  3 17'Production'
     A                                      DSPATR(HI)
     A                                  3 30'Date'
     A                                      DSPATR(HI)
     A                                  3 40'Date'
     A                                      DSPATR(HI)
     A                                  4  1'Group'
     A                                      DSPATR(HI)
     A                                  4 10'Status'
     A                                      DSPATR(HI)
     A                                  4 19'Phase'
     A                                      DSPATR(HI)
     A                                  4 29'Created'
     A                                      DSPATR(HI)
     A                                  4 39'Opened'
     A                                      DSPATR(HI)
     A          R HP533K1
     A*%%TS  SD  19940316  131942  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP533R1)
     A                                      OVERLAY
     A                                 15  1'F12=Previous'
