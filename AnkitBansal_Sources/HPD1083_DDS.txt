     A*%%TS  SD  20020612  082119  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1083
     A*  TITLE:         REVISE QUARTERLY PREDICTION VALUES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       01/30/02
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  61N62                               CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A N62                                  CF09(09 'ACCEPT')
     A  60N62                               CF11(11 'DELETE')
     A N62                                  CF13(13 'VIEW FARMS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1083R1
     A*%%TS  SD  20020612  082119  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(54)
     A                                      SETOF(73)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      SETOF(76)
     A                                      SETOF(77)
     A                                      SETOF(78)
     A                                      SETOF(79)
     A                                      SETOF(80)
     A                                      SETOF(81)
     A                                      SETOF(82)
     A                                      SETOF(83)
     A                                      SETOF(84)
     A                                      SETOF(85)
     A                                      SETOF(86)
     A                                      SETOF(87)
     A                                      SETOF(88)
     A                                      SETOF(89)
     A                                      SETOF(90)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 23'REVISE QUARTERLY PREDICTION VALUES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Year/quarter:'
     A            D1CDYR    R        B  4 16REFFLD(@@CDYR)
     A                                      EDTCDE(M)
     A  61N31                               DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A            D1CDQR    R        B  4 22REFFLD(@@CDQR)
     A                                      EDTCDE(M)
     A  61N32                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                  4 31'Cell:'
     A            D1CELL    R        B  4 37REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A  61N33                               DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                  4 48'Group type:'
     A  61                              4 60'?'
     A            D1GTCD    R        B  4 62REFFLD(@@GTCD)
     A  61N34                               DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A            D1GTDS    R        O  4 65REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A            D1QACYR   R        O  6 32REFFLD(@@CDYR)
     A                                      EDTCDE(Z)
     A                                  6 49'Low'
     A                                  6 61'High'
     A                                  6 70'Override'
     A                                  7 31'Actuals'
     A                                  7 47'Value'
     A                                  7 60'Value'
     A                                  7 73'Value'
     A                                  8 18'Services'
     A            D1QTOSV   R        O  8 31REFFLD(@@LTOSV)
     A                                      EDTCDE(K)
     A            D1LTOSV   R        B  8 46REFFLD(@@LTOSV)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N35                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HTOSV   R        B  8 59REFFLD(@@HTOSV)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OTOSV   R        B  8 72REFFLD(@@OTOSV)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 10 11'Conception rate'
     A            D1QCRPR   R        O 10 31REFFLD(@@CRPR)
     A                                      EDTCDE(K)
     A                                 10 42'%'
     A            D1LCRPR   R        B 10 46REFFLD(@@CRPR)
     A                                      EDTCDE(K)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N62N30                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HCRPR   R        B 10 59REFFLD(@@CRPR)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N38                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OCRPR   R        B 10 72REFFLD(@@CRPR)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N62N39                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 11 12'Farrowing rate'
     A            D1QFRPR   R        O 11 31REFFLD(@@FRPR)
     A                                      EDTCDE(K)
     A                                 11 42'%'
     A            D1LFRPR   R        B 11 46REFFLD(@@FRPR)
     A                                      EDTCDE(K)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A N62N44                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HFRPR   R        B 11 59REFFLD(@@FRPR)
     A                                      EDTCDE(K)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A N62N45                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OFRPR   R        B 11 72REFFLD(@@FRPR)
     A                                      EDTCDE(K)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A N62N46                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 12 13'Fall-out rate'
     A            D1QFOPR   R        O 12 31REFFLD(@@FOPR)
     A                                      EDTCDE(K)
     A                                 12 42'%'
     A            D1LFOPR   R        B 12 46REFFLD(@@FOPR)
     A                                      EDTCDE(K)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A N62N73                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HFOPR   R        B 12 59REFFLD(@@FOPR)
     A                                      EDTCDE(K)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N62N74                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OFOPR   R        B 12 72REFFLD(@@FOPR)
     A                                      EDTCDE(K)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A N62N75                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 14  5'Born alive per litter'
     A            D1QLTBAHD R        O 14 31REFFLD(@@LTBAHD)
     A                                      EDTCDE(K)
     A            D1LLTBAHD R        B 14 46REFFLD(@@LTBAHD)
     A                                      EDTCDE(K)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A N62N76                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HLTBAHD R        B 14 59REFFLD(@@LTBAHD)
     A                                      EDTCDE(K)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A N62N77                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OLTBAHD R        B 14 72REFFLD(@@LTBAHD)
     A                                      EDTCDE(K)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A N62N78                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 15  5'Piglet mortality rate'
     A            D1QPMPR   R        O 15 31REFFLD(@@PMPR)
     A                                      EDTCDE(K)
     A                                 15 42'%'
     A            D1LPMPR   R        B 15 46REFFLD(@@PMPR)
     A                                      EDTCDE(K)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A N62N79                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HPMPR   R        B 15 59REFFLD(@@PMPR)
     A                                      EDTCDE(K)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A N62N80                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OPMPR   R        B 15 72REFFLD(@@PMPR)
     A                                      EDTCDE(K)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A N62N81                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 17  6'Nursery removal rate'
     A            D1QNRMPR  R        O 17 31REFFLD(@@NRMPR)
     A                                      EDTCDE(K)
     A                                 17 42'%'
     A            D1LNRMPR  R        B 17 46REFFLD(@@NRMPR)
     A                                      EDTCDE(K)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A N62N82                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HNRMPR  R        B 17 59REFFLD(@@NRMPR)
     A                                      EDTCDE(K)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A N62N83                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1ONRMPR  R        B 17 72REFFLD(@@NRMPR)
     A                                      EDTCDE(K)
     A  84                                  DSPATR(PC)
     A  84                                  DSPATR(RI)
     A N62N84                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 18  5'Finisher removal rate'
     A            D1QFRMPR  R        O 18 31REFFLD(@@FRMPR)
     A                                      EDTCDE(K)
     A                                 18 42'%'
     A            D1LFRMPR  R        B 18 46REFFLD(@@FRMPR)
     A                                      EDTCDE(K)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A N62N85                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HFRMPR  R        B 18 59REFFLD(@@FRMPR)
     A                                      EDTCDE(K)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A N62N86                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OFRMPR  R        B 18 72REFFLD(@@FRMPR)
     A                                      EDTCDE(K)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A N62N87                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 20  4'Genetic selection rate'
     A            D1QGSPR   R        O 20 31REFFLD(@@GSPR)
     A                                      EDTCDE(K)
     A                                 20 42'%'
     A            D1LGSPR   R        B 20 46REFFLD(@@GSPR)
     A                                      EDTCDE(K)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A N62N88                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HGSPR   R        B 20 59REFFLD(@@GSPR)
     A                                      EDTCDE(K)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A N62N89                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OGSPR   R        B 20 72REFFLD(@@GSPR)
     A                                      EDTCDE(K)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A N62N90                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 21 13'Sort-off rate'
     A            D1QSOPR   R        O 21 31REFFLD(@@SOPR)
     A                                      EDTCDE(K)
     A                                 21 42'%'
     A            D1LSOPR   R        B 21 46REFFLD(@@SOPR)
     A                                      EDTCDE(K)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N62N40                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1HSOPR   R        B 21 59REFFLD(@@SOPR)
     A                                      EDTCDE(K)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A N62N41                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1OSOPR   R        B 21 72REFFLD(@@SOPR)
     A                                      EDTCDE(K)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A N62N42                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1083K1
     A*%%TS  SD  20020612  082119  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  61N62                          23 11'F4=Prompt'
     A N62N61                          23 23'F5=Refresh'
     A N62                             23 35'F9=Accept'
     A N62N61                          23 46'F11=Delete'
     A N62                             23 58'F13=View Farm Actuals'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1083ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1083EC                  SFLCTL(HP1083ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
