      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Workfile: Weekly Sire Line Dose Percents
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/13/01
      *
      * FUNCTION:    This function writes records to a workfile. The file
      *              is basically a clone of the Weekly Sire Line Doses file.
      *              Except, we:
      *              a) write 1 record per sire line instead of 1 per source/sire line
      *              b) calculate the percent of the week's doses represented by each
      *                 sire line
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsl081a   if   e           k disk
      *  Weekly sire line doses
      *
      *
     Fhsp325    o    e           k disk
      *  Workfile: Weekly sire line dose percents
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arrsicd         s              5a   dim(50)
     D arrdono         s              5s 0 dim(50)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D first           s              1
     D svcdyr          s                   like(swcdyr)
     D svcdwk          s                   like(swcdwk)
     D svwedt          s                   like(swwedt)
      *
      *
      * Workfields
      *
     D wkdono          s              5  0
     D wkdopr          s              8  5
     D wkrempr         s              5  2
      *
      * Array indexes
      *
     D x               s              3  0
     D y               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Process each BGF farm
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsp018                                 90
     C                   if        *in90 = *off and fsppcd = 'BGF  '            If not EOF
      *
      * Retrieve/process weekly sire line doses.
      *
     C                   exsr      $doses
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Retrieve/process weekly sire line doses
      *----------------------------------------------------------------
      *
     C     $doses        begsr
      *
     C                   move      yes           first
     C     fsfscd        setll     hsl081a
      *
     C                   dou       *in91 = *on                                  Do doses
     C     fsfscd        reade     hsl081a                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      first = yes
     C                   z-add     swcdyr        svcdyr
     C                   z-add     swcdwk        svcdwk
     C                   z-add     swwedt        svwedt
     C                   move      no            first
      *
     C                   when      swcdyr <> svcdyr or
     C                             swcdwk <> svcdwk
     C                   exsr      $wrt325
     C                   endsl
      *
      * Detail processing:
      *  1) accumulate total doses per week
      *  2) accumulate doses per sire line per week
      *
     C                   add       swdono        wkdono
      *
     C                   eval      x = 1
     C     swsicd        lookup    arrsicd(x)                             92
     C                   if        *in92 = *on                                  If exists
     C                   add       swdono        arrdono(x)
     C                   else
     C                   add       1             y
     C                   move      swsicd        arrsicd(y)
     C                   z-add     swdono        arrdono(y)
     C                   endif                                                  If exists
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do doses
      *
      *
      * Write out records for the farm's last week
      *
     C                   if        first = no
     C                   exsr      $wrt325
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Write sire line dose percent records
      *----------------------------------------------------------------
      *
     C     $wrt325       begsr
      *
     C                   z-add     fsfscd        w1fscd
     C                   z-add     svcdyr        w1cdyr
     C                   z-add     svcdwk        w1cdwk
     C                   z-add     svwedt        w1wedt
      *
     C                   z-add     100           wkrempr
      *
     C                   do        y             x
     C                   move      arrsicd(x)    w1sicd                   92
     C                   z-add     arrdono(x)    w1dono
     C                   exsr      $percent
     C                   z-add     y             w1sino
     C                   write     w1rec
     C                   enddo
      *
      * Clear arrays and set array indexes back to zero
      *
     C                   move      *blank        arrsicd
     C                   z-add     0             arrdono
     C                   z-add     0             x
     C                   z-add     0             y
      *
      * Clear accumulators and set save fields
      *
     C                   z-add     0             wkdono
     C                   z-add     swcdyr        svcdyr
     C                   z-add     swcdwk        svcdwk
     C                   z-add     swwedt        svwedt
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Calculate percent
      *----------------------------------------------------------------
      *
     C     $percent      begsr
      *
     C                   if        wkdono <> 0
      *
     C                   select
     C                   when      x = y
     C                   z-add     wkrempr       w1dopr
     C                   other
     C     w1dono        div       wkdono        wkdopr
     C     wkdopr        mult      100           w1dopr
     C                   sub       w1dopr        wkrempr
     C                   endsl
      *
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /EJECT
