      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP600
      * TITLE:       Listing of Feed Mills
      * PROGRAMMER:  Lara Buser
      * CREATED:     06/01/2017
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP400-Work with Feed Mills
      *
      *            The listing 'selections' will be the positioner/selector values
      *            in the control record at the time the user submitted the listing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      *   Date     Programmer
      * 10/30/2019 Brad Baden  E15736
      *            Recompiled for change to file HSP089.  Also added
      *            "***** END OF REPORT *****'.
      *
      ****************************************************************
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
     FHSP089    IF   E           K DISK
      *    Feed Mill Alias
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
     FHSP009    IF   E           K DISK
      *    Company
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfmbo                 1      5
     D  ldaist                 6      6
     D  ldmltcd                7      8
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      * Print fields
      *
     D dash            s             85    inz(*all'-')
      *
     D h1mill          s             75
     D h1status        s             75
     D h1mltype        s             75
     D h1fmalnm        s             30
     D svfmalnm        s             20
     D svmltds         s             15
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE TRUCKER FILE AND PRINT HEADINGS THE FIRST TIME THROUGH.
      *
     C                   EXCEPT    h1hdr
      *
      * READ AND PRINT ALL RECORDS IN THE FILE THAT MATCH THE USERS SELECTIONS.
      *
     C     ldfmbo        setll     hsp089
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp089                                 90
      *
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldaist = ' ' or ldaist = maaist) and
     C                             (ldmltcd = ' ' or ldmltcd = mamltcd) and
     C                             (ldfmbo = ' ' or ldfmbo = mafmbo)
      *
      * Get Company info or default to blanks
     C     mafmbo        chain     hsp009                             91
     C                   if        *in91 = *on
     C                   eval      bococd = *blank
     C                   eval      borgcd = *blank
     C                   endif
     C*
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    h1hdr
     C                   ENDIF
      *
      * Print detail line
      * move Feed Mill Alias name from 30 char to 20 char field for printing
     C                   eval      svfmalnm = mafmalnm                          30 to 20 char
     C                   eval      svmltds  = mamltds                           20 to 15 char
     C                   EXCEPT    d1dtl
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * End of File
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     c                   except    ENDRPT
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
      * Set up headings for user selections:
      *
     C                   if        ldfmbo   = *blank
     C                   eval      h1mill   = 'Listing begins with the ' +
     C                                        'first Feed Mill'
     C                   else
     C                   eval      h1mill   = 'Listing begins with Mill '   +
     C                                        'B/O Code: '  +
     C                                        ldfmbo
     C                   endif
      *
      * Active/inactive status
      *
     C                   select
     C                   when      ldaist = *blank
     C                   eval      h1status = 'Listing includes both Active ' +
     C                                        'and Inactive Feed Mills'
     C                   when      ldaist = 'A'
     C                   eval      h1status = 'Listing includes only Active ' +
     C                                        'Feed Mills'
     C                   when      ldaist = 'I'
     C                   eval      h1status = 'Listing includes only ' +
     C                                        'Inactive Feed Mills'
     C                   endsl
      *
      * Feed Mill Types
     C                   select
     C                   when      ldmltcd = *blank
     C                   eval      h1mltype = 'Listing includes Feed Mills' +
     C                                        ' with all Mill Types'
     C                   other

     C                   eval      h1mltype = 'Listing only includes Feed Mil' +
     C                                        'ls with Mill Type: ' +
     C                                        ldmltcd
     C                   endsl
      *
     C                   endsr
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            h1hdr          1 01
     O                       SDPGM               10
     O                                           49 'HOG PRODUCTION SYSTEM'
     O                                           76 'DATE'
     O                       UDATE         Y     85
      *
     O          E            h1hdr          1
     O                       SDUSR               10
     O                                           49 ' LISTING OF FEED MILLS'
     O                                           76 'TIME'
     O                       RTIME               85 '  :  :  '
      *
     O          E            h1hdr          2
     O                                           76 'PAGE'
     O                       PAGE          Z     81
      *
      *
     O          e            h1hdr          1
     O                       h1mill              75
      *
     O          e            h1hdr          1
     O                       h1status            75
      *
     O          e            h1hdr          2
     O                       h1mltype            75
      *
     O          E            h1hdr          1
     O                                           85 'Cost'
      *
     O          E            h1hdr          1
     O                                            5 'Feed'
     O                                           34 'Mill'
     O                                           45 'Mill Type'
     O                                           56 'Map'
     O                                           60 'Co'
     O                                           73 'G/L'
     O                                           77 'MNF'
     O                                           79 'A'
     O                                           85 'Thru'
      *
     O          E            h1hdr          1
     O                                            5 'Mill'
     O                                           28 'Feed Mill Description'
     O                                           34 'TYPE'
     O                                           47 'Description'
     O                                           57 'Mill#'
     O                                           61 'Nbr'
     O                                           67 'Rgn'
     O                                           73 'Sub'
     O                                           77 'Typ'
     O                                           79 'I'
     O                                           84 'Sys'
      *
     O          E            h1hdr          1
     O                       dash                85
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
     O          E            d1dtl       1
     O                       MAFMBO               5
     O                       SVFMALNM            27
     O                       MAMLTCD             33
     O                       MAMLTDS             55
     O                       MAMMNO              56
     O                       BOCOCD              61
     O                       BORGCD              69
     O                       MA2SUB              72
     O                       MAFTCD              76
     O                       MAAIST              79
     O                       macsfl              83
      *
      *-------------------------------------------------------------
      * END OF REPORT LINE
      *-------------------------------------------------------------
      *
     O          E            ENDRPT      2
     O                                           25 '***** END OF REPORT *****'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
