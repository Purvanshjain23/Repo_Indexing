      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF624
      * TITLE:       Margin: WIP Usage Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     07/13/07
      *
      * FUNCTION:  Batch program to display values in the Inventory Stock
      *            Closing file for the specified week.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/14/08  LeAnne Ramsey
      *           Added Company/Warehouse as LDA values. This replaces the
      *           hardcoding of 360/GP1.
      *
      * 11/15/12  LeAnne Ramsey (E2337)
      *           Recompile only.
      *           Rose had to increase the price/cwt field sizes in PPBTCPP-Inventory
      *           Value file and PPBSCPP-Inventory Stock Closing.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fppbscpl1  if   e           k disk
      *    Inventory stock closing
      *
      *
     Fombyrel1  if   e           k disk
      *  Item default
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Print fields
      *
     D l1prds          s                   like(byavna)
      *
     D t1m6wg          s                   like(bsm6wg)
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     d  ldyr                   1      4  0
     d  ldwk                   5      6  0
     d  ldwedt                 7     14  0
     d  ldwemdy               15     20  0
     d  ldcono                21     23  0
     d  ldwhs                 24     26
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read/process Inventory Stock Closing records for the week.
      * Only process WP items.
      *
     C     key01         setll     ppbscpl1
      *
     C                   dou       *in90 = *on                                  Main do
     C     key01         reade     ppbscpl1                               90
     C                   if        *in90 = *off and bsw1sx = 'WP '              If not EOF
     C                   move      no            first
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------
      * EOF processing
      *----------------
      * Print "no data" message or Report Total
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
     C                   exsr      $t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Accumulate WP Usage Pounds for Report Total
      *
     C                   add       bsm6wg        t1m6wg
      *
      * Retrieve the Item Description
      *
     C     bsejc7        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l1prds  = byavna
     C                   else
     C                   eval      l1prds = 'Unknown'
     C                   endif
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report total
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    ldwedt
     C                   kfld                    ldcono
     C                   kfld                    ldwhs
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           80 'WIP USAGE REPORT'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          122 'PAGE'
     O                       page          z    132
      *
     O          e            h1hdr          1
     O                                           18 'Week-ending date:'
     O                       ldwemdy             28 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           18 'Year/week:'
     O                       ldyr          z     24
     O                       ldwk          z     28
      *
     O          e            h1hdr          1
     O                                           18 'Warehouse company:'
     O                       ldcono        z     23
      *
     O          e            h1hdr          1
     O                                           18 'Warehouse code:'
     O                       ldwhs               23
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           22 'WIP'
     O                                           38 'Beg Inv'
     O                                           54 'Floor'
     O                                           70 'Floor'
     O                                           86 'End Inv'
      *
     O          e            h1hdr       0  1
     O                                            7 'Item'
     O                                           22 'Usage'
     O                                           38 'Final'
     O                                           54 'Production'
     O                                           70 'Production'
     O                                           86 'Final'
      *
     O          e            h1hdr       0  1
     O                                            7 'Code'
     O                                           22 'Pounds'
     O                                           38 'Pounds'
     O                                           54 'Pounds'
     O                                           70 'Adj Pounds'
     O                                           86 'Pounds'
     O                                          101 'Description'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       bsejc7        z      7
     O                       bsm6wg        k     23
     O                       bsmqwg        k     39
     O                       bsmswg        k     55
     O                       bsnawg        k     71
     O                       bsm5wg        k     87
     O                       l1prds             120
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       0  1
     O                                           22 '--------------'
      *
     O          e            t1tot       0  1
     O                       t1m6wg        k     23
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
