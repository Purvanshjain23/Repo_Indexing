      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT:  PORK DIVISION
      * SYSTEM:       AS/400
      * PROGRAM:      MP351
      * DESCRIPTION:  MP2: Build Workfile for List of Overridden Items
      * PROGRAMMER:   LeAnne Fedor
      * DATE:         04/30/02
      *
      *
      * FUNCION:     This program reads each record in the Invoice Header file that was
      *              selected by the open query (ie: posted to JDE and in the user's date
      *              range.)
      *
      *              If the user has limited the report to a single vendor, the logic
      *              then checks to see if the invoice is against a P.O. for the user's
      *              selected vendor.
      *
      *              The final step is to retrieve any invoice detail records that have a
      *              value of YES in the 'override amount' flag and write them to the
      *              workfile.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/24/02  LeAnne Fedor
      *           All amount fields in the database were increased from 7,2 to 9,2.
      *           Recompiled the workfile and this program.
      *
      * 10/01/02  LeAnne Fedor
      *           Removed: receipt number and serial number
      *           Added: cost center, object account, subsidiary
      *
      * 03/29/11  Barb Gutierrez
      *           Increased qty and rate in database.  Recompile only.  E001398.
      *
      * 03/10/14  LeAnne Ramsey (E2992)
      *           Added Accounting Company as an optional selection.
      *
      * 03/26/19  Brad Baden  (E14631)
      *           Recompile only due to field size changes in database.
      *
      * 05/24/21  Danny Nguyen   (S17068)
      *           DBFC on MPP103 file. Unit Cost Per Item 'PDITEMRT' field length changed
      *           from 9.4 to 11.4.
      *           DBFC on MPP104 file. The following field lengths changed from 9.2 to 11.2:
      *             Invoiced Amount 'IHINAM'.
      *             Amount To Apply 'IHTAAM'.
      *             Applied Amount 'IHAPAM'.
      *             Sales Tax Amount 'IHSTAM'.
      *             Shipping/Handling Amount 'IHSHAM'.
      *           DBFC on MPP105 file. Applied Amount 'IDAPAM' field length changed from
      *           9.2 to 11.2.
      *           DBFC on MPP351 files: The following field lengths changed from 9.2 to 11.2:
      *             Unit Cost Per Item 'W1ITEMRT'.
      *             Applied Amount 'W1APAM'.
      *             Difference Amount 'W1DIFFAM'.
      *           Changed work field 'wkapam' length from 9.2 to 11.2.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fmpp102    if   e           k disk
      *  MP2: Purchase order header
      *
      *
     Fmpp103    if   e           k disk
      *  MP2: Purchase order detail
      *
      *
     Fmpp104    if   e           k disk
      *  MP2: Invoice header
      *  (records selected by open query)
      *
      *
     Fmpp105    if   e           k disk
      *  MP2: Invoice detail
      *  (records selected by open query)
      *
      *
     Fmpp351    o    e           k disk
      *  MP2: Workfile for list of overridden items
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
     D procfl          s              1     inz('Y')
      *
      * Workfields
      *
17068D** wkapam          s                   like(w1apam)
17068D wkapam          s             11  2
      *
      *---------------------------------------------------------------
      * Data areas
      *---------------------------------------------------------------
      *
      * Local data area.
      *
     Dlda             uds                  dtaara(*lda)
     D  ldjdvnno              29     36  0
     D  ldacono               86     88  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process each record in the Invoice Header file (records were selected by
      * open query in the submit CLP.)
      *
     C     *loval        setll     mpp104
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      mpp104                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      yes           procfl
      *
      * Retrieve the associated purchase order header record.
      * If the user elected to view only a single vendor or accounting company
      * and this is not a match, don't process this invoice.
      *
     C     ihposn        chain     mpp102                             92
     C                   select
     C                   when      *in92 = *on
     C                   move      no            procfl
      *
     C                   when      ldjdvnno <> 0 and
     C                             ldjdvnno <> phjdvnno
     C                   move      no            procfl
      *
     C                   when      ldacono <> 0 and
     C                             ldacono <> phacono
     C                   move      no            procfl
     C                   endsl
      *
      *
      * Now, go write workfile records for each 'overridden' invoice detail
      * record that you find.
      *
     C                   if        procfl = yes                                 If process
     C                   exsr      $detail
     C                   endif                                                  If process
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        LR
      /eject
      *---------------------------------------------------------------
      * Process Invoice Detail records looking for 'overridden' ones
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
      * Clear record format
      *
     C                   clear                   w1rec
      *
     C                   move      phpono        w1pono
     C                   z-add     phjdvnno      w1jdvnno
     C                   move      phjdvnnm      w1jdvnnm
     C                   z-add     phacono       w1acono
      *
     C                   z-add     ihposn        w1posn
     C                   move      ihinno        w1inno
      *
      *
      * Read each 'overridden' record...records selected by open query on CL.
      *
     C     key01         setll     mpp105
      *
     C                   dou       *in91 = *on                                  Do detail
     C     key01         reade     mpp105                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   z-add     idapam        w1apam
     C                   z-add     idinqt        w1inqt
     C                   move      idcom         w1com
      *
      *
      * Retrieve data from the P.O. Detail file
      *
     C     key02         chain     mpp103                             92
     C                   if        *in92 = *off                                 If found
     C                   move(p)   pditemno      w1itemno
     C                   z-add     pditemrt      w1itemrt
     C                   move(p)   pdmcu         w1mcu
     C                   move(p)   pdobj         w1obj
     C                   move(p)   pdsub         w1sub
     C                   else
     C                   move      *blank        w1itemno
     C                   z-add     0             w1itemrt
     C                   move      *blank        w1mcu
     C                   move      *blank        w1obj
     C                   move      *blank        w1sub
     C                   endif                                                  If found
      *
      * Calculate the difference between a) the applied amount and
      * b) the calculated amount of cost X quantity
      *
      *
     C     w1itemrt      mult(h)   w1inqt        wkapam
     C     wkapam        sub       w1apam        w1diffam
      *
     C                   write     w1rec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    ihposn
     C                   kfld                    ihinno
      *
      *
     C     key02         klist
     C                   kfld                    idposn
     C                   kfld                    idrcno
     C                   kfld                    idserno
      *
     C                   endsr
      /eject
