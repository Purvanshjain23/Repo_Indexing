     A*%%TS  SD  20101014  104058  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1011
     A*  TITLE:         MAINTAIN WEEKLY SIRE LINE DOSES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/08/01
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  62  IS ON WHEN THE RECORD HAS BEEN DELETED OUT FROM UNDER THE USER
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A  60N90                               CF11(11 'DELETE')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP1011S1                  SFL
     A*%%TS  SD  20101014  104058  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1SSCD    R        B 11  3REFFLD(@@SSCD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N33N62                               DSPATR(HI)
     A N33N62                               DSPATR(UL)
     A            D1SICOMP  R        B 11 13REFFLD(HSRREF/@@SICOMP *LIBL/HSPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N34N62                               DSPATR(HI)
     A N34N62                               DSPATR(UL)
     A            D1PRIME   R        B 11 22REFFLD(HSRREF/@@PRIME *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N36N62                               DSPATR(HI)
     A N36N62                               DSPATR(UL)
     A            D1DONO    R        B 11 30REFFLD(@@DONO)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N35N62                               DSPATR(HI)
     A N35N62                               DSPATR(UL)
     A            D1SICD    R        O 11 40REFFLD(@@SICD)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP1011C1                  SFLCTL(HP1011S1)
     A*%%TS  SD  20050127  112346  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1     R        H      REFFLD(@@RRN)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 22'MAINTAIN WEEKLY SIRE LINE DOSES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site .............'
     A  61                              4 26'?'
     A            C1FSCD    R        B  4 28REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A  61N31                               DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                  5  2'Week-ending date ......'
     A            C1MDYWEDT R        B  5 28REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  -')
     A  61N32                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                  5 40'Calendar year/week:'
     A            C1CDYR    R        O  5 60REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A            C1CDWK    R        O  5 65REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                  7  5'?'
     A                                  7 14'?'
     A                                  7 23'?'
     A                                  8  3'-- Sire Line --'
     A                                  8 21'Prime'
     A                                  8 29'Number of'
     A                                  8 40'Sire'
     A                                  9  3'Source'
     A                                  9 11'Company'
     A                                  9 22'Line'
     A                                  9 31'Doses'
     A                                  9 40'Line'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP1011K1
     A*%%TS  SD  19971010  104506  ISLRAMS     REL-V3R6M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90                             23 34'F9=Accept'
     A  60N61N90                       23 45'F11=Delete'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP1011ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP1011EC                  SFLCTL(HP1011ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
