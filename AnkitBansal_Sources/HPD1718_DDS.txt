     A*%%TS  SD  20060117  152949  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         HPD1718
     A* DESCRIPTION:  MAINTAIN ORDER REQUEST
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         07/14/04
     A*
     A*
     A*  60  REVISE MODE
     A*  63  OPTION FIELD IS PROTECTED/NON-DISPLAY--USER CANNOT DELETE THE ITEM
     A*  64  USER CAN DELETE ITEM--BUT ALL OTHER FIELDS ON LINE ARE PROTECTED
     A*  99  THE RECORD THE USER SELECTED HAS BEEN DELETED BY ANOTHER USER
     A*      SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF09(09 'ACCEPT')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP1718S1                  SFL
     A*%%TS  SD  20040803  153418  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDLNNO    R        H      REFFLD(@@LNNO)
     A            HDITCD    R        H      REFFLD(@@ITCD)
     A            HDBGIT    R        H      REFFLD(@@BGIT)
     A            HDBGFL    R        H      REFFLD(@@BGFL)
     A            HDCRITFL  R        H      REFFLD(@@CRITFL)
     A            HDVOL     R        H      REFFLD(@@VOL)
     A            HDRQQT    R        H      REFFLD(@@RQQT)
     A            HDUSQT    R        H      REFFLD(@@USQT)
     A            HDFBSN    R        H      REFFLD(@@FBSN)
     A            HDOVFL         1A  H
     A            D1OPT          1A  B 12  2
     A  36N63                               DSPATR(PC)
     A  36N63                               DSPATR(RI)
     A N36N63                               DSPATR(HI)
     A N36N63                               DSPATR(UL)
     A  63
     AO 99                                  DSPATR(PR)
     A  63
     AO 99                                  DSPATR(ND)
     A            D1ITCD    R        B 12  5REFFLD(@@ITCD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N64                               DSPATR(HI)
     A N33N64                               DSPATR(UL)
     A  64
     AO 99                                  DSPATR(PR)
     A            D1RQQT    R        B 12 32REFFLD(HSRREF/@@RQQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N64                               DSPATR(HI)
     A N34N64                               DSPATR(UL)
     A  64
     AO 99                                  DSPATR(PR)
     A            D1APQT    R        O 12 48REFFLD(HSRREF/@@APQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A            D1ORCD    R        B 12 66REFFLD(@@ORCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35N64                               DSPATR(HI)
     A N35N64                               DSPATR(UL)
     A  64
     AO 99                                  DSPATR(PR)
     A            D1RQSCD   R        O 12 79REFFLD(HSRREF/@@RQSCD *LIBL/HSPREF)
     A            D1ITDS    R        O 13  5REFFLD(@@ITDS)
     A                                      COLOR(PNK)
     A                                 13 56'Com'
     A                                      COLOR(WHT)
     A            D1COM     R        B 13 60REFFLD(HSRREF/@@COM20 *LIBL/HSPREF)
     A                                      CHECK(LC)
     A N64                                  DSPATR(HI)
     A N64                                  DSPATR(UL)
     A  64
     AO 99                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R HP1718C1                  SFLCTL(HP1718S1)
     A*%%TS  SD  20060117  152949  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A N99                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(31)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(53)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A            C1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 26'MAINTAIN ORDER REQUEST'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  3 62'Request'
     A                                      DSPATR(UL)
     A                                  4  2'Work order number:'
     A            C1WO      R        O  4 21REFFLD(HSRREF/@@WO *LIBL/HSPREF)
     A                                  4 64'Date:'
     A            C1MDY          6Y 0O  4 70EDTWRD('  /  /  -')
     A                                  5  2'Farm site .......:'
     A            C1FSCD    R        O  5 21REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  5 32REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  5 62'Status:'
     A  60        C1RQSDS   R        O  5 70REFFLD(HSRREF/@@RQSDS *LIBL/HSPREF)
     A                                  7  2'Comment ..........'
     A            C1COM     R        B  7 21REFFLD(HSRREF/@@COM50 *LIBL/HSPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PR)
     A  60                              9  2'4=Delete'
     A                                  9 68'?'
     A                                      DSPATR(HI)
     A                                 10 12'?'
     A                                      DSPATR(HI)
     A                                 10 37'Requested'
     A                                      DSPATR(HI)
     A                                 10 54'Approved'
     A                                      DSPATR(HI)
     A                                 10 64'Override'
     A                                      DSPATR(HI)
     A                                 11  2'Op'
     A                                      DSPATR(HI)
     A                                 11  5'Item Number'
     A                                      DSPATR(HI)
     A                                 11 38'Quantity'
     A                                      DSPATR(HI)
     A                                 11 54'Quantity'
     A                                      DSPATR(HI)
     A                                 11 65'Reason'
     A                                      DSPATR(HI)
     A                                 11 78'St'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1718K1
     A*%%TS  SD  20040316  074541  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A N99                             23 22'F5=Refresh'
     A N99                             23 34'F9=Accept'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP1718ES                  SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP1718EC                  SFLCTL(HP1718ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
