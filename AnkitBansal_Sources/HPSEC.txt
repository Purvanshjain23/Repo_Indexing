      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPSEC
      * TITLE:       SECURITY PROGRAM
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     03/29/94
      *
      * FUNCTION:  THIS SECURITY MODULE PROGRAM IS CALLED FROM ALL
      *            PROGRAMS TO VERIFY AUTHORIZED USERS, FUNCTIONS,
      *            AND USER AUTHORIZATION TO FUNCTIONS.  IF THE
      *            FUNCTION IS BUSINESS OFFICE SPECIFIC, THE USER'S
      *            SECURITY LOCATION IS USED TO VERIFY THE USER'S
      *            AUTHORITY TO A BUSINESS OFFICE THROUGH ITS SECURITY
      *            LOCATION.  WHEN THE INCOMING PARM -INDVCD- IS YES,
      *            THE BUSINESS OFFICE DIVISION CODE IS COMPARED TO
      *            THE FUNCTION DIVISION CODE TO VERIFY
      *            THE BUSINESS OFFICE USAGE IN THE FUNCTION.
      *            WHEN A SECURITY CHECK IS NOT PASSED, NO FURTHER
      *            CHECKING IS DONE.
      *
      *            TO TURN OFF SECURITY CHECKING FOR A USER:
      *            IN FILE (HSP001) SET THE FIELD (AURUFL) TO 'N'.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 03/27/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'DELETE MOVEMENTS FLAG'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE.
      *
      * 04/30/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'MOVEMENT STATUS'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE TO SUPPORT
      *           THE LOGIC FOR DELETING MOVEMENTS.
      *
      * 05/14/97  LEANNE FEDOR
      *           RECOMPILE ONLY.  OUTQUEUE WAS ADDED TO THE
      *           AUTHORIZED USERS FILE. THIS FIELD WILL NOW BE THE
      *           USER'S DEFAULT PRINTER IN HPS INSTEAD OF THE OUTQ
      *           ON THE USER'S PROFILE.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP001    IF   E           K DISK
      *    AUTHORIZED USERS  (KEY: AUUSCD)
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS         (KEY: FNFNCD)
      *
     FHSP003    IF   E           K DISK
      *    FUNCTION AUTHORIZATION  (KEY: FAUSCD FAFNCD)
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE  (KEY: BOBOBO )
      *
     FHSL009A   IF   E           K DISK
     F                                     RENAME(BOREC:BORECA)
      *    BUSINESS OFFICE  (KEY: BOSLCD BODVCD)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D ALL             C                   CONST('A')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * EXECUTE THE SECURITY SUBROUTINE TO DETERMINE THAT THE USER
      * IS AN AUTHORIZED USER.
      *
     C                   EXSR      $SECAU
      *
      * EXECUTE THE SUBROUTINE TO VALIDATE THE FUNCTION AND RETRIEVE
      * THE DIVISION AND THE TYPE OF FUNCTION (MAINTAINCE OR VIEW)
      *
     C                   EXSR      $SECFN
      *
      *----------------------------------------------------------------
      * ONLY PROCEED WITH SECURITY CHECKING IF THERE ARE NO ERRORS
      *----------------------------------------------------------------
      *
     C     SECFL         IFEQ      NO                                           IF NO PROB
      *
      * IF THE USER IS A RESTRICTED USER
      * CHECK THE USER'S AUTHORITY TO MAINTENANCE FUNCTIONS
      *
     C     AURUFL        IFEQ      YES                                          IF RESTRICT
      *
     C     AUMDFL        IFEQ      NO                                           IF CAN'T MOD
     C     FNFNTY        ANDEQ     'M'
     C                   MOVEL     YES           SECFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS00560'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     INFNCD        MGW(1)
     C                   MOVEA     SDUSR         MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF CAN'T MOD
      *
     C                   ENDIF                                                  IF RESTRICT
      *
      *----------------------------------------------------------------
      * ONLY CONTINUE WITH SECURITY CHECKING IF THERE ARE NO ERRORS
      *----------------------------------------------------------------
      *
     C     SECFL         IFEQ      NO                                           IF ALL OK
      *
      * IF THIS FUNCTION REQUIRES A BUSINESS OFFICE,
      *   1) DEFAULT A BUSINESS OFFICE, OR
      *   2) VALIDATE THE BUSINESS OFFICE, OR
      *   3) ALL BUSINESS OFFICES ARE SELECTED, SO
      *       A) IF A USER IS RESTRICTED IN HIS MAINTENANCE
      *          TO HIS SECURITY LOCATION, HE MUST ENTER A
      *          SPECIFIC BUSINESS OFFICE. HE CAN'T LEAVE
      *          BUSINESS OFFICE BLANK FOR ALL.
      *       B) IF A USER IS RESTRICTED IN HIS VIEWING
      *          TO HIS SECURITY LOCATION, HE MUST ENTER A
      *          SPECIFIC BUSINESS OFFICE. HE CAN'T LEAVE
      *          BUSINESS OFFICE BLANK FOR ALL.
      *
     C     FNBOFL        IFEQ      YES                                          IF B/O REQ
      *
     C                   SELECT
     C     INBOFL        WHENEQ    NO                                           WH DEFAULT
     C                   EXSR      $DEFBO
      *
     C     INBOFL        WHENEQ    YES                                          WH VALIDATE
     C                   EXSR      $VALBO
      *
     C     INBOFL        WHENEQ    ALL                                          WH ALL
      *
     C     AURMFL        IFEQ      YES                                          IF RST MAINT
     C     FNFNTY        ANDEQ     'M'
     C                   MOVEL     YES           SECFL
     C                   MOVEL     YES           OUBOFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS00567'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     SDUSR         MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C     AURVFL        IFEQ      YES                                          IF RST VIEW
     C     FNFNTY        ANDEQ     'V'
     C                   MOVEL     YES           SECFL
     C                   MOVEL     YES           OUBOFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS00568'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     SDUSR         MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF RST VIEW
     C                   ENDIF                                                  IF RST MAINT
     C                   ENDSL
     C                   ENDIF                                                  IF B/O REQ
      *
      * IF THE USER HAS BEEN SET UP TO REQUIRE AUTHORIZATION TO
      * INDIVIDUAL FUNCTIONS, CHECK TO SEE IF THE USER IS AUTHORIZED
      * TO THIS PARTICULAR FUNCTION.
      *
     C     AUAFFL        IFEQ      YES
     C                   EXSR      $SECAF
     C                   ENDIF
      *
     C                   ENDIF                                                  IF ALL OK
     C                   ENDIF                                                  IF NO PROB
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $SECAU - DETERMINE IF USER IS AN AUTHORIZED USER
      *---------------------------------------------------------------
      *
     C     $SECAU        BEGSR
      *
     C     SDUSR         CHAIN     HSP001                             92
     C     *IN92         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL     YES           SECFL
     C                   MOVE      *BLANK        OURMFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS09102'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     SDUSR         MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     AURMFL        OURMFL
     C                   ENDIF                                                  IF NOT FOUND
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SECFN - DETERMINE IF FUNCTION EXISTS IN FUNCTION FILE
      *---------------------------------------------------------------
      *
     C     $SECFN        BEGSR
      *
     C     INFNCD        CHAIN     HSP002                             95
     C     *IN95         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FNDVCD        OUDVCD
     C                   ELSE
      *
     C                   MOVEL     YES           SECFL
     C                   MOVE      *BLANK        OUDVCD
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS09104'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     INFNCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SECAF - DETERMINE IF USER IS AUTHORIZED TO USE THIS FUNCTION
      *---------------------------------------------------------------
      *
     C     $SECAF        BEGSR
      *
      * KEY: SDUSR, INFNCD
      *
     C     K3P003        CHAIN     HSP003                             91
     C     *IN91         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL     YES           SECFL
     C     E             IFLT      MAXMSG                                       IF MAX MSG
     C                   ADD       1             E
     C                   MOVEL     'HS09103'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     SDUSR         MGW(1)
     C                   MOVEA     INFNCD        MGW(11)
     C                   MOVEA     FNFNDS        MGW(21)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF                                                  IF MAX MSG
     C                   ENDIF                                                  IF NOT FOUND
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DEFBO - RETRIEVE A DEFAULT BUSINESS OFFICE
      *---------------------------------------------------------------
      *
     C     $DEFBO        BEGSR
      *
      * RETRIEVE THE FIRST BUSINESS OFFICE CODE THAT YOU CAN FIND FOR
      * THIS USER'S DEFAULT SECURITY LOCATION TO SEND TO THE CALLING
      * PROGRAM.
      *
      * KEY FIELDS: AUSLCD, FNDVCD
      *
     C     FNDVCD        IFEQ      *BLANK
     C     AUSLCD        CHAIN     HSL009A                            93
     C                   ELSE
     C     K1L009        CHAIN     HSL009A                            93
     C                   ENDIF
      *
     C     *IN93         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     BOBOBO        OUBOBO
     C                   MOVEL     BOBODS        OUBODS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $VALBO - VALIDATE THE BUSINESS OFFICE
      *---------------------------------------------------------------
      *
     C     $VALBO        BEGSR
      *
     C     INBOBO        IFEQ      *BLANK                                       IF NO BO
     C                   MOVEL     YES           OUBOFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     INBOBO        CHAIN     HSP009                             93
     C     *IN93         IFEQ      *ON                                          IF BAD BO
     C                   MOVEL     YES           OUBOFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS00110'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     INBOBO        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
      * BUSINESS OFFICE IS VALID
      *
     C                   MOVEL     BOBOBO        OUBOBO
     C                   MOVEL     BOBODS        OUBODS
      *
      * IF  THE INCOMING CHECK-DIVISION-FLAG IS YES
      * AND RETRIEVED BO DIVISION IS NOT EQUAL TO THE FUNCTION DIVISION
      *
     C     INDVFL        IFEQ      YES                                          IF DIV ERROR
     C     BODVCD        ANDNE     FNDVCD
     C                   MOVEL     YES           OUBOFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
      *
      * PICK CORRECT MESSAGE FOR THE DIVISION
      *
     C                   SELECT
     C     FNDVCD        WHENEQ    'LIVE '
     C                   MOVEL     'HS00112'     MGID(E)
     C     FNDVCD        WHENEQ    'FEED '
     C                   MOVEL     'HS00113'     MGID(E)
     C     FNDVCD        WHENEQ    'PROC '
     C                   MOVEL     'HS00114'     MGID(E)
     C                   ENDSL
      *
     C                   CLEAR                   MGW
     C                   MOVEA     INBOBO        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
      *
     C                   ENDIF                                                  IF DIV ERROR
     C                   ENDIF                                                  IF BAD BO
     C                   ENDIF                                                  IF NO BO
      *
      * IF THERE ARE NO ERRORS ON THE BUSINESS OFFICE,
      * THEN CHECK THE USER'S RESTRICTIONS ON MAINTENANCE
      * AND VIEW--IS HE LIMITED/RESTRICTED TO HIS SECURITY
      * LOCATION?
      *
     C     OUBOFL        IFEQ      YES
     C                   MOVEL     YES           SECFL
     C                   ELSE
     C                   EXSR      $RSTMV
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $RSTMV - RESTRICTED MAINTENANCE OR VIEW
      *---------------------------------------------------------------
      *
     C     $RSTMV        BEGSR
      *
      * IF  THIS IS A MAINTENANCE FUNCTION                             Y
      * AND USER HAS RESTRICTED MAINTENANCE
      * AND THE USER'S SECURITY LOCATION DOES NOT MATCH THE
      *     BUSINESS OFFICE SECURITY LOCATION
      *
     C     FNFNTY        IFEQ      'M'                                          IF RES MAINT
     C     AURMFL        ANDEQ     YES
     C     AUSLCD        ANDNE     BOSLCD
      *
     C                   MOVEL     YES           OUBOFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS09101'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     AUUSCD        MGW(1)
     C                   MOVEA     BOBOBO        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF RES MAINT
      *
      * IF  THIS IS A VIEW FUNCTION                                    Y
      * AND USER HAS RESTRICTED VIEW
      * AND THE USER'S SECURITY LOCATION DOES NOT MATCH THE
      *     BUSINESS OFFICE SECURITY LOCATION
      *
     C     FNFNTY        IFEQ      'V'                                          IF RES VIEW
     C     AURVFL        ANDEQ     YES
     C     AUSLCD        ANDNE     BOSLCD
      *
     C                   MOVEL     YES           OUBOFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   MOVEL     'HS09100'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     AUUSCD        MGW(1)
     C                   MOVEA     BOBOBO        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF RES VIEW
      *
     C     OUBOFL        IFEQ      YES
     C                   MOVEL     YES           SECFL
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * FUNCTION AUTHORIZATION (HSP003)
      *
     C     K3P003        KLIST
     C                   KFLD                    SDUSR
     C                   KFLD                    INFNCD
      *
      * BUSINESS OFFICE
      *
     C     K1L009        KLIST
     C                   KFLD                    AUSLCD
     C                   KFLD                    FNDVCD
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    E                 3 0          # ERR MSGS
      *
      **** IN = SENT FROM CALLING PROGRAM
      *
     C                   PARM                    INFNCD                         FUNCTION ID
     C                   PARM                    INBOBO                         B.O.
     C                   PARM                    INDVFL                         CHECK DIV?
     C                   PARM                    INBOFL                         CHECK B.O.?
      *
      **** OUT = RETURN TO CALLING PROGRAM
      *
     C                   PARM                    OUBOBO                         DEFAULT B.O.
     C                   PARM                    OUBODS                         DEFAULT DESC.
     C                   PARM                    OUDVCD                         FUNCTION DIV
     C                   PARM                    SECFL                          SECURITY ERROR FLG
     C                   PARM                    OUBOFL                         B.O. ERROR
     C                   PARM                    OURMFL                         RSTRIC MAINT
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     NO            SECFL             1
     C                   MOVEL     NO            OUBOFL            1
      *
      * SET UP WORK FIELDS FOR STANDARD SECURITY CHECKS
      *
     C     *LIKE         DEFINE    FAFNCD        INFNCD
     C     *LIKE         DEFINE    BOBOBO        INBOBO
     C     *LIKE         DEFINE    BOBOBO        OUBOBO
     C     *LIKE         DEFINE    BOBODS        OUBODS
     C     *LIKE         DEFINE    AURUFL        INDVFL
     C     *LIKE         DEFINE    BODVCD        OUDVCD
     C     *LIKE         DEFINE    AURUFL        INBOFL
     C     *LIKE         DEFINE    AURMFL        OURMFL
      *
     C                   ENDSR
      /EJECT
