     A*%%TS  SD  20030626  103801  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4047
     A*  TITLE:         WORK WITH EXPENSE CODES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/16/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4047S1                  SFL
     A*%%TS  SD  20030626  103525  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1ECCD    R        O 10  7REFFLD(HSRREF/@@ECCD *LIBL/HSPREF)
     A            D1ESCD    R        O 10 17REFFLD(HSRREF/@@ESCD *LIBL/HSPREF)
     A            D1EXCD    R        O 10 28REFFLD(HSRREF/@@EXCD *LIBL/HSPREF)
     A            D1RPCD    R        O 10 38REFFLD(HSRREF/@@RPCD *LIBL/HSPREF)
     A            D1EXDS    R        O 10 49REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A            D1EXSEQ   R        O 10 72REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      EDTCDE(L)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4047C1                  SFLCTL(HP4047S1)
     A*%%TS  SD  20030626  103525  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH EXPENSE CODES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  6 10'?'
     A                                      DSPATR(HI)
     A                                  6 20'?'
     A                                      DSPATR(HI)
     A                                  6 40'?'
     A                                      DSPATR(HI)
     A                                  7  7'Expense'
     A                                      DSPATR(HI)
     A                                  7 17'Expense'
     A                                      DSPATR(HI)
     A                                  7 28'Expense'
     A                                      DSPATR(HI)
     A                                  7 38'Report'
     A                                      DSPATR(HI)
     A                                  7 49'Expense Code'
     A                                      DSPATR(HI)
     A                                  7 71'Exp Code'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'Class'
     A                                      DSPATR(HI)
     A                                  8 17'Subclass'
     A                                      DSPATR(HI)
     A                                  8 28'Code'
     A                                      DSPATR(HI)
     A                                  8 38'Code'
     A                                      DSPATR(HI)
     A                                  8 49'Description'
     A                                      DSPATR(HI)
     A                                  8 71'Sequence'
     A                                      DSPATR(HI)
     A            C1ECCD    R        B  9  7REFFLD(HSRREF/@@ECCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1ESCD    R        B  9 17REFFLD(HSRREF/@@ESCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1EXCD    R        B  9 28REFFLD(HSRREF/@@EXCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1RPCD    R        B  9 38REFFLD(HSRREF/@@RPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1EXDS    R        B  9 49REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A            C1EXSEQ   R        B  9 72REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4047K1
     A*%%TS  SD  20030618  165633  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F6=Create'
     A                                 23 45'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4047ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4047EC                  SFLCTL(HP4047ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
