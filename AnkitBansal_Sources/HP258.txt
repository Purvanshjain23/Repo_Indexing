      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP258 - Retrieve Group with Most Pigs on Sales Movement
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/26/02
      *
      * FUNCTION:    This program was created to be called from Synon program(s).
      *              The Synon program(s) that call this program pass in a Movement
      *              Number. This program retrieves the group on the movement that
      *              had the most pigs and returns the group number back to the Synon
      *              System(s).
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp084    if   e           k disk
      *    Sales detail
      *
      *
     Fhsp085    if   e           k disk
      *    Sales detail
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkhd            s                   like(sgshhd)
     D svhd            s                   like(sgshhd)
     D wkhgsn          s                   like(sghgsn)
      *
      *
      * Parms
      *
     D xxreturn        s              7
     D xxmvsn          s                   like(shmvsn)
     D xxhgcd          s                   like(hghgcd)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *-----------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Retrieve the Sales Movement header record to get the status. Then,
      * process all the detail records to find the group with the most pigs.
      *
      *
     C     xxmvsn        chain     hsp084                             92
     C                   if        *in92 = *off                                 If found
     C                   move      *blank        xxreturn
     C                   exsr      $hgsn
     C                   else
      *
     C                   eval      xxreturn = 'HPSM1'
     C                   move      *blank        xxhgcd
     C                   endif                                                  If found
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Find the group with the most pigs
      *----------------------------------------------------------------
      *
     C     $hgsn         begsr
      *
     C     xxmvsn        setll     hsp085
      *
     C                   dou       *in91 = *on                                  Do count
     C     xxmvsn        reade     hsp085                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      shmscd = 'PP' or
     C                             shmscd = 'FP' or
     C                             shmscd = 'NC'
     C     sglvhd        add       sgdohd        wkhd
     C                   other
     C                   z-add     sgshhd        wkhd
     C                   endsl
      *
     C                   if        wkhd > svhd
     C                   z-add     sghgsn        wkhgsn
     C                   z-add     wkhd          svhd
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do count
      *
      * Retrieve 'hog group code' to put into output parm
      *
     C     wkhgsn        chain     hsl034d                            92
     C                   if        *in92 = *off                                 If hit
     C                   move      hghgcd        xxhgcd
     C                   endif                                                  If hit
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxreturn
     C                   parm                    xxmvsn
     C                   parm                    xxhgcd
      *
     C                   endsr
      /EJECT
